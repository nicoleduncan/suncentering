<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN"
 "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">

<!-- Generated by IDLdoc 3.5.1 on Mon Sep 30 16:58:05 2013 -->

<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en">
  <head>
    <meta http-equiv="Content-Type" content="text/html;charset=utf-8"/>
    <title>t_nstar.pro (Documentation for ./)</title>

    
    <link rel="stylesheet" type="text/css" media="all"
          href="../idldoc-resources/main.css" />
    <link rel="stylesheet" type="text/css" media="print"
          href="../idldoc-resources/main-print.css" />
    

    <script type="text/javascript">
      function setTitle() {
        parent.document.title="t_nstar.pro (Documentation for ./)";
      }
    </script>
  </head>

  <body onload="setTitle();" id="root">
    <div class="content">
      <code class="source"><a id="t_nstar:source"></a>pro t_nstar,image,fitsfile,psfname,groupsel,SILENT=silent,PRINT=print, $
                NEWTABLE = newtable, VARSKY = varsky, DEBUG = debug
<span class="comments">;+</span>
<span class="comments">; NAME:</span>
<span class="comments">;       T_NSTAR</span>
<span class="comments">; PURPOSE:</span>
<span class="comments">;       Driver procedure (for NSTAR) for simultaneous PSF fitting.  </span>
<span class="comments">; EXPLANATION:</span>
<span class="comments">;       Input and output are to disk FITS ASCII tables.</span>
<span class="comments">;</span>
<span class="comments">; CALLING SEQUENCE:</span>
<span class="comments">;       T_NSTAR, image, fitsfile, [psfname, groupsel, /SILENT, /PRINT</span>
<span class="comments">;                               NEWTABLE = , /VARSKY ]</span>
<span class="comments">; INPUTS:</span>
<span class="comments">;       IMAGE - 2-d image array</span>
<span class="comments">;       FITSFILE  - scalar string giving name of disk FITS ASCII table.  Must </span>
<span class="comments">;               contain the keywords 'X','Y' (from T_FIND) 'AP1_MAG','SKY'</span>
<span class="comments">;               (from T_APER) and 'GROUP_ID' (from T_GROUP).   This table</span>
<span class="comments">;               will be updated with the results of T_NSTAR, unless the </span>
<span class="comments">;               keyword NEWTABLE is supplied.   </span>
<span class="comments">;</span>
<span class="comments">; OPTIONAL INPUTS:</span>
<span class="comments">;       PSFNAME - Name of the FITS file created by T_GETPSF containing</span>
<span class="comments">;               PSF residuals, scalar string</span>
<span class="comments">;       GROUPSEL - Scalar or vector listing the groups to process.  For</span>
<span class="comments">;               example, to process stars in groups 2 and 5 set</span>
<span class="comments">;               GROUPSEL = [2,5].  If omitted, or set equal to -1,</span>
<span class="comments">;               then NSTAR will process all groups.</span>
<span class="comments">;</span>
<span class="comments">; OPTIONAL KEYWORD INPUTS:</span>
<span class="comments">;       VARSKY - If this keyword is set and non-zero, then the mean sky level</span>
<span class="comments">;               in each group of stars, will be fit along with the brightness</span>
<span class="comments">;               and positions.</span>
<span class="comments">;       /SILENT - if set and non-zero, then NSTAR will not display its results</span>
<span class="comments">;               at the terminal</span>
<span class="comments">;       /PRINT - if set and non-zero then NSTAR will also write its results to</span>
<span class="comments">;               a file NSTAR.PRT.   One can specify the output file name by</span>
<span class="comments">;               setting PRINT = 'filename'.</span>
<span class="comments">;       NEWTABLE  - Name of output disk FITS ASCII table to contain the results</span>
<span class="comments">;               of NSTAR.   If not supplied, then the input FITSFILE will be </span>
<span class="comments">;               updated.  </span>
<span class="comments">;       DEBUG - if this keyword is set and non-zero, then the result of each</span>
<span class="comments">;               fitting iteration will be displayed.</span>
<span class="comments">;</span>
<span class="comments">; PROCEDURES CALLED:</span>
<span class="comments">;       FTADDCAL, FTINFO, FTGET(), FTPUT, NSTAR, SXADDHIST, </span>
<span class="comments">;       SXADDPAR, SXPAR(), READFITS(), WRITEFITS</span>
<span class="comments">; REVISION HISTORY:</span>
<span class="comments">;       Written        W. Landsman         STX Co.    May, 1988</span>
<span class="comments">;       Check for CCDGAIN, ATODGAIN keywords to get PHPADU  W. Landsman May 1997</span>
<span class="comments">;       Fixed typo preventing compilation, groupsel parameter W.L. July 1997</span>
<span class="comments">;       Converted to IDL V5.0   W. Landsman   September 1997</span>
<span class="comments">;       Update for new FTINFO call    W. Landsman   May 2000</span>
<span class="comments">;-</span>
 On_error,2

 if N_params() LT 2 then begin
        print, 'Syntax - T_NSTAR, image, fitsfile, [ psfname, groupsel, '
        print,'                          /VARSKY, NEWTABLE = ,/SILENT, PRINT=]'
        return
 endif

 if not keyword_set(NEWTABLE) then newtable = fitsfile

 dummy = readfits(fitsfile, hprimary, /SILENT)
 tab = readfits(fitsfile, h, /ext)

 ftinfo, h, ft_str
 ttype = strtrim(ft_str.ttype,2)

 idg = where(ttype EQ 'GROUP_ID', Nid)
 if Nid EQ 0 then begin
        message,'T_NSTAR: ERROR - Field GROUP_ID not found in header',/CON
        message,'Procedure T_GROUP must be run before T_NSTAR',/CON
        return
 endif else group = ftget(ft_str,tab,idg[0] + 1)

 if N_params() EQ 4 then begin
        nsel = N_elements(groupsel)
           if groupsel[0] LT 0 then select = indgen(N_elements(group)) $ 
        else begin
           select = where(group EQ groupsel[0])
           if nsel GT 1 then $ 
              for i=1,nsel-1 do select = [select,where(group eq groupsel[i])]
        endelse
 endif else select = indgen(N_elements(group)) 
 group = group[select]

 id = ftget( ft_str, tab, 'STAR_ID', select )
 x = ftget( ft_str, tab, 'X', select )-1.
 y = ftget( ft_str, tab, 'Y', select )-1.
 mags = ftget( ft_str, tab, 'AP1_MAG', select )          
 sky = ftget( ft_str, tab, 'SKY', select )

<span class="comments">;Try to get read-out noise from header</span>
 ronois = sxpar(hprimary,'RONOIS', Count = Nronois) 
 if Nronois EQ 0 then begin
    read,'Enter the read-out noise in ADU per pixel: ',ronois
    print,'Storing readout noise  of ',ronois,' in header'
    sxaddpar,hprimary,'RONOIS',ronois,' Read out noise (ADU/pixel)', $
                before='HISTORY'
 endif

 phpadu = sxpar( hprimary, 'PHPADU', COUNT = n )  <span class="comments">;Try to get photons per ADU</span>
 if n EQ 0 then begin
       phpadu = sxpar( hprimary, 'GAIN', Count  = n)
       if n EQ 0 then phpadu = sxpar( hprimary, 'CCDGAIN', Count = n)
       if n EQ 0 then phpadu = sxpar( hprimary, 'ATODGAIN', Count = n)
       if n EQ 0 then begin
            read,'Enter photons per ADU (CCD Gain):  ',phpadu
      sxaddpar,hprimary,'PHPADU',phpadu,' Photons Per ADU',before = 'HISTORY'
 endif
 endif

 message,'Using photon/ADU (CCD Gain) value of ' + strtrim(phpadu,2),/INF

 nstar, image, id, x, y, mags, sky, group, phpadu, ronois, psfname, errmag,$
         iter, chisq,peak,PRINT = print, SILENT = silent, VARSKY = varsky, $
         DEBUG = debug

 id = id-1

 sxaddpar,h,'EXTNAME','IDL DAOPHOT: NSTAR','DAOPHOT stage'

 g = where(ttype EQ 'X_PSF', Ng)
 if Ng EQ 0 then ftaddcol,h,tab,'X_PSF',8,'F7.2','PIX'
 ftput,h,tab,'X_PSF',id,x+1.

 g = where(ttype EQ 'Y_PSF', Ng)
 if Ng EQ 0 then ftaddcol,h,tab,'Y_PSF',8,'F7.2','PIX'
 ftput,h,tab,'Y_PSF',id,y+1.

 g = where(ttype EQ 'PSF_MAG', Ng)
 if Ng EQ 0 then ftaddcol,h,tab,'PSF_MAG',8,'F7.3','MAG'
 ftput,h,tab,'PSF_MAG',id,mags

 g = where(ttype EQ 'ERR_PSF', Ng)
 if Ng EQ 0 then ftaddcol,h,tab,'ERR_PSF',8,'F5.3','MAG'
 ftput,h,tab,'ERR_PSF',id,errmag

 g = where(ttype EQ 'ITER', Ng)
 if Ng EQ 0 then ftaddcol,h,tab,'ITER',4,'I2'
 ftput,h,tab,'ITER',id,iter

 g = where(ttype EQ 'CHI', Ng)
 if Ng EQ 0 then ftaddcol,h,tab,'CHI',8,'F5.2'
 ftput,h,tab,'CHI',id,chisq

 g = where(ttype EQ 'PEAK', Ng)
 if Ng EQ 0 then ftaddcol,h,tab,'PEAK',8,'F7.3'
 ftput,h,tab,'PEAK',id,peak

 sxaddhist,'T_NSTAR: ' + systime(), h

 writefits, newtable, 0, hprimary
 writefits, newtable, tab,h,/append

 return
 end
</code>
    </div>
  </body>
</html>