<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN"
 "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">

<!-- Generated by IDLdoc 3.5.1 on Mon Sep 30 16:58:02 2013 -->

<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en">
  <head>
    <meta http-equiv="Content-Type" content="text/html;charset=utf-8"/>
    <title>starast.pro (Documentation for ./)</title>

    
    <link rel="stylesheet" type="text/css" media="all"
          href="../idldoc-resources/main.css" />
    <link rel="stylesheet" type="text/css" media="print"
          href="../idldoc-resources/main-print.css" />
    

    <script type="text/javascript">
      function setTitle() {
        parent.document.title="starast.pro (Documentation for ./)";
      }
    </script>
  </head>

  <body onload="setTitle();" id="root">
    <div class="content">
      <code class="source"><a id="starast:source"></a>pro starast,ra,dec,x,y,cd, righthanded=right,hdr=hdr, projection=projection    
<span class="comments">;+</span>
<span class="comments">; NAME:</span>
<span class="comments">;       STARAST </span>
<span class="comments">; PURPOSE:</span>
<span class="comments">;       Compute astrometric solution using positions of 2 or 3 reference stars</span>
<span class="comments">; EXPLANATION:</span>
<span class="comments">;       Computes an exact astrometric solution using the positions and </span>
<span class="comments">;       coordinates from 2 or 3 reference stars and assuming a tangent </span>
<span class="comments">;       (gnomonic) projection.   If 2 stars are used, then</span>
<span class="comments">;       the X and Y plate scales are assumed to be identical, and the</span>
<span class="comments">;       axis are assumed to be orthogonal.   Use of three stars will</span>
<span class="comments">;       allow a unique determination of each element of the CD matrix.</span>
<span class="comments">;</span>
<span class="comments">; CALLING SEQUENCE:</span>
<span class="comments">;       starast, ra, dec, x, y, cd, [/Righthanded, HDR = h, PROJECTION=]</span>
<span class="comments">;</span>
<span class="comments">; INPUTS:</span>
<span class="comments">;       RA - 2 or 3 element vector containing the Right Ascension in DEGREES</span>
<span class="comments">;       DEC- 2 or 3 element vector containing the Declination in DEGREES</span>
<span class="comments">;       X -  2 or 3 element vector giving the X position of reference stars</span>
<span class="comments">;       Y -  2 or 3 element vector giving the Y position of reference stars</span>
<span class="comments">; OUTPUTS:</span>
<span class="comments">;       CD - CD (Coordinate Description) matrix (DEGREES/PIXEL) determined </span>
<span class="comments">;               from stellar positions and coordinates.</span>
<span class="comments">; OPTIONAL INPUT KEYWORD:</span>
<span class="comments">;       /RightHanded - If only 2 stars are supplied, then there is an ambiguity</span>
<span class="comments">;               in the orientation of the coordinate system.   By default,</span>
<span class="comments">;               STARAST assumes the astronomical standard left-handed system</span>
<span class="comments">;               (R.A. increase to the left).   If /Right is set then a </span>
<span class="comments">;               righthanded coordinate is assumed.  This keyword has no effect</span>
<span class="comments">;               if 3 star positions are supplied.</span>
<span class="comments">;        PROJECTION - Either a 3 letter scalar string giving the projection</span>
<span class="comments">;               type (e.g. 'TAN' or 'SIN') or an integer 1 - 25 specifying the</span>
<span class="comments">;               projection as given in the WCSSPH2XY procedure.   If not </span>
<span class="comments">;               specified then a tangent projection is computed.</span>
<span class="comments">; OPTIONAL INPUT-OUTPUT KEYWORD:</span>
<span class="comments">;        HDR - If a FITS header string array is supplied, then an astrometry </span>
<span class="comments">;              solution is added to the header using the CD matrix and star 0</span>
<span class="comments">;              as the reference pixel (see example).   Equinox 2000 is assumed.</span>
<span class="comments">; EXAMPLE:</span>
<span class="comments">;        To use STARAST to add astrometry to a FITS header H;</span>
<span class="comments">;</span>
<span class="comments">;        IDL> starast,ra,dec,x,y,cd       ;Determine CD matrix</span>
<span class="comments">;        IDL> crval = [ra[0],dec[0]]      ;Use Star 0 as reference star</span>
<span class="comments">;        IDL> crpix = [x[0],y[0]] +1      ;FITS is offset 1 pixel from IDL</span>
<span class="comments">;        IDL> putast,H,cd,crpix,crval     ;Add parameters to header</span>
<span class="comments">;</span>
<span class="comments">;        This is equivalent to the following command:</span>
<span class="comments">;        IDL> STARAST,ra,dec,x,y,hdr=h      </span>
<span class="comments">;  </span>
<span class="comments">; METHOD:</span>
<span class="comments">;       The CD parameters are determined by solving the linear set of equations</span>
<span class="comments">;       relating position to local coordinates (l,m)</span>
<span class="comments">;</span>
<span class="comments">;       For highest accuracy the first star position should be the one closest</span>
<span class="comments">;       to the reference pixel.</span>
<span class="comments">; REVISION HISTORY:</span>
<span class="comments">;       Written, W. Landsman             January 1988</span>
<span class="comments">;       Converted to IDL V5.0   W. Landsman   September 1997</span>
<span class="comments">;       Added /RightHanded and HDR keywords   W. Landsman   September 2000</span>
<span class="comments">;       Write CTYPE values into header   W. Landsman/A. Surkov  December 2002</span>
<span class="comments">;       CD matrix was mistakenly transpose in 3 star solution</span>
<span class="comments">;       Added projection keyword    W. Landsman   September 2003</span>
<span class="comments">;       Test for singular matrix W. Landsman  August 2011 </span>
<span class="comments">;-</span>
 On_ERROR,2
 compile_opt idl2

 if N_params() LT 4 then begin
        print,'Syntax - STARAST, ra, dec, x, y, cd, [/Right, HDR =h,Projection=]'
        return                         
 endif

 cdr = !DPI/180.0D 
 map_types=['DEF','AZP','TAN','SIN','STG','ARC','ZPN','ZEA','AIR','CYP',$
            'CAR','MER','CEA','COP','COD','COE','COO','BON','PCO','SFL',$
            'PAR','AIT','MOL','CSC','QSC','TSC']

 iterate = (N_elements(crpix) EQ 2) && (N_elements(crval) EQ 0)
 if N_elements(projection) EQ 0 then projection = 2    <span class="comments">;Default is tangent proj.</span>
 if size(projection,/TNAME) EQ 'STRING' then begin
      map_type  =where(map_types EQ strupcase(strtrim(projection,2)), Ng)
      if Ng EQ 0 then message, $
         'ERROR - supplied projection of ' + projection[0] + ' not recognized'
      map_type = map_type[0]
 endif else map_type = projection

 nstar = min( [N_elements(ra), N_elements(dec), N_elements(x), N_elements(y)])
 if (nstar NE 2) && (nstar NE 3) then $
        message,'ERROR -  Either 2 or 3 star positions required'
 crval1  = [ ra[0], dec[0] ]
 crpix1  = [ x[0], y[0] ]

<span class="comments">; Convert RA, Dec to Eta, Xi</span>

 wcssph2xy, crval = crval1, ra[1:*], dec[1:*], eta, xi, map_type, $
         latpole = 0.0
 delx1 = x[1] - crpix1[0] 
 dely1 = y[1] - crpix1[1]     

if nstar EQ 3 then begin

        delx2 = x[2] - crpix1[0] & dely2 = y[2] - crpix1[1]
        b = double([eta[0],xi[0],eta[1],xi[1]])
        a = double( [ [delx1, 0, delx2,    0    ], $
                      [dely1, 0,  dely2,    0  ], $
                      [0. , delx1, 0,    delx2    ], $
                      [0    , dely1   , 0. ,dely2] ] )
endif else begin

        b = double( [eta[0],xi[0]] )
        if keyword_set(right) then  $
              a = double( [ [delx1,dely1], [-dely1,delx1] ] ) else $
              a = double( [ [delx1,-dely1], [dely1,delx1] ] )

endelse

 cd = invert(a,status)#b        <span class="comments">;Solve linear equations</span>
 if status EQ 1 then $
    message,'ERROR - Singular matrix (collinear points)' 
 if nstar EQ 2 then begin
           if keyword_set(right) then $ 
               cd = [ [cd[0],cd[1]],[-cd[1],cd[0]] ] else $
               cd = [ [cd[0],cd[1]],[cd[1],-cd[0]] ] 
 endif else $ 
       cd = transpose(reform(cd,2,2))


<span class="comments">;Add parameters to header</span>
 if N_elements(hdr) GT 0 then begin
        proj = map_types[map_type]
        make_astr, astr,CD = cd, crval = crval1, crpix = crpix1+1, $
                   ctype = ['RA---','DEC--'] + proj
        putast, hdr, astr, equi=2000.0,cd_type=2
       
 endif
     
 return
 end
</code>
    </div>
  </body>
</html>