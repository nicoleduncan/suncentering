<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN"
 "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">

<!-- Generated by IDLdoc 3.5.1 on Mon Sep 30 16:57:31 2013 -->

<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en">
  <head>
    <meta http-equiv="Content-Type" content="text/html;charset=utf-8"/>
    <title>fitsdir.pro (Documentation for ./)</title>

    
    <link rel="stylesheet" type="text/css" media="all"
          href="../idldoc-resources/main.css" />
    <link rel="stylesheet" type="text/css" media="print"
          href="../idldoc-resources/main-print.css" />
    

    <script type="text/javascript">
      function setTitle() {
        parent.document.title="fitsdir.pro (Documentation for ./)";
      }
    </script>
  </head>

  <body onload="setTitle();" id="root">
    <div class="content">
      <code class="source"><a id="fitsdir:source"></a>pro fitsdir ,directory, TEXTOUT = textout, Keywords = keywords, $ 
     nosize = nosize, alt1_keywords=alt1_keywords, alt2_keywords=alt2_keywords,$
     alt3_keywords = alt3_keywords, NoTelescope = NoTelescope,exten=exten
<span class="comments">;+</span>
<span class="comments">; NAME:</span>
<span class="comments">;     FITSDIR </span>
<span class="comments">; PURPOSE:</span>
<span class="comments">;     Display selected FITS keywords from the headers of FITS files.   </span>
<span class="comments">; EXPLANATION:</span>
<span class="comments">;</span>
<span class="comments">;     The values of either user-specified or default FITS keywords are </span>
<span class="comments">;     displayed in either the primary header and/or the first extension header.</span>
<span class="comments">;     Unless the /NOSIZE keyword is set, the data size is also displayed.</span>
<span class="comments">;     The default keywords are as follows (with keywords in 2nd row used if</span>
<span class="comments">;     those in the first row not found, and the 3rd row if neither the keywords</span>
<span class="comments">;     in the first or second rows found:)</span>
<span class="comments">;</span>
<span class="comments">;     DATE-OBS     TELESCOP   OBJECT    EXPTIME       </span>
<span class="comments">;     TDATEOBS     TELNAME    TARGNAME  INTEG        ;First Alternative</span>
<span class="comments">;     DATE         OBSERVAT             EXPOSURE     ;Second Alternative</span>
<span class="comments">;                  INSTRUME             EXPTIM       ;Third Alternative</span>
<span class="comments">;</span>
<span class="comments">;      FITSDIR will also recognize gzip compressed files (must have a .gz </span>
<span class="comments">;      or FTZ extension).</span>
<span class="comments">; CALLING SEQUENCE:</span>
<span class="comments">;     FITSDIR , [ directory, TEXTOUT =, EXTEN=, KEYWORDS=, /NOSIZE, /NoTELESCOPE</span>
<span class="comments">;                            ALT1_KEYWORDS= ,ALT2_KEYWORDS = ,ALT3_KEYWORDS =  </span>
<span class="comments">;</span>
<span class="comments">; OPTIONAL INPUT PARAMETERS:</span>
<span class="comments">;     DIRECTORY - Scalar string giving file name, disk or directory to be </span>
<span class="comments">;             searched.   Wildcard file names are allowed.    Examples of </span>
<span class="comments">;             valid names include 'iraf/*.fits' (Unix), d:\myfiles\f*.fits',</span>
<span class="comments">;             (Windows) or 'Macintosh HD:Files:*c0f.fits' (Macintosh).  </span>
<span class="comments">;            </span>
<span class="comments">; OPTIONAL KEYWORD INPUT PARAMETER</span>
<span class="comments">;      KEYWORDS - FITS keywords to display, as either a vector of strings or as</span>
<span class="comments">;                 a comma delimited scalar string, e.g.'testname,dewar,filter'</span>
<span class="comments">;                 If not supplied, then the default keywords are 'DATE-OBS',</span>
<span class="comments">;                 'TELESCOP','OBJECT','EXPTIME'</span>
<span class="comments">;      ALT1_KEYWORDS - A list (either a vector of strings or a comma delimited</span>
<span class="comments">;                 strings of alternative keywords to use if the default </span>
<span class="comments">;                 KEYWORDS cannot be found.   By default, 'TDATEOBS', is the </span>
<span class="comments">;                 alternative to DATE-OBS, 'TELNAME' for 'TELESCOP','TARGNAME'</span>
<span class="comments">;                 for 'OBJECT', and 'INTEG' for EXPTIME</span>
<span class="comments">;      ALT2_KEYWORDS - A list (either a vector of strings or a comma delimited</span>
<span class="comments">;                 strings of alternative keywords to use if neither KEYWORDS </span>
<span class="comments">;                 nor ALT1_KEYWORDS can be found.    </span>
<span class="comments">;      ALT3_KEYWORDS - A list (either a vector of strings or a comma delimited</span>
<span class="comments">;                 strings of alternative keywords to use if neither KEYWORDS </span>
<span class="comments">;                 nor ALT1_KEYWORDS nor ALT2_KEYWORDS can be found.    </span>
<span class="comments">;      /NOSIZE - if set then information about the image size is not displayed  </span>
<span class="comments">;      TEXTOUT - Controls output device as described in TEXTOPEN procedure</span>
<span class="comments">;               textout=1       TERMINAL using /more option</span>
<span class="comments">;               textout=2       TERMINAL without /more option</span>
<span class="comments">;               textout=3       &lt;program>.prt</span>
<span class="comments">;               textout=4       laser.tmp</span>
<span class="comments">;               textout=5       user must open file</span>
<span class="comments">;               textout=7       Append to existing &lt;program>.prt file</span>
<span class="comments">;               textout = filename (default extension of .prt)</span>
<span class="comments">;       EXTEN - Specifies an extension number (/EXTEN works for first extension)</span>
<span class="comments">;               which is  checked for the  desired keywords.    </span>
<span class="comments">;       /NOTELESCOPE - If set, then if the default keywords are used, then the</span>
<span class="comments">;                TELESCOPE (or TELNAME, OBSERVAT, INSTRUME) keywords are omitted</span>
<span class="comments">;                to give more room for display other keywords.   The /NOTELESCOP</span>
<span class="comments">;                 keyword has no effect if the default keywords are not used.</span>
<span class="comments">; OUTPUT PARAMETERS:</span>
<span class="comments">;       None.</span>
<span class="comments">;</span>
<span class="comments">; EXAMPLES:  </span>
<span class="comments">;  (1) Print info on all'*.fits' files in the current  directory using default</span>
<span class="comments">;          keywords.   Include information from the extension header     </span>
<span class="comments">;       IDL> fitsdir,/exten</span>
<span class="comments">;</span>
<span class="comments">;  (2) Write a driver program to display selected keywords in HST/ACS drizzled</span>
<span class="comments">;       (*drz) images</span>
<span class="comments">;         pro acsdir</span>
<span class="comments">;        keywords = 'date-obs,targname,detector,filter1,filter2,exptime'</span>
<span class="comments">;        fitsdir,'*drz.fits',key=keywords,/exten</span>
<span class="comments">;        return & end</span>
<span class="comments">;</span>
<span class="comments">;   (3)  Write info on all *.fits files in the Unix directory /usr2/smith, to a </span>
<span class="comments">;       file 'smith.txt' using the default keywords, but don't display the value</span>
<span class="comments">;        of the TELESCOPE keyword</span>
<span class="comments">;</span>
<span class="comments">;       IDL> fitsdir ,'/usr2/smith/*.fits',t='smith.txt', /NoTel </span>
<span class="comments">;</span>
<span class="comments">; PROCEDURE:</span>
<span class="comments">;       FILE_SEARCH()  is used to find the specified FITS files.   The </span>
<span class="comments">;       header of each file is read, and the selected keywords are extracted.</span>
<span class="comments">;       The formatting is adjusted so that no value is truncated on display.        </span>
<span class="comments">;</span>
<span class="comments">; SYSTEM VARIABLES:</span>
<span class="comments">;       TEXTOPEN (called by FITSDIR) will automatically define the following </span>
<span class="comments">;       non-standard system variables if they are not previously defined:</span>
<span class="comments">;</span>
<span class="comments">;       DEFSYSV,'!TEXTOUT',1</span>
<span class="comments">;       DEFSYSV,'!TEXTUNIT',0</span>
<span class="comments">;</span>
<span class="comments">; PROCEDURES USED:</span>
<span class="comments">;       FDECOMP, FXMOVE, MRD_HREAD, REMCHAR</span>
<span class="comments">;       TEXTOPEN, TEXTCLOSE</span>
<span class="comments">; MODIFICATION HISTORY:</span>
<span class="comments">;       Written, W. Landsman,  HSTX    February, 1993</span>
<span class="comments">;       Search alternate keyword names    W.Landsman    October 1998</span>
<span class="comments">;       Avoid integer truncation for NAXISi >32767  W. Landsman  July 2000</span>
<span class="comments">;       Don't leave open unit    W. Landsman  July 2000 </span>
<span class="comments">;       Added EXTEN keyword, work with compressed files, additional alternate</span>
<span class="comments">;       keywords W. Landsman     December 2000</span>
<span class="comments">;       Don't assume floating pt. exposure time W. Landsman   September 2001</span>
<span class="comments">;       Major rewrite, KEYWORD & ALT*_KEYWORDS keywords, no truncation, </span>
<span class="comments">;             /NOSIZE keyword     W. Landsman,  SSAI   August 2002</span>
<span class="comments">;       Assume V5.3 or later W. Landsman November 2002</span>
<span class="comments">;       Fix case where no keywords supplied  W. Landsman January 2003</span>
<span class="comments">;       NAXIS* values must be integers W. Landsman SSAI  June 2003</span>
<span class="comments">;       Trim spaces off of input KEYWORD values W. Landsman March 2004</span>
<span class="comments">;       Treat .FTZ extension as gzip compressed  W. Landsman September 2004</span>
<span class="comments">;       Assume since V5.5, file_search() available W. Landsman Aug 2006</span>
<span class="comments">;       Don't assume all images compressed or uncompressed W. L. Apr 2010</span>
<span class="comments">;       Use V6.0 notation W.L. Feb 2011</span>
<span class="comments">;-</span>
 On_error,2

 compile_opt idl2     
 
 if N_elements(directory) EQ 0 then directory = '*.fits'
 if N_elements(exten) EQ 0 then exten = 0 

 FDECOMP, directory, disk, dir, filename, ext
 if filename EQ '' then begin 
      directory = disk + dir + '*.fits'
      filename = '*'
      ext = 'fits'
 endif else if !VERSION.OS_FAMILY EQ 'unix' then begin
        if (strpos(filename,'*') LT 0) and (ext EQ '') then begin  
        directory = disk + dir + filename + '/*.fits'
        filename = '*'
        ext = 'fits'
        endif
 endif

 if N_elements(keywords) EQ 0 then begin
     keywords = ['date-obs','telescop','object','exptime'] 
     if N_elements(alt1_keywords) EQ 0 then $
          alt1_keywords = ['tdateobs','telname','targname','integ']
     if N_elements(alt2_keywords) EQ 0 then $
          alt2_keywords = ['date','observat','','exposure']
     if N_elements(alt3_keywords) EQ 0 then $
          alt3_keywords = ['','instrume','','exptim' ]
     if keyword_set(NoTelescope) then begin
        ii = [0,2,3]
        keywords = keywords[ii] & alt1_keywords = alt1_keywords[ii]
        alt2_keywords = alt2_keywords[ii] & alt3_keywords = alt3_keywords[ii]
      endif
 endif
 if N_elements(keywords) EQ 1 then $
   keys = strtrim(strupcase(strsplit(keywords,',',/EXTRACT)),2) else $
   keys = strupcase(keywords)
   Nkey = N_elements(keys)

  case N_elements(alt1_keywords) of
  0: alt1_set = bytarr(Nkey)
  1: alt1_keys = strtrim(strupcase(strsplit(alt1_keywords[0],',',/EXTRACT)),2) 
  else: alt1_keys = strupcase(alt1_keywords) 
  endcase
  if N_elements(alt1_set) EQ 0 then alt1_set = strlen(strtrim(alt1_keys,2)) GT 0

  case N_elements(alt2_keywords) of
  0: alt2_set = bytarr(Nkey)
  1: alt2_keys = strtrim(strupcase(strsplit(alt2_keywords,',',/EXTRACT)),2) 
  else: alt2_keys = strupcase(alt2_keywords) 
  endcase
 if N_elements(alt2_set) EQ 0 then alt2_set = strlen(strtrim(alt2_keys,2)) GT 0

  case N_elements(alt3_keywords) of
  0: alt3_set = bytarr(Nkey)
  1: alt3_keys = strtrim(strupcase(strsplit(alt3_keywords,',',/EXTRACT)),2) 
   else: alt3_keys = strupcase(alt3_keywords) 
  endcase
  if N_elements(alt3_set) EQ 0 then alt3_set = strlen(strtrim(alt3_keys,2)) GT 0
  
   keylen = strlen(keys)
 
  direct = spec_dir(directory)
  files = file_search(directory,COUNT = n,/full) 

 if n EQ 0 then begin                                      <span class="comments">;Any files found?</span>
       message,'No files found on '+ direct, /CON
       return
 endif 

  good = where( strlen(files) GT 0, Ngood)
  if Ngood EQ 0 then message,'No FITS files found on '+ directory $
                 else files = files[good]

<span class="comments">; Set output device according to keyword TEXTOUT or system variable !TEXTOUT</span>

  defsysv,'!TEXTOUT',exists=ex                  <span class="comments">; Check if !TEXTOUT exists.</span>
  if ex eq 0 then defsysv,'!TEXTOUT',1          <span class="comments">; If not define it.</span>
  defsysv,'!TEXTUNIT',exists=ex                  <span class="comments">; Check if !TEXTOUT exists.</span>
  if ex eq 0 then defsysv,'!TEXTUNIT',1          <span class="comments">; If not define it.</span>
  if ~keyword_set( TEXTOUT ) then textout= !TEXTOUT

 dir = 'dummy'
 num = 0

 get_lun,unit

 fdecomp, files, disk, dir2, fname, qual     <span class="comments">;Decompose into disk+filename</span>
 fname = strtrim(fname,2)
 keyvalue = strarr(n,nkey)
 bignaxis = strarr(n)
 namelen = max(strlen(fname))

 for i = 0,n-1 do begin                           <span class="comments">;Loop over each FITS file</span>
     compress = (qual[i] EQ 'gz') || (strupcase(qual[i]) EQ 'FTZ') 
     openr, unit, files[i], error = error, compress = compress 
    if error LT 0 then goto, BADHD
    mrd_hread, unit, h, status, /silent
   if status LT 0 then goto, BADHD

   if exten GT 0 then begin 
         close,unit
            openr, unit, files[i], error = error, compress = compress   
         stat = fxmove(unit, exten, /silent)
         mrd_hread, unit, h1, status, /silent
         if status EQ 0 then h = [h1,h] 
    endif 

   keyword = strtrim( strmid(h,0,8),2 )       <span class="comments">;First 8 chars is FITS keyword</span>
   lvalue = strtrim(strmid(h,10,20),2 ) 
   value = strtrim( strmid(h,10,68),2 )        <span class="comments">;Chars 10-30 is FITS value</span>
 
 if ~keyword_set(nosize) then begin
 l= where(keyword EQ 'NAXIS',Nfound)            <span class="comments">;Must have NAXIS keyword</span>
    if Nfound GT 0 then naxis  = long( lvalue[ l[0] ] ) else goto, BADHD

 if naxis EQ 0 then naxisi = '0' else begin

 l = where( keyword EQ 'NAXIS1', Nfound)         <span class="comments">;Must have NAXIS1 keyword</span>
    if Nfound gt 0 then naxis1  = long( lvalue[l[0] ] ) else goto, BADHD 
    naxisi = strtrim( naxis1,2 )
 endelse

 if NAXIS GE 2 then begin
 l = where(keyword EQ 'NAXIS2', Nfound)          <span class="comments">;Must have NAXIS2 keyword</span>
    if Nfound gt 0 then naxis2  = long(lvalue[l[0]]) else goto, BADHD
    naxisi = naxisi + ' ' + strtrim( naxis2, 2 )
 endif

 if NAXIS GE 3 then begin
 l = where( keyword EQ 'NAXIS3', Nfound )          <span class="comments">;Must have NAXIS3 keyword</span>
    if Nfound GT 0 then naxis3  = long( lvalue[l[0]] ) else goto, BADHD
    naxisi = naxisi + ' ' + strtrim( naxis3, 2 )
 endif
 bignaxis[i] = strtrim(naxisi)
 endif

 for k=0,nkey-1 do begin
     l = where(keyword EQ keys[k], Nfound)
     if Nfound EQ 0 then  if alt1_set[k] then $
        l = where(keyword EQ alt1_keys[k], Nfound)
     if Nfound EQ 0 then  if alt2_set[k] then $
        l = where(keyword EQ alt2_keys[k], Nfound)
     if Nfound EQ 0 then  if alt3_set[k] then $
        l = where(keyword EQ alt3_keys[k], Nfound)
     if nfound GT 0 then begin
            kvalue = value[l[0]]
            if strpos(kvalue,"'") GE 0 then begin
               temp = gettok(kvalue,"'")
               keyvalue[i,k] = strtrim(gettok(kvalue,"'"),2)
            endif else keyvalue[i,k] = strtrim(gettok(kvalue,'/'),2) 
     endif 
        
    endfor

 BADHD:  

 close,unit
 endfor
 DONE: 
 free_lun, unit
 vallen = lonarr(nkey)
 for k=0,nkey-1 do vallen[k]  = max(strlen(keyvalue[*,k]))


 textopen, 'fitsdir', TEXTOUT=textout,/STDOUT 
 printf,!TEXTUNIT,'FITS File Directory ' + systime()
 printf,!TEXTUNIT, direct
  printf,!TEXTUNIT, ' '

 pheader = ' NAME '
 if namelen GT 5 then pheader = pheader + string(replicate(32b,namelen-5))
 if ~keyword_set(nosize) then begin 
    pheader = pheader + 'SIZE '
    naxislen = max(strlen(bignaxis))+1
    if naxislen GT 5 then pheader = pheader + string(replicate(32b,naxislen-5))
 endif
 for k=0,nkey-1 do begin
     pheader = pheader + keys[k] + ' '
     if vallen[k] GT keylen[k] then  $
        pheader = pheader + string(replicate(32b,vallen[k]-keylen[k]))
 endfor
  printf,!TEXTUNIT, pheader
  printf,!TEXTUNIT, ' '
  xx = namelen + 2
 fmt = '(A' 
 if ~keyword_set(nosize) then begin 
   fmt = fmt + ',T' + strtrim(xx,2)
   xx = xx + (naxislen>4) + 1
 endif 
   fmt = fmt + ',A'  
 remchar,keyvalue,"'"

 for k=0,nkey-1 do begin 
  
   fmt = fmt + ',T' + strtrim(xx,2) + ',A'
   xx = xx + (vallen[k]>keylen[k]) +1
 endfor
 fmt = fmt + ')'

 for i=0,n-1 do printf, f= fmt, $
      !TEXTUNIT,fname[i],bignaxis[i], keyvalue[i,*]
    
 textclose,textout=textout 
 return      <span class="comments">;Normal return   </span>
 end
</code>
    </div>
  </body>
</html>