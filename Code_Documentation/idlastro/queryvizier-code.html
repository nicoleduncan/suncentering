<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN"
 "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">

<!-- Generated by IDLdoc 3.5.1 on Mon Sep 30 16:57:58 2013 -->

<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en">
  <head>
    <meta http-equiv="Content-Type" content="text/html;charset=utf-8"/>
    <title>queryvizier.pro (Documentation for ./)</title>

    
    <link rel="stylesheet" type="text/css" media="all"
          href="../idldoc-resources/main.css" />
    <link rel="stylesheet" type="text/css" media="print"
          href="../idldoc-resources/main-print.css" />
    

    <script type="text/javascript">
      function setTitle() {
        parent.document.title="queryvizier.pro (Documentation for ./)";
      }
    </script>
  </head>

  <body onload="setTitle();" id="root">
    <div class="content">
      <code class="source"><a id="Queryvizier:source"></a>function Queryvizier, catalog, target, dis, VERBOSE=verbose, CANADA = canada, $
               CONSTRAINT = constraint, ALLCOLUMNS=allcolumns, SILENT=silent
<span class="comments">;+</span>
<span class="comments">; NAME: </span>
<span class="comments">;   QUERYVIZIER</span>
<span class="comments">;</span>
<span class="comments">; PURPOSE: </span>
<span class="comments">;   Query any catalog in the Vizier database by position</span>
<span class="comments">; </span>
<span class="comments">; EXPLANATION:</span>
<span class="comments">;   Uses the IDL SOCKET command to provide a positional query of any catalog </span>
<span class="comments">;   in the the Vizier (http://vizier.u-strasbg.fr/) database over the Web and</span>
<span class="comments">;   return results in an IDL structure.    </span>
<span class="comments">; </span>
<span class="comments">;    </span>
<span class="comments">; CALLING SEQUENCE: </span>
<span class="comments">;     info = QueryVizier(catalog, targetname_or_coords, [ dis</span>
<span class="comments">;                        /ALLCOLUMNS, /CANADA, CONSTRAINT= ,/VERBOSE ])</span>
<span class="comments">;</span>
<span class="comments">; INPUTS: </span>
<span class="comments">;      CATALOG - Scalar string giving the name of the VIZIER catalog to be</span>
<span class="comments">;            searched.    The complete list of catalog names is available at</span>
<span class="comments">;            http://vizier.u-strasbg.fr/vizier/cats/U.htx . </span>
<span class="comments">;</span>
<span class="comments">;            Popular VIZIER catalogs include  </span>
<span class="comments">;            'II/294' - Sloan SDSS photometric catalog Release 7 (2009)</span>
<span class="comments">;            '2MASS-PSC' - 2MASS point source catalog (2003)</span>
<span class="comments">;            'GSC2.3' - Version 2.3.2 of the HST Guide Star Catalog (2006)</span>
<span class="comments">;            'USNO-B1' - Verson B1 of the US Naval Observatory catalog (2003)</span>
<span class="comments">;            'NVSS'  - NRAO VLA Sky Survey (1998)</span>
<span class="comments">;            'B/DENIS/DENIS' - 2nd Deep Near Infrared Survey of southern Sky</span>
<span class="comments">;            'I/259/TYC2' - Tycho-2 main catalog (2000)</span>
<span class="comments">;            'I/311/HIP2' - Hipparcos main catalog, new reduction (2007)</span>
<span class="comments">;</span>
<span class="comments">;          Note that some names will prompt a search of multiple catalogs</span>
<span class="comments">;          and QUERYVIZIER will only return the result of the first search.</span>
<span class="comments">;          Thus, setting catalog to "HIP2" will search all catalogs </span>
<span class="comments">;          associated with the Hipparcos mission, and return results for the</span>
<span class="comments">;          first catalog found.    To specifically search the Hipparcos or</span>
<span class="comments">;          Tycho main catalogs use the VIZIER catalog names listed above</span>
<span class="comments">;                             </span>
<span class="comments">;      TARGETNAME_OR_COORDS - Either a scalar string giving a target name, </span>
<span class="comments">;          (with J2000 coordinates determined by SIMBAD), or a 2-element</span>
<span class="comments">;          numeric vector giving the J2000 right ascension in *degrees* and </span>
<span class="comments">;          the target declination in degrees.</span>
<span class="comments">;          If the targetname is set to 'NONE' then QUERYVIZIER will perform</span>
<span class="comments">;          an all-sky search using the constraints given in the CONSTRAINT</span>
<span class="comments">;          keyword.   </span>
<span class="comments">; OPTIONAL INPUT:</span>
<span class="comments">;    dis - scalar or 2-element vector.   If one value is supplied then this</span>
<span class="comments">;          is the search radius in arcminutes.     If two values are supplied</span>
<span class="comments">;          then this is the width (i.e., in longitude direction) and height</span>
<span class="comments">;          of the search box.   Default is a radius search with radius of</span>
<span class="comments">;          5 arcminutes</span>
<span class="comments">;</span>
<span class="comments">; OUTPUTS: </span>
<span class="comments">;   info - Anonymous IDL structure containing information on the catalog  </span>
<span class="comments">;          sources within the specified distance of the specified center.  The </span>
<span class="comments">;          structure tag names are identical with the VIZIER  catalog column </span>
<span class="comments">;          names, with the exception of an occasional underscore</span>
<span class="comments">;          addition, if necessary to convert the column name to a valid </span>
<span class="comments">;          structure tag.    The VIZIER Web  page should consulted for the </span>
<span class="comments">;          column names and their meaning for each particular catalog..</span>
<span class="comments">;           </span>
<span class="comments">;          If the tagname is numeric and the catalog field is blank then either</span>
<span class="comments">;          NaN  (if floating) or -1 (if integer) is placed in the tag.</span>
<span class="comments">;</span>
<span class="comments">;          If no sources are found within the specified radius, or an</span>
<span class="comments">;          error occurs in the query then -1 is returned. </span>
<span class="comments">; OPTIONAL KEYWORDS:</span>
<span class="comments">;          /ALLCOLUMNS - if set, then all columns for the catalog are returned</span>
<span class="comments">;                 The default is to return a smaller VIZIER default set. </span>
<span class="comments">;</span>
<span class="comments">;          /CANADA - By default, the query is sent to the main VIZIER site in</span>
<span class="comments">;            Strasbourg, France.   If /CANADA is set then the VIZIER site</span>
<span class="comments">;            at the Canadian Astronomical Data Center (CADC) is used instead.</span>
<span class="comments">;            Note that not all Vizier sites have the option to return</span>
<span class="comments">;            tab-separated values (TSV) which is required by this program.</span>
<span class="comments">;   </span>
<span class="comments">;          CONSTRAINT - string giving additional nonpositional numeric </span>
<span class="comments">;            constraints on the entries to be selected.     For example, when </span>
<span class="comments">;            in the GSC2.3  catalog, to only select sources with Rmag &lt; 16 set </span>
<span class="comments">;            Constraint = 'Rmag&lt;16'.    Multiple constraints can be </span>
<span class="comments">;            separated by commas.    Use '!=' for "not equal", '&lt;=' for smaller</span>
<span class="comments">;            or equal, ">=" for greater than or equal.  See the complete list</span>
<span class="comments">;            of operators at  </span>
<span class="comments">;                 http://vizier.u-strasbg.fr/doc/asu.html#AnnexQual</span>
<span class="comments">;            For this keyword only, **THE COLUMN NAME IS CASE SENSITIVE** and </span>
<span class="comments">;            must be written exactly as displayed on the VIZIER Web page.  </span>
<span class="comments">;            Thus for the GSC2.3 catalog one must use 'Rmag' and not 'rmag' or</span>
<span class="comments">;            'RMAG'.    In addition, *DO NOT INCLUDE ANY BLANK SPACE* unless it </span>
<span class="comments">;            is a necessary part of the query.</span>
<span class="comments">;         </span>
<span class="comments">;           /SILENT - If set, then no message will be displayed if no sources</span>
<span class="comments">;                are found.    Error messages are still displayed.</span>
<span class="comments">;           /VERBOSE - If set then the query sent to the VIZIER site is</span>
<span class="comments">;               displayed, along with the returned title(s) of found catalog(s)</span>
<span class="comments">; EXAMPLES: </span>
<span class="comments">;          (1) Plot a histogram of the J magnitudes of all 2MASS point sources </span>
<span class="comments">;          stars within 10 arcminutes of the center of the globular cluster M13 </span>
<span class="comments">;</span>
<span class="comments">;          IDL>  info = queryvizier('2MASS-PSC','m13',10)</span>
<span class="comments">;          IDL> plothist,info.jmag,xran=[10,20]</span>
<span class="comments">;</span>
<span class="comments">;          (2)  Find the brightest J mag GSC2.3 source within 3' of the </span>
<span class="comments">;               J2000 position ra = 10:12:34, dec = -23:34:35</span>
<span class="comments">;          </span>
<span class="comments">;          IDL> str = queryvizier('GSC2.3',[ten(10,12,34)*15,ten(-23,34,35)],3)</span>
<span class="comments">;          IDL> print,min(str.jmag,/NAN)</span>
<span class="comments">;</span>
<span class="comments">;          (3) Find sources with V &lt; 19 in the Magellanic Clouds Photometric </span>
<span class="comments">;              Survey (Zaritsky+, 2002) within 5 arc minutes of  the position </span>
<span class="comments">;              00:47:34 -73:06:27</span>
<span class="comments">;</span>
<span class="comments">;              Checking the VIZIER Web page we find that this catalog is</span>
<span class="comments">;          IDL>  catname =  'J/AJ/123/855/table1'</span>
<span class="comments">;          IDL>  ra = ten(0,47,34)*15 & dec = ten(-73,6,27)</span>
<span class="comments">;          IDL> str = queryvizier(catname, [ra,dec], 5, constra='Vmag&lt;19')</span>
<span class="comments">;</span>
<span class="comments">;          (4) Perform an all-sky search of the Tycho-2 catalog for stars with</span>
<span class="comments">;              BTmag = 13+/-0.1</span>
<span class="comments">;</span>
<span class="comments">;         IDL> str = queryvizier('I/259/TYC2','NONE',constrain='BTmag=13+/-0.1')</span>
<span class="comments">;</span>
<span class="comments">; PROCEDURES USED:</span>
<span class="comments">;          GETTOK(), REMCHAR, REPSTR(), STRCOMPRESS2(), WEBGET()</span>
<span class="comments">; TO DO:</span>
<span class="comments">;       (1) Allow specification of output sorting</span>
<span class="comments">; MODIFICATION HISTORY: </span>
<span class="comments">;         Written by W. Landsman  SSAI  October 2003</span>
<span class="comments">;         Give structure name returned by VIZIER not that given by user</span>
<span class="comments">;                    W. Landsman   February 2004 </span>
<span class="comments">;         Don't assume same format for all found sources W. L. March 2004</span>
<span class="comments">;         Added CONSTRAINT keyword for non-positional constraints WL July 2004</span>
<span class="comments">;         Remove use of EXECUTE() statement WL June 2005</span>
<span class="comments">;         Make dis optional as advertised WL August 2005</span>
<span class="comments">;         Update for change in Vizier output format WL February 2006</span>
<span class="comments">;         Fix problem in Feb 2006 update when only 1 object found</span>
<span class="comments">;                     WL/D.Apai        March 2006</span>
<span class="comments">;         Accept 'E' format for floating point. M. Perrin April 2006</span>
<span class="comments">;         Added /ALLCOLUMNS option to return even more data.  M. Perrin, May 2006</span>
<span class="comments">;         Return anonymous structure W. Landsman  May 2006</span>
<span class="comments">;         Removed V6.0 notation to restore V5 compatibility W.Landsman July2006</span>
<span class="comments">;         Accept target='NONE' for all-sky search, allow '+/-' constraints</span>
<span class="comments">;                W. Landsman  October 2006</span>
<span class="comments">;         Use HTTP 1.0 protocol in call to webget.pro</span>
<span class="comments">;         Use vector form of IDL_VALIDNAME if V6.4 or later W.L. Dec 2007</span>
<span class="comments">;         Update Strasbourg Web address for target name W.L. 3 March 2008</span>
<span class="comments">;         Also update Web address for coordinate search W.L. 7 March 2008 </span>
<span class="comments">;         Allow for 'D' specification format  R. Gutermuth/W.L.  June 2008</span>
<span class="comments">;         Allow for possible lower-case returned formats  W.L. July 2008</span>
<span class="comments">;         Use STRCOMPRESS2()to remove blanks around operators in constraint</span>
<span class="comments">;              string  W.L.  August 2008</span>
<span class="comments">;         Added /SILENT keyword  W.L.  Jan 2009</span>
<span class="comments">;         Avoid error if output columns but not data returned W.L. Mar 2010</span>
<span class="comments">;         Ignore vector tags (e.g. SED spectra) W.L.   April 2011</span>
<span class="comments">;         Better checking when more than one catalog returned W.L. June 2012</span>
<span class="comments">;-</span>
  On_error,2
  compile_opt idl2
  if N_params() LT 2 then begin
       print,'Syntax - info = QueryVizier(catalog, targetname_or_coord, dis,'
       print,'         [/ALLCOLUMNS, /SILENT, /VERBOSE, /CANADA, CONSTRAINT= ]'
       print,'                       '
       print,'  Coordinates (if supplied) should be J2000 RA (degrees) and Dec'
       print,'  dis -- search radius or box in arcminutes'
       if N_elements(info) GT 0 then return,info else return, -1
  endif

 if keyword_set(canada) then root = "http://vizier.hia.nrc.ca/cgi-bin/" $
                       else  root = "http://webviz.u-strasbg.fr/viz-bin/" 
 silent = keyword_set(silent)
 
  if N_elements(catalog) EQ 0 then $
            message,'ERROR - A catalog name must be supplied as a keyword'
  targname = 0b
 if N_elements(dis) EQ 0 then dis = 5
 if min(dis) LE 0 then $
     message,'ERROR - Search distances must be greater than zero'
 
 nopoint = 0b
 if N_elements(dis) EQ 2 then $
    search = "&-c.bm=" + strtrim(dis[0],2) + '/' + strtrim(dis[1],2) else $
    search = "&-c.rm=" + strtrim(dis,2) 
    if N_elements(target) EQ 2 then begin
      ra = float(target[0])
      dec = float(target[1])
   endif else begin
       nopoint = strupcase( strtrim(target,2) ) EQ 'NONE' 
       object = repstr(target,'+','%2B')
        object = repstr(strcompress(object),' ','+')
       targname = 1b 
  endelse

<span class="comments">; Add any additional constraints to the search. Convert any URL special </span>
<span class="comments">; special characters in the constraint string.</span>

 if N_elements(constraint) EQ 0 then constraint = '' 
 if strlen(constraint) GT 0 then begin
     urlconstrain = strtrim(constraint,2)
     urlconstrain = strcompress2(constraint,['&lt;','>','='])
      urlconstrain = repstr(urlconstrain, ',','&')
     urlconstrain = repstr(urlconstrain, '&lt;','=%3C')
     urlconstrain = repstr(urlconstrain, '>','=%3E')
     urlconstrain = repstr(urlconstrain, '+','%2B')
     urlconstrain = repstr(urlconstrain, '/','%2F')
     urlconstrain = repstr(urlconstrain, '!','=!')
     if nopoint then search = urlconstrain else $
                     search = search + '&' + urlconstrain
 endif
 <span class="comments">;</span>
 if nopoint then $
  QueryURL = root + "asu-tsv/?-source=" + catalog + '&' + $
              search + '&-out.max=unlimited' else $
 if targname then $
  QueryURL = $
   root + "asu-tsv/?-source=" + catalog + $
     "&-c=" + object + search + '&-out.max=unlimited' else $
  queryURL = $
   root + "asu-tsv/?-source=" + catalog + $
       "&-c.ra=" + strtrim(ra,2) + '&-c.dec=' + strtrim(dec,2) + $
       search + '&-out.max=unlimited'

  if keyword_set(allcolumns) then queryURL = queryURL + '&-out.all=1'
 if keyword_set(verbose) then message,queryurl,/inf

  Result = webget(QueryURL,/http10, silent=silent)
<span class="comments">;</span>
  t = strtrim(result.text,2)
  keyword = strtrim(strmid(t,0,7),2)

  linecon = where(keyword EQ '#---Lis', Ncon)
  if Ncon GT 0 then remove,linecon, t, keyword
 
<span class="comments">; Check to see if more than one catalog has been searched</span>
<span class="comments">; Use only the first catalog found</span>

  rcol = where(keyword Eq '#RESOUR', Nfound) 
  if N_elements(rcol) GT 1 then begin 
       t = t[0:rcol[1]-1 ]
       keyword = keyword[0:rcol[1]-1]
  endif     
  lcol = where(keyword EQ "#Column", Nfound)
  if Nfound EQ 0 then begin
       if max(strpos(strlowcase(t),'errors')) GE 0 then begin 
            message,'ERROR - Unsuccessful VIZIER query',/CON 
            print,t
       endif else if ~silent then $
            message,'No sources found within specified radius',/INF
       return,-1
  endif
  

  if keyword_set(verbose) then begin
    titcol = where(keyword EQ '#Title:', Ntit)
        if Ntit GT 0 then message,/inform, $
        strtrim(strmid(t[titcol[0]],8),2)
  endif
<span class="comments">;Check if any Warnings or fatal errors in the VIZIER output</span>
   badflag = strmid(keyword,0,5)
   warn = where(badflag EQ '#++++', Nwarn)
   if Nwarn GT 0 then for i=0,Nwarn-1 do $
        message,'Warning: ' + strtrim(t[warn[i]],2),/info
   
   fatal = where(badflag EQ '#****', Nfatal)
   if Nfatal GT 0 then for i=0,Nfatal-1 do $
        message,'Error: ' + strtrim(t[fatal[i]],2),/info


  trow = t[lcol]
  dum = gettok(trow,' ')
  colname = gettok(trow,' ')
  fmt = gettok(trow,' ')

  remchar,fmt,'('
  remchar,fmt,')' 
  remchar,colname,')'
   if !VERSION.RELEASE GE '6.4' then $
          colname = IDL_VALIDNAME(colname,/convert_all) else $
  for i=0,N_elements(colname)-1 do $
         colname[i] = IDL_VALIDNAME(colname[i],/convert_all)

 
<span class="comments">; Find the vector tags (Format begins with a number) and remove them </span>

 bad = where(stregex(fmt,'^[0-9]') GE 0, Nbad)
 if Nbad GT 0 then remove,bad,fmt,colname 
 
 ntag = N_elements(colname)
 fmt = strupcase(fmt)
 val = fix(strmid(fmt,1,4))
 
 for i=0,Ntag-1 do begin

 case strmid(fmt[i],0,1) of 
 
  'A': cval = ' '
  'I': cval = (val[i] LE 4) ? 0 : 0L         <span class="comments">;16 bit integer if 4 chars or less</span>
  'F': cval = (val[i] LE 7) ? 0. : 0.0d      <span class="comments">;floating point if 7 chars or less</span>
   'E': cval = (val[i] LE 7) ? 0. : 0.0d 
  'D': cval = (val[i] LE 7) ? 0. : 0.0d 
   else: message,'ERROR - unrecognized format ' + fmt[i]
 
  endcase

   if i EQ 0 then   info = create_struct(colname[0], cval) else begin
	   <span class="comments">; If you set the /ALLCOLUMNS flag, in some cases (2MASS) you</span>
	   <span class="comments">; get a duplicate column name. Check for this and avoid it by appending</span>
	   <span class="comments">; an extra bit to the duplicate name</span>
	   if where(tag_names(info) eq strupcase(colname[i])) ge 0 then $
	      colname[i] = colname[i] + '_2'
   info =  create_struct(temporary(info), colname[i],cval)
   endelse
 endfor
 
  i0 = max(lcol) + 4  
  if i0 GT (N_elements(t)-1) then begin 
       message,'No sources found within specified radius',/INF
       return,-1
  endif
  
  iend = where( t[i0:*] EQ '', Nend)
  if Nend EQ  0  then iend = N_elements(t) else iend = iend[0] + i0
  nstar = iend - i0 
  info = replicate(info, nstar)

<span class="comments">; Find positions of tab characters </span>
  t = t[i0:iend-1]

  for j=0,Ntag-1 do begin
      x = strtrim( gettok(t,string(9b),/exact ),2)
       dtype = size(info[0].(j),/type)
       if dtype NE 7 then begin
       bad = where(strlen(x) EQ 0, Nbad)
      if (Nbad GT 0) then $
           if (dtype EQ 4) || (dtype EQ 5) then x[bad] = 'NaN' $
                                           else x[bad] = -1
      endif
      info.(j) = x 
   endfor
 return,info
END 
  

</code>
    </div>
  </body>
</html>