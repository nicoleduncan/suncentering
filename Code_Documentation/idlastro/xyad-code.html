<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN"
 "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">

<!-- Generated by IDLdoc 3.5.1 on Mon Sep 30 16:58:13 2013 -->

<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en">
  <head>
    <meta http-equiv="Content-Type" content="text/html;charset=utf-8"/>
    <title>xyad.pro (Documentation for ./)</title>

    
    <link rel="stylesheet" type="text/css" media="all"
          href="../idldoc-resources/main.css" />
    <link rel="stylesheet" type="text/css" media="print"
          href="../idldoc-resources/main-print.css" />
    

    <script type="text/javascript">
      function setTitle() {
        parent.document.title="xyad.pro (Documentation for ./)";
      }
    </script>
  </head>

  <body onload="setTitle();" id="root">
    <div class="content">
      <code class="source"><a id="xyad:source"></a>pro xyad, hdr, x, y, a, d, PRINT = print, GALACTIC = galactic, ALT = alt, $
         CELESTIAL = celestial, ECLIPTIC = ecliptic, PRECISION = precision
<span class="comments">;+</span>
<span class="comments">; NAME:</span>
<span class="comments">;       XYAD</span>
<span class="comments">; PURPOSE:</span>
<span class="comments">;       Use a FITS header to convert pixel (X,Y) to world coordinates</span>
<span class="comments">; EXPLANATION: </span>
<span class="comments">;       Use astrometry in a FITS image header to compute world</span>
<span class="comments">;       coordinates in decimal degrees from X and Y.    </span>
<span class="comments">;</span>
<span class="comments">;       If spherical coordinates (Calabretta & Greisen 2002, A&A, 395, 1077) are </span>
<span class="comments">;       not present, then XYAD will still perform the transformation specified</span>
<span class="comments">;       by the CD, CRVAL, and CRPIX keywords.</span>
<span class="comments">; CALLING SEQUENCE:</span>
<span class="comments">;       XYAD, HDR               ;Prompt for X and Y positions</span>
<span class="comments">;       XYAD, HDR, X, Y, A, D, [ /PRINT, /Galactic, /Celestial, /Ecliptic, </span>
<span class="comments">;                                ALT =, PRECISION=]</span>
<span class="comments">; INPUTS:</span>
<span class="comments">;       HDR - FITS Image header containing astrometry info</span>
<span class="comments">;</span>
<span class="comments">; OPTIONAL INPUTS:</span>
<span class="comments">;       X     - row position in pixels, scalar or vector</span>
<span class="comments">;       Y     - column position in pixels, scalar or vector</span>
<span class="comments">;</span>
<span class="comments">;       X and Y should be in IDL convention, (first pixel is (0,0) where</span>
<span class="comments">;       the integral value corresponds to the center of the pixel.)</span>
<span class="comments">;</span>
<span class="comments">; OPTIONAL OUTPUT:</span>
<span class="comments">;       A - Output longitude in decimal DEGREES (for spherical coordinates), </span>
<span class="comments">;               same number of elements as X and Y.    For celestial </span>
<span class="comments">;               coordinates, this is the Right ascension.</span>
<span class="comments">;       D - Output latitude in decimal DEGREES.   For celestial coordinates,</span>
<span class="comments">;               this is the declination.</span>
<span class="comments">; OPTIONAL KEYWORD INPUT:</span>
<span class="comments">;       ALT -  single character 'A' through 'Z' or ' ' specifying an alternate </span>
<span class="comments">;             astrometry system present in the FITS header.    The default is</span>
<span class="comments">;             to use the primary astrometry or ALT = ' '.   If /ALT is set, </span>
<span class="comments">;             then this is equivalent to ALT = 'A'.   See Section 3.3 of </span>
<span class="comments">;             Greisen & Calabretta (2002, A&A, 395, 1061) for information about</span>
<span class="comments">;             alternate astrometry keywords.</span>
<span class="comments">;       PRECISION - Integer scalar (0-4) specifying the number of digits </span>
<span class="comments">;             displayed after the decimal of declination.   The RA is</span>
<span class="comments">;             automatically one digit more.   See ADSTRING() for more info.</span>
<span class="comments">;             Default value is 1, and the keyword is ignored if results are not </span>
<span class="comments">;             displayed at the terminal </span>
<span class="comments">;       /PRINT - If this keyword is set and non-zero, then results are displayed</span>
<span class="comments">;               at the terminal.in both decimal and sexagesimal notation.</span>
<span class="comments">;</span>
<span class="comments">;       The default for XYAD is to return the coordinate system present in</span>
<span class="comments">;       in the FITS header.    However, the following mutually exclusive </span>
<span class="comments">;       keywords can be used to convert to a particular coordinate system:</span>
<span class="comments">;</span>
<span class="comments">;       /CELESTIAL - Output is Right Ascension and declination</span>
<span class="comments">;       /ECLIPTIC - Output is Ecliptic longitude and latitude</span>
<span class="comments">;       /GALACTIC - Output is Galactic longitude and latitude</span>
<span class="comments">;    </span>
<span class="comments">; OPERATIONAL NOTES:</span>
<span class="comments">;       If less than 5 parameters are supplied, or if the /PRINT keyword is</span>
<span class="comments">;       set, then the computed astronomical coordinates are displayed at the </span>
<span class="comments">;       terminal.</span>
<span class="comments">;</span>
<span class="comments">;       If this procedure is to be used repeatedly with the same header,</span>
<span class="comments">;       then it would be faster to use XY2AD.</span>
<span class="comments">;</span>
<span class="comments">; EXAMPLE:</span>
<span class="comments">;       A FITS header, hdr, contains astrometric information in celestial</span>
<span class="comments">;       coordinates.   Find the RA and Dec corresponding to position X=23.3</span>
<span class="comments">;        Y = 100.2 on an image</span>
<span class="comments">;        IDL> xyad, hdr, 23.3, 100.2      ;Displays results at the terminal</span>
<span class="comments">;       To display the results in Galactic coordinates</span>
<span class="comments">;        IDL> xyad, hdr, 23.3, 100.2, /GALACTIC</span>
<span class="comments">;</span>
<span class="comments">; PROCEDURES CALLED</span>
<span class="comments">;       ADSTRING(), EULER, EXTAST, GSSSXYAD, REPCHR(),  XY2AD</span>
<span class="comments">;</span>
<span class="comments">; REVISION HISTORY:</span>
<span class="comments">;       W. Landsman                 STX          Jan, 1988</span>
<span class="comments">;       Use astrometry structure  W. Landsman    Jan, 1994</span>
<span class="comments">;       Recognize GSSS header  W. Landsman       June, 1994</span>
<span class="comments">;       Changed ADSTRING output format   W. Landsman    September 1995</span>
<span class="comments">;       Converted to IDL V5.0   W. Landsman   September 1997</span>
<span class="comments">;       Use vector call to ADSTRING() W. Landsman February 2000</span>
<span class="comments">;       Added ALT input keyword  W. Landsman June 2003</span>
<span class="comments">;       Add precision keyword  W. Landsman February 2004</span>
<span class="comments">;       Fix display if 'RA','DEC' reversed in CTYPE  W. Landsman Feb. 2004</span>
<span class="comments">;       Handle display of NaN values W. Landsman May 2004</span>
<span class="comments">;       Work for non-spherical coordinate transformations W. Landsman Oct 2004</span>
<span class="comments">;       Fix output display units if ALT keyword used W. Landsman March 2005</span>
<span class="comments">;       More informative error message if no astrometry present W.L Nov 2007</span>
<span class="comments">;       Fix display when no equinox in header W.L. Dec 2007</span>
<span class="comments">;       Fix header display for noncelestial coords W.L. Jan 2008</span>
<span class="comments">;-</span>
 compile_opt idl2
 On_error,2

 npar = N_params()
 if ( npar EQ 0 ) then begin
        print,'Syntax -  XYAD, hdr, [x, y, a, d, /PRINT, Alt=, Precision=, '
        print,'                      /Galactic, /Celestial, /Ecliptic ]'
        print,'HDR - FITS header (string array) containing astrometry'
        print,'X,Y - Input X and Y positions (scalar or vector)'
        print,'A,D - Output RA and Dec in decimal degrees'
        return
 endif                                                         

  extast, hdr, astr, noparams, ALT = alt       <span class="comments">;Extract astrometry structure</span>

  if ( noparams LT 0 ) then begin
        if N_elements(alt) EQ 0 then $
        message,'ERROR - No astrometry info in supplied FITS header' $
	else  message, $
	'ERROR  - No alt=' + alt + ' astrometry info in supplied FITS header'
  endif	


  if ( npar lt 3 ) then read,'XYAD: Enter X and Y positions: ',x,y

  case strmid(astr.ctype[0],5,3)  of 
        'GSS': gsssxyad, astr, x, y, a, d
         else: xy2ad, x, y, astr, a, d
  endcase
  titname = strmid(astr.ctype,0,4)
  if (titname[0] EQ 'DEC-') or (titname[0] EQ 'ELAT') or $
          (titname[0] EQ 'GLAT') then titname = rotate(titname,2)

  if keyword_set(GALACTIC) then begin
      case titname[0] of 
      'RA--': euler, a,d, select=1
      'ELON': euler, a,d, select=5
      else:
      endcase
      titname = ['GLON','GLAT']
  endif else if keyword_set(ECLIPTIC) then begin 
      case titname[0] of 
      'RA--': euler, a, d, select=3
      'GLON': euler, a,d, select=6
      else: 
      endcase
      titname = ['ELON','ELAT']
  endif else if keyword_set(CELESTIAL) then begin
      case titname[0] of 
      'ELON': euler, a, d, select=4
      'GLON': euler, a,d, select=2
      else: 
      endcase
      titname = ['RA--','DEC-']
  endif 

  if (npar lt 5) or keyword_set(PRINT) then begin
        g = where( finite(d) and finite(a), Ng)
	 tit1= titname[0]
	 t1 = strpos(tit1,'-')
	 if t1 gt 0 then tit1 = strmid(tit1,0,t1)
	 tit2= titname[1]
	 t1 = strpos(tit2,'-')
	 if t1 gt 0 then tit2 = strmid(tit2,0,t1)
        npts = N_elements(X)
        spherical = strmid(astr.ctype[0],4,1) EQ '-'
        fmt = '(2F8.2,2x,2F9.4,2x,A)'
        if spherical then begin

        tit = '    X       Y         ' + tit1 + '      ' + tit2 
	sexig = strmid(titname[0],0,4) EQ 'RA--'
	if sexig then begin 
  
  	eqnx = get_equinox(hdr,code)
	eqnx = code NE -1 ? '_' + string(eqnx,f='(I4)') :  '    '
	tit = tit +  $
	   '        ' + tit1  + eqnx +  '      ' + tit2 + eqnx
        if N_elements(precision) EQ 0 then precision = 1
        str = replicate('    ---          ---    ', Npts)
        if Ng GT 0 then str[g] = adstring(a[g],d[g],precision)
	endif else str = replicate('', npts)
	print,tit
        for i=0l, npts-1 do $
        print,FORMAT=fmt, float(x[i]), float(y[i]), a[i], d[i], str[i]	
	
        endif else begin
            unit1 = strtrim( sxpar( hdr, 'CUNIT1'+alt,count = N_unit1),2)
            if N_unit1 EQ 0 then unit1 = ''
	    unit2 = strtrim( sxpar( hdr, 'CUNIT2'+alt,count = N_unit2),2)
            if N_unit2 EQ 0 then unit2 = ''
       print,'    X       Y         ' + titname[0] + '     ' + titname[1] 
       if (N_unit1 GT 0) or (N_unit2 GT 0) then $
       print,unit1 ,unit2,f='(t23,a,t33,a)' 	    
        for i=0l, npts-1 do $
       print,FORMAT=fmt, float(x[i]), float(y[i]), a[i], d[i]
        endelse 
   endif
   
   return
   end
</code>
    </div>
  </body>
</html>