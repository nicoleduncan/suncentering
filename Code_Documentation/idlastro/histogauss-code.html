<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN"
 "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">

<!-- Generated by IDLdoc 3.5.1 on Mon Sep 30 16:57:41 2013 -->

<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en">
  <head>
    <meta http-equiv="Content-Type" content="text/html;charset=utf-8"/>
    <title>histogauss.pro (Documentation for ./)</title>

    
    <link rel="stylesheet" type="text/css" media="all"
          href="../idldoc-resources/main.css" />
    <link rel="stylesheet" type="text/css" media="print"
          href="../idldoc-resources/main-print.css" />
    

    <script type="text/javascript">
      function setTitle() {
        parent.document.title="histogauss.pro (Documentation for ./)";
      }
    </script>
  </head>

  <body onload="setTitle();" id="root">
    <div class="content">
      <code class="source">PRO HISTOGAUSS,SAMPLE,A,XX,YY,GX,GY,NOPLOT=noplot,NOFIT=SIMPL, $
               CHARSIZE=CSIZE, FONT=font, _EXTRA = _extra,Window=window
<span class="comments">;</span>
<span class="comments">;+</span>
<span class="comments">;NAME:</span>
<span class="comments">;       HISTOGAUSS</span>
<span class="comments">;</span>
<span class="comments">; PURPOSE:</span>
<span class="comments">;       Histograms data and overlays it with a Gaussian. Draws the mean, sigma,</span>
<span class="comments">;       and number of points on the plot.</span>
<span class="comments">;</span>
<span class="comments">; CALLING SEQUENCE:</span>
<span class="comments">;       HISTOGAUSS, Sample, A, [XX, YY, GX, GY, /NOPLOT, /NOFIT, FONT=, </span>
<span class="comments">;                               CHARSIZE = ]</span>
<span class="comments">;</span>
<span class="comments">; INPUT:</span>
<span class="comments">;       SAMPLE = Vector to be histogrammed</span>
<span class="comments">;</span>
<span class="comments">; OUTPUT ARGUMENTS:</span>
<span class="comments">;       A = coefficients of the Gaussian fit: Height, mean, sigma</span>
<span class="comments">;               A[0]= the height of the Gaussian</span>
<span class="comments">;               A[1]= the mean</span>
<span class="comments">;               A[2]= the standard deviation</span>
<span class="comments">;               A[3]= the half-width of the 95% conf. interval of the standard</span>
<span class="comments">;                     mean</span>
<span class="comments">;               A[4]= 1/(N-1)*total( (y-mean)/sigma)^2 ) = a measure of </span>
<span class="comments">;                       normality</span>
<span class="comments">;</span>
<span class="comments">;       Below: superceded. The formula is not entirely reliable.</span>
<span class="comments">;       A[4]= measure of the normality of the distribution. =1.0, perfectly</span>
<span class="comments">;       normal. If no more than a few hundred points are input, there are</span>
<span class="comments">;       formulae for the 90 and 95% confidence intervals of this quantity:</span>
<span class="comments">;       M=ALOG10(N-1) ; N = number of points</span>
<span class="comments">;       T90=ABS(.6376-1.1535*M+.1266*M^2)  ; = 90% confidence interval</span>
<span class="comments">;       IF N LT 50 THEN T95=ABS(-1.9065-2.5465*M+.5652*M^2) $</span>
<span class="comments">;                  ELSE T95=ABS( 0.7824-1.1021*M+.1021*M^2)   ;95% conf.</span>
<span class="comments">;       (From Martinez, J. and Iglewicz, I., 1981, Biometrika, 68, 331-333.)</span>
<span class="comments">;</span>
<span class="comments">;       XX = the X coordinates of the histogram bins (CENTER)</span>
<span class="comments">;       YY = the Y coordinates of the histogram bins</span>
<span class="comments">;       GX = the X coordinates of the Gaussian fit</span>
<span class="comments">;       GY = the Y coordinates of the Gaussian fit</span>
<span class="comments">;</span>
<span class="comments">; OPTIONAL INPUT KEYWORDS:</span>
<span class="comments">;       /NOPLOT - If set, nothing is drawn</span>
<span class="comments">;       /FITIT   If set, a Gaussian is actually fitted to the distribution.</span>
<span class="comments">;               By default, a Gaussian with the same mean and sigma is drawn; </span>
<span class="comments">;               the height is the only free parameter.</span>
<span class="comments">;       CHARSIZE Size of the characters in the annotation. Default = 0.82.</span>
<span class="comments">;       FONT - scalar font graphics keyword (-1,0 or 1) for text</span>
<span class="comments">;       /WINDOW - set to plot to a resizeable graphics window</span>
<span class="comments">;       _EXTRA - Any value keywords to the cgPLOT command (e.g. XTITLE) may also</span>
<span class="comments">;               be passed to HISTOGAUSS</span>
<span class="comments">; SUBROUTINE CALLS:</span>
<span class="comments">;       BIWEIGHT_MEAN, which determines the mean and std. dev.</span>
<span class="comments">;       AUTOHIST, which draws the histogram</span>
<span class="comments">;       GAUSSFIT() (IDL Library) which does just that</span>
<span class="comments">;</span>
<span class="comments">; REVISION HISTORY:</span>
<span class="comments">;       Written, H. Freudenreich, STX, 12/89</span>
<span class="comments">;       More quantities returned in A, 2/94, HF</span>
<span class="comments">;       Added NOPLOT keyword and print if Gaussian, 3/94</span>
<span class="comments">;       Stopped printing confidence limits on normality 3/31/94 HF</span>
<span class="comments">;       Added CHARSIZE keyword, changed annotation format, 8/94 HF</span>
<span class="comments">;       Simplified calculation of Gaussian height, 5/95 HF</span>
<span class="comments">;       Convert to V5.0, use T_CVF instead of STUDENT_T, GAUSSFIT instead of</span>
<span class="comments">;           FITAGAUSS  W. Landsman April 2002 </span>
<span class="comments">;       Correct call to T_CVF for calculation of A[3], 95% confidence interval</span>
<span class="comments">;                P. Broos/W. Landsman   July 2003</span>
<span class="comments">;       Allow FONT keyword to be passed.  T. Robishaw Apr. 2006</span>
<span class="comments">;       Use Coyote Graphics for plotting W.L. Mar 2011</span>
<span class="comments">;       Better formatting of text output W.L. May 2012</span>
<span class="comments">;-</span>

 On_error,2
 compile_opt idl2

 if N_params() LT 2 then begin
    print,'Syntax - HISTOGAUSS, Sample, A, [XX, YY, GX, GY,  '
    print,'                   /NOPLOT, /NOFIT, CHARSIZE=, Plotting keywords...]'
    return
 endif

 if (N_elements(FONT) eq 0) then font = !p.font
 DATA = SAMPLE
 N = N_ELEMENTS(DATA)

<span class="comments">; First make sure that not everything is in the same bin. If most</span>
<span class="comments">; data = 0, reject zeroes. If they = some other value, complain and</span>
<span class="comments">; give up.</span>
 A = 0.
 DATA = DATA[SORT(DATA)]  
 N3 = 0.75*N & N1 = 0.25*N
IF DATA[N3] EQ DATA[N1] THEN BEGIN
   IF DATA[N/2] EQ 0. THEN BEGIN
      Q = WHERE(DATA NE 0.,NON0)
      IF (N-NON0) GT 15 THEN BEGIN
         message,/INF,'Suppressing Zeroes!'
         DATA=DATA[Q]
         N=NON0
      ENDIF ELSE BEGIN 
         message,' Too Few Non-0 Values!',/CON
         RETURN
      ENDELSE
      Q=0
   ENDIF ELSE BEGIN
      message,/CON,' Too Many Identical Values: ' + strtrim(DATA[N/2],2)
      RETURN
   ENDELSE
ENDIF

A = FLTARR(5) 

<span class="comments">; The "mean":</span>
A[1] = BIWEIGHT_MEAN(DATA,S)
<span class="comments">; The "standard deviation":</span>
A[2] = S  
<span class="comments">; The 95% confidence interval:</span>
M=.7*(N-1)  <span class="comments">;appropriate for a biweighted mean</span>
CL = 0.95
two_tail_area = 1 - CL
A[3]=ABS( T_CVF(1 - (two_tail_area)/2.0,M) )*S/sqrt(n)

<span class="comments">; A measure of the Gaussianness:</span>
A[4]=TOTAL((DATA-A[1])^2)/((N-1)*A[2]^2)
<span class="comments">;Q=WHERE( ABS(DATA-A(1)) LT (5.*S), COUNT )   ; "robust I" unreliable</span>
<span class="comments">;ROB_I=TOTAL((DATA(Q)-A(1))^2)/((COUNT-1)*A(2)^2)</span>
<span class="comments">;PRINT,A(4),ROB_I</span>

<span class="comments">; Set bounds on the data:</span>
 U1 = A[1] - 5.*A[2]
 U2 = A[1] + 5.*A[2]
 Q = WHERE(DATA LT U1, NQ)
 IF NQ GT 0 THEN DATA[Q] = U1
 Q = WHERE(DATA GT U2, NQ)
 IF NQ GT 0 THEN DATA[Q] = U2

<span class="comments">; Draw the histogram</span>
 font_in = !P.FONT & !P.FONT=font
 AUTOHIST,DATA,X,Y,XX,YY,NOPLOT = noplot, _EXTRA = _extra,Window=window
 !P.FONT=font_in
 
<span class="comments">; Check for error in AUTOHIST:</span>

M = N_ELEMENTS(X)
MM = N_ELEMENTS(XX)
IF M LT 2 THEN BEGIN
   XX=0. & YY=0. & A=0.
   RETURN <span class="comments">; (AUTOHIST has already screamed)</span>
ENDIF

<span class="comments">; Calculate the height of the Gaussian:</span>
Z = EXP(-.5*(X-A[1])^2/A[2]^2 )
XQ1 = A[1] - 1.3*A[2]
XQ2 = A[1] + 1.3*A[2]
QQ = WHERE((X GT XQ1) AND (X LT XQ2),COUNT)
IF COUNT GT 0 THEN HYTE = MEDIAN(Y[QQ]/Z[QQ],/EVEN) ELSE BEGIN
   print,'HISTOGAUSS: Distribution too Weird!'
   HYTE = MAX(SMOOTH(Y,5))
ENDELSE
A[0]=HYTE

<span class="comments">; Fit a Gaussian, unless the /NOFIT qualifier is present</span>
IF ~KEYWORD_SET(SIMPL) THEN BEGIN
   PARM=A[0:2]
   YFIT = GAUSSFIT(XX,YY,PARM,NTERMS=3)
   A[0:2]=PARM
ENDIF

<span class="comments">; It the /NOPLOT qualifier is present, we're done.</span>
IF KEYWORD_SET(NOPLOT) THEN RETURN

<span class="comments">; Overplot the Gaussian, </span>
 DU = (U2-U1)/199.
 GX = U1 + FINDGEN(200)*DU

 Z = (GX-A[1])/A[2]
 GY = A[0]*EXP(-Z^2/2. )
 cgplot,/over,GX,GY,window=window

<span class="comments">; Annotate. </span>
MEANST = STRING(A[1],'(G12.5)')
SIGST = STRING(A[2],'(G12.5)')
NUM = N_ELEMENTS(DATA)
NUMST =STRING(N,'(I6)')

IF KEYWORD_SET(CSIZE) THEN ANNOT=CSIZE ELSE ANNOT=.82
 if FONT EQ 0 then LABL = '#, !Mm!X, !Ms!X=' else  LABL='#, !7l!6, !7r!3='
 LABL = LABL +numst+','+meanst+','+sigst 
X1 = !x.crange[0] + annot*(!x.crange[1]-!x.crange[0])/20./0.82 
y1 = !y.crange[1] - annot*(!y.crange[1]-!y.crange[0])/23./0.82 
cgtext, X1, Y1, LABL, CHARSIZE=ANNOT, FONT=font,window=window

RETURN
END

</code>
    </div>
  </body>
</html>