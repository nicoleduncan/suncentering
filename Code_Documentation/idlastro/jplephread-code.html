<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN"
 "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">

<!-- Generated by IDLdoc 3.5.1 on Mon Sep 30 16:57:44 2013 -->

<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en">
  <head>
    <meta http-equiv="Content-Type" content="text/html;charset=utf-8"/>
    <title>jplephread.pro (Documentation for ./)</title>

    
    <link rel="stylesheet" type="text/css" media="all"
          href="../idldoc-resources/main.css" />
    <link rel="stylesheet" type="text/css" media="print"
          href="../idldoc-resources/main-print.css" />
    

    <script type="text/javascript">
      function setTitle() {
        parent.document.title="jplephread.pro (Documentation for ./)";
      }
    </script>
  </head>

  <body onload="setTitle();" id="root">
    <div class="content">
      <code class="source"><span class="comments">;+</span>
<span class="comments">; NAME:</span>
<span class="comments">;   JPLEPHREAD</span>
<span class="comments">;</span>
<span class="comments">; AUTHOR:</span>
<span class="comments">;   Craig B. Markwardt, NASA/GSFC Code 662, Greenbelt, MD 20770</span>
<span class="comments">;   craigm@lheamail.gsfc.nasa.gov</span>
<span class="comments">;   UPDATED VERSIONs can be found on my WEB PAGE: </span>
<span class="comments">;      http://cow.physics.wisc.edu/~craigm/idl/idl.html</span>
<span class="comments">;</span>
<span class="comments">; PURPOSE:</span>
<span class="comments">;   Open and read JPL DE200 or DE405 Ephemeride FITS File</span>
<span class="comments">;</span>
<span class="comments">; MAJOR TOPICS:</span>
<span class="comments">;   Planetary Orbits, Interpolation</span>
<span class="comments">;</span>
<span class="comments">; CALLING SEQUENCE:</span>
<span class="comments">;   JPLEPHREAD, FILENAME, INFO, RAWDATA, JDLIMITS, STATUS=, ERRMSG=</span>
<span class="comments">;</span>
<span class="comments">; DESCRIPTION:</span>
<span class="comments">;</span>
<span class="comments">;   JPLEPHREAD opens and reads the JPL DE200 or DE405 planetary</span>
<span class="comments">;   ephemerides, as available in FITS format.  The user must have the</span>
<span class="comments">;   IDL Astronomy Library installed to use this routine.</span>
<span class="comments">;</span>
<span class="comments">;   This routine is the initialization stage of a two-stage process to</span>
<span class="comments">;   interpolate the JPL ephemeris.  In this first stage, the file is</span>
<span class="comments">;   opened, and the relevant portions of the table are read and stored</span>
<span class="comments">;   into the two variables INFO and RAWDATA.  In the second stage, the</span>
<span class="comments">;   user actually interpolates the ephemeris for the desired bodies</span>
<span class="comments">;   and to the desired ephemeris time using JPLEPHINTERP.</span>
<span class="comments">;</span>
<span class="comments">;   Users must decide ahead of time the approximate dates of interest,</span>
<span class="comments">;   and pass this range in the JDLIMITS parameter.  Any date covered</span>
<span class="comments">;   by the ephemeris is valid.</span>
<span class="comments">;</span>
<span class="comments">;   JPLEPHREAD is able to read files of the following format:</span>
<span class="comments">;     DE200 - Chebyshev - FITS format - Note 1</span>
<span class="comments">;     DE405 - Chebyshev - FITS format - Note 1</span>
<span class="comments">;     DE200 - Taylor    - FITS format - Note 2</span>
<span class="comments">;</span>
<span class="comments">;   Note 1 - Chebyshev formatted FITS files are available in the</span>
<span class="comments">;            AXBARY package by Arnold Rots, found here:</span>
<span class="comments">;              ftp://heasarc.gsfc.nasa.gov/xte/calib_data/clock/bary/</span>
<span class="comments">;            or at the Markwardt FTP site:</span>
<span class="comments">;              ftp://cow.physics.wisc.edu/pub/craigm/bary/</span>
<span class="comments">;</span>
<span class="comments">;   Note 2 - Taylor-series based ephemerides have been available for</span>
<span class="comments">;            years in the FTOOLS / LHEASOFT package produced by NASA's</span>
<span class="comments">;            Goddard Space Flight Center.  The original file is</span>
<span class="comments">;            de200_new.fits, which covers the years 1959-2000,</span>
<span class="comments">;            inclusive.  A newer file is named</span>
<span class="comments">;            de200_1950-2050_v2.fits, and covers the years 1959-2050.</span>
<span class="comments">;            See Markwardt FTP site for these files.</span>
<span class="comments">;</span>
<span class="comments">; PARAMETERS: </span>
<span class="comments">;</span>
<span class="comments">;   FILENAME - name of ephemeris file (scalar string).</span>
<span class="comments">;</span>
<span class="comments">;   INFO - upon completion, information about the ephemeris data is</span>
<span class="comments">;          returned in this parameter in the form of a structure.</span>
<span class="comments">;          Users must not modify INFO, although several fields are</span>
<span class="comments">;          useful and may be accessed read-only:</span>
<span class="comments">;              TSTART/TSTOP (start and stop time of data in Julian</span>
<span class="comments">;                            days);</span>
<span class="comments">;              C (speed of light in km/s);</span>
<span class="comments">;              DENUM (development ephemeris number [200 or 405])</span>
<span class="comments">;              AU (1 astronomical unit, in units of light-seconds)</span>
<span class="comments">;</span>
<span class="comments">;   RAWDATA - upon completion, raw ephemeris data is returned in this</span>
<span class="comments">;             parameter.  Users are not meant to access this data</span>
<span class="comments">;             directly, but rather to pass it to JPLEPHINTERP.</span>
<span class="comments">;</span>
<span class="comments">;   JDLIMITS - a two-element vector (optional), describing the desired</span>
<span class="comments">;              time range of interest.  The vector should have the</span>
<span class="comments">;              form [TSTART, TSTOP], where TSTART and TSTOP are the</span>
<span class="comments">;              beginning and ending times of the range, expressed in</span>
<span class="comments">;              Julian days.</span>
<span class="comments">;              Default: entire table is read (note, this can be</span>
<span class="comments">;              several megabytes)</span>
<span class="comments">;</span>
<span class="comments">;</span>
<span class="comments">; KEYWORD PARAMETERS:</span>
<span class="comments">;</span>
<span class="comments">;   STATUS - upon completion, a value of 1 indicates success, and 0</span>
<span class="comments">;            indicates failure.</span>
<span class="comments">;</span>
<span class="comments">;   ERRMSG - upon completion, an error message is returned in this</span>
<span class="comments">;            keyword.  If there were no errors, then the returned</span>
<span class="comments">;            value is the empty string, ''.</span>
<span class="comments">;</span>
<span class="comments">;</span>
<span class="comments">; EXAMPLE:</span>
<span class="comments">;</span>
<span class="comments">;   Find position of earth at ephemeris time 2451544.5 JD.  Units are</span>
<span class="comments">;   in Astronomical Units.</span>
<span class="comments">;</span>
<span class="comments">;   JPLEPHREAD, 'JPLEPH.200', pinfo, pdata, [2451544D, 2451545D]</span>
<span class="comments">;</span>
<span class="comments">;   JPLEPHINTERP, pinfo, pdata, 2451544.5D, xearth, yearth, zearth, $</span>
<span class="comments">;                 /EARTH, posunits='AU'</span>
<span class="comments">;     </span>
<span class="comments">;</span>
<span class="comments">; REFERENCES:</span>
<span class="comments">;</span>
<span class="comments">;   AXBARY, Arnold Rots.</span>
<span class="comments">;      ftp://heasarc.gsfc.nasa.gov/xte/calib_data/clock/bary/</span>
<span class="comments">;</span>
<span class="comments">;   HORIZONS, JPL Web-based ephermis calculator (Ephemeris DE406)</span>
<span class="comments">;      http://ssd.jpl.nasa.gov/horizons.html</span>
<span class="comments">;   </span>
<span class="comments">;   JPL Export Ephemeris FTP Site</span>
<span class="comments">;      ftp://ssd.jpl.nasa.gov/pub/eph/planets/</span>
<span class="comments">;      (ephemeris files are available here, however, they must be</span>
<span class="comments">;      converted to FITS format using the "bin2eph" utility found in</span>
<span class="comments">;      AXBARY)</span>
<span class="comments">;</span>
<span class="comments">;   JPL Export Ephemeris CD-ROM - Ordering Information</span>
<span class="comments">;      http://www.willbell.com/software/jpl.htm</span>
<span class="comments">;</span>
<span class="comments">;   Standish, E.M. 1982, "Orientation of the JPL Ephemerides,</span>
<span class="comments">;      DE200/LE200, to the Dynamical Equinox of J2000", Astronomy &</span>
<span class="comments">;      Astrophysics, vol. 114, pp. 297-302.</span>
<span class="comments">;</span>
<span class="comments">;   Standish, E.M.: 1990, "The Observational Basis for JPL's DE200,</span>
<span class="comments">;      the planetary ephemeris of the Astronomical Almanac", Astronomy</span>
<span class="comments">;      & Astrophysics, vol. 233, pp. 252-271.    </span>
<span class="comments">;</span>
<span class="comments">; SEE ALSO</span>
<span class="comments">;   JPLEPHREAD, JPLEPHINTERP, JPLEPHTEST</span>
<span class="comments">; PROCEDURES USED:</span>
<span class="comments">;     FXBCLOSE, FXBOPEN, FXPAR(), </span>
<span class="comments">;   </span>
<span class="comments">; MODIFICATION HISTORY:</span>
<span class="comments">;   Written and Documented, CM, Jun 2001</span>
<span class="comments">;   Use GETTOK() instead of STR_SEP()  W. Landsman  July 2002</span>
<span class="comments">;</span>
<span class="comments">;  $Id: jplephread.pro,v 1.6 2001/07/01 03:32:02 craigm Exp $</span>
<span class="comments">;</span>
<span class="comments">;-</span>
<span class="comments">; Copyright (C) 2001, Craig Markwardt</span>
<span class="comments">; This software is provided as is without any warranty whatsoever.</span>
<span class="comments">; Permission to use, copy and distribute unmodified copies for</span>
<span class="comments">; non-commercial purposes, and to modify and use for personal or</span>
<span class="comments">; internal use, is granted.  All other rights are reserved.</span>
<span class="comments">;-</span>


<a id="jplephpar:source"></a>function jplephpar, header, parname, default=default, fatal=fatal
compile_opt idl2

  <span class="comments">; '$Id: jplephread.pro,v 1.6 2001/07/01 03:32:02 craigm Exp $'</span>

  value = fxpar(header, parname, Count = N_value)
  if N_value EQ 0 then begin
      if keyword_set(fatal) then $
        message, 'ERROR: keyword '+strupcase(parname)+' was not found'
        return, default
  endif
  return, value
end

<a id="jplephval:source"></a>function jplephval, names, values, name, default=default, fatal=fatal
  wh = where(names EQ strupcase(name), ct)
  if ct EQ 0 then begin
      if keyword_set(fatal) then $
        message, 'ERROR: value '+strupcase(name)+' was not found in file'
      return, default
  endif
  return, values[wh[0]]
end

<a id="jplephread:source"></a>pro jplephread, filename, info, raw, jdlimits, $
                status=status, errmsg=errmsg

  status = 0
  printerror = 1 - arg_present(errmsg)
  errmsg = ''

  if n_params() EQ 0 then begin
      message, 'USAGE: JPLEPHREAD, filename, info, rawdata, jdlimits', /info
      return
  endif

<span class="comments">;  if n_elements(jdlimits) LT 2 then begin</span>
<span class="comments">;      errmsg = 'ERROR: You must specify JDLIMITS'</span>
<span class="comments">;      return</span>
<span class="comments">;  endif</span>

  fxbopen, unit, filename, 1, ephhead, errmsg=errmsg
  if errmsg NE '' then $
            if printerror then message,errmsg else return

  extname = strtrim(fxpar(ephhead, 'EXTNAME'),2)
  ttype1  = strtrim(fxpar(ephhead, 'TTYPE1'),2)

  if (extname EQ 'EPHEM' AND ttype1 EQ 'EARTH') then begin
      <span class="comments">;; This is the DE200_NEW format (standard FTOOLS)</span>

      nrows  = fxpar(ephhead, 'NAXIS2')
      tstart = fxpar(ephhead, 'TSTART')
      tstop  = fxpar(ephhead, 'TSTOP')
      timedel = jplephpar(ephhead, 'TIMEDEL', default=1D) <span class="comments">;; 1-day default</span>

      <span class="comments">;; Constants from XTEBARYCEN.F</span>
      C=2.99792458D+8
      TWOPI=6.28318530717958648D0
      DAYSEC=1.D0/86400.D0
      AULTSC=499.004782D0
      GAUSS=0.01720209895D0
      RSCHW=(GAUSS^2)*(AULTSC^3)*(DAYSEC^2)
      SUNRAD=2.315D0

      if n_elements(jdlimits) GE 2 then begin
          if (min(jdlimits) LT tstart OR $
              max(jdlimits) GT tstop) then begin
              errmsg = 'ERROR: '+filename+$
                ' does not cover the time of interest'
              fxbclose, unit
              return
          endif
          <span class="comments">;; Expand by one row either side</span>
          rowlimits = floor((jdlimits-tstart)/timedel) + [-2,2]
          rowlimits = rowlimits > 1 &lt<span class="comments">; nrows</span>
      endif else begin
          jdlimits  = [tstart, tstop]
          rowlimits = [1L, nrows]
      endelse

      <span class="comments">;; Read raw data</span>
      fxbread, unit, cearth, 'EARTH', rowlimits, errmsg=errmsg
      if errmsg EQ '' then $
        fxbread, unit, csun, 'SUN', rowlimits, errmsg=errmsg
      if errmsg EQ '' then $
        fxbread, unit, ctdb2tdt, 'TIMEDIFF', rowlimits, errmsg=errmsg
      fxbclose, unit
      if errmsg NE '' then $
         if printerror then message,errmsg else return

      nr = rowlimits[1]-rowlimits[0]+1
      t0 = dindgen(nr)*timedel - (jdlimits[1]-jdlimits[0])/2D
      dtt = spl_init(t0, ctdb2tdt)
      raw = reform(dblarr(18, nr), 18, nr, /overwrite)
      raw[0 :11,*] = cearth * c/1000D  <span class="comments">;; units of lt-s</span>
      raw[12:14,*] = csun * c/1000D    <span class="comments">;; units of lt-s/day</span>
      raw[15,   *] = t0
      raw[16   ,*] = ctdb2tdt
      raw[17   ,*] = dtt

      jdlimits1 = (rowlimits+[-1,0])*timedel + tstart

      info = {nrows: nrows, tstart: tstart, tstop: tstop, $
              timedel: timedel, format: 'DENEW', $
              denum: 200L, c: c, emrat: 0.012150586D, $
              au: aultsc, msol: rschw, sunrad: sunrad, $
              jdlimits: jdlimits1, jdrows: nr }

  endif else if (extname EQ 'DE1' AND ttype1 EQ 'Cname') then begin
      <span class="comments">;; This is the BINEPH2FITS format (either DE200 or DE405)</span>

      <span class="comments">;; ---------------------------------------------</span>
      <span class="comments">;; First extension contains parameter data</span>
      fxbread, unit, cname, 'Cname'
      fxbread, unit, cvalue, 'Cvalue'
      cname = strtrim(cname,2)

      denum = 0L & clight = 0D & emrat = 0D & au = 0D
      msol = 0D & radsol = 0D

      denum  = round(jplephval(cname, cvalue, 'DENUM', /fatal))
      clight = jplephval(cname, cvalue, 'CLIGHT', /fatal)
      emrat  = jplephval(cname, cvalue, 'EMRAT',  /fatal)
      au     = jplephval(cname, cvalue, 'AU',     /fatal)
      msol   = jplephval(cname, cvalue, 'GMS',    /fatal)
      radsol = jplephval(cname, cvalue, 'RADS', default=-1D)
      if radsol EQ -1D then $
        radsol = jplephval(cname, cvalue, 'ASUN', default=-1D)

      emrat = 1D / (1D + emrat)
        
      if clight EQ 0 then begin
          errmsg = 'ERROR: Could not load physical constants from '+filename
          fxbclose, unit
          return
      endif

      x = au / clight
      msol = msol * x * x * x / 86400D^2  <span class="comments">;; GM_sun (in lt sec)</span>
      radsol = radsol / clight            <span class="comments">;; Solar radius (lt sec)</span>
      clight = clight * 1000              <span class="comments">;; Speed of light (m/s)</span>
      
      fxbclose, unit

      <span class="comments">;; ---------------------------------------------</span>
      <span class="comments">;; Second extension contains accounting data</span>
      fxbopen, unit, filename, 2, ephhead, errmsg=errmsg
      if errmsg NE '' then $
            if printerror then message,errmsg else return

      extname = strtrim(fxpar(ephhead, 'EXTNAME'),2)
      if extname NE 'DE2' then begin
          errmsg = 'ERROR: '+filename+' is not a JPL ephemeris file'
          fxbclose, unit
          return
      endif

      fxbread, unit, ephobj, 'Object', errmsg=errmsg
      if errmsg EQ '' then $
        fxbread, unit, ephptr, 'Pointer', errmsg=errmsg
      if errmsg EQ '' then $
        fxbread, unit, ephncoeff, 'NumCoeff', errmsg=errmsg
      if errmsg EQ '' then $
        fxbread, unit, ephnsub, 'NumSubIntv', errmsg=errmsg
      fxbclose, unit
      if errmsg NE '' then begin
            errmsg = 'ERROR: could not read '+filename+' extension 2'
            if printerror then message,errmsg else return
      endif

      <span class="comments">;; Trim each object name to first word only</span>
          ephobj = strupcase(gettok(ephobj, ' '))
      
      <span class="comments">;; ---------------------------------------------</span>
      <span class="comments">;; Third extension contains Chebyshev coefficients</span>
      fxbopen, unit, filename, 3, ephhead, errmsg=errmsg
      if errmsg NE '' then return
      extname = strtrim(fxpar(ephhead, 'EXTNAME'),2)
      if extname NE 'DE3' then begin
          errmsg = 'ERROR: '+filename+' is not a JPL ephemeris file'
          fxbclose, unit
          if printerror then message,errmsg else return
      endif
      
      nrows  = fxpar(ephhead, 'NAXIS2')
      tstart = fxpar(ephhead, 'TSTART')
      tstop  = fxpar(ephhead, 'TSTOP')
      timedel = jplephpar(ephhead, 'TIMEDEL', default=32D) <span class="comments">;; 32-day default</span>

      if floor((tstop-tstart + 0.5)/timedel) NE nrows then begin
          errmsg = 'ERROR: Incorrect number of rows in '+filename
          fxbclose, unit
          if printerror then message,errmsg else return
      endif

      if n_elements(jdlimits) GE 2 then begin
          if (min(jdlimits) LT tstart OR $
              max(jdlimits) GT tstop) then begin
              errmsg = 'ERROR: '+filename+$
                ' does not cover the time of interest'
              fxbclose, unit
              if printerror then message,errmsg else return
          endif
          <span class="comments">;; Expand by two rows either side</span>
          rowlimits = floor((jdlimits-tstart)/timedel) + [-2,2]
          rowlimits = rowlimits > 1 &lt<span class="comments">; nrows</span>
      endif else begin
          jdlimits  = [tstart, tstop]
          rowlimits = [1L, nrows]
      endelse

      <span class="comments">;; Read raw data</span>
      dims = fxbdimen(unit, 'ChebCoeffs')
      fxbread, unit, coeffs, 'ChebCoeffs', rowlimits, errmsg=errmsg
      fxbclose, unit
      if errmsg NE '' then $
         if printerror then message,errmsg else return


      raw = reform(coeffs, [dims, rowlimits[1]-rowlimits[0]+1], /overwrite)

      jdlimits1 = (rowlimits+[-1,0])*timedel + tstart
      if (abs(min(raw[0,*]) - jdlimits1[0]) GT 1d-6 OR $
          abs(max(raw[1,*]) - jdlimits1[1]) GT 1d-6) then begin
          errmsg = 'ERROR: JDLIMITS and time column do not match'
          if printerror then message,errmsg else return
      endif

      nr = rowlimits[1]-rowlimits[0]+1
      info = {nrows: nrows, tstart: tstart, tstop: tstop, $
              timedel: timedel, format: 'BINEPH2FITS', $
              denum: denum, c: clight, emrat: emrat, $
              au: au*1000/clight, msol: msol, sunrad: radsol, $
              jdlimits: jdlimits1, jdrows: nr, $
              objname: ephobj, ptr: ephptr, ncoeff: ephncoeff, $
              nsub: ephnsub}
<span class="comments">;                 aufac: 1D/clight, velfac: 2D/(timedel*86400D), $</span>

  endif else begin
      errmsg = 'ERROR: '+filename+' was not in a recognized format'
      fxbclose, unit
      if printerror then message,errmsg else return
  endelse

  errmsg = ''
  status = 1
  return
end
</code>
    </div>
  </body>
</html>