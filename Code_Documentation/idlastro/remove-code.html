<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN"
 "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">

<!-- Generated by IDLdoc 3.5.1 on Mon Sep 30 16:58:00 2013 -->

<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en">
  <head>
    <meta http-equiv="Content-Type" content="text/html;charset=utf-8"/>
    <title>remove.pro (Documentation for ./)</title>

    
    <link rel="stylesheet" type="text/css" media="all"
          href="../idldoc-resources/main.css" />
    <link rel="stylesheet" type="text/css" media="print"
          href="../idldoc-resources/main-print.css" />
    

    <script type="text/javascript">
      function setTitle() {
        parent.document.title="remove.pro (Documentation for ./)";
      }
    </script>
  </head>

  <body onload="setTitle();" id="root">
    <div class="content">
      <code class="source"><a id="remove:source"></a>pro remove,index, v1, v2, v3, v4, v5, v6, v7, v8, v9, v10, v11, v12, v13, v14, $
     v15, v16, v17, v18, v19, v20, v21, v22, v23, v24, v25
<span class="comments">;+</span>
<span class="comments">; NAME:</span>
<span class="comments">;       REMOVE</span>
<span class="comments">; PURPOSE:</span>
<span class="comments">;       Contract a vector or up to 25 vectors by removing specified elements   </span>
<span class="comments">; CALLING SEQUENCE:</span>
<span class="comments">;       REMOVE, index, v1,[ v2, v3, v4, v5, v6, ... v25]     </span>
<span class="comments">; INPUTS:</span>
<span class="comments">;       INDEX - scalar or vector giving the index number of elements to</span>
<span class="comments">;               be removed from vectors.  Duplicate entries in index are</span>
<span class="comments">;               ignored.    An error will occur if one attempts to remove</span>
<span class="comments">;               all the elements of a vector.     REMOVE will return quietly</span>
<span class="comments">;               (no error message) if index is !NULL or undefined.</span>
<span class="comments">;</span>
<span class="comments">; INPUT-OUTPUT:</span>
<span class="comments">;       v1 - Vector or array.  Elements specifed by INDEX will be </span>
<span class="comments">;               removed from v1.  Upon return v1 will contain</span>
<span class="comments">;               N fewer elements, where N is the number of distinct values in</span>
<span class="comments">;               INDEX.</span>
<span class="comments">;</span>
<span class="comments">; OPTIONAL INPUT-OUTPUTS:</span>
<span class="comments">;       v2,v3,...v25 - additional vectors containing</span>
<span class="comments">;               the same number of elements as v1.  These will be</span>
<span class="comments">;               contracted in the same manner as v1.</span>
<span class="comments">;</span>
<span class="comments">; EXAMPLES:</span>
<span class="comments">;       (1) If INDEX = [2,4,6,4] and V = [1,3,4,3,2,5,7,3] then after the call</span>
<span class="comments">;</span>
<span class="comments">;               IDL> remove,index,v      </span>
<span class="comments">;</span>
<span class="comments">;       V will contain the values [1,3,3,5,3]</span>
<span class="comments">;</span>
<span class="comments">;       (2) Suppose one has a wavelength vector W, and three associated flux</span>
<span class="comments">;       vectors F1, F2, and F3.    Remove all points where a quality vector,</span>
<span class="comments">;       EPS is negative</span>
<span class="comments">;</span>
<span class="comments">;               IDL> bad = where( EPS LT 0, Nbad)</span>
<span class="comments">;               IDL> if Nbad GT 0 then remove, bad, w, f1, f2, f3</span>
<span class="comments">;</span>
<span class="comments">; METHOD:</span>
<span class="comments">;       If more than one element is to be removed, then HISTOGRAM is used</span>
<span class="comments">;       to generate a 'keep' subscripting vector.    To minimize the length of </span>
<span class="comments">;       the subscripting vector, it is only computed between the minimum and </span>
<span class="comments">;       maximum values of the index.   Therefore, the slowest case of REMOVE</span>
<span class="comments">;       is when both the first and last element are removed.</span>
<span class="comments">;</span>
<span class="comments">; REVISION HISTORY:</span>
<span class="comments">;       Written W. Landsman        ST Systems Co.       April 28, 1988</span>
<span class="comments">;       Cleaned up code          W. Landsman            September, 1992</span>
<span class="comments">;       Major rewrite for improved speed   W. Landsman    April 2000</span>
<span class="comments">;       Accept up to 25 variables, use SCOPE_VARFETCH internally</span>
<span class="comments">;              W. Landsman   Feb 2010</span>
<span class="comments">;       Fix occasional integer overflow problem  V. Geers  Feb 2011</span>
<span class="comments">;       Quietly return if index is !null or undefined W.L. Aug 2011</span>
<span class="comments">;             </span>
<span class="comments">;-</span>
 On_error,2
 compile_opt idl2,strictarrsubs

 npar = N_params()
 nvar = npar-1
 if npar LT 2 then begin
      print,'Syntax - remove, index, v1, [v2, v3, v4,..., v25]'
      return
 endif

 if N_elements(index) EQ 0 then return

  vv = 'v' + strtrim( indgen(nvar)+1, 2) 
  npts = N_elements(v1)
   
  max_index = max(index, MIN = min_index)

 if ( min_index LT 0 ) || (max_index GT npts-1) then message, $
             'ERROR - Index vector is out of range'

 if ( max_index Eq min_index ) then begin   <span class="comments">;Remove only 1 element?</span>
     Ngood = 0  
    if npts EQ 1 then message, $ 
         'ERROR - Cannot delete all elements from a vector'
  endif else begin 
         

<span class="comments">;  Begin case where more than 1 element is to be removed.   Use HISTOGRAM</span>
<span class="comments">;  to determine then indices to keep</span>

 nhist = max_index - min_index +1 

 hist = histogram( index)      <span class="comments">;Find unique index values to remove</span>
 keep = where( hist EQ 0, Ngood ) + min_index

 if ngood EQ 0 then begin 
    if ( npts LE nhist ) then message, $
          'ERROR - Cannot delete all elements from a vector'
  endif 
 endelse

 imin = min_index - 1
 imax = max_index + 1
 i0 = (min_index EQ 0) + 2*(max_index EQ npts-1) 
 case i0 of 
 3: begin
    for i=0, nvar-1 do  $
         (SCOPE_VARFETCH(vv[i],LEVEL=0)) = $
	 (SCOPE_VARFETCH(vv[i],LEVEL=0))[keep]
     return	 
     end

 1:  ii = Ngood EQ 0 ? imax + lindgen(npts-imax) : $
                      [keep, imax + lindgen(npts-imax) ]
 2:  ii = Ngood EQ 0 ? lindgen(imin+1)               :  $
                       [lindgen(imin+1), keep ]
 0:   ii = Ngood EQ 0 ? [lindgen(imin+1), imax + lindgen(npts-imax) ]  : $
                      [lindgen(imin+1), keep, imax + lindgen(npts-imax) ]
 endcase 

      for i=0,nvar-1 do  $
         (SCOPE_VARFETCH(vv[i],LEVEL=0)) =    $
	        (SCOPE_VARFETCH(vv[i],LEVEL=0))[ii]
 
 return
 end
</code>
    </div>
  </body>
</html>