<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN"
 "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">

<!-- Generated by IDLdoc 3.5.1 on Mon Sep 30 16:57:29 2013 -->

<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en">
  <head>
    <meta http-equiv="Content-Type" content="text/html;charset=utf-8"/>
    <title>extast.pro (Documentation for ./)</title>

    
    <link rel="stylesheet" type="text/css" media="all"
          href="../idldoc-resources/main.css" />
    <link rel="stylesheet" type="text/css" media="print"
          href="../idldoc-resources/main-print.css" />
    

    <script type="text/javascript">
      function setTitle() {
        parent.document.title="extast.pro (Documentation for ./)";
      }
    </script>
  </head>

  <body onload="setTitle();" id="root">
    <div class="content">
      <code class="source"><a id="extast:source"></a>pro extast,hdr,astr,noparams, alt=alt
<span class="comments">;+</span>
<span class="comments">; NAME:</span>
<span class="comments">;     EXTAST</span>
<span class="comments">; PURPOSE:</span>
<span class="comments">;     Extract ASTrometry parameters from a FITS image header.</span>
<span class="comments">; EXPLANATION:</span>
<span class="comments">;     Extract World Coordinate System information </span>
<span class="comments">;     ( http://fits.gsfc.nasa.gov/fits_wcs.html ) from a FITS header and </span>
<span class="comments">;     place it into an IDL structure.</span>
<span class="comments">;</span>
<span class="comments">; CALLING SEQUENCE:</span>
<span class="comments">;     EXTAST, hdr, [ astr, noparams, ALT= ]   </span>
<span class="comments">;</span>
<span class="comments">; INPUT:</span>
<span class="comments">;     HDR - variable containing the FITS header (string array)</span>
<span class="comments">;</span>
<span class="comments">; OUTPUTS:</span>
<span class="comments">;     ASTR - Anonymous structure containing astrometry info from the FITS </span>
<span class="comments">;             header ASTR always contains the following tags (even though </span>
<span class="comments">;             some projections do not require all the parameters)</span>
<span class="comments">;       .NAXIS - 2 element array giving image size</span>
<span class="comments">;      .CD   -  2 x 2 array containing the astrometry parameters CD1_1 CD1_2</span>
<span class="comments">;               in DEGREES/PIXEL                                 CD2_1 CD2_2</span>
<span class="comments">;      .CDELT - 2 element double vector giving physical increment at the </span>
<span class="comments">;                 reference pixel</span>
<span class="comments">;      .CRPIX - 2 element double vector giving X and Y coordinates of reference </span>
<span class="comments">;               pixel (def = NAXIS/2) in FITS convention (first pixel is 1,1)</span>
<span class="comments">;      .CRVAL - 2 element double precision vector giving R.A. and DEC of </span>
<span class="comments">;             reference pixel in DEGREES</span>
<span class="comments">;      .CTYPE - 2 element string vector giving projection types, default</span>
<span class="comments">;             ['RA---TAN','DEC--TAN']</span>
<span class="comments">;      .LONGPOLE - scalar giving native longitude of the celestial pole </span>
<span class="comments">;             (default = 180 for zenithal projections) </span>
<span class="comments">;      .LATPOLE - scalar giving native latitude of the celestial pole default=0)</span>
<span class="comments">;      .PV2 - Vector of projection parameter associated with latitude axis</span>
<span class="comments">;             PV2 will have up to 21 elements for the ZPN projection, up to 3 </span>
<span class="comments">;             for the SIN projection and no more than 2 for any other </span>
<span class="comments">;             projection  </span>
<span class="comments">;      .DISTORT - optional substructure specifying any distortion parameters</span>
<span class="comments">;                 currently implemented only for "SIP" (Spitzer Imaging </span>
<span class="comments">;                 Polynomial) distortion parameters</span>
<span class="comments">;</span>
<span class="comments">;       NOPARAMS -  Scalar indicating the results of EXTAST</span>
<span class="comments">;             -1 = Failure - Header missing astrometry parameters</span>
<span class="comments">;             1 = Success - Header contains CROTA + CDELT (AIPS-type) astrometry</span>
<span class="comments">;             2 = Success - Header contains CDn_m astrometry, rec.    </span>
<span class="comments">;             3 = Success - Header contains PCn_m + CDELT astrometry. </span>
<span class="comments">;             4 = Success - Header contains ST  Guide Star Survey astrometry</span>
<span class="comments">;                           (see gsssextast.pro )</span>
<span class="comments">; OPTIONAL INPUT/OUTPUT KEYWORDS:</span>
<span class="comments">;       ALT -  single character 'A' through 'Z' or ' ' specifying an alternate </span>
<span class="comments">;              astrometry system present in the FITS header.    The default is</span>
<span class="comments">;              to use the primary astrometry or ALT = ' '.   If /ALT is set, </span>
<span class="comments">;              then this is equivalent to ALT = 'A'.   See Section 3.3 of </span>
<span class="comments">;              Greisen & Calabretta (2002, A&A, 395, 1061) for information about</span>
<span class="comments">;              alternate astrometry keywords.    If not set on input, then</span>
<span class="comments">;              ALT is set to ' ' on output.</span>
<span class="comments">; PROCEDURE:</span>
<span class="comments">;       EXTAST checks for astrometry parameters in the following order:</span>
<span class="comments">;</span>
<span class="comments">;       (1) the CD matrix PC1_1,PC1_2...plus CDELT*, CRPIX and CRVAL</span>
<span class="comments">;       (2) the CD matrix CD1_1,CD1_2... plus CRPIX and CRVAL.   </span>
<span class="comments">;       (3) CROTA2 (or CROTA1) and CDELT plus CRPIX and CRVAL.</span>
<span class="comments">;</span>
<span class="comments">;       All three forms are valid FITS according to the paper "Representations </span>
<span class="comments">;       of World Coordinates in FITS by Greisen and Calabretta (2002, A&A, 395,</span>
<span class="comments">;       1061 http://fits.gsfc.nasa.gov/fits_wcs.html ) although form (1) is </span>
<span class="comments">;       preferred.</span>
<span class="comments">;</span>
<span class="comments">; NOTES:</span>
<span class="comments">;       1.  An anonymous structure is created to avoid structure definition</span>
<span class="comments">;       conflicts.    This is needed because some projection systems</span>
<span class="comments">;       require additional dimensions (i.e. spherical cube</span>
<span class="comments">;       projections require a specification of the cube face).</span>
<span class="comments">;</span>
<span class="comments">;       2,   Some FITS headers (e.g.from HST/ACS) include SIP forward distortion</span>
<span class="comments">;       coefficients but do not include the reverse coefficients.   Currently,</span>
<span class="comments">;       EXTAST only gives a warning that the reverse coefficients (RA,Dec to</span>
<span class="comments">;       X,Y) are not present.   EXTAST should actually compute </span>
<span class="comments">;       the inverse coefficients, but this is not yet implemented..</span>
<span class="comments">; PROCEDURES CALLED:</span>
<span class="comments">;      GSSSEXTAST, ZPARCHECK</span>
<span class="comments">; REVISION HISTORY</span>
<span class="comments">;      Written by B. Boothman 4/15/86</span>
<span class="comments">;      Accept CD001001 keywords               1-3-88</span>
<span class="comments">;      Accept CD1_1, CD2_1... keywords    W. Landsman    Nov. 92</span>
<span class="comments">;      Recognize GSSS FITS header         W. Landsman    June 94</span>
<span class="comments">;      Get correct sign, when converting CDELT* to CD matrix for right-handed</span>
<span class="comments">;      coordinate system                  W. Landsman   November 1998</span>
<span class="comments">;      Consistent conversion between CROTA and CD matrix  October 2000</span>
<span class="comments">;      CTYPE = 'PIXEL' means no astrometry params  W. Landsman January 2001</span>
<span class="comments">;      Don't choke if only 1 CTYPE value given W. Landsman  August 2001</span>
<span class="comments">;      Recognize PC00n00m keywords again (sigh...)  W. Landsman December 2001</span>
<span class="comments">;      Recognize GSSS in ctype also       D. Finkbeiner Jan 2002</span>
<span class="comments">;      Introduce ALT keyword              W. Landsman June 2003</span>
<span class="comments">;      Fix error introduced June 2003 where free-format values would be</span>
<span class="comments">;      truncated if more than 20 characters.  W. Landsman Aug 2003</span>
<span class="comments">;      Further fix to free-format values -- slash need not be present Sep 2003</span>
<span class="comments">;      Default value of LATPOLE is 90.0  W. Landsman February 2004</span>
<span class="comments">;      Allow for distortion substructure, currently implemented only for</span>
<span class="comments">;          SIP (Spitzer Imaging Polynomial)   W. Landsman February 2004 </span>
<span class="comments">;      Correct LONGPOLE computation if CTYPE = ['*DEC','*RA'] W. L. Feb. 2004</span>
<span class="comments">;      Assume since V5.3 (vector STRMID)  W. Landsman Feb 2004</span>
<span class="comments">;      Yet another fix to free-format values   W. Landsman April 2004</span>
<span class="comments">;      Introduce PV2 tag to replace PROJP1, PROJP2.. etc.  W. Landsman May 2004</span>
<span class="comments">;      Convert NCP projection to generalized SIN   W. Landsman Aug 2004</span>
<span class="comments">;      Add NAXIS tag to output structure  W. Landsman Jan 2007</span>
<span class="comments">;      .CRPIX tag now Double instead of Float   W. Landsman  Apr 2007</span>
<span class="comments">;      If duplicate keywords use the *last* value W. Landsman Aug 2008</span>
<span class="comments">;      Fix typo for AZP projection, nonzero longpole N. Cunningham Feb 2009</span>
<span class="comments">;      Give warning if reverse SIP coefficient not present  W. Landsman Nov 2011</span>
<span class="comments">;      Allow obsolete CD matrix representations W. Landsman May 2012</span>
<span class="comments">;-</span>
 On_error,2
 compile_opt idl2

 if ( N_params() LT 2 ) then begin
     print,'Syntax - EXTAST, hdr, astr, [ noparams, ALT = ]'
     return
 endif

 proj0 = ['CYP','CEA','CAR','MER','SFL','PAR','MOL','AIT','BON','PCO', $
          'TSC','CSC','QSC']
 radeg = 180.0D0/!DPI
 keyword = strtrim(strmid( hdr, 0, 8), 2)

<span class="comments">; Extract values from the FITS header.   This is either up to the first slash</span>
<span class="comments">; (free format) or first space</span>

 space = strpos( hdr, ' ', 10) + 1
 slash = strpos( hdr, '/', 10)  > space
 
 N = N_elements(hdr)
 len = (slash -10) > 20
 len = reform(len,1,N)
 lvalue = strtrim(strmid(hdr, 10, len),2)
 zparcheck,'EXTAST',hdr,1,7,1,'FITS image header'   <span class="comments">;Make sure valid header</span>
 noparams = -1                                    <span class="comments">;Assume no astrometry to start</span>

 if N_elements(alt) EQ 0 then alt = '' else if (alt EQ '1') then alt = 'A' $
    else alt = strupcase(alt)
 naxis = lonarr(2) 
 l = where(keyword EQ 'NAXIS1',  N_ctype1)
 if N_ctype1 GT 0 then naxis[0] = lvalue[l[N_ctype1-1]]
 l = where(keyword EQ 'NAXIS2',  N_ctype2)
 if N_ctype2 GT 0 then naxis[1] = lvalue[l[N_ctype2-1]]
  
 ctype = ['','']
 l = where(keyword EQ 'CTYPE1'+alt,  N_ctype1)
 if N_ctype1 GT 0 then ctype[0] = lvalue[l[N_ctype1-1]]
 l = where(keyword EQ 'CTYPE2'+alt,  N_ctype2)
 if N_ctype2 GT 0 then ctype[1] = lvalue[l[N_ctype2-1]]
 remchar,ctype,"'"
 ctype = strtrim(ctype,2)

<span class="comments">; If the standard CTYPE* astrometry keywords not found, then check if the</span>
<span class="comments">; ST guidestar astrometry is present</span>

 check_gsss = (N_ctype1 EQ 0)
 if N_ctype1 GE 1  then check_gsss = (strmid(ctype[0], 5, 3) EQ 'GSS')

 if check_gsss then begin

        l = where(keyword EQ 'PPO1'+alt,  N_ppo1)
        if N_ppo1 EQ 1 then begin 
                gsssextast, hdr, astr, gsssparams
                if gsssparams EQ 0 then noparams = 4
                return
        endif
        ctype = ['RA---TAN','DEC--TAN']
  endif

  if (ctype[0] EQ 'PIXEL') then return
  if N_ctype2 EQ 1 then if (ctype[1] EQ 'PIXEL') then return

 crval = dblarr(2)

 l = where(keyword EQ 'CRVAL1'+alt,  N_crval1)
 if N_crval1 GT 0 then crval[0] = lvalue[l[N_crval1-1]]
 l = where(keyword EQ 'CRVAL2'+alt,  N_crval2)
 if N_crval2 GT 0 then crval[1] = lvalue[l[N_crval2-1]]
 if (N_crval1 EQ 0) || (N_crval2 EQ 0) then return  

 crpix = dblarr(2)
 l = where(keyword EQ 'CRPIX1'+alt,  N_crpix1)
 if N_crpix1 GT 0 then crpix[0] = lvalue[l[N_crpix1-1]]
 l = where(keyword EQ 'CRPIX2'+alt,  N_crpix2)
 if N_crpix2 GT 0 then crpix[1] = lvalue[l[N_crpix2-1]]
 if (N_crpix1 EQ 0) || (N_crpix2 EQ 0) then return  


 cd = dblarr(2,2)
cdelt = [1.0d,1.0d]
GET_CD_MATRIX:

 l = where(keyword EQ 'PC1_1' + alt,  N_pc11) 
 if N_PC11 GT 0 then begin 
        cd[0,0]  = lvalue[l]
        l = where(keyword EQ 'PC1_2' + alt,  N_pc12) 
        if N_pc12 GT 0 then cd[0,1]  = lvalue[l[N_pc12-1]]
        l = where(keyword EQ 'PC2_1' + alt,  N_pc21) 
        if N_pc21 GT 0 then cd[1,0]  = lvalue[l[N_pc21-1]]
        l = where(keyword EQ 'PC2_2' + alt,  N_pc22) 
        if N_pc22 GT 0 then cd[1,1]  = lvalue[l[N_pc22-1]]
         l = where(keyword EQ 'CDELT1' + alt,  N_cdelt1) 
        if N_cdelt1 GT 0 then cdelt[0]  = lvalue[l[N_cdelt1-1]]
        l = where(keyword EQ 'CDELT2' + alt,  N_cdelt2) 
        if N_cdelt2 GT 0 then cdelt[1]  = lvalue[l[N_cdelt2-1]]
        noparams = 3
 endif else begin 

    l = where(keyword EQ 'CD1_1' + alt,  N_cd11) 
     if N_CD11 GT 0 then begin        <span class="comments">;If CD parameters don't exist, try CROTA</span>
        cd[0,0]  = strtrim(lvalue[l[N_cd11-1]],2)
        l = where(keyword EQ 'CD1_2' + alt,  N_cd12) 
        if N_cd12 GT 0 then cd[0,1]  = lvalue[l[N_cd12-1]]
        l = where(keyword EQ 'CD2_1' + alt,  N_cd21) 
        if N_cd21 GT 0 then cd[1,0]  = lvalue[l[N_cd21-1]]
        l = where(keyword EQ 'CD2_2' + alt,  N_cd22) 
        if N_cd22 GT 0 then cd[1,1]  = lvalue[l[N_cd22-1]]
        noparams = 2
    endif else begin

<span class="comments">; Now get rotation, first try CROTA2, if not found try CROTA1, if that</span>
<span class="comments">; not found assume North-up.   Then convert to CD matrix - see Section 5 in</span>
<span class="comments">; Greisen and Calabretta</span>

        l = where(keyword EQ 'CDELT1' + alt,  N_cdelt1) 
        if N_cdelt1 GT 0 then cdelt[0]  = lvalue[l[N_cdelt1-1]]
        l = where(keyword EQ 'CDELT2' + alt,  N_cdelt2) 
        if N_cdelt2 GT 0 then cdelt[1]  = lvalue[l[N_cdelt2-1]]
        if (N_cdelt1 EQ 0) || (N_Cdelt2 EQ 0) then return   <span class="comments">;Must have CDELT1 and CDELT2</span>

        l = where(keyword EQ 'CROTA2' + alt,  N_crota) 
        if N_Crota EQ 0 then $
            l = where(keyword EQ 'CROTA1' + alt,  N_crota) 
        if N_crota EQ 0 then begin
	      l = where(keyword EQ 'PC001001', N_PC00)
	      l = where(keyword EQ 'CD001001', N_CD00)
	      if (N_PC00 GT 0) || (N_CD00 GT 0) then begin
	          message,'Updating obsolete CD matrix representation',/INF
		  FITS_CD_FIX, hdr
		  keyword = strtrim(strmid(hdr,0,8),2)
		  goto, GET_CD_MATRIX
	      endif else crota = 0.0d 
	 endif else crota = double(lvalue[l[N_crota-1]])/RADEG
        cd = [ [cos(crota), -sin(crota)],[sin(crota), cos(crota)] ] 
 
       noparams = 1           <span class="comments">;Signal AIPS-type astrometry found</span>
     
  endelse
  endelse

  
  proj = strmid( ctype[0], 5, 3)
  case proj of 
 'ZPN': npv = 21
 'SZP': npv = 3
 else:  npv = 2
  endcase

  index = proj EQ 'ZPN' ? strtrim(indgen(npv),2) : strtrim(indgen(npv)+1,2)
      pv2 = dblarr(npv)
      for i=0,npv-1 do begin 
      l = where(keyword EQ 'PV2_' + index[i] + alt,  N_pv2)
      if N_pv2 GT 0 then pv2[i] = lvalue[l[N_pv2-1]] 
      endfor
 
          
  l = where(keyword EQ 'PV1_3' + alt,  N_pv1_3)
  if N_pv1_3 GT 0 then  longpole = double(lvalue[l[N_pv1_3-1]]) else begin
      l = where(keyword EQ 'LONPOLE' + alt,  N_lonpole)
      if N_lonpole GT 0 then  longpole = double(lvalue[l[N_lonpole-1]]) 
  endelse

<span class="comments">; If LONPOLE (or PV1_3) is not defined in the header, then we must determine </span>
<span class="comments">; its default value.    This depends on the value of theta0 (the native</span>
<span class="comments">; longitude of the fiducial point) of the particular projection)</span>

  conic = (proj EQ 'COP') || (proj EQ 'COE') || (proj EQ 'COD') || $
          (proj EQ 'COO')


  if N_elements(longpole) EQ 0 then  begin 
    if conic then begin 
      if N_pv2 EQ 0 then message, $
     'ERROR -- Conic projections require a PV2_1 keyword in FITS header' else $
      theta0 = PV2[0]
    endif else if (proj EQ 'AZP') || (proj EQ 'SZP') || (proj EQ 'TAN') || $
          (proj EQ 'STG') || (proj EQ 'SIN') || (proj EQ 'ARC') || $
          (proj EQ 'ZPN') || (proj EQ 'ZEA') || (proj EQ 'AIR') then begin
       theta0 = 90.0
    endif else theta0 = 0. 
    celcoord = strmid(ctype[1],0,4)
<span class="comments">;Check to see if RA and DEC are reversed in CRVAL</span>
    if (celcoord EQ 'RA--') || (celcoord EQ 'GLON') || (celcoord EQ 'ELON') $
           then cellat = crval[0] else cellat = crval[1]
    if cellat GE theta0 then longpole = 0.0 else longpole = 180.0
 endif

  l = where(keyword EQ 'LATPOLE' + alt,  N_latpole)
  if N_latpole GT 0 then  latpole = double(lvalue[l[0]]) else latpole = 90.0d


<span class="comments">; Convert NCP projection to generalized SIN projection (see Section 6.1.2 of </span>
<span class="comments">; Calabretta and Greisen (2002)</span>

  if proj EQ 'NCP' then begin
       ctype = repstr(ctype,'NCP','SIN')
       PV2 = [0., 1/tan(crval[1]/radeg) ]
       longpole = 180.0
  endif 

<span class="comments">; Note that the dimensions and datatype of each tag must be explicit, so that</span>
<span class="comments">; there is no conflict with structure definitions from different FITS headers</span>

  ASTR = {NAXIS:naxis, CD: cd, CDELT: cdelt, $
                CRPIX: crpix, CRVAL:crval, $
                CTYPE: string(ctype), LONGPOLE: double( longpole[0]),  $
                LATPOLE: double(latpole[0]), PV2: pv2 }

<span class="comments">; Check for any distortion keywords</span>

  if strlen(ctype[0]) GE 12 then begin
       distort_flag = strmid(ctype[0],9,3)
       case distort_flag of 
       'SIP': begin
              l = where(keyword EQ 'A_ORDER',  N) 
              if N GT 0 then a_order  = lvalue[l[N-1]] else a_order = 0
              l = where(keyword EQ 'B_ORDER',  N) 
              if N GT 0 then b_order  = lvalue[l[N-1]] else b_order = 0
              l = where(keyword EQ 'AP_ORDER',  N) 
              if N GT 0 then ap_order  = lvalue[l[N-1]] else ap_order = 0
              l = where(keyword EQ 'BP_ORDER',  N) 
              if N GT 0 then bp_order  = lvalue[l[N-1]] else bp_order = 0
  a = fltarr(a_order+1,a_order+1) & b = fltarr(b_order+1,b_order+1) 
  ap = fltarr(ap_order+1,ap_order+1) &  bp = fltarr(bp_order+1,bp_order+1)

  for i=0, a_order do begin
    for j=0, a_order do begin
     l = where(keyword EQ 'A_' + strtrim(i,2) + '_' + strtrim(j,2), N)
     if N GT 0 then a[i,j] = lvalue[l[N-1]]
  endfor & endfor

   for i=0, b_order  do begin
    for j=0, b_order do begin
     l = where(keyword EQ 'B_' + strtrim(i,2) + '_' + strtrim(j,2), N)
     if N GT 0 then b[i,j] = lvalue[l[N-1]]
  endfor & endfor

   for i=0, bp_order do begin
    for j=0, bp_order do begin
     l = where(keyword EQ 'BP_' + strtrim(i,2) + '_' + strtrim(j,2), N)
     if N GT 0 then bp[i,j] = lvalue[l[N-1]]
  endfor & endfor

    if ap_order EQ 0 then message,/CON, $
        'WARNING - Inverse SIP coefficients not present in FITS header'
    for i=0, ap_order do begin
    for j=0, ap_order do begin
     l = where(keyword EQ 'AP_' + strtrim(i,2) + '_' + strtrim(j,2), N)
     if N GT 0 then ap[i,j] = lvalue[l[N-1]]
  endfor & endfor
   
  distort = {name:distort_flag, a:a, b:b, ap:ap, bp:bp}
  astr = create_struct(temporary(astr), 'distort', distort)
  end
  else: message,/con,'Unrecognized distortion acronym: ' + distort_flag 
  endcase
  endif
  return
  end
</code>
    </div>
  </body>
</html>