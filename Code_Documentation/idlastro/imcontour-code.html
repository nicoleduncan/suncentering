<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN"
 "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">

<!-- Generated by IDLdoc 3.5.1 on Mon Sep 30 16:57:42 2013 -->

<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en">
  <head>
    <meta http-equiv="Content-Type" content="text/html;charset=utf-8"/>
    <title>imcontour.pro (Documentation for ./)</title>

    
    <link rel="stylesheet" type="text/css" media="all"
          href="../idldoc-resources/main.css" />
    <link rel="stylesheet" type="text/css" media="print"
          href="../idldoc-resources/main-print.css" />
    

    <script type="text/javascript">
      function setTitle() {
        parent.document.title="imcontour.pro (Documentation for ./)";
      }
    </script>
  </head>

  <body onload="setTitle();" id="root">
    <div class="content">
      <code class="source"><a id="imcontour:source"></a>pro imcontour, im, hdr, TYPE=type, PUTINFO=putinfo, XTITLE=xtitle,  $
      YTITLE=ytitle, SUBTITLE = subtitle, XDELTA = xdelta, YDELTA = ydelta, $
      _EXTRA = extra, XMID = xmid, YMID = ymid, OVERLAY = OVERLAY, $
       NOerase = noerase,window=window
<span class="comments">;+</span>
<span class="comments">; NAME:</span>
<span class="comments">;       IMCONTOUR</span>
<span class="comments">; PURPOSE:</span>
<span class="comments">;       Make a contour plot labeled with astronomical coordinates.</span>
<span class="comments">; EXPLANATION:</span>
<span class="comments">;       The type of coordinate display is controlled by the keyword TYPE</span>
<span class="comments">;       Set TYPE=0 (default) to measure distances from the center of the image</span>
<span class="comments">;       (IMCONTOUR will decide whether the plotting units will be in</span>
<span class="comments">;       arc seconds, arc minutes, or degrees depending on image size.)</span>
<span class="comments">;       Set /TYPE for standard RA and Dec labeling</span>
<span class="comments">;</span>
<span class="comments">;       By using the /NODATA keyword, IMCONTOUR can also be used to simply</span>
<span class="comments">;       provide astronomical labeling of a previously displayed image.</span>
<span class="comments">; CALLING SEQUENCE</span>
<span class="comments">;       IMCONTOUR, im, hdr,[ /TYPE, /PUTINFO, XDELTA = , YDELTA =, _EXTRA = </span>
<span class="comments">;                            XMID=, YMID= ]</span>
<span class="comments">;</span>
<span class="comments">; INPUTS:</span>
<span class="comments">;       IM - 2-dimensional image array</span>
<span class="comments">;       HDR - FITS header associated with IM, string array, must include</span>
<span class="comments">;               astrometry keywords.   IMCONTOUR will also look for the</span>
<span class="comments">;               OBJECT and IMAGE keywords, and print these if found and the </span>
<span class="comments">;               PUTINFO keyword is set.</span>
<span class="comments">;</span>
<span class="comments">; OPTIONAL PLOTTING KEYWORDS:</span>
<span class="comments">;       /TYPE - the type of astronomical labeling to be displayed.   Either set</span>
<span class="comments">;               TYPE = 0 (default), distance to center of the image is</span>
<span class="comments">;               marked in units of Arc seconds, arc minutes, or degrees</span>
<span class="comments">;</span>
<span class="comments">;               TYPE = 1 astronomical labeling with Right ascension and </span>
<span class="comments">;               declination.</span>
<span class="comments">;</span>
<span class="comments">;       /PUTINFO - If set, then IMCONTOUR will add information about the image</span>
<span class="comments">;               to the right of the contour plot.  Information includes image</span>
<span class="comments">;               name, object, image center, image center, contour levels, and</span>
<span class="comments">;               date plot was made</span>
<span class="comments">;</span>
<span class="comments">;       XDELTA, YDELTA - Integer scalars giving spacing of labels for TYPE=1.  </span>
<span class="comments">;               Default is to label every major tick (XDELTA=1) but if </span>
<span class="comments">;               crowding occurs, then the user might wish to label every other</span>
<span class="comments">;               tick (XDELTA=2) or every third tick (XDELTA=3)</span>
<span class="comments">;</span>
<span class="comments">;       XMID, YMID - Scalars giving the X,Y position from which offset distances</span>
<span class="comments">;               will be measured when TYPE=0.   By default, offset distances </span>
<span class="comments">;               are measured from the center of the image.</span>
<span class="comments">;       /OVERLAY - If set, then IMCONTOUR is assumed to overlay an image.</span>
<span class="comments">;               This requires 1 extra pixel be included on the X and Y axis,</span>
<span class="comments">;               to account for edge effects in the image display.    Setting</span>
<span class="comments">;               OVERLAY provide a better match of the contour and underlying</span>
<span class="comments">;               image but is not as aesthetically pleasing because the contours</span>
<span class="comments">;               will not extend to the axes. </span>
<span class="comments">;               </span>
<span class="comments">;</span>
<span class="comments">;       Any keyword accepted by CONTOUR may also be passed through IMCONTOUR</span>
<span class="comments">;       since IMCONTOUR uses the _EXTRA facility.     IMCONTOUR uses its own</span>
<span class="comments">;       defaults for the XTITLE, YTITLE XMINOR, YMINOR, and SUBTITLE keywords</span>
<span class="comments">;       but these may be overridden.    Note in particular the /NODATA keyword</span>
<span class="comments">;       which can be used if imcontour.pro is to only provide labeling.</span>
<span class="comments">;</span>
<span class="comments">; NOTES:</span>
<span class="comments">;       (1) The contour plot will have the same dimensional ratio as the input</span>
<span class="comments">;           image array</span>
<span class="comments">;       (2) To contour a subimage, use HEXTRACT before calling IMCONTOUR</span>
<span class="comments">;       (3) Use the /NODATA keyword to simply provide astronomical labeling</span>
<span class="comments">;           of a previously displayed image.</span>
<span class="comments">;       (4) The IMCONTOUR display currently does not indicate the image </span>
<span class="comments">;           rotation in any way, but only specifies coordinates along the </span>
<span class="comments">;           edges of the image </span>
<span class="comments">;</span>
<span class="comments">; EXAMPLE:</span>
<span class="comments">;       Overlay the contour of an image, im2, with FITS header, h2, on top</span>
<span class="comments">;       of the display of a different image, im1.   Use RA, Dec labeling, and</span>
<span class="comments">;       seven equally spaced contour levels.    The use of a program like</span>
<span class="comments">;       David Fanning's cgImage  http://www.idlcoyote.com/programs/cgimage.pro</span>
<span class="comments">;       is suggested to properly overlay plotting and image coordinates.  The</span>
<span class="comments">;       /Keep_aspect_ratio keyword must be used.</span>
<span class="comments">;</span>
<span class="comments">;       IDL> cgimage,im1,/keep_aspect, position = pos</span>
<span class="comments">;       IDL> imcontour,im2,h2,nlevels=7,/Noerase,/TYPE,position = pos</span>
<span class="comments">;</span>
<span class="comments">; PROCEDURES USED:</span>
<span class="comments">;       CHECK_FITS, EXTAST, GETROT, TICPOS, TICLABEL, TIC_ONE, TICS, XYAD</span>
<span class="comments">;       CONS_RA(), CONS_DEC(), ADSTRING()</span>
<span class="comments">;</span>
<span class="comments">; REVISION HISTORY:</span>
<span class="comments">;       Written   W. Landsman   STX                    May, 1989</span>
<span class="comments">;       Fixed RA,Dec labeling  W. Landsman             November, 1991</span>
<span class="comments">;       Fix plotting keywords  W.Landsman             July, 1992</span>
<span class="comments">;       Recognize GSSS headers  W. Landsman            July, 1994</span>
<span class="comments">;       Removed Channel keyword for V4.0 compatibility June, 1995</span>
<span class="comments">;       Add _EXTRA CONTOUR plotting keywords  W. Landsman  August, 1995</span>
<span class="comments">;       Add XDELTA, YDELTA keywords  W. Landsman   November, 1995</span>
<span class="comments">;       Use SYSTIME() instead of !STIME                August, 1997</span>
<span class="comments">;       Remove obsolete !ERR system variable W. Landsman   May 2000 </span>
<span class="comments">;       Added XMID, YMID keywords to specify central position (default is still</span>
<span class="comments">;          center of image)  W. Landsman               March 2002     </span>
<span class="comments">;       Recognize Galactic coordinates, fix Levels display when /PUTINFO set</span>
<span class="comments">;           W. Landsman                May 2003</span>
<span class="comments">;       Correct conversion from seconds of RA to arcmin is 4 not 15.</span>
<span class="comments">;       	M. Perrin					July 2003</span>
<span class="comments">;       Fix integer truncation which appears with tiny images WL  July 2004</span>
<span class="comments">;       Changed some keyword_set() to N_elements WL  Sep 2006</span>
<span class="comments">;       Work to 1 pixels level when overlaying an image,added /OVERLAY keyword</span>
<span class="comments">;        Use FORMAT_AXIS_VALUES()  W. Landsman   Jan 2008 </span>
<span class="comments">;       Make /OVERLAY  always optional   W. Landsman  Feb 2008</span>
<span class="comments">;       Check if RA crosses 0 hours  WL  Aug 2008</span>
<span class="comments">;       Use Coyote Graphics WL Feb 2011</span>
<span class="comments">;-</span>
  On_error,2                                 <span class="comments">;Return to caller</span>
  compile_opt idl2

  if N_params() LT 2 then begin             <span class="comments">;Sufficient parameters?</span>
      print,'Syntax - imcontour, im, hdr, [ /TYPE, /PUTINFO, XDELTA=, YDELT= '
      print,'                               XMID=, YMID = ]'
      print,'         Any CONTOUR keyword is also accepted by IMCONTOUR'  
     return
  endif

  <span class="comments">;Make sure header appropriate to image</span>
  check_fits, im, hdr, dimen, /NOTYPE, ERRMSG = errmsg    
  if errmsg NE '' then message,errmsg

<span class="comments">; Set defaults if keywords not set</span>

  if ~keyword_set( TYPE ) then type = 0
  if ~keyword_set( XDELTA ) then xdelta = 1
  if ~keyword_set( YDELTA ) then ydelta = 1
  
  if N_Elements(XMINOR) EQ 0 then $
       xminor = !X.MINOR EQ 0 ? 5 : !X.MINOR

  if N_Elements(YMINOR) EQ 0 then $
       yminor = !Y.MINOR EQ 0 ?  5 : !Y.MINOR

  EXTAST, hdr, astr, noparams      <span class="comments">;Extract astrometry from header</span>
  if noparams LT 0 then $                       <span class="comments">;Does astrometry exist?</span>
      message,'FITS header does not contain astrometry'
  if strmid( astr.ctype[0], 5, 3) EQ 'GSS' then begin
        hdr1 = hdr
        gsss_STDAST, hdr1
        extast, hdr1, astr, noparams
  endif
  sexig = strmid(astr.ctype[0],0,4) EQ 'RA--'
 
<span class="comments">; Adjust plotting window so that contour plot will have same dimensional </span>
<span class="comments">; ratio as the image</span>

  xlength = !D.X_VSIZE &  ylength = !D.Y_VSIZE
  xsize = fix( dimen[0] )  &   ysize = fix( dimen[1] )
  xsize1 = xsize-1 & ysize1 = ysize-1
     if keyword_set(OVERLAY) then begin 
         xran  = [0,xsize]-0.5  & yran = [0,ysize]-0.5
    endif else begin
         xran = [0,xsize1] & yran = [0,ysize1]	 
    endelse

 xratio = xsize / float(ysize)
  yratio = ysize / float(xsize)
  if N_elements(XMID) EQ 0 then xmid = (xran[1] -xran[0]-1)/2.
  if N_elements(YMID) EQ 0 then ymid = (yran[1] -yran[0]-1)/2.

  if ( ylength*xratio LT xlength ) then begin

    xmax = 0.15 + 0.8*ylength*xratio/xlength
    pos = [ 0.15, 0.15, xmax, 0.95 ]

  endif else begin

     xmax = 0.95
     pos = [ 0.15, 0.15, xmax, 0.15+ 0.8*xlength*yratio/ylength ]

  endelse

  xtics = !X.TICKS GT 0 ? abs(!X.TICKS) : 8
  ytics = !Y.TICKS GT 0 ? abs(!Y.TICKS) : 8
 
  pixx = float(xsize)/xtics            <span class="comments">;Number of X pixels between tic marks</span>
  pixy = float(ysize)/ytics            <span class="comments">;Number of Y pixels between tic marks</span>

  getrot,hdr,rot,cdelt               <span class="comments">;Get the rotation and plate scale</span>

  xyad,hdr,xsize1/2.,ysize1/2.,ra_cen,dec_cen         <span class="comments">;Get coordinates of image center</span>
  if sexig then ra_dec = adstring(ra_cen,dec_cen,1)       <span class="comments">;Make a nice string</span>

<span class="comments">; Determine tic positions and labels for the different type of contour plots</span>

  if type NE 0 then begin                  <span class="comments">;RA and Dec labeling</span>

     xedge = [ xran[0], xran[1], xran[0]]          <span class="comments">;X pixel values of the four corners</span>
     yedge = [ yran[0], yran[0], yran[1] ]          <span class="comments">;Y pixel values of the four corners</span>

     xy2ad, xedge, yedge, astr, a, d
 
     pixx = float(xmid*2)/xtics          <span class="comments">;Number of X pixels between tic marks</span>
     pixy = float(ymid*2)/ytics          <span class="comments">;Number of Y pixels between tic marks</span>

<span class="comments">; Find an even increment on each axis, for RA check crossing of 0 hours</span>
     case 1 of 
     ( a[1] GT a[0] ) and (cdelt[0] LT 0 ) : $ 
        tics, a[0], a[1] - 360.0d , xsize, pixx, raincr, RA=sexig 
     ( a[1] LT a[0] ) and (cdelt[0] GT 0 ) : $ 
        tics, a[0], 360.0d + a[1], xsize, pixx, raincr, RA=sexig 
     else: tics, a[0], a[1], xsize, pixx, raincr, RA=sexig 
     endcase
     tics, d[0], d[2], ysize, pixy, decincr    <span class="comments">;Find an even increment for Dec</span>

<span class="comments">; Find position of first tic on each axis</span>
     tic_one, a[0], pixx, raincr, botmin, xtic1, RA= sexig  <span class="comments">;Position of first RA tic</span>
     tic_one, d[0], pixy, decincr,leftmin,ytic1       <span class="comments">;Position of first Dec tic</span>

     nx = fix( (xsize1-xtic1)/pixx )             <span class="comments">;Number of X tic marks</span>
     ny = fix( (ysize1-ytic1)/pixy )             <span class="comments">;Number of Y tic marks</span>

     if sexig then ra_grid = (botmin + findgen(nx+1)*raincr/4.) else $ 
                   ra_grid = (botmin + findgen(nx+1)*raincr/60.)
     dec_grid = (leftmin + findgen(ny+1)*decincr/60.)

     ticlabels, botmin, nx+1, raincr, xlab, RA=sexig, DELTA=xdelta
     ticlabels, leftmin, ny+1, decincr, ylab,DELTA=ydelta

     xpos = cons_ra( ra_grid,0,astr )     <span class="comments">;Line of constant RA</span>
     ypos = cons_dec( dec_grid,0,astr)   <span class="comments">;Line of constant Dec</span>

     if sexig then begin 
        xunits = 'Right Ascension'
        yunits = 'Declination'
     endif else begin
        xunits = 'Longitude'
        yunits = 'Latitude'
     endelse                          

  endif else begin <span class="comments">; label with distance from center.</span>
     ticpos, xsize*cdelt[0], xsize, pixx, incrx, xunits     
     numx = fix((xmid-xran[0])/pixx)              <span class="comments">;Number of ticks from left edge</span>
     ticpos, ysize*cdelt[1], ysize, pixy, incry, yunits
     numy = fix((ymid-yran[0])/pixy)             <span class="comments">;Number of ticks from bottom to center</span>
     nx = numx + fix((xran[1]-xmid)/pixx)    <span class="comments">;Total number of X ticks </span>
     ny = numy + fix((yran[1]-ymid)/pixy)    <span class="comments">;Total number of Y ticks  </span>
     xpos = xmid  + (findgen(nx+1)-numx)*pixx
     ypos = ymid   + (findgen(ny+1)-numy)*pixy
     xlab = format_axis_values( indgen(nx+1)*incrx - incrx*numx)
     ylab = format_axis_values( indgen(ny+1)*incry - incry*numy)
    
     
  endelse

<span class="comments">; Get default values of XTITLE, YTITLE, TITLE and SUBTITLE</span>

  putinfo = keyword_set(PUTINFO)

  if N_elements(xtitle) EQ 0 then $
  xtitle = !X.TITLE eq ''? xunits : !X.TITLE

  if N_elements(ytitle) EQ 0 then $
        ytitle = !Y.TITLE eq ''? yunits : !Y.TITLE

  if (~keyword_set( SUBTITLE) ) && (putinfo LT 1) then $
      if sexig then $
      subtitle = 'Center:  R.A. '+ strmid(ra_dec,1,13)+'  Dec ' + $
               strmid(ra_dec,13,13) else $
     subtitle = 'Center:  Longitude '+ strtrim(string(ra_cen,'(f6.2)'),2) + $
                          ' Latitude ' + strtrim(string(dec_cen,'(f6.2)'),2)

  if N_elements( SUBTITLE)  EQ 0 then subtitle = !P.SUBTITLE
  cgContour,im, $
         XTICKS = nx, YTICKS = ny, POSITION=pos, XSTYLE=1, YSTYLE=1,$
         XTICKV = xpos, YTICKV = ypos, XTITLE=xtitle, YTITLE=ytitle, $
         XTICKNAME = xlab, YTICKNAME = ylab, SUBTITLE = subtitle, $
         XMINOR = xminor, YMINOR = yminor, _EXTRA = extra, XRAn=xran, $
	 YRAN = yran,noerase=noerase,WINDOW=window
	 
  
<span class="comments">;  Write info about the contour plot if desired</span>

  if putinfo GE 1 then begin

    sv = !D.NAME
    set_plot,'null'
    contour,im, _EXTRA = extra, PATH_INFO = info
    set_plot,sv


  if keyword_set(window) then cgcontrol, execute= 0	
   xmax = xmax + 0.01

     ypos = 0.92
     object = sxpar( hdr, 'OBJECT', Count = N_object )
     if N_object GT 0  then begin 
           cgText, xmax, ypos, object, /NORM, addcmd=window
           ypos = ypos-0.05
     endif

     name = sxpar( hdr, 'IMAGE', Count = N_image )
     if N_image GT 0 then begin 
           cgtext,xmax,ypos,name, /NORM, addcmd= window
           ypos = ypos - 0.05
     endif

     cgText, xmax, ypos,'Center:',/NORM, addcmd=window
     ypos = ypos - 0.05
     if sexig then begin
     cgText, xmax, ypos, 'R.A. '+ strmid(ra_dec,1,13),/NORM,addcmd=window
     cgText, xmax, ypos-0.05, 'Dec '+  strmid(ra_dec,13,13),/NORM,addcmd=window
     endif else begin
     cgText, xmax, ypos, 'Longitude: '+ strtrim(string(ra_cen,'(f6.2)'),2), $
             /NORM, addcmd=window
     cgText, xmax, ypos-0.05, addcmd=window, $
             'Latitude: '+  strtrim(string(dec_cen,'(f6.2)'),2),/NORM
     endelse
     ypos = ypos - 0.1
     cgText, xmax, ypos, 'Image Size', /NORM, addcmd=window
     cgText, xmax, ypos-0.05, 'X: ' + strtrim(xsize,2), /NORM, addcmd=window
     cgText, xmax, ypos-0.1, 'Y: ' + strtrim(ysize,2), /NORM, addcmd=window
     cgText, xmax, ypos- 0.15, strmid(systime(),4,20),/NORM, addcmd=window
     cgText, xmax, ypos - 0.2, 'Contour Levels:',/NORM, addcmd=window


    ypos = ypos - 0.25
    val = info.value
    val = val[uniq(val,sort(val))]
     nlevels = N_elements(val)
     for i = 0,(nlevels &lt<span class="comments">; 7)-1 do $</span>
          cgText,xmax,ypos-0.05*i,string(i,'(i2)') + ':' + $
                              string(val[i]), /NORM,addcmd=window
     if keyword_set(window) then cgcontrol, execute=1
 
  endif
  
  return                                          
  end                                         
</code>
    </div>
  </body>
</html>