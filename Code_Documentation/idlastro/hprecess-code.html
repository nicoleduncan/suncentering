<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN"
 "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">

<!-- Generated by IDLdoc 3.5.1 on Mon Sep 30 16:57:42 2013 -->

<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en">
  <head>
    <meta http-equiv="Content-Type" content="text/html;charset=utf-8"/>
    <title>hprecess.pro (Documentation for ./)</title>

    
    <link rel="stylesheet" type="text/css" media="all"
          href="../idldoc-resources/main.css" />
    <link rel="stylesheet" type="text/css" media="print"
          href="../idldoc-resources/main-print.css" />
    

    <script type="text/javascript">
      function setTitle() {
        parent.document.title="hprecess.pro (Documentation for ./)";
      }
    </script>
  </head>

  <body onload="setTitle();" id="root">
    <div class="content">
      <code class="source">PRO HPRECESS, HDR, YEARF                                      
<span class="comments">;+</span>
<span class="comments">; NAME:</span>
<span class="comments">;       HPRECESS</span>
<span class="comments">; PURPOSE:</span>
<span class="comments">;       Precess the astrometry in a FITS header to a new equinox</span>
<span class="comments">;</span>
<span class="comments">; CALLING SEQUENCE:</span>
<span class="comments">;       HPRECESS, HDR, [ yearf ]      </span>
<span class="comments">;</span>
<span class="comments">; INPUT-OUTPUT:</span>
<span class="comments">;       HDR - FITS Header, must contain the CRVAL astrometry keywords,</span>
<span class="comments">;               and either an EPOCH or EQUINOX keyword.</span>
<span class="comments">;               HDR will be modified to contain the precessed astrometry</span>
<span class="comments">;</span>
<span class="comments">; OPTIONAL INPUT:</span>
<span class="comments">;       YEARF - Scalar, giving the year of the new (Final) equinox.</span>
<span class="comments">;               If not supplied, user will be prompted for this value.</span>
<span class="comments">;</span>
<span class="comments">; METHOD:</span>
<span class="comments">;       The CRVAL and CD (or CROTA) keywords are extracted from the header </span>
<span class="comments">;       and precessed to the new equinox.  The EPOCH or EQUINOX keyword in </span>
<span class="comments">;       the header is  updated.  A HISTORY record is added</span>
<span class="comments">;</span>
<span class="comments">; RESTRICTIONS:</span>
<span class="comments">;       The FK5 reference frame is assumed for both equinoxes.</span>
<span class="comments">;</span>
<span class="comments">; PROCEDURES USED:</span>
<span class="comments">;       EXTAST, GET_EQUINOX(), SXADDPAR, SXADDHIST, PRECESS, PRECESS_CD</span>
<span class="comments">;       PUTAST, ZPARCHECK</span>
<span class="comments">; REVISION HISTORY:                                               </span>
<span class="comments">;       Written  W. Landsman        STX              July, 1988</span>
<span class="comments">;       CD matrix precessed -                        February, 1989</span>
<span class="comments">;       Update EQUINOX keyword when CROTA2 present   November, 1992</span>
<span class="comments">;       Recognize a GSSS header                      June, 1994</span>
<span class="comments">;       Additional Noparams value recognize for storing CDs.  RSH, 6 Apr 95</span>
<span class="comments">;       Understand reversed X,Y (X-Dec, Y-RA) axes,   W. Landsman  October 1998</span>
<span class="comments">;       Correct algorithm when CROTA2 is in header W. Landsman  April 2006</span>
<span class="comments">;       Correct sign error introduced April 2006, include CDELT values</span>
<span class="comments">;         when computing rotation of pole   W. Landsman July 2007</span>
<span class="comments">;       Call hprecess/jprecess for 1950&lt;>2000   W. L. Aug 2009</span>
<span class="comments">;-     </span>
 On_error, 2   
 compile_opt idl2
 
 if N_params() EQ 0 then begin       
        print,'Syntax - HPRECESS, hdr, [ yearf]'
        return   
 endif else zparcheck, 'HPRECESS', hdr, 1, 7, 1, 'FITS Header Array'

 yeari = GET_EQUINOX( hdr, code)    <span class="comments">;YEAR of Initial equinox</span>
 if code EQ -1 then $     
       message,'Header does not contain EPOCH or EQUINOX keyword'

 if N_params() LT 2 then begin 
   print, 'HPRECESS: Astrometry in supplied header is in equinox ', $
   strtrim(yeari,2)      
   read, 'Enter year of new equinox: ',yearf 
 endif                                             

 if yeari EQ yearf then $                                           
    message,'Astrometry in header is already in Equinox ' + strtrim(YEARF,2)

 extast, hdr, astr, noparams        <span class="comments">;Extract astrometry from header</span>

 if noparams EQ -1 THEN $
    message,'FITS Header does not contain CRVAL keywords'
        
 if strmid(astr.ctype[0],5,3) EQ 'GSS' then begin
        gsss_stdast, hdr
        extast, hdr, astr, noparams
 endif
        
 cd = astr.cd
 crval = astr.crval
 cdelt = astr.cdelt
 if N_elements(CDELT) GE 2 then if (cdelt[0] NE 1.0) then begin
        cd[0,0] = cd[0,0]*cdelt[0] & cd[0,1] =  cd[0,1]*cdelt[0]
        cd[1,1] = cd[1,1]*cdelt[1] & cd[1,0] =  cd[1,0]*cdelt[1]
 endif

 coord = strmid(astr.ctype,0,4)    <span class="comments">;Test if RA and Dec reversed in 'CTYPE*'</span>
 reverse = ((coord[0] EQ 'DEC-') and (coord[1] EQ 'RA--'))
 if reverse then crval = rotate(crval,2)
 a = crval[0] & d = crval[1]
 if (yeari EQ 2000.) and (yearf EQ 1950.) then begin 
       bprecess,a,d,ai,di
       sxaddpar,hdr,'RADECSYS','FK4'
       a = ai & d = di
 endif else if (yeari EQ 1950) and (yearf EQ 2000) then begin 
       jprecess,a,d,ai,di
       sxaddpar,hdr,'RADECSYS','FK5'
       a = ai & d = di
       
 endif else precess, a, d, yeari, yearf                    <span class="comments">;Precess the CRVAL coordinates</span>
 precess_cd, cd, yeari, yearf, crval,[ a, d]    <span class="comments">;Precess the CD matrix</span>
 if N_elements(CDELT) GE 2 then if (cdelt[0] NE 1.0) then begin
        cd[0,0] = cd[0,0]/cdelt[0] & cd[0,1] =  cd[0,1]/cdelt[0]
        cd[1,1] = cd[1,1]/cdelt[1] & cd[1,0] =  cd[1,0]/cdelt[1]
 endif

 
 if reverse then begin                          <span class="comments">;Update CRVAL values</span>
    sxaddpar, hdr, 'CRVAL1', double(d)             
    sxaddpar, hdr, 'CRVAL2', double(a)    
 endif else begin
    sxaddpar, hdr, 'CRVAL1', double(a)            
    sxaddpar, hdr, 'CRVAL2', double(d)    
 endelse

 if (noparams EQ 3) or (noparams EQ 2)  then begin
 
       putast, hdr, cd, EQUINOX = float(yearf)          <span class="comments">;Update CD values</span>
 endif else begin
       astr.cd= cd
       getrot, astr, ROT                               <span class="comments">;or CROTA2 value</span>
       sxaddpar,hdr, 'EQUINOX', yearf, ' Equinox of Ref. Coord.', 'HISTORY'
       sxaddpar, hdr, 'CROTA2', rot
 endelse        

 sxaddhist, 'HPRECESS: ' + STRMID(systime(),4,20) +  $ 
   ' Astrometry Precessed From Year' + string(form='(f7.1)',float(yeari)),hdr
 message, 'Header astrometry has been precessed to ' + strtrim(yearf,2),/INF

 return
 end                                                        
</code>
    </div>
  </body>
</html>