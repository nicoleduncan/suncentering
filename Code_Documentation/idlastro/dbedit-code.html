<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN"
 "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">

<!-- Generated by IDLdoc 3.5.1 on Mon Sep 30 16:57:25 2013 -->

<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en">
  <head>
    <meta http-equiv="Content-Type" content="text/html;charset=utf-8"/>
    <title>dbedit.pro (Documentation for ./)</title>

    
    <link rel="stylesheet" type="text/css" media="all"
          href="../idldoc-resources/main.css" />
    <link rel="stylesheet" type="text/css" media="print"
          href="../idldoc-resources/main-print.css" />
    

    <script type="text/javascript">
      function setTitle() {
        parent.document.title="dbedit.pro (Documentation for ./)";
      }
    </script>
  </head>

  <body onload="setTitle();" id="root">
    <div class="content">
      <code class="source"><span class="comments">;+</span>
<span class="comments">; NAME:</span>
<span class="comments">;      DBEDIT</span>
<span class="comments">;</span>
<span class="comments">; PURPOSE:</span>
<span class="comments">;       Interactively edit specified fields in an IDL database. </span>
<span class="comments">; EXPLANATION:</span>
<span class="comments">;       The value of each field is displayed, and the user has the option</span>
<span class="comments">;       of changing or keeping the value.  Widgets will be used if they</span>
<span class="comments">;       are available.</span>
<span class="comments">;</span>
<span class="comments">; CALLING SEQUENCE:</span>
<span class="comments">;       dbedit, list, [ items ]</span>
<span class="comments">;</span>
<span class="comments">; INPUTS:</span>
<span class="comments">;       list - scalar or vector of database entry numbers.  Set list = 0 to </span>
<span class="comments">;       interactively add a new entry to a database.  Set list = -1 to edit </span>
<span class="comments">;       all entries.</span>
<span class="comments">;</span>
<span class="comments">; OPTIONAL INPUTS:</span>
<span class="comments">;       items - list of items to be edited.  If omitted, all fields can be </span>
<span class="comments">;               edited.      </span>
<span class="comments">;</span>
<span class="comments">; KEYWORDS:</span>
<span class="comments">;       BYTENUM = If set, treat byte variables as numbers instead of</span>
<span class="comments">;                 characters.</span>
<span class="comments">;</span>
<span class="comments">; COMMON BLOCKS:</span>
<span class="comments">;       DB_COM -- contains information about the opened database.</span>
<span class="comments">;       DBW_C -- contains information intrinsic to this program.</span>
<span class="comments">;</span>
<span class="comments">; SIDE EFFECTS:</span>
<span class="comments">;       Will update the database files.</span>
<span class="comments">;</span>
<span class="comments">; RESTRICTIIONS:</span>
<span class="comments">;       Database must be opened for update prior to running</span>
<span class="comments">;       this program.  User must be running DBEDIT from an </span>
<span class="comments">;       account that has write privileges to the databases.  </span>
<span class="comments">;</span>
<span class="comments">;       If one is editing an indexed item, then after all edits are complete,</span>
<span class="comments">;       DBINDEX will be called to reindex the entire item.    This may</span>
<span class="comments">;       be time consuming.</span>
<span class="comments">;</span>
<span class="comments">;       Cannot be used to edit items with multiple values</span>
<span class="comments">;</span>
<span class="comments">; EXAMPLE:</span>
<span class="comments">;       Suppose one had new parallaxes for all stars fainter than 5th magnitude</span>
<span class="comments">;       in the Yale Bright Star Catalog and wanted to update the PRLAX and</span>
<span class="comments">;       PRLAX_CODE fields with these new numbers</span>
<span class="comments">;</span>
<span class="comments">;       IDL> !priv=2                    </span>
<span class="comments">;       IDL> dbopen, 'yale_bs', 1            ;Open catalog for update</span>
<span class="comments">;       IDL> list = dbfind( 'v>5')     ;Find fainter than 5th magnitude</span>
<span class="comments">;       IDL> dbedit, list, 'prlax, prlax_code'   ;Manual entry of new values</span>
<span class="comments">;</span>
<span class="comments">; PROCEDURE:</span>
<span class="comments">;       (1) Use the cursor and point to the value you want to edit.   </span>
<span class="comments">;       (2) Type the new field value over the old field value.</span>
<span class="comments">;       (3) When you are done changing all of the field values for each entry</span>
<span class="comments">;       save the entry to the databases by pressing 'SAVE ENTRY TO DATABASES'.</span>
<span class="comments">;       Here all of the values will be checked to see if they are the correct</span>
<span class="comments">;       data type.  If a field value is not of the correct data type, it will</span>
<span class="comments">;       not be saved.  </span>
<span class="comments">;</span>
<span class="comments">;       Use the buttons "PREV ENTRY" and "NEXT ENTRY" to move between entry </span>
<span class="comments">;       numbers.  You must save each entry before going on to another entry in </span>
<span class="comments">;       order for your changes to be saved.</span>
<span class="comments">;</span>
<span class="comments">;       Pressing "RESET THIS ENTRY" will remove any unsaved changes to the </span>
<span class="comments">;       current entry.</span>
<span class="comments">;</span>
<span class="comments">;REVISION HISTORY:</span>
<span class="comments">;       Adapted from Landsman's DBEDIT</span>
<span class="comments">;       added widgets,  Melissa Marsh, HSTX, August 1993</span>
<span class="comments">;       do not need to press return after entering each entry,</span>
<span class="comments">;                       fixed layout problem on SUN,</span>
<span class="comments">;                       Melissa Marsh, HSTX, January 1994</span>
<span class="comments">;       Only updates the fields which are changed. Joel Offenberg, HSTX, Mar 94</span>
<span class="comments">;       Corrected test for changed fields  Wayne Landsman  HSTX, Mar 94</span>
<span class="comments">;       Removed a couple of redundant statements W. Landsman HSTX Jan 96</span>
<span class="comments">;       Converted to IDL V5.0   W. Landsman   September 1997</span>
<span class="comments">;       Replace DATAYPE() with size(/TNAME)   W. Landsman   November 2001</span>
<span class="comments">;       Work for entry numbers > 32767     W. Landsman   December 2001</span>
<span class="comments">;       Added /BYTENUM  William Thompson        13-Mar-2006</span>
<span class="comments">;       Use DIALOG_MESSAGE for error messages  W. Landsman  April 2006</span>
<span class="comments">;       Assume since V5.5, remove VMS support  W. Landsman  Sep 2006</span>
<span class="comments">;-</span>

<span class="comments">;----------------------------------------------------------------</span>


<span class="comments">;event handler for main part of program</span>

<a id="widgetedit_event:source"></a>pro widgetedit_event,event

common db_com,qdb,QITEMS,QDBREC

common dbw_c,liston,main,holder,widlabel,widtext,middle,nitems,names,$
        it,itnum,dtype,numvals,sbyte,nbytes,buts,prevbut,but2,resetbut,$
        endbut,nextbut,mid,minlist,maxlist,savebut,bigmid,entry,wid_warn,$
        holder0,widtext0,widlabel0,thislist,nlist,wereat,newflag,bytenum

CASE event.id OF

    endbut: widget_control,event.top,/destroy <span class="comments">;destory main widget--end session</span>

    prevbut:begin       <span class="comments">;go to previous entry</span>
        if wereat ne 0 then wereat= wereat-1
        liston = thislist[wereat]
        widedit
    end

    nextbut:begin       <span class="comments">;go to next entry</span>
        if wereat lt nlist-1 then wereat = wereat+1 else $
              widget_control,event.top,/destroy          <span class="comments">;end session</span>
        liston = thislist[wereat]
        widedit
    end

    resetbut:begin      <span class="comments">;reset this entry</span>
        liston = liston
        widedit 
    end

    savebut: begin      <span class="comments">;save entry to databases</span>
          <span class="comments">;update database</span>
        for i = 0, nitems -1 do begin
          widget_control,widtext[i],get_value=val
          <span class="comments">;test value</span>
          valid = 0
           oldval = dbxval(entry,dtype[i],numvals[i],sbyte[i],nbytes[i])

          on_ioerror,BADVAL
          IF (strtrim(oldval[0],2) ne (strtrim(val[0],2))) THEN BEGIN
              oldval[0] = strtrim(val,2)
              valid = 1
              dbxput,oldval,entry,dtype[i],sbyte[i],nbytes[i]
              print,strcompress('Entry ' + string(liston) +':  '  + $
              names[i] + ' = ' + string(val))
              newflag[ wereat, i ] = 1b
    BADVAL:     if (not valid) then begin
                result = dialog_message(title='Bad Value',/ERROR, $
                   'Item '+ strcompress(names[i],/rem) + $ 
                        ' must be of type ' + size(oldval[0],/TNAME) )
                str = dbxval(entry,dtype[i],numvals[i],sbyte[i],nbytes[i])
                if (dtype[i] eq 1) and keyword_set(bytenum) then str=fix(str)
                str = '    '+string(str[0])
                widget_control,widtext[i],set_value=str         
                endif
          endIF 
          on_ioerror,NULL
        endfor
        
        if (liston EQ 0) then begin
                 dbwrt,entry,0,1        <span class="comments">;new entry</span>
        endif else begin
                 dbwrt,entry
        endelse
        widedit
        <span class="comments">;create widget telling the user that the changes have been made.</span>
    end

    else: <span class="comments">;donothing</span>
   
     endcase
end

<span class="comments">;--------------------------------------------------------------------</span>
<a id="widedit:source"></a>pro widedit
<span class="comments">;program that makes "middle" of main widget (field values)</span>


common db_com,qdb,QITEMS,QDBREC
                           

common dbw_c,liston,main,holder,widlabel,widtext,middle,nitems,names,$
        it,itnum,dtype,numvals,sbyte,nbytes,buts,prevbut,but2,resetbut,$
        endbut,nextbut,mid,minlist,maxlist,savebut,bigmid,entry,wid_warn,$
        holder0,widtext0,widlabel0,thislist,nlist,wereat,newflag,bytenum


<span class="comments">;get entry number</span>
 dbrd, liston, entry

<span class="comments">;get field values for this entry</span>
 widget_control, widtext0, set_value=string(liston)
 for i = 0,nitems-1 do begin
        str = dbxval(entry,dtype[i],numvals[i],sbyte[i],nbytes[i])
        if (dtype[i] eq 1) and keyword_set(bytenum) then str=fix(str)
        str = '    '+string(str[0])
        widget_control,widtext[i],set_value=str
 endfor

<span class="comments">;check to see if this entry is the minimum or maximum entry </span>
 if (liston EQ minlist) then widget_control,prevbut,sensitive=0 else $
                widget_control,prevbut,sensitive=1 
 if (liston EQ maxlist) then widget_control,nextbut,sensitive=0 else $
                widget_control,nextbut,sensitive=1

 end
<span class="comments">;-------------------------------------------------------------------------</span>
<span class="comments">;main program</span>

<a id="dbedit:source"></a>pro dbedit,list,items,bytenum=k_bytenum

 compile_opt idl2
common db_com,qdb,QITEMS,QDBREC

<span class="comments">;Nitems - Number elements in input list</span>
<span class="comments">;Thislist - Sorted list of entry numbers</span>
<span class="comments">;Minlist - Minimum input entry number</span>
<span class="comments">;Maxlist - Maximum input entry number</span>
<span class="comments">;Liston - The current entry number being edited (scalar)</span>
<span class="comments">;wereat - The index of ThisList vector being edited, i.e. Thislist(wereat)=LIston</span>
<span class="comments">;dtype - data type(s) (1=string,2=byte,4=i*4,...)</span>
<span class="comments">;sbyte - starting byte(s) in entry</span>
<span class="comments">;numvals - number of data values for item(s)</span>
<span class="comments">;    NOTE: dtype, sbyte, numvals are dimensioned for *all* entries </span>

common dbw_c,liston,main,holder,widlabel,widtext,middle,nitems,names,$
        it,itnum,dtype,numvals,sbyte,nbytes,buts,prevbut,but2,resetbut,$
        endbut,nextbut,mid,minlist,maxlist,savebut,bigmid,entry,wid_warn,$
        holder0,widtext0,widlabel0,thislist,nlist,wereat,newflag,bytenum
                          
 On_error,2
 if N_params() LT 1 then begin
        print,'Syntax - dbedit, list, [ items ]'
        return
 endif
        
<span class="comments">;Set the value of bytenum</span>
bytenum = keyword_set(k_bytenum)

<span class="comments">;make sure widgets are available</span>
 if (!D.FLAGS AND 65536) EQ 0 then begin  
        dbedit_basic, list, items
        return
 endif

<span class="comments">;check to make sure database is open</span>
    <span class="comments">;first check to see if there is an open database</span>
    s = size(qdb)
    if (s[0] EQ 0) then begin
    
           result = dialog_message(/ERROR, title='NOT OPEN FOR UPDATE', $
	        'No database has been opened')
            goto, PROEND  
    endif
<span class="comments">;check to make sure the database is opened for update</span>
    dbname = db_info('NAME',0)
    if not db_info('UPDATE') then begin

        result = dialog_message(/ERROR, title='NOT OPEN FOR UPDATE', $
	        'Database ' + dbname + ' must be opened for update.')
        goto,PROEND

    endif


    <span class="comments">;check parameters</span>
    zparcheck, 'DBEDIT', list, 1, [1,2,3], [0,1], 'Database entry numbers'

    <span class="comments">;get items.  If items not specified use all items except ENTRY</span>
    if ( N_params() LT 2 ) then begin       
        nitems = db_info('ITEMS',0) -1       
        items = indgen(nitems) + 1
    endif

    nlist = N_elements(list)

    if nlist gt 1 then begin <span class="comments">;sort entry numbers</span>

        sar = sort(list)
        thislist = list[sar]

    endif else begin

        thislist = lonarr(1) 
        thislist[0] = list

    endelse

    <span class="comments">;edit all entries?  get number of entries</span>
    if ( list[0] EQ -1 ) then begin          
        nlist = db_info('ENTRIES',0)           
        if nlist le 0 then begin
           print,'Empty database cannot be edited. Use list=0 to add new entry'
           goto, PROEND
        endif
        thislist = lindgen(nlist) + 1
    endif

    minlist = min(thislist, max = maxlist)


    nentry = db_info('ENTRIES',0)
    if (maxlist gt nentry) then begin
        result = dialog_message(title='INVALID ENTRY NUMBER',/ERROR, $
           dbname + ' entry numbers must be less than ' + strtrim(nentry+1,2) )
         goto, PROEND
    endif

    nitems = db_info('ITEMS',0) -1
    allitems = indgen(nitems) + 1

    <span class="comments">;get information about items</span>
    db_item,allitems,itnum,ivalnum,dtype,sbyte,numvals,nbytes
    nvalues = db_item_info('nvalues')

    db_item,items,it

    nit = n_elements(it)                      <span class="comments">;Number of items to be edited</span>
    names = db_item_info('name',itnum)        <span class="comments">;Get names of each item</span>
    newflag = bytarr(nlist,nitems)  <span class="comments">;Keeps track of fields actually updated</span>

    wereat = 0
    liston = thislist[wereat]
    dbrd,liston,entry

    <span class="comments">;create widget and display</span>
    main = widget_base(/COLUMN,title='Widgetized Database Editor')
    w1 = widget_label(main,value='******  '  + dbname + '  ******')
    bigmid = widget_base(main,/column,x_scroll_size=325,y_scroll_size=650)


    butbase = widget_base(main,/column,/frame)
    savebut = widget_button(butbase,value='SAVE THIS ENTRY')
    buts = widget_base(butbase,/row)
    prevbut = widget_button(buts,value='&lt;- PREV ENTRY')
    but2 = widget_base(buts,/column)
    resetbut = widget_button(but2,value='RESET THIS ENTRY')
    endbut = widget_button(but2,value='END SESSION')
    nextbut = widget_button(buts,value='NEXT ENTRY ->')

    widlabel = lonarr(nitems+1)
    widtext = lonarr(nitems+1)
    holder = lonarr(nitems+1)

    mid = widget_base(bigmid,/column)

    holder0 = widget_base(mid,/row)
    widlabel0 =widget_label(holder0,value='  ENTRY NUMBER  ',/frame)
    num = string(liston)
    widtext0 = widget_label(holder0,value=num)

    middle = widget_base(mid,/column)

    for i = 0,nitems-1 do begin
        ed = 'N'
        str1 = names[i]

        for j = 0, N_elements(it)-1 do begin
                if it[j] EQ itnum[i] then ed = 'Y'
        endfor

        str = dbxval(entry,dtype[i],numvals[i],sbyte[i],nbytes[i])
        if (dtype[i] eq 1) and keyword_set(bytenum) then str=fix(str)
        str = '    ' + string(str[0])
        if ed eq 'Y' then  begin
                holder[i] = widget_base(middle,/row)
                widlabel[i] = widget_label(holder[i],value = str1,/frame)
                widtext[i] = widget_text(holder[i],/frame,value=str,/edit)
        endif else begin
                holder[i] = widget_base(middle,/row)
                widlabel[i] = widget_label(holder[i],value = str1,/frame)
                widtext[i] = widget_label(holder[i],value=str)
        endelse 
    endfor

    if (liston EQ minlist) then widget_control,prevbut,sensitive=0 else $
                widget_control,prevbut,sensitive=1
    if (liston EQ maxlist) then widget_control,nextbut,sensitive=0 else $
                widget_control,nextbut,sensitive=1

    widget_control,main,/realize
    xmanager,'widgetedit',main

 newitem = total(newflag, 1)
 indexnum = where(newitem, nindex)

   if ( nindex GT 0 ) then begin                          <span class="comments">;Any mods made?</span>
      indexnum = itnum[indexnum]
      indextype = db_item_info('INDEX',indexnum)<span class="comments">;Index type of modified fields </span>
      good = where(indextype GE 1, Ngood)         <span class="comments">;Which fields are indexed?</span>
      if Ngood GT 0 then begin 
        message, 'Now updating index file', /INF
        dbindex, indexnum[good]
      endif
      dbopen,strlowcase(dbname),1
    endif

PROEND:

 return
 end
</code>
    </div>
  </body>
</html>