<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN"
 "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">

<!-- Generated by IDLdoc 3.5.1 on Mon Sep 30 16:57:41 2013 -->

<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en">
  <head>
    <meta http-equiv="Content-Type" content="text/html;charset=utf-8"/>
    <title>helio.pro (Documentation for ./)</title>

    
    <link rel="stylesheet" type="text/css" media="all"
          href="../idldoc-resources/main.css" />
    <link rel="stylesheet" type="text/css" media="print"
          href="../idldoc-resources/main-print.css" />
    

    <script type="text/javascript">
      function setTitle() {
        parent.document.title="helio.pro (Documentation for ./)";
      }
    </script>
  </head>

  <body onload="setTitle();" id="root">
    <div class="content">
      <code class="source">PRO HELIO, JD, LIST, HRAD, HLONG, HLAT, RADIAN = radian
<span class="comments">;+</span>
<span class="comments">; NAME: </span>
<span class="comments">;      HELIO</span>
<span class="comments">; PURPOSE: </span>
<span class="comments">;      Compute (low-precision) heliocentric coordinates for the planets.</span>
<span class="comments">; EXPLANATION:</span>
<span class="comments">;      The mean orbital elements for epoch J2000 are used.   These are derived</span>
<span class="comments">;      from a 250 yr least squares fit of the DE 200 planetary ephemeris to a </span>
<span class="comments">;      Keplerian orbit where each element is allowed to vary linearly with </span>
<span class="comments">;      time.  For dates between 1800 and 2050, this solution fits the </span>
<span class="comments">;      terrestrial planet orbits to ~25" or better, but achieves only ~600" </span>
<span class="comments">;      for Saturn.   </span>
<span class="comments">;</span>
<span class="comments">;      Use PLANET_COORDS (which calls HELIO) to get celestial (RA, Dec)</span>
<span class="comments">;      coordinates of the planets</span>
<span class="comments">; CALLING SEQUENCE: </span>
<span class="comments">;       HELIO, JD, LIST, HRAD, HLONG, HLAT, [/RADIAN]</span>
<span class="comments">; INPUTS:</span>
<span class="comments">;       JD = Julian date, double precision scalar or vector</span>
<span class="comments">;       LIST = List of planets array.  May be a single number.</span>
<span class="comments">;               1 = merc, 2 = venus, ... 9 = pluto.</span>
<span class="comments">;</span>
<span class="comments">; OUTPUTS:</span>
<span class="comments">;       HRAD = array of Heliocentric radii (A.U).</span>
<span class="comments">;       HLONG = array of Heliocentric (ecliptic) longitudes (degrees).</span>
<span class="comments">;       HLAT = array of Heliocentric latitudes (degrees).</span>
<span class="comments">;             These output parameters will be dimensioned Nplanet by Ndate,</span>
<span class="comments">;             where Nplanet is the number of elements of list, and Ndate is </span>
<span class="comments">;             the number of elements of JD.</span>
<span class="comments">;</span>
<span class="comments">; OPTIONAL INPUT KEYWORD:</span>
<span class="comments">;       /RADIAN - If set, then the output longitude and latitude are given in </span>
<span class="comments">;                 radians.         </span>
<span class="comments">; EXAMPLE:</span>
<span class="comments">;       (1) Find the current heliocentric positions of all the planets</span>
<span class="comments">;</span>
<span class="comments">;        IDL> GET_JULDATE, jd      ;Get current Julian date</span>
<span class="comments">;        IDL> HELIO,jd,indgen(9)+1,hrad,hlong,hlat  ;Get radius, long, and lat</span>
<span class="comments">;</span>
<span class="comments">;       (2) Find heliocentric position of Mars on August 23, 2000 </span>
<span class="comments">;         IDL> JDCNV, 2000,08,23,0,jd</span>
<span class="comments">;         IDL> HELIO,JD,2,HRAD,HLONG,HLAT</span>
<span class="comments">;                  ===> hrad = 1.6407 AU hlong = 124.3197 hlat = 1.7853</span>
<span class="comments">;         For comparison, the JPL ephemeris gives</span>
<span class="comments">;                       hrad = 1.6407 AU hlong = 124.2985 hlat = 1.7845</span>
<span class="comments">;       (3) Find the heliocentric positions of Mars and Venus for every day in</span>
<span class="comments">;           November 2000</span>
<span class="comments">;        IDL> JDCNV, 2000, 11, 1, 0, jd    ;Julian date of November 1, 2000</span>
<span class="comments">;        IDL> helio, jd+indgen(30), [4,2], hrad,hlong,hlat   ;Mars=4, Venus=2 </span>
<span class="comments">;                   hrad, hlong, and hlat will be dimensioned [2,30]</span>
<span class="comments">;                   first column contains Mars data, second column Venus</span>
<span class="comments">; COMMON BLOCKS: </span>
<span class="comments">;       None </span>
<span class="comments">; ROUTINES USED: </span>
<span class="comments">;       CIRRANGE - force angle between 0 and 2*!PI</span>
<span class="comments">; NOTES:</span>
<span class="comments">;       (1) The calling sequence for this procedure was changed in August 2000</span>
<span class="comments">;       (2) This program is based on the two-body model and thus neglects </span>
<span class="comments">;           interactions between the planets.   This is why the worst results</span>
<span class="comments">;           are for Saturn.  Use the procedure JPLEPHINTERp for more accurate</span>
<span class="comments">;           positions using the JPL ephemeris.   Also see </span>
<span class="comments">;           http://ssd.jpl.nasa.gov/cgi-bin/eph for a more accurate ephemeris </span>
<span class="comments">;           generator online.     </span>
<span class="comments">;       (3) The coordinates are given for equinox 2000 and *not* the equinox</span>
<span class="comments">;           of the supplied date(s)</span>
<span class="comments">; MODIFICATION HISTORY: </span>
<span class="comments">;       R. Sterner.  20 Aug, 1986.</span>
<span class="comments">;       Code cleaned up a bit      W. Landsman             December 1992</span>
<span class="comments">;       Major rewrite, use modern orbital elements, vectorize, more accurate</span>
<span class="comments">;         solution to Kepler's equation          W. Landsman August 2000</span>
<span class="comments">;       Wasn't working for planet vectors        W. Landsman August 2000</span>
<span class="comments">;       Work for more than 32767 positions       S. Leach Jan 2009</span>
<span class="comments">;-</span>
 On_error,2
 compile_opt idl2

   if N_params() LT 3 then begin
     print,'Syntax - Helio, jd, list, hrad, hlong, hlat, [/RADIAN]'
     print,'     jd - Scalar or vector Julian date'
     print,'     list - scalar or vector of planet numbers [1-9]'
     print, $
     '     hrad, hlong, hlat - output heliocentric distance, longitude latitude'    
     return
  endif

<span class="comments">; Mean orbital elements taken from http://ssd.jpl.nasa.gov/elem_planets.html</span>
<span class="comments">; (1) semi-major axis in AU, (2) eccentricity, (3) inclination (degrees),</span>
<span class="comments">; (4) longitude of the ascending node (degrees), (5) longitude of perihelion</span>
<span class="comments">; (degrees) and (6) mean longitude (degrees)</span>
<span class="comments">;Mercury </span>
PD = [ [ 0.38709893d, 0.20563069, 7.00487,  48.33167,  77.45645, 252.25084 ], $
<span class="comments">;Venus  </span>
     [ 0.72333199d, 0.00677323, 3.39471,  76.68069, 131.53298, 181.97973 ], $ 
<span class="comments">;Earth</span>
     [ 1.00000011d, 0.01671022, 0.00005, -11.26064, 102.94719, 100.46435], $
<span class="comments">;Mars </span>
     [ 1.52366231d, 0.09341233, 1.85061,  49.57854, 336.04084, 355.45332], $
<span class="comments">;Jupiter</span>
     [ 5.20336301d, 0.04839266, 1.30530, 100.55615,  14.75385,  34.40438], $ 
<span class="comments">;Saturn</span>
     [ 9.53707032d, 0.05415060, 2.48446, 113.71504,  92.43194,  49.94432], $
<span class="comments">;Uranus</span>
     [19.19126393d, 0.04716771, 0.76986,  74.22988, 170.96424, 313.23218], $ 
<span class="comments">;Neptune</span>
     [30.06896348d, 0.00858587, 1.76917, 131.72169,  44.97135, 304.88003], $
<span class="comments">;Pluto</span>
     [39.48168677d, 0.24880766,17.14175, 110.30347, 224.06676, 238.92881] ]

<span class="comments">; DPD gives the time rate of change of the above quantities ("/century)</span>

DPD = [  [0.00000066d, 0.00002527, -23.51, -446.30, 573.57, 538101628.29 ], $
 [ 0.00000092d, -0.00004938, -2.86, -996.89, -108.80, 210664136.06], $
 [-0.00000005d, -0.00003804, -46.94, -18228.25, 1198.28, 129597740.63], $ 
 [-0.00007221d, 0.00011902, -25.47, -1020.19, 1560.78, 68905103.78 ], $
 [0.00060737d, -0.00012880, -4.15, 1217.17, 839.93, 10925078.35 ], $
 [-0.00301530d, -0.00036762, 6.11, -1591.05, -1948.89, 4401052.95],  $
 [0.00152025d, -0.00019150, -2.09, -1681.40, 1312.56, 1542547.79 ], $
 [-0.00125196d, 0.0000251, -3.64, -151.25, -844.43, 786449.21 ], $
 [-0.00076912d, 0.00006465, 11.07, -37.33, -132.25, 522747.90] ] 

 JD0 = 2451545.0d    <span class="comments">;Julian Date for Epoch 2000.0</span>
 radeg = 180/!DPI

<span class="comments">;-----------------  Days since Epoch  ---------------</span>

  T = (JD - JD0)/36525.0d          <span class="comments">;Time in centuries since 2000.0</span>
 

        ip = list-1
        dpd[2:5,ip] = dpd[2:5,ip]/3600.0d       <span class="comments">;Convert arc seconds to degrees</span>
        ntime = N_elements(t)
        nplanet = N_elements(list)
        hrad = fltarr(nplanet,ntime) & hlong = hrad & hlat = hrad

<span class="comments">;-----------------  Loop over dates  --------------</span>
 
        for i =0L,ntime-1L do begin         <span class="comments">;SML made longword</span>
 
        pd1 = pd[*,ip] + dpd[*,ip]*T[i]
        
        a = pd1[0,*]                            <span class="comments">;semi-major axis</span>
        eccen = pd1[1,*]                        <span class="comments">;eccentricity</span>
        n = 0.9856076686/a/sqrt(a)/RADEG      <span class="comments">;mean motion, in radians/day</span>
        L =  pd1[5,*]/RADEG                     <span class="comments">;mean longitude</span>
        pi = pd1[4,*]/RADEG                  <span class="comments">;longitude of the perihelion</span>
        omega = pd1[3,*]/RADEG               <span class="comments">;longitude of the ascending node</span>
        inc = pd1[2,*]/RADEG          <span class="comments">;inclination in radians</span>
        
        m = L - pi
        cirrange,m,/RADIAN
        e1 = m + (m + eccen*sin(m) - m)/(1 - eccen*cos(m) )
        e = e1 + (m + eccen*sin(e1) - e1)/(1 - eccen*cos(e1) )
        maxdif = max(abs(e-e1))
        niter = 0
        while (maxdif GT 1e-5) and (niter lt 10) do begin        
             e1 = e
             e = e1 + (m + eccen*sin(e1) - e1)/(1 - eccen*cos(e1) )
             maxdif = max(abs(e-e1))
             niter = niter+1
        endwhile       
        
      
        nu = 2*atan( sqrt( (1+eccen)/(1-eccen) )* tan(E/2))   <span class="comments">;true anomaly </span>

        hrad[0,i] = reform( a*(1 - eccen*cos(e) ) )
        hlong[0,i] = reform (nu + pi)                 
         hlat[0,i] = reform( asin(sin(hlong[*,i] - omega)*sin(inc) ) )
 endfor 

       cirrange,hlong,/RADIAN
       if not keyword_set(RADIAN) then begin 
           hlong = hlong*RADEG
           hlat = hlat*RADEG
       endif
       if N_elements(hrad) GT 1 then begin
             hrad = reform(hrad,/over)
             hlong = reform(hlong,/over)
             hlat = reform(hlat,/over)
       endif else begin
             if N_elements(size(jd)) EQ 3 then begin      <span class="comments">;scalar?</span>
                     hrad = hrad[0]
                     hlong = hlong[0]
                     hlat = hlat[0]
              endif
       endelse 

   return
   end
</code>
    </div>
  </body>
</html>