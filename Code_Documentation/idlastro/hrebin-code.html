<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN"
 "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">

<!-- Generated by IDLdoc 3.5.1 on Mon Sep 30 16:57:42 2013 -->

<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en">
  <head>
    <meta http-equiv="Content-Type" content="text/html;charset=utf-8"/>
    <title>hrebin.pro (Documentation for ./)</title>

    
    <link rel="stylesheet" type="text/css" media="all"
          href="../idldoc-resources/main.css" />
    <link rel="stylesheet" type="text/css" media="print"
          href="../idldoc-resources/main-print.css" />
    

    <script type="text/javascript">
      function setTitle() {
        parent.document.title="hrebin.pro (Documentation for ./)";
      }
    </script>
  </head>

  <body onload="setTitle();" id="root">
    <div class="content">
      <code class="source"><a id="hrebin:source"></a> pro hrebin, oldim, oldhd, newim, newhd, newx, newy, $
            SAMPLE=sample, OUTSIZE = outsize, ERRMSG = errmsg, ALT=alt
<span class="comments">;+</span>
<span class="comments">; NAME:</span>
<span class="comments">;    HREBIN</span>
<span class="comments">; PURPOSE:</span>
<span class="comments">;    Expand or contract a FITS image using (F)REBIN and update the header </span>
<span class="comments">; EXPLANATION:</span>
<span class="comments">;    If the output size is an exact multiple of the input size then REBIN is </span>
<span class="comments">;    used, else FREBIN is used.     User can either overwrite the input array,</span>
<span class="comments">;    or write to new variables.</span>
<span class="comments">;</span>
<span class="comments">; CALLING SEQUENCE:</span>
<span class="comments">;    HREBIN, oldhd        ;Special calling sequence to just update header</span>
<span class="comments">;    HREBIN, oldim, oldhd, [ newim, newhd, newx, newy, OUTSIZE = ,/SAMPLE, </span>
<span class="comments">;                            ERRMSG =  ]</span>
<span class="comments">;</span>
<span class="comments">; INPUTS:</span>
<span class="comments">;    OLDIM - the original image array</span>
<span class="comments">;    OLDHD - the original image FITS header, string array</span>
<span class="comments">;</span>
<span class="comments">; OPTIONAL INPUTS:</span>
<span class="comments">;    NEWX - size of the new image in the X direction, integer scalar</span>
<span class="comments">;    NEWY - size of the new image in the Y direction, integer scalar</span>
<span class="comments">;            HREBIN will prompt for NEWX and NEWY if not supplied</span>
<span class="comments">;</span>
<span class="comments">; OPTIONAL OUTPUTS:</span>
<span class="comments">;    NEWIM - the image after expansion or contraction with REBIN</span>
<span class="comments">;    NEWHD - header for newim containing updated astrometry info</span>
<span class="comments">;            If output parameters are not supplied, the program will modify</span>
<span class="comments">;            the input parameters OLDIM and OLDHD to contain the new array and </span>
<span class="comments">;            updated header.</span>
<span class="comments">;</span>
<span class="comments">; OPTIONAL INPUT KEYWORDS:</span>
<span class="comments">;    /SAMPLE - Expansion or contraction is done using REBIN which uses </span>
<span class="comments">;              bilinear interpolation when magnifying and boxaveraging when </span>
<span class="comments">;              minifying.   If the SAMPLE keyword is supplied and non-zero, </span>
<span class="comments">;              then nearest neighbor sampling is used in both cases.   Keyword</span>
<span class="comments">;              has no effect when output size is not a multiple of input size.</span>
<span class="comments">;</span>
<span class="comments">;    OUTSIZE - Two element integer vector which can be used instead of the</span>
<span class="comments">;             NEWX and NEWY parameters to specify the output image dimensions</span>
<span class="comments">;</span>
<span class="comments">;    ALT - Single character 'A' through 'Z' or ' ' specifying which astrometry</span>
<span class="comments">;          system to modify in the FITS header.    The default is to use the</span>
<span class="comments">;          primary astrometry of ALT = ' '.    See Greisen and Calabretta (2002)</span>
<span class="comments">;          for information about alternate astrometry keywords.</span>
<span class="comments">; OPTIONAL KEYWORD OUTPUT:</span>
<span class="comments">;       ERRMSG - If this keyword is supplied, then any error mesasges will be</span>
<span class="comments">;               returned to the user in this parameter rather than depending on</span>
<span class="comments">;               on the MESSAGE routine in IDL.   If no errors are encountered</span>
<span class="comments">;               then a null string is returned.               </span>
<span class="comments">; PROCEDURE:</span>
<span class="comments">;     The parameters BSCALE, NAXIS1, NAXIS2, CRPIX1, and CRPIX2 and the CD </span>
<span class="comments">;     (or CDELT) parameters are updated for the new FITS header.</span>
<span class="comments">;</span>
<span class="comments">; EXAMPLE:</span>
<span class="comments">;     Compress a 2048 x 2048 image array IM, with FITS header HDR, to a </span>
<span class="comments">;     724 x 724 array.   Overwrite the input variables with the compressed </span>
<span class="comments">;     image and header.</span>
<span class="comments">;</span>
<span class="comments">;     IDL> hrebin, im, hdr, OUT = [724, 724]</span>
<span class="comments">;</span>
<span class="comments">; PROCEDURES USED:</span>
<span class="comments">;     CHECK_FITS, EXTAST, FREBIN, GSSS_STDAST, STRN(), SXPAR(), SXADDHIST, </span>
<span class="comments">;     SXADDPAR, ZPARCHECK</span>
<span class="comments">;</span>
<span class="comments">; MODIFICATION HISTORY:</span>
<span class="comments">;     Written, December 1990  W. Landsman, ST System Corp.</span>
<span class="comments">;     Update CD1_1 keywords   W. Landsman   November 1992</span>
<span class="comments">;     Check for a GSSS header   W. Landsman  June 1994</span>
<span class="comments">;     Update BSCALE even if no astrometry present   W. Landsman  May 1997</span>
<span class="comments">;     Converted to IDL V5.0   W. Landsman   September 1997</span>
<span class="comments">;     Use FREBIN to accept sizes that are not a integer multiple of the original</span>
<span class="comments">;         size    W. Landsman     August 1998</span>
<span class="comments">;     Correct for "edge" effects when expanding with REBIN W. Landsman Apr. 1999</span>
<span class="comments">;     Fixed initialization of header only call broken in Apr 98 change May. 1999</span>
<span class="comments">;     Remove reference to obsolete !ERR  W. Landsman   February 2000</span>
<span class="comments">;     Use double precision formatting for CD matrix W. Landsman April 2000</span>
<span class="comments">;     Recognize PC00n00m astrometry format   W. Landsman   December 2001</span>
<span class="comments">;     Correct astrometry for integral contraction W. Landsman  April 2002</span>
<span class="comments">;     Fix output astrometry for non-equal plate scales for PC matrix or</span>
<span class="comments">;     CROTA2 keyword, added ALT keyword.   W. Landsman May 2005</span>
<span class="comments">;     Update distortion parameters if present  W. Landsman August 2007</span>
<span class="comments">;     Don't update BSCALE/BZERO for unsigned integer W.Landsman Mar 2008</span>
<span class="comments">;     Use post-V6.0 notation   W. Landsman  Nov 2011</span>
<span class="comments">;     Write CRPIX values as double precision if necessay W. Landsman Oct. 2012</span>
<span class="comments">;- </span>
 On_error,2
 compile_opt idl2

 npar = N_params()      <span class="comments">;Check # of parameters</span>
 if (npar EQ 3) || (npar EQ 5) || (npar EQ 0) then begin
     print,'Syntax - HREBIN, oldim, oldhd,[ newim, newhd, OUTSIZE=, ' + $
                           '/SAMPLE, ERRMSG= ]'
     return
 endif

 if ~keyword_set(SAMPLE) then sample = 0
 save_err = arg_present(errmsg)      <span class="comments">;Does user want to return error messages?</span>

<span class="comments">; If only 1 parameter is supplied, then assume it is a FITS header</span>

 if ( npar EQ 1 ) then begin           

        zparcheck, 'HREBIN', oldim, 1, 7, 1, 'Image header'
        oldhd = oldim
        xsize = sxpar( oldhd,'NAXIS1' )
        ysize = sxpar( oldhd,'NAXIS2' )

 endif else begin 

     check_FITS, oldim, oldhd, dimen, /NOTYPE, ERRMSG = errmsg
     if errmsg NE '' then begin
        if ~save_err then message,'ERROR - ' + errmsg,/CON
        return
     endif
     if N_elements(dimen) NE 2 then begin 
           errmsg = 'Input image array must be 2-dimensional'
           if ~save_err then message,'ERROR - ' + errmsg,/CON
           return
     endif
      xsize = dimen[0]  &  ysize = dimen[1]
 endelse
 tname = size(oldim,/tname)
 
 if ( npar LT 6 ) then begin

    if ( N_elements(OUTSIZE) NE 2 ) then begin
    tit = !MSG_PREFIX + 'HREBIN: '
    print, tit, 'Original array size is '+ strn(xsize) + ' by ' + strn(ysize)
    read, tit + 'Enter size of new image in the X direction: ',newx
    read, tit + 'Enter size of new image in the Y direction: ',newy
  endif else begin
     newx = outsize[0]
     newy = outsize[1]
   endelse 
 
 endif

<span class="comments">;  If an image array supplied then apply the REBIN  or FREBIN functions</span>
<span class="comments">; If output size is a multiple of input size then use REBIN else use FREBIN</span>

  exact = (~(xsize mod newx)  || ~(newx mod xsize)) && $
          (~(ysize mod newy)  || ~(newy mod ysize)  )

 if npar GT 1 then begin
 if exact then begin
   if npar GT 2 then newim = rebin( oldim, newx, newy, SAMPLE=sample) $
                else oldim = rebin( oldim, newx, newy, SAMPLE=sample)
 
 endif else begin
   if npar GT 2 then newim = frebin( oldim, newx, newy) $
                else oldim = frebin( oldim, newx, newy)
 endelse 
   endif


 if ( sample GT 0 ) then type = ' Nearest Neighbor Approximation' else begin
          if ( newx LT xsize ) then type = ' Box Averaging' else $
                                    type = ' Bilinear Interpolation'
 endelse

 newhd = oldhd
 sxaddpar, newhd, 'NAXIS1', fix(newx)
 sxaddpar, newhd, 'NAXIS2', fix(newy)
 label = 'HREBIN: '+ strmid( systime(),4,20 )
 sxaddpar,newhd,'history',label + ' Original Image Size Was '+ $
         strn(xsize) +' by ' +  strn(ysize) 
 if ( npar GT 1 ) then sxaddpar,newhd,'history',label+type
 
 xratio = float(newx) / xsize   <span class="comments">;Expansion or contraction in X</span>
 yratio = float(newy) / ysize   <span class="comments">;Expansion or contraction in Y</span>
 lambda = yratio/xratio         <span class="comments">;Measures change in aspect ratio.</span>
 pix_ratio = xratio*yratio      <span class="comments">;Ratio of pixel areas</span>


<span class="comments">; Update astrometry info if it exists</span>

 extast, newhd, astr, noparams, ALT = alt
 if noparams GE 0 then begin

 if strmid(astr.ctype[0],5,3) EQ 'GSS' then begin
        gsss_stdast, newhd
        extast, newhd, astr, noparams
 endif


<span class="comments">; Correct the position of the reference pixel.   Note that CRPIX values are</span>
<span class="comments">; given in FORTRAN (first pixel is (1,1)) convention</span>

 crpix = astr.crpix

<span class="comments">; When expanding with REBIN with bilinear interpolation (SAMPLE = 0), edge</span>
<span class="comments">; effects are introduced, which require a different calculation of the updated</span>
<span class="comments">; CRPIX1 and CRPIX2 values.</span>

 if (exact) && (~keyword_set(SAMPLE)) && (xratio GT 1) then $
      crpix1 = (crpix[0]-1.0)*xratio + 1.0                  else $
      crpix1 = (crpix[0]-0.5)*xratio + 0.5

 if (exact) && (~keyword_set(SAMPLE)) && (yratio GT 1) then $
      crpix2 = (crpix[1]-1.0)*yratio + 1.0                  else $
      crpix2 = (crpix[1]-0.5)*yratio + 0.5

 if N_elements(alt) EQ 0 then alt = ''
 sxaddpar, newhd, 'CRPIX1' + alt, crpix1
 sxaddpar, newhd, 'CRPIX2' + alt, crpix2
 
  if tag_exist(astr,'DISTORT') then begin
         distort = astr.distort
	 message,'Updating SIP distortion parameters',/INF
         update_distort,distort, [1./xratio,0],[1./yratio,0]
	 astr.distort= distort
	 add_distort, newhd, astr
   endif	 



<span class="comments">; Scale either the CDELT parameters or the CD1_1 parameters.</span>

 if (noparams NE 2) then begin 

    cdelt = astr.cdelt
    sxaddpar, newhd, 'CDELT1' + alt, CDELT[0]/xratio
    sxaddpar, newhd, 'CDELT2' + alt, CDELT[1]/yratio
<span class="comments">; Adjust the PC matrix if aspect ratio has changed.   See equation 187 in </span>
<span class="comments">; Calabretta & Greisen (2002)</span>
    if lambda NE 1.0 then begin
        cd = astr.cd
	if noparams EQ 1 then begin
<span class="comments">;Can no longer use the simple CROTA2 convention, change to PC keywords</span>
	 sxaddpar,newhd,'PC1_1'+alt, cd[0,0]
	 sxaddpar, newhd,'PC2_2'+alt, cd[1,1]
         sxdelpar, newhd, ['CROTA2','CROTA1']
        endif	
        sxaddpar, newhd, 'PC1_2'+alt, cd[0,1]/lambda
        sxaddpar, newhd, 'PC2_1'+alt, cd[1,0]*lambda
   endif	

 endif else begin     <span class="comments">;CDn_m Matrix format</span>

    cd = astr.cd
    sxaddpar, newhd, 'CD1_1'+alt, cd[0,0]/xratio
    sxaddpar, newhd, 'CD1_2'+alt, cd[0,1]/yratio
    sxaddpar, newhd, 'CD2_1'+alt, cd[1,0]/xratio
    sxaddpar, newhd, 'CD2_2'+alt, cd[1,1]/yratio

 endelse
 endif

<span class="comments">; Adjust BZERO and BSCALE for new pixel size, unless these values are used</span>
<span class="comments">; to define unsigned integer data types.  </span>

 bscale = sxpar( oldhd, 'BSCALE')
 bzero = sxpar( oldhd, 'BZERO')
 unsgn = (tname EQ 'UINT') || (tname EQ 'ULONG') 

 if ~unsgn then begin 
 if (bscale NE 0) && (bscale NE 1) then $
    sxaddpar, newhd, 'BSCALE', bscale/pix_ratio, 'Calibration Factor'
 if (bzero NE 0) then sxaddpar, newhd, 'BZERO', bzero/pix_ratio, $
       ' Additive Constant for Calibration'
 endif 
 
  pixelsiz = sxpar( oldhd,'PIXELSIZ' , Count = N_pixelsiz)
 if N_pixelsiz GT 0 then sxaddpar, newhd, 'PIXELSIZ', pixelsiz/xratio

 if npar EQ 2 then oldhd = newhd else $
    if npar EQ 1 then oldim = newhd

 return
 end
</code>
    </div>
  </body>
</html>