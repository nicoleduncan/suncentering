<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN"
 "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">

<!-- Generated by IDLdoc 3.5.1 on Mon Sep 30 16:57:20 2013 -->

<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en">
  <head>
    <meta http-equiv="Content-Type" content="text/html;charset=utf-8"/>
    <title>bprecess.pro (Documentation for ./)</title>

    
    <link rel="stylesheet" type="text/css" media="all"
          href="../idldoc-resources/main.css" />
    <link rel="stylesheet" type="text/css" media="print"
          href="../idldoc-resources/main-print.css" />
    

    <script type="text/javascript">
      function setTitle() {
        parent.document.title="bprecess.pro (Documentation for ./)";
      }
    </script>
  </head>

  <body onload="setTitle();" id="root">
    <div class="content">
      <code class="source"><a id="Bprecess:source"></a>pro Bprecess, ra, dec, ra_1950, dec_1950, MU_RADEC = mu_radec,  $
                  PARALLAX = parallax,  RAD_VEL = rad_vel, EPOCH = epoch
<span class="comments">;+</span>
<span class="comments">; NAME:</span>
<span class="comments">;       BPRECESS</span>
<span class="comments">; PURPOSE:</span>
<span class="comments">;       Precess positions from J2000.0 (FK5) to B1950.0 (FK4)</span>
<span class="comments">; EXPLANATION:</span>
<span class="comments">;       Calculates the mean place of a star at B1950.0 on the FK4 system from</span>
<span class="comments">;       the mean place at J2000.0 on the FK5 system.    </span>
<span class="comments">;</span>
<span class="comments">; CALLING SEQUENCE:</span>
<span class="comments">;       bprecess, ra, dec, ra_1950, dec_1950, [ MU_RADEC = , PARALLAX = </span>
<span class="comments">;                                       RAD_VEL =, EPOCH =   ]</span>
<span class="comments">;</span>
<span class="comments">; INPUTS:</span>
<span class="comments">;       RA,DEC - Input J2000 right ascension and declination in *degrees*.</span>
<span class="comments">;               Scalar or N element vector</span>
<span class="comments">;</span>
<span class="comments">; OUTPUTS:</span>
<span class="comments">;       RA_1950, DEC_1950 - The corresponding B1950 right ascension and </span>
<span class="comments">;               declination in *degrees*.    Same number of elements as</span>
<span class="comments">;               RA,DEC but always double precision.</span>
<span class="comments">;</span>
<span class="comments">; OPTIONAL INPUT-OUTPUT KEYWORDS</span>
<span class="comments">;       MU_RADEC - 2xN element double precision vector containing the proper </span>
<span class="comments">;                  motion in seconds of arc per tropical *century* in right </span>
<span class="comments">;                  ascension and declination.</span>
<span class="comments">;       PARALLAX - N_element vector giving stellar parallax (seconds of arc)</span>
<span class="comments">;       RAD_VEL  - N_element vector giving radial velocity in km/s</span>
<span class="comments">;</span>
<span class="comments">;       The values of MU_RADEC, PARALLAX, and RADVEL will all be modified</span>
<span class="comments">;       upon output to contain the values of these quantities in the</span>
<span class="comments">;       B1950 system.  The parallax and radial velocity will have a very </span>
<span class="comments">;       minor influence on the B1950 position.   </span>
<span class="comments">;</span>
<span class="comments">;       EPOCH - scalar giving epoch of original observations, default 2000.0d</span>
<span class="comments">;           This keyword value is only used if the MU_RADEC keyword is not set.</span>
<span class="comments">; NOTES:</span>
<span class="comments">;       The algorithm is taken from the Explanatory Supplement to the </span>
<span class="comments">;       Astronomical Almanac 1992, page 186.</span>
<span class="comments">;       Also see Aoki et al (1983), A&A, 128,263</span>
<span class="comments">;</span>
<span class="comments">;       BPRECESS distinguishes between the following two cases:</span>
<span class="comments">;       (1) The proper motion is known and non-zero</span>
<span class="comments">;       (2) the proper motion is unknown or known to be exactly zero (i.e.</span>
<span class="comments">;               extragalactic radio sources).   In this case, the reverse of </span>
<span class="comments">;               the algorithm in Appendix 2 of Aoki et al. (1983) is used to </span>
<span class="comments">;               ensure that the output proper motion is  exactly zero. Better </span>
<span class="comments">;               precision can be achieved in this case by inputting the EPOCH </span>
<span class="comments">;               of the original observations.</span>
<span class="comments">;</span>
<span class="comments">;       The error in using the IDL procedure PRECESS for converting between</span>
<span class="comments">;       B1950 and J1950 can be up to 12", mainly in right ascension.   If</span>
<span class="comments">;       better accuracy than this is needed then BPRECESS should be used.</span>
<span class="comments">;</span>
<span class="comments">;       An unsystematic comparison of BPRECESS with the IPAC precession </span>
<span class="comments">;       routine (http://nedwww.ipac.caltech.edu/forms/calculator.html) always </span>
<span class="comments">;       gives differences less than 0.15".</span>
<span class="comments">; EXAMPLE:</span>
<span class="comments">;       The SAO2000 catalogue gives the J2000 position and proper motion for</span>
<span class="comments">;       the star HD 119288.   Find the B1950 position. </span>
<span class="comments">;</span>
<span class="comments">;       RA(2000) = 13h 42m 12.740s      Dec(2000) = 8d 23' 17.69''  </span>
<span class="comments">;       Mu(RA) = -.0257 s/yr      Mu(Dec) = -.090 ''/yr</span>
<span class="comments">;</span>
<span class="comments">;       IDL> mu_radec = 100D* [ -15D*.0257, -0.090 ]</span>
<span class="comments">;       IDL> ra = ten(13, 42, 12.740)*15.D </span>
<span class="comments">;       IDL> dec = ten(8, 23, 17.69)</span>
<span class="comments">;       IDL> bprecess, ra, dec, ra1950, dec1950, mu_radec = mu_radec</span>
<span class="comments">;       IDL> print, adstring(ra1950, dec1950,2)</span>
<span class="comments">;               ===> 13h 39m 44.526s    +08d 38' 28.63"</span>
<span class="comments">;</span>
<span class="comments">; REVISION HISTORY:</span>
<span class="comments">;       Written,    W. Landsman                October, 1992</span>
<span class="comments">;       Vectorized, W. Landsman                February, 1994</span>
<span class="comments">;       Treat case where proper motion not known or exactly zero  November 1994</span>
<span class="comments">;       Handling of arrays larger than 32767   Lars L. Christensen, march, 1995</span>
<span class="comments">;       Fixed bug where A term not initialized for vector input </span>
<span class="comments">;            W. Landsman        February 2000</span>
<span class="comments">;       Use V6.0 notation  W. Landsman Mar 2011</span>
<span class="comments">;       </span>
<span class="comments">;-   </span>
  On_error,2
  compile_opt idl2

  if N_params() LT 4 then begin
     print,'Syntax - BPRECESS, ra,dec, ra_1950, dec_1950, [MU_RADEC =' 
     print,'                            PARALLAX = , RAD_VEL = ]'
     print,'  Input RA and Dec should be given in DEGREES for J2000'
     print,'  Proper motion, MU_RADEC, (optional) in arc seconds per *century*'
     print,'  Parallax (optional) in arc seconds'      
     print,'  Radial Velocity (optional) in km/s'
     return

  endif

  N = N_elements( ra )
  if N EQ 0 then message,'ERROR - First parameter (RA vector) is undefined'

  if ~keyword_set( RAD_VEL) then rad_vel = dblarr(N) else begin
        rad_vel = rad_vel*1.
        if N_elements( RAD_VEL) NE N then message, $
        'ERROR - RAD_VEL keyword vector must contain ' + strtrim(N,2) +' values'
  endelse

  if keyword_set( MU_RADEC) then begin
         if (N_elements( mu_radec) NE 2*N ) then message, $
    'ERROR - MU_RADEC keyword (proper motion) be dimensioned (2,' + $
                 strtrim(N,2) + ')'
        mu_radec = mu_radec*1.
  endif

  if ~keyword_set( Parallax) then parallax = dblarr(N) else $
        parallax = parallax*1.

  if ~keyword_set(Epoch) then epoch = 2000.0d0

  radeg = 180.D/!DPI
  sec_to_radian = 1.d0/radeg/3600.d0

 M =  [ [+0.9999256795D, -0.0111814828D, -0.0048590040D,  $
         -0.000551D,  -0.238560D,     +0.435730D     ], $
       [ +0.0111814828D, +0.9999374849D, -0.0000271557D,  $ 
         +0.238509D,     -0.002667D,      -0.008541D     ], $
       [ +0.0048590039D, -0.0000271771D, +0.9999881946D , $
         -0.435614D,      +0.012254D,      +0.002117D      ], $
       [ -0.00000242389840D, +0.00000002710544D, +0.00000001177742D, $
         +0.99990432D,    -0.01118145D,    -0.00485852D    ], $
       [ -0.00000002710544D, -0.00000242392702D, +0.00000000006585D, $
         +0.01118145D,     +0.99991613D,    -0.00002716D    ], $
       [ -0.00000001177742D, +0.00000000006585D,-0.00000242404995D, $
         +0.00485852D,   -0.00002717D,    +0.99996684D] ] 

 A_dot = 1D-3*[1.244D, -1.579D, -0.660D ]           <span class="comments">;in arc seconds per century</span>

 ra_rad = ra/radeg       &      dec_rad = dec/radeg
 cosra =  cos( ra_rad )  &       sinra = sin( ra_rad )
 cosdec = cos( dec_rad ) &      sindec = sin( dec_rad )

 dec_1950 = dec*0.
 ra_1950 = ra*0.

 for i = 0L, N-1 do begin

<span class="comments">; Following statement moved inside loop in Feb 2000.</span>
 A = 1D-6*[ -1.62557D, -0.31919D, -0.13843D]        <span class="comments">;in radians</span>

 r0 = [ cosra[i]*cosdec[i], sinra[i]*cosdec[i], sindec[i] ]

 if keyword_set(mu_radec) then begin

 mu_a = mu_radec[ 0, i ]
 mu_d = mu_radec[ 1, i ]
 r0_dot = [ -mu_a*sinra[i]*cosdec[i] - mu_d*cosra[i]*sindec[i] , $ <span class="comments">;Velocity vector</span>
             mu_a*cosra[i]*cosdec[i] - mu_d*sinra[i]*sindec[i] , $
             mu_d*cosdec[i] ] + 21.095d * rad_vel[i] * parallax[i] * r0

 endif else r0_dot = [0.0d0, 0.0d0, 0.0d0]

  R_0 = [ r0, r0_dot ]
  R_1 =  M # R_0

 <span class="comments">; Include the effects of the E-terms of aberration to form r and r_dot.</span>

 r1 = R_1[0:2]  
 r1_dot = R_1[3:5] 

 if ~keyword_set(Mu_radec) then begin
        r1 = r1 + sec_to_radian * r1_dot * (epoch - 1950.0d)/100.
        A = A + sec_to_radian * A_dot * (epoch - 1950.0d)/100.
 endif

 x1 = R_1[0]   &   y1 = R_1[1]    &  z1 = R_1[2]
 rmag = sqrt( x1^2 + y1^2 + z1^2 )


 s1 = r1/rmag    & s1_dot = r1_dot/rmag

 s = s1
 for j = 0,2 do begin
    r = s1 + A - (total(s * A))*s
    s = r/rmag
 endfor 
 x = r[0]          & y = r[1]     &  z = r[2]  
 r2 = x^2 + y^2 + z^2
 rmag = sqrt( r2 )
 
 if keyword_set(Mu_radec) then begin
         r_dot = s1_dot + A_dot - ( total( s * A_dot))*s
         x_dot = r_dot[0]  & y_dot= r_dot[1]  &  z_dot = r_dot[2]
         mu_radec[0,i] = ( x*y_dot - y*x_dot) / ( x^2 + y^2)
         mu_radec[1,i] = ( z_dot* (x^2 + y^2) - z*(x*x_dot + y*y_dot) ) /  $
                     ( r2*sqrt( x^2 + y^2) )
 endif

 dec_1950[i] = asin( z / rmag)
 ra_1950[i] = atan( y, x)

  if parallax[i] GT 0. then begin
      rad_vel[i] = ( x*x_dot + y*y_dot + z*z_dot )/ (21.095*Parallax[i]*rmag)
      parallax[i] = parallax[i] / rmag
  endif
 endfor

 neg = where( ra_1950 LT 0, NNeg )
 if Nneg GT 0 then ra_1950[neg] = ra_1950[neg] + 2.D*!DPI

 ra_1950 = ra_1950*radeg & dec_1950 = dec_1950*radeg

<span class="comments">; Make output scalar if input was scalar</span>

 sz = size(ra)
 if sz[0] EQ 0 then begin
        ra_1950 = ra_1950[0]     &      dec_1950 = dec_1950[0]
 endif

 return
 end
</code>
    </div>
  </body>
</html>