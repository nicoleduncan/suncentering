<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN"
 "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">

<!-- Generated by IDLdoc 3.5.1 on Mon Sep 30 16:57:56 2013 -->

<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en">
  <head>
    <meta http-equiv="Content-Type" content="text/html;charset=utf-8"/>
    <title>precess.pro (Documentation for ./)</title>

    
    <link rel="stylesheet" type="text/css" media="all"
          href="../idldoc-resources/main.css" />
    <link rel="stylesheet" type="text/css" media="print"
          href="../idldoc-resources/main-print.css" />
    

    <script type="text/javascript">
      function setTitle() {
        parent.document.title="precess.pro (Documentation for ./)";
      }
    </script>
  </head>

  <body onload="setTitle();" id="root">
    <div class="content">
      <code class="source"><a id="precess:source"></a>pro precess, ra, dec, equinox1, equinox2, PRINT = print, FK4 = FK4, $
        RADIAN=radian
<span class="comments">;+</span>
<span class="comments">; NAME:</span>
<span class="comments">;      PRECESS</span>
<span class="comments">; PURPOSE:</span>
<span class="comments">;      Precess coordinates from EQUINOX1 to EQUINOX2.  </span>
<span class="comments">; EXPLANATION:</span>
<span class="comments">;      For interactive display, one can use the procedure ASTRO which calls </span>
<span class="comments">;      PRECESS or use the /PRINT keyword.   The default (RA,DEC) system is </span>
<span class="comments">;      FK5 based on epoch J2000.0 but FK4 based on B1950.0 is available via </span>
<span class="comments">;      the /FK4 keyword.</span>
<span class="comments">;</span>
<span class="comments">;      Use BPRECESS and JPRECESS to convert between FK4 and FK5 systems</span>
<span class="comments">; CALLING SEQUENCE:</span>
<span class="comments">;      PRECESS, ra, dec, [ equinox1, equinox2, /PRINT, /FK4, /RADIAN ]</span>
<span class="comments">;</span>
<span class="comments">; INPUT - OUTPUT:</span>
<span class="comments">;      RA - Input right ascension (scalar or vector) in DEGREES, unless the </span>
<span class="comments">;              /RADIAN keyword is set</span>
<span class="comments">;      DEC - Input declination in DEGREES (scalar or vector), unless the </span>
<span class="comments">;              /RADIAN keyword is set</span>
<span class="comments">;              </span>
<span class="comments">;      The input RA and DEC are modified by PRECESS to give the </span>
<span class="comments">;      values after precession.</span>
<span class="comments">;</span>
<span class="comments">; OPTIONAL INPUTS:</span>
<span class="comments">;      EQUINOX1 - Original equinox of coordinates, numeric scalar.  If </span>
<span class="comments">;               omitted, then PRECESS will query for EQUINOX1 and EQUINOX2.</span>
<span class="comments">;      EQUINOX2 - Equinox of precessed coordinates.</span>
<span class="comments">;</span>
<span class="comments">; OPTIONAL INPUT KEYWORDS:</span>
<span class="comments">;      /PRINT - If this keyword is set and non-zero, then the precessed</span>
<span class="comments">;               coordinates are displayed at the terminal.    Cannot be used</span>
<span class="comments">;               with the /RADIAN keyword</span>
<span class="comments">;      /FK4   - If this keyword is set and non-zero, the FK4 (B1950.0) system</span>
<span class="comments">;               will be used otherwise FK5 (J2000.0) will be used instead.</span>
<span class="comments">;      /RADIAN - If this keyword is set and non-zero, then the input and </span>
<span class="comments">;               output RA and DEC vectors are in radians rather than degrees</span>
<span class="comments">;</span>
<span class="comments">; RESTRICTIONS:</span>
<span class="comments">;       Accuracy of precession decreases for declination values near 90 </span>
<span class="comments">;       degrees.  PRECESS should not be used more than 2.5 centuries from</span>
<span class="comments">;       2000 on the FK5 system (1950.0 on the FK4 system).</span>
<span class="comments">;</span>
<span class="comments">; EXAMPLES:</span>
<span class="comments">;       (1) The Pole Star has J2000.0 coordinates (2h, 31m, 46.3s, </span>
<span class="comments">;               89d 15' 50.6"); compute its coordinates at J1985.0</span>
<span class="comments">;</span>
<span class="comments">;       IDL> precess, ten(2,31,46.3)*15, ten(89,15,50.6), 2000, 1985, /PRINT</span>
<span class="comments">;</span>
<span class="comments">;               ====> 2h 16m 22.73s, 89d 11' 47.3"</span>
<span class="comments">;</span>
<span class="comments">;       (2) Precess the B1950 coordinates of Eps Ind (RA = 21h 59m,33.053s,</span>
<span class="comments">;       DEC = (-56d, 59', 33.053") to equinox B1975.</span>
<span class="comments">;</span>
<span class="comments">;       IDL> ra = ten(21, 59, 33.053)*15</span>
<span class="comments">;       IDL> dec = ten(-56, 59, 33.053)</span>
<span class="comments">;       IDL> precess, ra, dec ,1950, 1975, /fk4</span>
<span class="comments">;</span>
<span class="comments">; PROCEDURE:</span>
<span class="comments">;       Algorithm from Computational Spherical Astronomy by Taff (1983), </span>
<span class="comments">;       p. 24. (FK4). FK5 constants from "Astronomical Almanac Explanatory</span>
<span class="comments">;       Supplement 1992, page 104 Table 3.211.1.</span>
<span class="comments">;</span>
<span class="comments">; PROCEDURE CALLED:</span>
<span class="comments">;       Function PREMAT - computes precession matrix </span>
<span class="comments">;</span>
<span class="comments">; REVISION HISTORY</span>
<span class="comments">;       Written, Wayne Landsman, STI Corporation  August 1986</span>
<span class="comments">;       Correct negative output RA values   February 1989</span>
<span class="comments">;       Added /PRINT keyword      W. Landsman   November, 1991</span>
<span class="comments">;       Provided FK5 (J2000.0)  I. Freedman   January 1994</span>
<span class="comments">;       Precession Matrix computation now in PREMAT   W. Landsman June 1994</span>
<span class="comments">;       Added /RADIAN keyword                         W. Landsman June 1997</span>
<span class="comments">;       Converted to IDL V5.0   W. Landsman   September 1997</span>
<span class="comments">;       Correct negative output RA values when /RADIAN used    March 1999 </span>
<span class="comments">;       Work for arrays, not just vectors  W. Landsman    September 2003 </span>
<span class="comments">;-    </span>
  On_error,2                                           <span class="comments">;Return to caller</span>

  npar = N_params()
  deg_to_rad = !DPI/180.0D0

   if ( npar LT 2 ) then begin 

     print,'Syntax - PRECESS, ra, dec, [ equinox1, equinox2,' + $ 
                ' /PRINT, /FK4, /RADIAN ]'
     print,'         NOTE: RA and DEC must be in DEGREES unless /RADIAN is set'
     return 

  endif else if (npar LT 4) then $
      read,'Enter original and new equinox of coordinates: ',equinox1,equinox2 

  npts = min( [N_elements(ra), N_elements(dec)] )
  if npts EQ 0 then $  
       message,'ERROR - Input RA and DEC must be vectors or scalars'
  array  = size(ra,/N_dimen) GE 2
  if array then dimen = size(ra,/dimen)

  if ~keyword_set( RADIAN) then begin
          ra_rad = ra*deg_to_rad     <span class="comments">;Convert to double precision if not already</span>
          dec_rad = dec*deg_to_rad 
  endif else begin
        ra_rad= double(ra) & dec_rad = double(dec)
  endelse

  a = cos( dec_rad )  

 CASE npts of                    <span class="comments">;Is RA a vector or scalar?</span>

   1:    x = [a*cos(ra_rad), a*sin(ra_rad), sin(dec_rad)] <span class="comments">;input direction </span>

   else: begin          

         x = dblarr(npts,3)
         x[0,0] = reform(a*cos(ra_rad),npts,/over)
         x[0,1] = reform(a*sin(ra_rad),npts,/over)
         x[0,2] = reform(sin(dec_rad),npts,/over)
         x = transpose(x)
         end

   ENDCASE  

   sec_to_rad = deg_to_rad/3600.d0

<span class="comments">; Use PREMAT function to get precession matrix from Equinox1 to Equinox2</span>

  r = premat(equinox1, equinox2, FK4 = fk4)

  x2 = r#x      <span class="comments">;rotate to get output direction cosines</span>

 if npts EQ 1 then begin                 <span class="comments">;Scalar</span>

        ra_rad = atan(x2[1],x2[0])
        dec_rad = asin(x2[2])

 endif else begin                <span class="comments">;Vector     </span>

        ra_rad = dblarr(npts) + atan(x2[1,*],x2[0,*])
        dec_rad = dblarr(npts) + asin(x2[2,*])

 endelse

  if ~keyword_set(RADIAN) then begin
        ra = ra_rad/deg_to_rad
        ra = ra + (ra LT 0.)*360.D            <span class="comments">;RA between 0 and 360 degrees</span>
        dec = dec_rad/deg_to_rad
  endif else begin
        ra = ra_rad & dec = dec_rad
        ra = ra + (ra LT 0.)*2.0d*!DPI
  endelse

  if array then begin
       ra = reform(ra, dimen , /over)
       dec = reform(dec, dimen, /over)
  endif

  if keyword_set( PRINT ) then $
      print, 'Equinox (' + strtrim(equinox2,2) + '): ',adstring(ra,dec,1)

  return
  end
</code>
    </div>
  </body>
</html>