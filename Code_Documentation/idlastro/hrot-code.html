<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN"
 "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">

<!-- Generated by IDLdoc 3.5.1 on Mon Sep 30 16:57:42 2013 -->

<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en">
  <head>
    <meta http-equiv="Content-Type" content="text/html;charset=utf-8"/>
    <title>hrot.pro (Documentation for ./)</title>

    
    <link rel="stylesheet" type="text/css" media="all"
          href="../idldoc-resources/main.css" />
    <link rel="stylesheet" type="text/css" media="print"
          href="../idldoc-resources/main-print.css" />
    

    <script type="text/javascript">
      function setTitle() {
        parent.document.title="hrot.pro (Documentation for ./)";
      }
    </script>
  </head>

  <body onload="setTitle();" id="root">
    <div class="content">
      <code class="source"><a id="hrot:source"></a>pro hrot, oldim, oldhd, newim, newhd, angle, xc, yc, int, MISSING=missing, $
                INTERP = interp, CUBIC = cubic, PIVOT = pivot,ERRMSG= errmsg
<span class="comments">;+</span>
<span class="comments">; NAME:</span>
<span class="comments">;       HROT</span>
<span class="comments">; PURPOSE:</span>
<span class="comments">;       Rotate an image and create new FITS header with updated astrometry.</span>
<span class="comments">; EXPLANATION: </span>
<span class="comments">;       Cubic, bilinear or nearest neighbor interpolation can be used.</span>
<span class="comments">;</span>
<span class="comments">; CALLING SEQUENCE:</span>
<span class="comments">;       HROT, oldim, oldhd, [ newim, newhd, angle, xc, yc, int, </span>
<span class="comments">;                       MISSING =, INTERP =, CUBIC = , /PIVOT]</span>
<span class="comments">; INPUTS:</span>
<span class="comments">;       OLDIM - the original image array                             </span>
<span class="comments">;       OLDHD - the original FITS image header, string array</span>
<span class="comments">;</span>
<span class="comments">; OPTIONAL INPUTS:</span>
<span class="comments">;       NEWIM - If NEWIM is set to -1, then the old image and header will</span>
<span class="comments">;               be updated</span>
<span class="comments">;       ANGLE - Rotation angle, degrees clockwise, scalar</span>
<span class="comments">;       XC    - X Center of rotation (-1 for center of image)</span>
<span class="comments">;       YC    - Y Center of rotation (-1 for center of image)</span>
<span class="comments">;       INT   - 0 for nearest neighbor, 1 for bilinear interpolation</span>
<span class="comments">;               2 for cubic interpolation.  </span>
<span class="comments">;</span>
<span class="comments">; OPTIONAL OUTPUTS:</span>
<span class="comments">;       NEWIM - the rotated image, with the same dimensions as Oldim </span>
<span class="comments">;       NEWHD - header for newim containing updated astrometry info</span>
<span class="comments">;               If output parameters are not supplied, the program</span>
<span class="comments">;               will modify the input parameters OLDIM and OLDHD</span>
<span class="comments">;               to contain the rotated image and updated header.</span>
<span class="comments">;</span>
<span class="comments">; OPTIONAL INPUT KEYWORD:</span>
<span class="comments">;       MISSING - Set this keyword to a scalar value which will be assigned</span>
<span class="comments">;               to pixels in the output image which do not correspond to </span>
<span class="comments">;               existing input images (e.g if one rotates off-center). </span>
<span class="comments">;               If not supplied then linear extrapolation is used.</span>
<span class="comments">;               ***NOTE: A bug was introduced into the POLY_2D function in IDL</span>
<span class="comments">;               V5.5 (fixed in V6.1) such that the MISSING keyword</span>
<span class="comments">;               may not work properly with floating point data***</span>
<span class="comments">;</span>
<span class="comments">;       INTERP - scalar set to either 0 (nearest neighbor interpolation),</span>
<span class="comments">;               1 (bilinear interpolation), or 2 (cubic interpolation).    </span>
<span class="comments">;               The interpolation type can be specified by either the INTERP </span>
<span class="comments">;               keyword or the int parameter</span>
<span class="comments">;             </span>
<span class="comments">;       CUBIC - If set and non-zero then cubic interpolation is used (see ROT),</span>
<span class="comments">;               which is equivalent to setting INT = 2.   In IDL V5.0 and later,</span>
<span class="comments">;                this keyword can also be set to a value between -1 and 0.</span>
<span class="comments">;</span>
<span class="comments">;       /PIVOT - Setting this keyword causes the image to pivot around the point</span>
<span class="comments">;		XC, YC, so that this point maps into the same point in the</span>
<span class="comments">;		output image.  If this keyword is set to 0 or omitted, then the</span>
<span class="comments">;		point XC, YC in the input image is mapped into the center of</span>
<span class="comments">;		the output image.</span>
<span class="comments">;</span>
<span class="comments">; OPTIONAL OUTPUT KEYWORD:</span>
<span class="comments">;       ERRMSG - If this keyword is supplied, then any error mesasges will be</span>
<span class="comments">;               returned to the user in this parameter rather than depending on</span>
<span class="comments">;               on the MESSAGE routine in IDL.   If no errors are encountered</span>
<span class="comments">;               then a null string is returned.               </span>
<span class="comments">; EXAMPLE:</span>
<span class="comments">;       Rotate an image non-interactively 30 degrees clockwise.  Use</span>
<span class="comments">;       bilinear interpolation, and set missing values to 0.</span>
<span class="comments">;</span>
<span class="comments">;       IDL>  HROT, im_old, h_old, im_new, h_new, 30, -1, -1, 1, MIS = 0</span>
<span class="comments">;</span>
<span class="comments">;       As above but update the input image and header and pivot about (100,120)</span>
<span class="comments">;</span>
<span class="comments">;       IDL>  HROT, im_old, h_old, -1, -1, 30, 100, 120, 1, MIS = 0, /PIVOT</span>
<span class="comments">; RESTRICTIONS:</span>
<span class="comments">;       Unlike the ROT procedure, HROT cannot be used to magnify or</span>
<span class="comments">;       or demagnify an image. Use HCONGRID or HREBIN instead.</span>
<span class="comments">;</span>
<span class="comments">; PROCEDURE:</span>
<span class="comments">;       The image array is rotated using the ROT procedure.</span>
<span class="comments">;       The CD (or CROTA) and CRPIX parameters, if present in the FITS header,</span>
<span class="comments">;       are updated for the new rotation.</span>
<span class="comments">;       History records are also added to the header</span>
<span class="comments">;</span>
<span class="comments">; PROCEDURES USED:</span>
<span class="comments">;       CHECK_FITS, EXTAST, GETOPT(), GETROT, ROT(), STRN(), SXADDPAR</span>
<span class="comments">;</span>
<span class="comments">; MODIFICATION HISTORY:</span>
<span class="comments">;       Written, Aug. 1986 W. Landsman, ST Systems Corp.</span>
<span class="comments">;       Added MISSING keyword, W. Landsman March, 1991</span>
<span class="comments">;       Added cubic interpolation, use astrometry structure   Feb 1994</span>
<span class="comments">;       Removed call to SINCE_VERSION()  W. Landsman  March 1996</span>
<span class="comments">;       Assume at least V3.5, add CUBIC parameter       W. Landsman  March 1997</span>
<span class="comments">;       Converted to IDL V5.0   W. Landsman   September 1997</span>
<span class="comments">;       Fix for CROTA2 defined and CDELT1 NE CDELT2, W. Landsman  November 1998</span>
<span class="comments">;       Fix documentation  to specify clockwise rotation W. Landsman Dec. 1999</span>
<span class="comments">;       Added /PIVOT keyword    W. Landsman  January 2000</span>
<span class="comments">;       Added ERRMSG, Use double precision formatting, W. Landsman April 2000</span>
<span class="comments">;       Consistent conversion between CROTA and CD matrix W. Landsman Oct 2000</span>
<span class="comments">;       Work for both CD001001 and CDELT defined  W. Landsman   March 2001</span>
<span class="comments">;       Recognize PC matrix astrometry  W. Landsman December 2001</span>
<span class="comments">;       Update astrometry correctly when /PIVOT applied W. Landsman March 2002</span>
<span class="comments">;       Update CROTA2 astrometry correctly, approximate GSSS W.L. June 2003</span>
<span class="comments">;       Work with CD1_1, PC1_1 and CROTA keywords W. L. July 2003 </span>
<span class="comments">;       Work with angle as a 1 element vector  W.L.  May 2006</span>
<span class="comments">;- </span>
 On_error,2
 compile_opt idl2
 npar = N_params()

 if (npar LT 2) or (npar EQ 3) then begin       <span class="comments">;Check # of parameters</span>
  print,'Syntax: HROT, oldim, oldhd, [ newim, newhd, angle, xc, yc, int,'
  print,'                   CUBIC =, INTERP = , MISSING = ,/PIVOT, ERRMSG= ]'
  print, 'Oldim and Oldhd will be updated if only 2 parameters supplied '
     return
 endif 

 cdr = !DPI/180.0D              <span class="comments">;Change degrees to radians</span>
<span class="comments">;                               Check that input header matches input image</span>
 save_err = arg_present(errmsg)     <span class="comments">;Does user want error msgs returned?</span>
<span class="comments">;                                    Check for valid 2-D image & header</span>
 check_FITS, oldim, oldhd, dimen, /NOTYPE, ERRMSG = errmsg
  if errmsg NE '' then begin
        if not save_err then message,'ERROR - ' + errmsg,/CON
        return
  endif

  if N_elements(dimen) NE 2 then begin 
        errmsg =  'ERROR - Input image array must be 2-dimensional'
        if not save_err then message,'ERROR - ' + errmsg,/CON
        return
 endif

  xsize = dimen[0]  &  ysize = dimen[1]

  xc_new = (xsize - 1)/2.
  yc_new = (ysize - 1)/2.
 if npar LT 8 then begin
  if npar EQ 2 then print,'Program will modify old image and header'
  print,'Original array size is '+ strn(xsize) + ' by ' + strn(ysize)
  read,'Angle of rotation (degrees clockwise): ',angle
  ans = '' 
  read,'Enter center (x,y) of rotation ( [RETURN] for center of image): ',ans
  center = getopt(ans,'F',2)
  if N_elements(center) EQ 1 then begin
      xc = -1 & yc = -1
  endif else begin
      xc = center[0] & yc = center[1]
  endelse
 endif

 if keyword_set( INTERP ) then int = interp
 if keyword_set( CUBIC ) then int = 2
 if N_elements(int) NE 1 then $
   read,'Enter 0 for nearest neighbor, 1 for bilinear, 2 for cubic interpolation: ',int

 case int of 
 0: type = ' Nearest Neighbor Approximation'
 1: type = ' Bilinear Intepolation' 
 2: type = ' Cubic Interpolation'
 else: message,'Illegal value of Interp parameter: must be 0,1, or 2'
 endcase

 if xc LT 0 then xc = xc_new
 if yc LT 0 then yc = yc_new

 if N_elements(newim) EQ 1 then $
   if newim EQ -1 then npar = 2                                      

 newhd = oldhd
 if N_elements(cubic) EQ 0 then cubic = (int EQ 2) 
 angle = angle[0]

  if N_elements(MISSING) NE 1 then begin

        if npar EQ 2 then begin 
               oldim = rot( oldim, angle, 1, xc,yc, $
                            CUBIC = cubic, INTERP = int, PIVOT = pivot)
        endif else begin
               newim = rot( oldim, angle, 1, xc,yc, $
                            CUBIC = cubic, INTERP = int, PIVOT = pivot)
        endelse
 
 endif else begin

        if npar EQ 2 then begin
           oldim = rot( oldim,angle,1,xc,yc, $
                CUBIC = cubic, MISSING = missing, INTERP = int, PIVOT = pivot) 
        endif else begin
           newim = rot( oldim, angle, 1, xc, yc, $
                CUBIC = cubic, MISSING = missing, INTERP = int, PIVOT = pivot)
        endelse
  endelse

 label = 'HROT:' + strmid(systime(),4,20)
 sxaddpar, newhd, 'HISTORY', label + $ 
   ' Rotated by' + string(float(angle), FORM = '(f7.2)') + ' Degrees'
 sxaddpar,newhd,'history',label+type 

<span class="comments">; Update astrometry info if it exists</span>

 extast, oldhd, astr, noparams
 if strmid(astr.ctype[0],5,3) EQ 'GSS' then begin
        gsss_stdast, newhd
        extast, newhd, astr, noparams
 endif


 if noparams GE 0 then begin    <span class="comments">;Astrometry parameters exist in header?</span>
    crpix = astr.crpix
    cd = astr.cd
    cdelt = astr.cdelt
 
    theta = angle*cdr
    rot_mat = [ [ cos(theta), sin(theta)], $   <span class="comments">;Rotation matrix</span>
                [-sin(theta), cos(theta)] ] 
    
    ncrpix =  transpose(rot_mat)#(crpix-1-[xc,yc]) + 1
    if not keyword_set(PIVOT) then ncrpix = [xc_new,yc_new] + ncrpix $
                              else ncrpix = [xc,yc] + ncrpix
    sxaddpar, newhd, 'CRPIX1', ncrpix[0]
    sxaddpar, newhd, 'CRPIX2', ncrpix[1]
 
    newcd = cd # rot_mat

    if noparams EQ 3 then begin     <span class="comments">;Transformation matrix format</span>

        sxaddpar, newhd, 'PC1_1', newcd[0,0] 
        sxaddpar, newhd, 'PC1_2', newcd[0,1] 
        sxaddpar, newhd, 'PC2_1', newcd[1,0]
        sxaddpar, newhd, 'PC2_2', newcd[1,1]
 
                                  
    endif else if noparams EQ 2 then begin

        sxaddpar, newhd, 'CD1_1', newcd[0,0]
        sxaddpar, newhd, 'CD1_2', newcd[0,1]
        sxaddpar, newhd, 'CD2_1', newcd[1,0]
        sxaddpar, newhd, 'CD2_2', newcd[1,1]

     endif else begin  
<span class="comments">; Just need to update the CROTA keywords </span>
        crota  = atan( -newcd[1,0],newcd[1,1] )*180.0/!DPI
        sxaddpar, newhd,'CROTA1', crota
        sxaddpar, newhd,'CROTA2', crota
    
     endelse

 endif 

 if npar eq 2 then oldhd = newhd                <span class="comments">;update old image and header</span>
 
 return
 end
</code>
    </div>
  </body>
</html>