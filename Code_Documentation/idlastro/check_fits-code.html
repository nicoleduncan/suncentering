<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN"
 "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">

<!-- Generated by IDLdoc 3.5.1 on Mon Sep 30 16:57:20 2013 -->

<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en">
  <head>
    <meta http-equiv="Content-Type" content="text/html;charset=utf-8"/>
    <title>check_fits.pro (Documentation for ./)</title>

    
    <link rel="stylesheet" type="text/css" media="all"
          href="../idldoc-resources/main.css" />
    <link rel="stylesheet" type="text/css" media="print"
          href="../idldoc-resources/main-print.css" />
    

    <script type="text/javascript">
      function setTitle() {
        parent.document.title="check_fits.pro (Documentation for ./)";
      }
    </script>
  </head>

  <body onload="setTitle();" id="root">
    <div class="content">
      <code class="source"><a id="check_FITS:source"></a>pro check_FITS, im, hdr, dimen, idltype, UPDATE = update, NOTYPE = notype, $
                   SDAS = sdas, FITS = fits, SILENT = silent, ERRMSG = errmsg
<span class="comments">;+</span>
<span class="comments">; NAME:</span>
<span class="comments">;       CHECK_FITS</span>
<span class="comments">; PURPOSE:</span>
<span class="comments">;       Check that keywords in a FITS header array match the associated data  </span>
<span class="comments">; EXPLANATION:</span>
<span class="comments">;       Given a FITS array IM, and a associated FITS header HDR, this</span>
<span class="comments">;       procedure will check that</span>
<span class="comments">;               (1) HDR is a string array, and IM is defined and numeric   </span>
<span class="comments">;               (2) The NAXISi values in HDR are appropriate to the dimensions </span>
<span class="comments">;                   of IM</span>
<span class="comments">;               (3) The BITPIX value in HDR is appropriate to the datatype of IM</span>
<span class="comments">;       If the /UPDATE keyword is present, then the FITS header will be </span>
<span class="comments">;       modified, if necessary, to force agreement with the image array</span>
<span class="comments">;</span>
<span class="comments">; CALLING SEQUENCE:</span>
<span class="comments">;       check_FITS, im, hdr, [ dimen, idltype, /UPDATE, /NOTYPE, /SILENT</span>
<span class="comments">;                              ERRMSG = ]'</span>
<span class="comments">;</span>
<span class="comments">; INPUT PARAMETERS:</span>
<span class="comments">;       IM -  FITS array, e.g. as read by READFITS</span>
<span class="comments">;       HDR - FITS header (string array) associated with IM</span>
<span class="comments">;</span>
<span class="comments">; OPTIONAL OUTPUTS:</span>
<span class="comments">;       dimen - vector containing actual array dimensions</span>
<span class="comments">;       idltype- data type of the FITS array as specified in the IDL SIZE</span>
<span class="comments">;               function (1 for BYTE, 2 for INTEGER*2, 3 for INTEGER*4, etc.)</span>
<span class="comments">;</span>
<span class="comments">; OPTIONAL KEYWORD INPUTS:</span>
<span class="comments">;       /NOTYPE - If this keyword is set, then only agreement of the array</span>
<span class="comments">;               dimensions with the FITS header are checked, and not the </span>
<span class="comments">;               data type.</span>
<span class="comments">;       /UPDATE - If this keyword is set then the BITPIX, NAXIS and NAXISi</span>
<span class="comments">;               FITS keywords will be updated to agree with the array</span>
<span class="comments">;       /FITS, /SDAS -  these are obsolete keywords that now do nothing </span>
<span class="comments">;       /SILENT - If keyword is set and nonzero, the informational messages </span>
<span class="comments">;               will not be printed</span>
<span class="comments">; OPTIONAL KEYWORD OUTPUT:</span>
<span class="comments">;       ERRMSG  = If this keyword is present, then any error messages will be</span>
<span class="comments">;                 returned to the user in this parameter rather than</span>
<span class="comments">;                 depending on the MESSAGE routine in IDL.  If no errors are</span>
<span class="comments">;                 encountered, then a null string is returned.  </span>
<span class="comments">;</span>
<span class="comments">; PROCEDURE:</span>
<span class="comments">;       Program checks the NAXIS and NAXISi keywords in the header to</span>
<span class="comments">;       see if they match the image array dimensions, and checks whether</span>
<span class="comments">;       the BITPIX keyword agrees with the array type.</span>
<span class="comments">;</span>
<span class="comments">; PROCEDURE CALLS:</span>
<span class="comments">;       FXADDPAR, FXPAR(), SXDELPAR</span>
<span class="comments">; MODIFICATION HISTORY:</span>
<span class="comments">;       Written, December 1991  W. Landsman Hughes/STX to replace CHKIMHD</span>
<span class="comments">;       No error returned if NAXIS=0 and IM is a scalar   W. Landsman  Feb 93</span>
<span class="comments">;       Fixed bug for REAL*8 STSDAS data W. Landsman July 93</span>
<span class="comments">;       Make sure NAXIS agrees with NAXISi  W. Landsman  October 93</span>
<span class="comments">;        Converted to IDL V5.0   W. Landsman   September 1997</span>
<span class="comments">;       Allow unsigned data types   W. Landsman December 1999</span>
<span class="comments">;       Allow BZERO = 0 for unsigned data types   W. Landsman January 2000</span>
<span class="comments">;       Added ERRMSG keyword, W. Landsman February 2000</span>
<span class="comments">;       Use FXADDPAR to put NAXISi in proper order   W. Landsman August 2000</span>
<span class="comments">;       Improper FXADDPAR call for DATATYPE keyword  W. Landsman December 2000</span>
<span class="comments">;       Remove explicit setting of obsolete !err W. Landsman February 2004</span>
<span class="comments">;       Remove SDAS support   W. Landsman       November 2006</span>
<span class="comments">;       Fix dimension errors introduced Nov 2006</span>
<span class="comments">;       Work again for null arrays W. Landsman/E. Hivon May 2007</span>
<span class="comments">;       Use V6.0 notation  W.L.  Feb. 2011 </span>
<span class="comments">;- </span>
 compile_opt idl2
 On_error,2

 if N_params() LT 2 then begin
    print,'Syntax - CHECK_FITS, im, hdr, dimen, idltype, '
    print,'            [ /UPDATE, /NOTYPE, ERRMSG=, /SILENT ]'
    return
 endif

 if arg_present(errmsg) then errmsg = ''       

 if size(hdr,/TNAME) NE 'STRING' then begin        <span class="comments">;Is hdr of string type?</span>
        message= 'FITS header is not a string array'
        if  N_elements(ERRMSG) GT 0 then errmsg = message else $
             message, 'ERROR - ' + message, /CON
             return 
 endif

 im_info = size(im,/struc)
 ndimen = im_info.n_dimensions
 if ndimen GT 0 then dimen = im_info.dimensions[0:ndimen-1]
 idltype = im_info.type

 
 nax = fxpar( hdr, 'NAXIS', Count = N_naxis ) 
 if N_naxis EQ 0 then begin
        message = 'FITS header missing NAXIS keyword'
        if  N_elements(errmsg) GT 0 then errmsg = message else $
             message,'ERROR - ' + message,/CON 
             return 
 endif
        
 if ndimen EQ 0  then $             <span class="comments">;Null primary array</span>
     if nax EQ 0 then return else begin
         message = 'FITS array is not defined'
         if  N_elements(errmsg) GT 0 then errmsg = message else $
             message,'ERROR - ' +message,/con 
             return 
     endelse

 
 naxis = fxpar( hdr, 'NAXIS*')
 naxi = N_elements( naxis )
 if nax GT naxi then begin                 <span class="comments">;Does NAXIS agree with # of NAXISi?</span>
        if keyword_set( UPDATE) then begin
                fxaddpar, hdr, 'NAXIS', naxi
                if ~keyword_set(SILENT) then message, /INF, $
        'NAXIS changed from ' + strtrim(nax,2) + ' to ' + strtrim(naxi,2)
        endif else begin 
                message =  'FITS header has NAXIS = ' + strtrim(nax,2) + $
                ', but only ' + strtrim(naxi, 2) + ' axes defined'
                if  N_elements(ERRMSG) GT 0 then errmsg = message else $
                    message, 'ERROR - ' + message
                return
        endelse
 endif

 last = naxi-1                        <span class="comments">;Remove degenerate dimensions</span>
 while ( (naxis[last] EQ 1) && (last GE 1) ) do last--
 if last NE nax-1 then begin
     naxis = naxis[ 0:last]
 endif 

 if ( ndimen NE last + 1 ) then begin
    if ~keyword_set( UPDATE) THEN begin
        message = $
        '# of NAXISi keywords does not match # of array dimensions'
        if  N_elements(ERRMSG) GT 0 then errmsg = message else $
                                     message,'ERROR - ' + message,/CON 
        return 
 
     endif else goto, DIMEN_ERROR
 endif

 for i = 0,last do begin
      if naxis[i] NE dimen[i] then begin
      if ~keyword_set( UPDATE ) then begin
          message =  'Invalid NAXIS' + strtrim( i+1,2 ) + $
	             ' keyword value in header'
          if  N_elements(ERRMSG) GT 0 then errmsg = message else $ 
                                       message,'ERROR - ' + message,/CON
          return 
      endif else goto, DIMEN_ERROR
    endif
 endfor

BITPIX:     

 if ~keyword_set( NOTYPE ) then begin

 
  bitpix = fxpar( hdr, 'BITPIX')
  
    case idltype of

     1: if bitpix NE 8 then goto, BITPIX_ERROR
     2: if bitpix NE 16 then goto, BITPIX_ERROR  
     4: if bitpix NE -32 then goto, BITPIX_ERROR       
     3: if bitpix NE 32 then goto, BITPIX_ERROR 
     5: if bitpix NE -64 then goto, BITPIX_ERROR 
     12:if bitpix NE 16 then goto, BITPIX_ERROR
     13: if bitpix NE 32 then goto, BITPIX_ERROR
     
     else: begin
              message = 'Data array is not a valid FITS datatype'
             if  N_elements(ERRMSG) GT 0 then errmsg = message else $
                                          message,'ERROR - ' + message,/CON
             return 
      end

   endcase

 endif

 return

BITPIX_ERROR:
    if keyword_set( UPDATE ) then begin
    bpix = [0, 8, 16, 32, -32, -64, 32, 0, 0, 0, 0, 0, 16,32 ]
    comm = ['',' Character or unsigned binary integer', $
               ' 16-bit twos complement binary integer', $
               ' 32-bit twos complement binary integer', $
               ' IEEE single precision floating point', $
               ' IEEE double precision floating point', $
               ' 32-bit twos complement binary integer','','','','','', $
               ' 16-bit unsigned binary integer', $
               ' 32-bit unsigned binary integer' ]
    bitpix = bpix[idltype]
    comment = comm[idltype]
    if ~keyword_set(SILENT) then message, /INF, $
        'BITPIX value of ' + strtrim(bitpix,2) +  ' added to FITS header'
    fxaddpar, hdr, 'BITPIX', bitpix, comment
    return

  endif else begin 
       message = 'BITPIX value of ' + strtrim(bitpix,2) + $
                 ' in FITS header does not match array'
      if  N_elements(ERRMSG) GT 0  then errmsg = message else  $
          message,'ERROR - ' + message,/CON
      return
 endelse

DIMEN_ERROR:
   if keyword_set( UPDATE ) then begin
        fxaddpar, hdr, 'NAXIS', ndimen, before = 'NAXIS1'
	naxis = 'NAXIS' + strtrim(indgen(ndimen)+1,2)
        for i = 1, ndimen do fxaddpar, hdr, naxis[i-1], dimen[i-1], $
                'Number of positions along axis ' + strtrim(i,2), $
                after = 'NAXIS' + strtrim(i-1,2)          
        if naxi GT ndimen then begin
                for i = ndimen+1, naxi do sxdelpar, hdr, 'NAXIS'+strtrim(i,2)
        endif
        if ~keyword_set(SILENT) then message, /INF, $
                'NAXIS keywords in FITS header have been updated'
        goto, BITPIX
   endif

 end
</code>
    </div>
  </body>
</html>