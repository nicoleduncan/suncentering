<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN"
 "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">

<!-- Generated by IDLdoc 3.5.1 on Mon Sep 30 16:58:23 2013 -->

<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en">
  <head>
    <meta http-equiv="Content-Type" content="text/html;charset=utf-8"/>
    <title>fsc_field.pro (Documentation for ./)</title>

    
    <link rel="stylesheet" type="text/css" media="all"
          href="../../idldoc-resources/main.css" />
    <link rel="stylesheet" type="text/css" media="print"
          href="../../idldoc-resources/main-print.css" />
    

    <script type="text/javascript">
      function setTitle() {
        parent.document.title="fsc_field.pro (Documentation for ./)";
      }
    </script>
  </head>

  <body onload="setTitle();" id="root">
    <div class="content">
      <code class="source"><span class="comments">;+</span>
<span class="comments">; NAME:</span>
<span class="comments">;   FSC_FIELD</span>
<span class="comments">;</span>
<span class="comments">; PURPOSE:</span>
<span class="comments">;</span>
<span class="comments">;   The purpose of this compound widget is to provide an alternative</span>
<span class="comments">;   to the CW_FIELD widget offered in the IDL distribution. One weakness</span>
<span class="comments">;   of the CW_FIELD compound widget is that the text widgets do not</span>
<span class="comments">;   look editable to the users on Windows platforms. This program</span>
<span class="comments">;   corrects that deficiency and adds some features that I think</span>
<span class="comments">;   will be helpful. For example, you can now assign an event handler</span>
<span class="comments">;   to the compound widget, ask for positive numbers only, and limit</span>
<span class="comments">;   the number of digits in a number, or the number of digits to the</span>
<span class="comments">;   right of a decimal point. The program is written as a widget object,</span>
<span class="comments">;   which allows the user to call object methods directly, affording</span>
<span class="comments">;   even more flexibility in use. This program replaces the earlier</span>
<span class="comments">;   programs FSC_INPUTFIELD and COYOTE_FIELD.</span>
<span class="comments">;</span>
<span class="comments">;   The program consists of a label widget next to a one-line text widget.</span>
<span class="comments">;   The "value" of the compound widget is shown in the text widget. If the</span>
<span class="comments">;   value is a number, it will not be possible (generally) to type</span>
<span class="comments">;   alphanumeric values in the text widget. String values behave like</span>
<span class="comments">;   strings in any one-line text widget.</span>
<span class="comments">;</span>
<span class="comments">; AUTHOR:</span>
<span class="comments">;</span>
<span class="comments">;   FANNING SOFTWARE CONSULTING</span>
<span class="comments">;   David Fanning, Ph.D.</span>
<span class="comments">;   1645 Sheely Drive</span>
<span class="comments">;   Fort Collins, CO 80526 USA</span>
<span class="comments">;   Phone: 970-221-0438</span>
<span class="comments">;   E-mail: david@idlcoyote.com</span>
<span class="comments">;   Coyote's Guide to IDL Programming: http://www.idlcoyote.com/</span>
<span class="comments">;</span>
<span class="comments">; CATEGORY:</span>
<span class="comments">;</span>
<span class="comments">;   General programming.</span>
<span class="comments">;</span>
<span class="comments">; TYPICAL CALLING SEQUENCE:</span>
<span class="comments">;</span>
<span class="comments">;   fieldID = FSC_FIELD(parent, Title="X Size:", Value=256, Object=fieldObject, Digits=3)</span>
<span class="comments">;</span>
<span class="comments">; INPUT PARAMETERS:</span>
<span class="comments">;</span>
<span class="comments">;   parent -- The parent widget ID of the compound widget. Required.</span>
<span class="comments">;</span>
<span class="comments">; INPUT KEYWORDS:</span>
<span class="comments">;</span>
<span class="comments">;   COLUMN        Set this keyword to have the Label widget above the Text widget.</span>
<span class="comments">;                 The default is to have the Label widget in a row with the Text widget.</span>
<span class="comments">;</span>
<span class="comments">;   CR_ONLY       Set this keyword if you only want Carriage Return events returned to</span>
<span class="comments">;                 your event handler. If this keyword is not set, all events are returned.</span>
<span class="comments">;                 Setting this keyword has no effect unless either the EVENT_PRO or</span>
<span class="comments">;                 EVENT_FUNC keyword is used.</span>
<span class="comments">;</span>
<span class="comments">;   DECIMAL       Set this keyword to the number of digits to the right of the decimal</span>
<span class="comments">;                 point in floating point or double precision numbers. Ignored for STRING values.</span>
<span class="comments">;</span>
<span class="comments">;   DIGITS        Set this keyword to the number of digits permitted in integer numbers.</span>
<span class="comments">;</span>
<span class="comments">;   EVENT_FUNC    Set this keyword to the name of an event handler function. If this</span>
<span class="comments">;                 keyword is undefined and the Event_Pro keyword is undefined,</span>
<span class="comments">;                 all compound widget events are handled internally and not</span>
<span class="comments">;                 passed on to the parent widget.</span>
<span class="comments">;</span>
<span class="comments">;   EVENT_PRO     Set this keyword to the name of an event handler procedure. If this</span>
<span class="comments">;                 keyword is undefined and the Event_Func keyword is undefined,</span>
<span class="comments">;                 all compound widget events are handled internally and not</span>
<span class="comments">;                 passed on to the parent widget.</span>
<span class="comments">;</span>
<span class="comments">;   FIELDFONT     The font name for the text in the text widget.</span>
<span class="comments">;</span>
<span class="comments">;   FRAME         Set this keyword to put a frame around the compound widget.</span>
<span class="comments">;</span>
<span class="comments">;   FOCUS_EVENTS  Set this keyword to enable event generation for keyboard focus</span>
<span class="comments">;                 events. Ignored unless EVENT_FUNC or EVENT_PRO keywords are specified.</span>
<span class="comments">;</span>
<span class="comments">;   HIGHLIGHT     Set this keyword to highlight the existing text if the widget gain</span>
<span class="comments">;                 the keyboard focus. This keyword MUST be set for tabbing to work naturally</span>
<span class="comments">;                 in IDL 6.2 and higher.</span>
<span class="comments">;</span>
<span class="comments">;   LABEL_LEFT    Set this keyword to align the text on the label to the left.</span>
<span class="comments">;</span>
<span class="comments">;   LABEL_RIGHT   Set this keyword to align the text on the label to the right.</span>
<span class="comments">;</span>
<span class="comments">;   LABELFONT     The font name for the text in the label widget.</span>
<span class="comments">;</span>
<span class="comments">;   LABELSIZE     The X screen size of the label widget.</span>
<span class="comments">;</span>
<span class="comments">;   NAME          A string containing the name of the object. The default is ''.</span>
<span class="comments">;</span>
<span class="comments">;   NOEDIT        Set this keyword to allow no user editing of the input text widget.</span>
<span class="comments">;</span>
<span class="comments">;   NONSENSITIVE  Set this keyword to make the input text widget non-sensitive.</span>
<span class="comments">;</span>
<span class="comments">;   POSITIVE      Set this keyword if you want only positive numbers allowed.</span>
<span class="comments">;</span>
<span class="comments">;   SCR_XSIZE     The X screen size of the compound widget.</span>
<span class="comments">;</span>
<span class="comments">;   SCR_YSIZE     The Y screen size of the compound widget.</span>
<span class="comments">;</span>
<span class="comments">;   TITLE         The string text placed on the label widget.</span>
<span class="comments">;</span>
<span class="comments">;   UNDEFINED     Set this keyword to the value to use for "undefined" values. If</span>
<span class="comments">;                 not set, then !Value.F_NAN is used for numerical fields and a</span>
<span class="comments">;                 NULL string is used for string fields. This applies to values</span>
<span class="comments">;                 obtained with the GET_VALUE method or the GET_VALUE function.</span>
<span class="comments">;</span>
<span class="comments">;   UVALUE        A user value for any purpose.</span>
<span class="comments">;</span>
<span class="comments">;   VALUE         The "value" of the compound widget. Any type of integer, floating, or string</span>
<span class="comments">;                 variable is allowed. The data "type" is determined automatically from the</span>
<span class="comments">;                 value supplied with this keyword. Be sure you set the type appropriately for</span>
<span class="comments">;                 your intended use of the value.</span>
<span class="comments">;</span>
<span class="comments">;   XSIZE         The X size of the text widget in the usual character units.</span>
<span class="comments">;</span>
<span class="comments">; OUTPUT KEYWORDS:</span>
<span class="comments">;</span>
<span class="comments">;   OBJECT        Set this keyword to a named variable to receive the compound widget's</span>
<span class="comments">;                 object reference. This is required if you wish to call methods on the object.</span>
<span class="comments">;                 Note that the object reference is also available in the event structure</span>
<span class="comments">;                 generated by the widget object. Note that the object reference will be</span>
<span class="comments">;                 necessary if you want to get or set values in the compound widget.</span>
<span class="comments">;</span>
<span class="comments">; COMMON BLOCKS:</span>
<span class="comments">;</span>
<span class="comments">;   None.</span>
<span class="comments">;</span>
<span class="comments">; RESTRICTIONS:</span>
<span class="comments">;</span>
<span class="comments">;   Requires DBLTOSTR from the Coyote Library:</span>
<span class="comments">;      http://www.idlcoyote.com/programs/dbltostr.pro</span>
<span class="comments">;</span>
<span class="comments">; EVENT STRUCTURE:</span>
<span class="comments">;</span>
<span class="comments">;   All events are handled internally unless either the Event_Pro or Event_Func</span>
<span class="comments">;   keywords are used to assign an event handler to the compound widget. By</span>
<span class="comments">;   default all events generated by the text widget are passed to the assigned</span>
<span class="comments">;   event handler. If you wish to receive only Carriage Return events, set the</span>
<span class="comments">;   CR_Only keyword.</span>
<span class="comments">;</span>
<span class="comments">;   event = { FSC_FIELD_EVENT, $   ; The name of the event structure.</span>
<span class="comments">;             ID: 0L, $            ; The ID of the compound widget's top-level base.</span>
<span class="comments">;             TOP: 0L, $           ; The widget ID of the top-level base of the hierarchy.</span>
<span class="comments">;             HANDLER: 0L, $       ; The event handler ID. Filled out by IDL.</span>
<span class="comments">;             OBJECT: Obj_New(), $ ; The "self" object reference. Provided so you can call methods.</span>
<span class="comments">;             VALUE: Ptr_New(), $  ; A pointer to the widget value.</span>
<span class="comments">;             TYPE:""              ; A string indicating the type of data in the VALUE field.</span>
<span class="comments">;           }</span>
<span class="comments">;</span>
<span class="comments">;   Note that if the field is "empty", the VALUE will be a pointer</span>
<span class="comments">;   to an undefined variable. You should check this value before you</span>
<span class="comments">;   use it. You code will look something like this:</span>
<span class="comments">;</span>
<span class="comments">;     IF N_Elements(*event.value) EQ 0 THEN $</span>
<span class="comments">;         Print, 'Current Value UNDEFINED.' ELSE $</span>
<span class="comments">;         Print, 'Current Value: ', *event.value</span>
<span class="comments">;</span>
<span class="comments">; GETTING and SETTING VALUES:</span>
<span class="comments">;</span>
<span class="comments">;   Almost all the properties of the widget can be obtained or set via</span>
<span class="comments">;   the object's GetProperty and SetProperty methods (described below).</span>
<span class="comments">;   Traditional compound widgets have the ability to get and set the "value"</span>
<span class="comments">;   of the compound widget identifier (e.g., fieldID in the calling</span>
<span class="comments">;   sequence above). Unfortunately, it is impossible to retreive a variable</span>
<span class="comments">;   in this way when the variable is undefined. In practical terms, this</span>
<span class="comments">;   means that the undefined variable must be set to *something*. You can</span>
<span class="comments">;   determine what that something is with the UNDEFINED keyword, or I will set</span>
<span class="comments">;   it to !VALUES.F_NAN for numerical fields and to the null string for string</span>
<span class="comments">;   fields. In any case, you will have to check for undefined variables before</span>
<span class="comments">;   you try to do something with the value. For a numerical field, the code</span>
<span class="comments">;   might look something like this:</span>
<span class="comments">;</span>
<span class="comments">;      fieldID = FSC_FIELD(parent, Title="X Size:", Value=256, Object=fieldObject, Digits=3)</span>
<span class="comments">;      currentValue = fieldObject->Get_Value()</span>
<span class="comments">;      IF Finite(currentValue) EQ 0 THEN Print, 'Value is Undefined' ELSE Print, currentValue</span>
<span class="comments">;</span>
<span class="comments">;   Additional examples are provided in the numerical example fields in Example Program below.</span>
<span class="comments">;</span>
<span class="comments">;   Setting the value of the compound widget is the same as calling the Set_Value</span>
<span class="comments">;   method on the object reference. In other words, these two statements are equivalent.</span>
<span class="comments">;</span>
<span class="comments">;        fieldObject->Set_Value, 45.4</span>
<span class="comments">;        Widget_Control, fieldID, Set_Value=45.4</span>
<span class="comments">;</span>
<span class="comments">;   The data type of the value is determined from the value itself. Be sure you set it appropriately.</span>
<span class="comments">;</span>
<span class="comments">; OBJECT PROCEDURE METHODS:</span>
<span class="comments">;</span>
<span class="comments">;   GetProperty -- This method allows various properties of the widget to be</span>
<span class="comments">;       returned via output keywords. The keywords that are available are:</span>
<span class="comments">;</span>
<span class="comments">;       CR_Only -- A flag, if set, means only report carriage return events.</span>
<span class="comments">;       DataType -- The data type of the field variable.</span>
<span class="comments">;       Decimal -- Set this keyword to the number of digits to the right of the decimal</span>
<span class="comments">;              point in FLOATVALUE and DOUBLEVALUE numbers.</span>
<span class="comments">;       Digits -- Set this keyword to the number of digits permitted in INTERGERVALUE and LONGVALUE numbers.</span>
<span class="comments">;       Event_Func -- The name of the event handler function.</span>
<span class="comments">;       Event_Pro -- The name of the event handler function.</span>
<span class="comments">;       Has_Focus -- Set to 1 if the text widget currently has the keyboard focus.</span>
<span class="comments">;       Highlight -- The highlight flag.</span>
<span class="comments">;       NoEdit -- The NoEdit flag.</span>
<span class="comments">;       NonSensitive -- The NonSensitive flag.</span>
<span class="comments">;       Undefined -- The "value" of any undefined value.</span>
<span class="comments">;       UValue -- The user value assigned to the compound widget.</span>
<span class="comments">;       Value -- The "value" of the compound widget.</span>
<span class="comments">;     Name -- A scalar string name of the object.</span>
<span class="comments">;</span>
<span class="comments">;   Resize -- This method allows you to resize the compound widget's text field.</span>
<span class="comments">;        The value parameter is an X screen size for the entire widget. The text</span>
<span class="comments">;        widget is sized by using the value obtained from this value minus the</span>
<span class="comments">;        X screen size of the label widget.</span>
<span class="comments">;</span>
<span class="comments">;          objectRef->Resize, screen_xsize_value</span>
<span class="comments">;</span>
<span class="comments">;   Set_Value -- This method allows you to set the "value" of the field. It takes</span>
<span class="comments">;       one positional parameter, which is the value.</span>
<span class="comments">;</span>
<span class="comments">;          objectRef->Set_Value, 5</span>
<span class="comments">;</span>
<span class="comments">;   SetProperty -- This method allows various properties of the widget to be</span>
<span class="comments">;       set via input keywords. The keywords that are available are:</span>
<span class="comments">;</span>
<span class="comments">;       CR_Only -- Set this keyword if you only want Carriage Return events.</span>
<span class="comments">;       Decimal -- Set this keyword to the number of digits to the right of the decimal</span>
<span class="comments">;              point in FLOAT and DOUBLE numbers.</span>
<span class="comments">;       Digits -- Set this keyword to the number of digits permitted in INTERGER and LONG numbers.</span>
<span class="comments">;       Event_Func -- Set this keyword to the name of an Event Function.</span>
<span class="comments">;       Event_Pro -- Set this keyword to the name of an Event Procedure.</span>
<span class="comments">;       Highlight -- Set this keyword to highlight the existing text</span>
<span class="comments">;                    when the widget gets the keyboard focus</span>
<span class="comments">;       LabelSize --  The X screen size of the Label Widget.</span>
<span class="comments">;       Name -- A scalar string name of the object. (default = '')</span>
<span class="comments">;       NoEdit -- Set this keyword to make the text widget uneditable</span>
<span class="comments">;       NonSensitive -- Set this keyword to make the widget nonsensitive</span>
<span class="comments">;       Scr_XSize -- The X screen size of the text widget.</span>
<span class="comments">;       Scr_YSize -- The Y screen size of the text widget.</span>
<span class="comments">;       Title -- The text to go on the Label Widget.</span>
<span class="comments">;       UValue -- A user value for any purpose.</span>
<span class="comments">;       Value -- The "value" of the compound widget.</span>
<span class="comments">;       XSize -- The X size of the Text Widget.</span>
<span class="comments">;</span>
<span class="comments">;   SetTabNext -- This method allows you to specify which field to go to when a TAB character</span>
<span class="comments">;      is typed in the text widget. See the Example program below for an example of how to</span>
<span class="comments">;      use this method.</span>
<span class="comments">;</span>
<span class="comments">; OBJECT FUNCTIONS METHODS:</span>
<span class="comments">;</span>
<span class="comments">;      Get_Value -- Returns the "value" of the field. No parameters. Will be undefined</span>
<span class="comments">;          if a "number" field is blank. Should be checked before using:</span>
<span class="comments">;</span>
<span class="comments">;          IF N_Elements(objectRef->Get_Value()) NE 0 THEN Print, Value is: ', objectRef->Get_Value()</span>
<span class="comments">;</span>
<span class="comments">;      GetID -- Returns the widget identifier of the compound widget's top-level base.</span>
<span class="comments">;         (The first child of the parent widget.) No parameters.</span>
<span class="comments">;</span>
<span class="comments">;      GetLabelSize -- Returns the X screen size of the label widget. No parameters.</span>
<span class="comments">;</span>
<span class="comments">;      GetTextID -- Returns the widget identifier of the compound widget's text widget.</span>
<span class="comments">;         No parameters.</span>
<span class="comments">;</span>
<span class="comments">;      GetTextSize -- Returns the X screen size of the text widget. No parameters.</span>
<span class="comments">;</span>
<span class="comments">; PRIVATE OBJECT METHODS:</span>
<span class="comments">;</span>
<span class="comments">;   Although there is really no such thing as a "private" method in IDL's</span>
<span class="comments">;   object implementation, some methods are used internally and not meant to</span>
<span class="comments">;   be acessed publicly. Here are a few of those methods. I list them because</span>
<span class="comments">;   it may be these private methods are ones you wish to override in subclassed</span>
<span class="comments">;   objects.</span>
<span class="comments">;</span>
<span class="comments">;      MoveTab -- This method moves the focus to the widget identified in the "next" field,</span>
<span class="comments">;        which must be set with the SetTabNext method. No parameters. Called automatically</span>
<span class="comments">;        when a TAB character is typed in the text widget.</span>
<span class="comments">;</span>
<span class="comments">;      Text_Events -- The main event handler method for the compound widget. All</span>
<span class="comments">;        text widget events are processed here.</span>
<span class="comments">;</span>
<span class="comments">;      ReturnValue -- This function method accepts a string input value and converts</span>
<span class="comments">;        it to the type of data requested by the user.</span>
<span class="comments">;</span>
<span class="comments">;      Validate -- This function method examines all text input and removes unwanted</span>
<span class="comments">;        characters, depending upon the requested data type for the field. It makes it</span>
<span class="comments">;        impossible, for example, to type alphanumeric characters in an INTEGER field.</span>
<span class="comments">;</span>
<span class="comments">; EXAMPLE:</span>
<span class="comments">;</span>
<span class="comments">;   An example program is provided at the end of the FSC_FIELD code. To run it,</span>
<span class="comments">;   type these commands:</span>
<span class="comments">;</span>
<span class="comments">;      IDL> .Compile FSC_Field</span>
<span class="comments">;      IDL> Example</span>
<span class="comments">;</span>
<span class="comments">; MODIFICATION HISTORY:</span>
<span class="comments">;</span>
<span class="comments">;   Written by: David W. Fanning, 18 October 2000. Based heavily on an earlier</span>
<span class="comments">;      FSC_INPUTFIELD program and new ideas about the best way to write</span>
<span class="comments">;      widget objects.</span>
<span class="comments">;   Added LABEL_LEFT, LABEL_RIGHT, and UNDEFINED keywords. 29 Dec 2000. DWF.</span>
<span class="comments">;   Modified the way the value is returned in the GET_VALUE method and the</span>
<span class="comments">;      GET_VALUE function. Modified Example program to demonstrate. 30 Dec 2000. DWF.</span>
<span class="comments">;   Added NOEDIT and NONSENSITIVE keywords, with corresponding SETEDIT and SETSENNSITIVE</span>
<span class="comments">;      methods. 19 Jan 2001. DWF.</span>
<span class="comments">;   Actually followed through with the changes I _said_" I made 29 Dec 2000. (Don't ask....) 13 June 2001. DWF.</span>
<span class="comments">;   Added GetTextSize and GetLabelSize methods for obtaining the X screen</span>
<span class="comments">;      size of the text and label widgets, respectively. 21 July 2001. DWF.</span>
<span class="comments">;   Fixed a problem in SetProperty method where I was setting self.xsize, which doesn't exist. 24 April 2002. DWF.</span>
<span class="comments">;   Small modification to the SetEdit method. 6 August 2003. DWF.</span>
<span class="comments">;   Added Highlight keyword. Ported Focus_Events keyword from</span>
<span class="comments">;      fsc_inputfield.pro. Updated documentation. 17 November</span>
<span class="comments">;      2004. DWF and Benjamin Hornberger</span>
<span class="comments">;   Added Has_Focus keyword to the GetProperty method. 18 November</span>
<span class="comments">;      2004. Benjamin Hornberger</span>
<span class="comments">;   Fixed bug in GetProperty method (set value to *self.undefined if</span>
<span class="comments">;      *self.value is undefined. 24 Feb 2004. Benjamin Hornberger</span>
<span class="comments">;   Modified FOCUS_EVENTS keyword handling so that *all* focus events are now</span>
<span class="comments">;      passed to specified event handlers. Check event.select to see if the</span>
<span class="comments">;      widget is gaining or losing focus. 10 August 2005. DWF.</span>
<span class="comments">;   Added new tabbing functionality, introduced in IDL 6.2. To use tabbing</span>
<span class="comments">;      functionality natually, the HIGHTLIGHT keywords must be set.</span>
<span class="comments">;      See included EXAMPLE program for details. 10 August 2005. DWF.</span>
<span class="comments">;   Added functionality to covert double precision values to strings properly. 30 Nov 2005. DWF.</span>
<span class="comments">;   Set the default fonts to be the current widget font, rather than the default widget font. 4 Oct 2008. DWF.</span>
<span class="comments">;   Fixed a problem with validating a float or double value when it was written with</span>
<span class="comments">;      exponential notation. 2 April 2010. DWF.</span>
<span class="comments">;-</span>
<span class="comments">;******************************************************************************************;</span>
<span class="comments">;  Copyright (c) 2008, by Fanning Software Consulting, Inc.                                ;</span>
<span class="comments">;  All rights reserved.                                                                    ;</span>
<span class="comments">;                                                                                          ;</span>
<span class="comments">;  Redistribution and use in source and binary forms, with or without                      ;</span>
<span class="comments">;  modification, are permitted provided that the following conditions are met:             ;</span>
<span class="comments">;                                                                                          ;</span>
<span class="comments">;      * Redistributions of source code must retain the above copyright                    ;</span>
<span class="comments">;        notice, this list of conditions and the following disclaimer.                     ;</span>
<span class="comments">;      * Redistributions in binary form must reproduce the above copyright                 ;</span>
<span class="comments">;        notice, this list of conditions and the following disclaimer in the               ;</span>
<span class="comments">;        documentation and/or other materials provided with the distribution.              ;</span>
<span class="comments">;      * Neither the name of Fanning Software Consulting, Inc. nor the names of its        ;</span>
<span class="comments">;        contributors may be used to endorse or promote products derived from this         ;</span>
<span class="comments">;        software without specific prior written permission.                               ;</span>
<span class="comments">;                                                                                          ;</span>
<span class="comments">;  THIS SOFTWARE IS PROVIDED BY FANNING SOFTWARE CONSULTING, INC. ''AS IS'' AND ANY        ;</span>
<span class="comments">;  EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES    ;</span>
<span class="comments">;  OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT     ;</span>
<span class="comments">;  SHALL FANNING SOFTWARE CONSULTING, INC. BE LIABLE FOR ANY DIRECT, INDIRECT,             ;</span>
<span class="comments">;  INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED    ;</span>
<span class="comments">;  TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS;         ;</span>
<span class="comments">;  LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND             ;</span>
<span class="comments">;  ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT              ;</span>
<span class="comments">;  (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS           ;</span>
<span class="comments">;  SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.                            ;</span>
<span class="comments">;******************************************************************************************;</span>

FUNCTION FSC_Field_WidgetFont, DEFAULT=default

   <span class="comments">; Build a small widget to determine the current </span>
   <span class="comments">; and default widget fonts.</span>
   
   base = Widget_Base(MAP=0)
   button = Widget_Button(base, Value='TEST')
   
   <span class="comments">; Checking before realization gives default font.</span>
   defaultFont = Widget_Info(button, /FONTNAME)
   
   <span class="comments">; Checking after realization gives current font.</span>
   Widget_Control, base, /REALIZE
   currentFont = Widget_Info(button, /FONTNAME)
   
   <span class="comments">; Clean up.</span>
   Widget_Control, base, /DESTROY

   IF Keyword_Set(default) THEN $
        RETURN, defaultFont ELSE $
        RETURN, currentFont
    
END <span class="comments">;-----------------------------------------------------------------------------------------------------------------------------</span>



FUNCTION FSC_Field_Error_Message, theMessage, Traceback=traceback, NoName=noName, _Extra=extra

On_Error, 2

   <span class="comments">; Check for presence and type of message.</span>

IF N_Elements(theMessage) EQ 0 THEN theMessage = !Error_State.Msg
s = Size(theMessage)
messageType = s[s[0]+1]
IF messageType NE 7 THEN BEGIN
   Message, "The message parameter must be a string.", _Extra=extra
ENDIF

   <span class="comments">; Get the call stack and the calling routine's name.</span>

Help, Calls=callStack
callingRoutine = (StrSplit(StrCompress(callStack[1])," ", /Extract))[0]

   <span class="comments">; Are widgets supported? Doesn't matter in IDL 5.3 and higher.</span>

widgetsSupported = ((!D.Flags AND 65536L) NE 0) OR Float(!Version.Release) GE 5.3
IF widgetsSupported THEN BEGIN
   IF Keyword_Set(noName) THEN answer = Dialog_Message(theMessage, _Extra=extra) ELSE BEGIN
      IF StrUpCase(callingRoutine) EQ "$MAIN$" THEN answer = Dialog_Message(theMessage, _Extra=extra) ELSE $
         answer = Dialog_Message(StrUpCase(callingRoutine) + ": " + theMessage, _Extra=extra)
   ENDELSE
ENDIF ELSE BEGIN
      Message, theMessage, /Continue, /NoPrint, /NoName, /NoPrefix, _Extra=extra
      Print, '%' + callingRoutine + ': ' + theMessage
      answer = 'OK'
ENDELSE

   <span class="comments">; Provide traceback information if requested.</span>

IF Keyword_Set(traceback) THEN BEGIN
   Help, /Last_Message, Output=traceback
   Print,''
   Print, 'Traceback Report from ' + StrUpCase(callingRoutine) + ':'
   Print, ''
   FOR j=0,N_Elements(traceback)-1 DO Print, "     " + traceback[j]
ENDIF

RETURN, answer
END <span class="comments">;-----------------------------------------------------------------------------------------------------------------------------</span>



FUNCTION FSC_Field::GetLabelSize

<span class="comments">; This method returns the X screen size of the label widget.</span>

geom = Widget_Info(self.labelID, /Geometry)
RETURN, geom.scr_xsize
END <span class="comments">;-----------------------------------------------------------------------------------------------------------------------------</span>



FUNCTION FSC_Field::GetTextSize

<span class="comments">; This method returns the X screen size of the text widget.</span>

geom = Widget_Info(self.textID, /Geometry)
RETURN, geom.scr_xsize
END <span class="comments">;-----------------------------------------------------------------------------------------------------------------------------</span>



PRO FSC_Field::MoveTab

   <span class="comments">; If the TABNEXT field has a valid widget, will set the</span>
   <span class="comments">; focus to this widget when a TAB event occurs in the widget.</span>

IF  NOT Widget_Info(self.tabnext, /Valid_ID) THEN RETURN
Widget_Control, self.tabnext, /Input_Focus
Widget_Control, self.tabnext, Get_Value=theText
theText = theText[0]
Widget_Control, self.tabnext, Set_Text_Select=[0,StrLen(theText)]
END <span class="comments">;-----------------------------------------------------------------------------------------------------------------------------</span>



PRO FSC_Field::SetTabNext, nextID

<span class="comments">; This method assigns a text id the TABNEXT field of</span>
<span class="comments">; the object. This is required for the MOVETAB method.</span>

self.tabnext = nextID
END <span class="comments">;-----------------------------------------------------------------------------------------------------------------------------</span>



FUNCTION FSC_Field::GetTextID

<span class="comments">; This method returns the ID of the text widget of the compound widget.</span>

RETURN, self.textID
END <span class="comments">;-----------------------------------------------------------------------------------------------------------------------------</span>



PRO FSC_Field::Resize, newsize

<span class="comments">; This method resizes the widget by making the text widget fit the new size.</span>

l = Widget_Info(self.labelID, /Geometry)
Widget_Control, self.textID, Scr_XSize = (newsize - l.scr_xsize)

   <span class="comments">; Set the text widget sensitivitiy.</span>


Widget_Control, self.textID, Sensitive=1-Keyword_Set(self.nonsensitive)

END <span class="comments">;-----------------------------------------------------------------------------------------------------------------------------</span>



FUNCTION FSC_Field::GetID

<span class="comments">; This method returns the ID of the top-level base of the compound widget.</span>

RETURN, self.tlb
END <span class="comments">;-----------------------------------------------------------------------------------------------------------------------------</span>



FUNCTION FSC_Field::Geometry

<span class="comments">; This method returns the geometry of the compound widget.</span>

RETURN, Widget_Info(self.tlb,/Geometry)
END <span class="comments">;-----------------------------------------------------------------------------------------------------------------------------</span>



FUNCTION FSC_Field::Get_Value

<span class="comments">; This method returns the actual value of the compound widget.</span>

IF N_Elements(*self.theValue) EQ 0 THEN RETURN, *self.undefined ELSE RETURN, *self.theValue
END <span class="comments">;-----------------------------------------------------------------------------------------------------------------------------</span>



PRO FSC_Field::Set_Value, value

<span class="comments">; This method sets the value of the compound widget.</span>

   <span class="comments">; Error Handling.</span>

Catch, theError
IF theError NE 0 THEN BEGIN
   Catch, /Cancel
   ok = FSC_Field_Error_Message(/Traceback)
   RETURN
ENDIF

dataType = Size(value, /TNAME)
CASE dataType OF
   'BYTE'   : BEGIN
      genType = 'INTEGER'
      dataType = 'INT'
      positive = 1
      value = Fix(value)
      Message, 'BYTE data not supported. Value will be converted to INT.', /Informational
      END
   'INT'    : genType = 'INTEGER'
   'LONG'   : genType = 'INTEGER'
   'LONG64' : genType = 'INTEGER'
   'UINT'   : genType = 'UNSIGNED'
   'ULONG'  : genType = 'UNSIGNED'
   'ULONG64': genType = 'UNSIGNED'
   'FLOAT'  : genType = 'FLOAT'
   'DOUBLE' : genType = 'DOUBLE'
   'STRING' : genType = 'STRING'
   ELSE     : Message, 'Data type ' + dataType + ' is not supported. Returning.'
ENDCASE
self.dataType = dataType
self.gentype = genType

IF self.gentype EQ 'DOUBLE' THEN theText = DblToStr(value) ELSE theText = StrTrim(value,2)
theText = self->Validate(theText)

   <span class="comments">; Load the value in the widget.</span>

Widget_Control, self.textID, Set_Value=theText, Set_Text_Select=[StrLen(theText),0]
self.theText = theText

   <span class="comments">; Set the actual value of the compound widget.</span>

*self.theValue = self->ReturnValue(theText)
END <span class="comments">;-----------------------------------------------------------------------------------------------------------------------------</span>



FUNCTION FSC_Field::Validate, value

<span class="comments">; This function eliminates illegal characters from a string that represents</span>
<span class="comments">; a number. The return value is a properly formatted string that can be turned into</span>
<span class="comments">; an INT, LONG, FLOAT, or DOUBLE value. This is a "private" method.</span>
<span class="comments">;</span>
<span class="comments">; + 43B</span>
<span class="comments">; - 45B</span>
<span class="comments">; . 46B</span>
<span class="comments">; 0 - 9 48B -57B</span>
<span class="comments">; 'eEdD' [101B, 69B, 100B, 68B]</span>

   <span class="comments">; Error Handling.</span>

Catch, theError
IF theError NE 0 THEN BEGIN
   Catch, /Cancel
   ok = FSC_Field_Error_Message(/Traceback)
   testValue = self->ReturnValue(value)
   IF String(testValue) NE 'NULLVALUE' THEN numCheck = Finite(testValue) ELSE numCheck = 1
   IF numCheck THEN BEGIN
      RETURN, retValue
   ENDIF ELSE BEGIN
      ok = Dialog_Message('The requested number is not representable.')
      RETURN, ""
   ENDELSE
ENDIF

   <span class="comments">; A null string should be returned at once.</span>

IF N_Elements(value) EQ 0 THEN value = ""
value = value[0]
IF value EQ "" THEN RETURN, String(value)

   <span class="comments">; No leading or trailing blank characters to evaluate.</span>

value = StrTrim(value, 2)

   <span class="comments">; A string value should be returned at once. Nothing to check.</span>

IF StrUpCase(self.gentype) EQ 'STRING' THEN RETURN, String(value)

   <span class="comments">; Check integers and longs. A "-" or "+" in the first character is allowed. Otherwise,</span>
   <span class="comments">; only number between 0 and 9, or 43B to 57B.</span>

IF self.gentype EQ 'INTEGER' THEN BEGIN

   returnValue = Ptr_New(/Allocate_Heap)
   asBytes = Byte(value)

   IF self.positive THEN BEGIN
      IF (asBytes[0] EQ 43B) OR $
         (asBytes[0] GE 48B AND asBytes[0] LE 57B) THEN *returnValue = [asBytes[0]]
   ENDIF ELSE BEGIN
      IF (asBytes[0] EQ 45B) OR (asBytes[0] EQ 43B) OR $
         (asBytes[0] GE 48B AND asBytes[0] LE 57B) THEN *returnValue = [asBytes[0]]
   ENDELSE
   length = StrLen(asBytes)
   IF length EQ 1 THEN BEGIN
      IF N_Elements(*returnValue) EQ 0 THEN  *returnValue = [32B] ELSE $
            *returnValue = [asBytes[0]]
   ENDIF ELSE BEGIN
      FOR j=1,length-1 DO BEGIN
         IF (asBytes[j] GE 48B AND asBytes[j] LE 57B) THEN BEGIN
            IF N_Elements(*returnValue) EQ 0 THEN  *returnValue = [asBytes[j]] ELSE $
               *returnValue = [*returnValue, asBytes[j]]
         ENDIF
      ENDFOR
  ENDELSE
  IF N_Elements(*returnValue) NE 0 THEN retValue = String(*returnValue) ELSE retValue = ""
  Ptr_Free, returnValue

      <span class="comments">; Check for digit restrictions.</span>

  IF self.digits GT 0 THEN BEGIN
      retValue = StrTrim(retValue, 2)
      IF StrMid(retValue, 0, 1) EQ "-" THEN digits = self.digits + 1 ELSE digits = self.digits
      retValue = StrMid(retValue, 0, digits)
  ENDIF

  RETURN, retValue

ENDIF

   <span class="comments">; Check unsigned data types.</span>

IF self.gentype EQ 'UNSIGNED' THEN BEGIN

   returnValue = Ptr_New(/Allocate_Heap)
   asBytes = Byte(value)

   IF self.positive THEN BEGIN
      IF (asBytes[0] GE 48B AND asBytes[0] LE 57B) THEN *returnValue = [asBytes[0]]
   ENDIF ELSE BEGIN
      IF (asBytes[0] GE 48B AND asBytes[0] LE 57B) THEN *returnValue = [asBytes[0]]
   ENDELSE
   length = StrLen(asBytes)
   IF length EQ 1 THEN BEGIN
      IF N_Elements(*returnValue) EQ 0 THEN  *returnValue = [32B] ELSE $
            *returnValue = [asBytes[0]]
   ENDIF ELSE BEGIN
      FOR j=1,length-1 DO BEGIN
         IF (asBytes[j] GE 48B AND asBytes[j] LE 57B) THEN BEGIN
            IF N_Elements(*returnValue) EQ 0 THEN  *returnValue = [asBytes[j]] ELSE $
               *returnValue = [*returnValue, asBytes[j]]
         ENDIF
      ENDFOR
  ENDELSE
  IF N_Elements(*returnValue) NE 0 THEN retValue = String(*returnValue) ELSE retValue = ""
  Ptr_Free, returnValue

      <span class="comments">; Check for digit restrictions.</span>

  IF self.digits GT 0 THEN BEGIN
      retValue = StrTrim(retValue, 2)
      digits = self.digits
      retValue = StrMid(retValue, 0, digits)
  ENDIF

  RETURN, retValue

ENDIF

   <span class="comments">; Check floating values. (+,-) in first character or after 'eEdD'.</span>
   <span class="comments">; Only numbers, signs, decimal points, and 'eEdD' allowed.</span>

IF (self.gentype EQ 'FLOAT') OR (self.gentype EQ 'DOUBLE') THEN BEGIN
   returnValue = Ptr_New(/Allocate_Heap)
   asBytes = Byte(value)
   IF self.positive THEN BEGIN
      IF (asBytes[0] EQ 43B) OR $
         (asBytes[0] GE 48B AND asBytes[0] LE 57B) OR $
         (asBytes[0] EQ 46B) THEN *returnValue = [asBytes[0]]
      IF (asBytes[0] EQ 46B) THEN haveDecimal = 1 ELSE haveDecimal = 0
   ENDIF ELSE BEGIN
      IF (asBytes[0] EQ 45B) OR (asBytes[0] EQ 43B) OR $
         (asBytes[0] GE 48B AND asBytes[0] LE 57B) OR $
         (asBytes[0] EQ 46B) THEN *returnValue = [asBytes[0]]
      IF (asBytes[0] EQ 46B) THEN haveDecimal = 1 ELSE haveDecimal = 0
   ENDELSE
   haveExponent = 0
   length = StrLen(asBytes)
   prevByte = asBytes[0]
   exponents = Byte('eEdD')
   IF length EQ 1 THEN BEGIN
      IF N_Elements(*returnValue) EQ 0 THEN  *returnValue = [32B] ELSE $
            *returnValue = [asBytes[0]]
   ENDIF ELSE BEGIN
      FOR j=1,length-1 DO BEGIN
         IF (asBytes[j] GE 48B AND asBytes[j] LE 57B) THEN BEGIN
            IF N_Elements(*returnValue) EQ 0 THEN  *returnValue = [asBytes[j]] ELSE $
               *returnValue = [*returnValue, asBytes[j]]
            prevByte = asBytes[j]
         ENDIF ELSE BEGIN

            <span class="comments">; What kind of thing is it?</span>

            IF (asBytes[j] EQ 46B) THEN BEGIN <span class="comments">; A decimal point.</span>
               IF haveDecimal EQ 0 THEN BEGIN
                  *returnValue = [*returnValue, asBytes[j]]
                  haveDecimal = 1
                  prevByte = asBytes[j]
               ENDIF
            ENDIF

            IF (asBytes[j] EQ 45B) OR (asBytes[j] EQ 43B) THEN BEGIN <span class="comments">; A + or - sign.</span>
               index = Where(exponents EQ prevByte, count)
               IF count EQ 1 AND haveExponent THEN BEGIN
                  *returnValue = [*returnValue, asBytes[j]]
                  haveDecimal = 1
                  prevByte = asBytes[j]
               ENDIF
            ENDIF

            index = Where(exponents EQ asBytes[j], count)
            IF count EQ 1 AND haveExponent EQ 0 THEN BEGIN <span class="comments">; An exponent</span>
               *returnValue = [*returnValue, asBytes[j]]
               haveExponent = 1
               prevByte = asBytes[j]
            ENDIF
         ENDELSE
      ENDFOR
   ENDELSE
      IF N_Elements(*returnValue) NE 0 THEN BEGIN

      retValue = String(*returnValue)
      retValue = StrTrim(retValue, 2)

               <span class="comments">; Check for decimal restrictions</span>

      IF self.decimal GE 0 THEN BEGIN
         theDecimalPt = StrPos(retValue, '.')
         IF theDecimalPt NE -1 THEN retValue = StrMid(retValue, 0, theDecimalPt + self.decimal + 1)
      ENDIF

   ENDIF ELSE retValue = ""
   Ptr_Free, returnValue

      <span class="comments">; Is this a representable number?</span>

   testValue = self->ReturnValue(retValue)
   IF String(testValue) NE 'NULLVALUE' THEN numCheck = Finite(testValue) ELSE numCheck = 1
   IF numCheck THEN BEGIN
      RETURN, retValue
   ENDIF ELSE BEGIN
      ok = Dialog_Message('The requested number is not representable.')
      RETURN, ""
   ENDELSE
ENDIF


END <span class="comments">;-----------------------------------------------------------------------------------------------------------------------------</span>



FUNCTION FSC_Field::ReturnValue, inputValue

<span class="comments">; This method takes a string and turns it into a number,</span>
<span class="comments">; depending upon the current data type of the compound widget.</span>
<span class="comments">; This is a "private" method. For numbers, if the input value</span>
<span class="comments">; is a null string, then an undefined variable is returned.</span>

   <span class="comments">; Error Handling.</span>

ON_IOERROR, CatchIt

CASE self.datatype OF
   'BYTE': IF inputValue EQ "" OR inputValue EQ "-" OR inputValue EQ "+" THEN $
      retValue = 'NULLVALUE' ELSE retValue = Fix(inputValue)
   'INT': IF inputValue EQ "" OR inputValue EQ "-" OR inputValue EQ "+" THEN $
      retValue = 'NULLVALUE' ELSE retValue = Fix(inputValue)
   'LONG': IF inputValue EQ "" OR inputValue EQ "-" OR inputValue EQ "+" THEN $
      retValue = 'NULLVALUE' ELSE retValue = Long(inputValue)
   'LONG64': IF inputValue EQ "" OR inputValue EQ "-" OR inputValue EQ "+" THEN $
      retValue = 'NULLVALUE' ELSE retValue = Long64(inputValue)
   'UINT': IF inputValue EQ "" OR inputValue EQ "-" OR inputValue EQ "+" THEN $
      retValue = 'NULLVALUE' ELSE retValue = UInt(inputValue)
   'ULONG': IF inputValue EQ "" OR inputValue EQ "-" OR inputValue EQ "+" THEN $
      retValue = 'NULLVALUE' ELSE retValue = ULong(inputValue)
   'ULONG64': IF inputValue EQ "" OR inputValue EQ "-" OR inputValue EQ "+" THEN $
      retValue = 'NULLVALUE' ELSE retValue = ULong64(inputValue)
   'FLOAT' : IF inputValue EQ "" OR inputValue EQ "-" OR inputValue EQ "+" THEN $
      retValue = 'NULLVALUE' ELSE retValue = Float(inputValue)
   'DOUBLE': IF inputValue EQ "" OR inputValue EQ "-" OR inputValue EQ "+" THEN $
      retValue = 'NULLVALUE' ELSE retValue = Double(inputValue)
   'STRING' : retValue = inputValue
ENDCASE

RETURN, retValue

CatchIt:
   retValue = 'NULLVALUE'
   RETURN, retValue
END <span class="comments">;-----------------------------------------------------------------------------------------------------------------------------</span>



FUNCTION FSC_Field::TextEvents, event

<span class="comments">; The event handler method for the text widget of the compound widget.</span>

   <span class="comments">; Error Handling.</span>

Catch, theError
IF theError NE 0 THEN BEGIN
   Catch, /Cancel
   ok = FSC_Field_Error_Message(/Traceback)
   RETURN, 0
ENDIF

   <span class="comments">; Get the previous text, the current cursor location in the text widget,</span>
   <span class="comments">; and indicate this is not a Carriage Return event.</span>

previousText = self.theText
textLocation = Widget_Info(event.id, /Text_Select)
cr_event = 0

<span class="comments">; Handle keyboard focus events.</span>
IF Tag_Names(event, /structure_name) EQ 'WIDGET_KBRD_FOCUS' THEN BEGIN
    IF event.enter THEN BEGIN
        self.has_focus = 1L
        IF self.highlight THEN BEGIN
            Widget_Control, self.textID, get_value=text
            text = text[0]
            Widget_Control, self.textID, set_text_select=[0, Strlen(text)]
        ENDIF

        IF self.focus THEN BEGIN

              <span class="comments">; Get the current contents of text widget. Validate it.</span>

            Widget_Control, self.textID, Get_Value=newText
            newText = newText[0]
            validText = self->Validate(newText)

               <span class="comments">; Load the valid text.</span>

           self.theText = validText
           testValue  = self->ReturnValue(validText)
           IF String(testValue) EQ "NULLVALUE" THEN BEGIN
               Ptr_Free, self.theValue
               self.theValue = Ptr_New(/Allocate_Heap)
           ENDIF ELSE *self.theValue = testValue

           <span class="comments">; Send the keyboard focus events if there is an event handler to accept them.</span>
           IF self.event_func NE "" THEN BEGIN
              thisEvent = {FSC_Field_Event, self.tlb, event.top, 0L, self.theValue, self.dataType, self}
              RETURN, thisEvent
           ENDIF

           IF self.event_pro NE "" THEN BEGIN
              thisEvent = {FSC_Field_Event, self.tlb, event.top, 0L, self.theValue, self.dataType, self}
              RETURN, thisEvent
           ENDIF

       ENDIF

       RETURN, 0

    ENDIF ELSE BEGIN

        self.has_focus = 0L
        Widget_Control, self.textID, set_text_select=[0]

        IF self.focus THEN BEGIN

              <span class="comments">; Get the current contents of text widget. Validate it.</span>

            Widget_Control, self.textID, Get_Value=newText
            newText = newText[0]
            validText = self->Validate(newText)

               <span class="comments">; Load the valid text.</span>

           self.theText = validText
           testValue  = self->ReturnValue(validText)
           IF String(testValue) EQ "NULLVALUE" THEN BEGIN
               Ptr_Free, self.theValue
               self.theValue = Ptr_New(/Allocate_Heap)
           ENDIF ELSE *self.theValue = testValue

           <span class="comments">; Send the keyboard focus events if there is an event handler to accept them.</span>
           IF self.event_func NE "" THEN BEGIN
              thisEvent = {FSC_Field_Event, self.tlb, event.top, 0L, self.theValue, self.dataType, self}
              RETURN, thisEvent
           ENDIF

           IF self.event_pro NE "" THEN BEGIN
              thisEvent = {FSC_Field_Event, self.tlb, event.top, 0L, self.theValue, self.dataType, self}
              RETURN, thisEvent
           ENDIF

       ENDIF

    ENDELSE

   RETURN, 0

ENDIF

   <span class="comments">; What kind of event is this?</span>

possibleTypes = ['INSERT SINGLE CHARACTER', 'INSERT MULTIPLE CHARACTERS', 'DELETE TEXT', 'SELECT TEXT']
thisType = possibleTypes[event.type]

   <span class="comments">; Branch on event type.</span>

CASE thisType OF

   'INSERT SINGLE CHARACTER': BEGIN

            <span class="comments">; If the character is a TAB see if there is something to do.</span>

         IF event.ch EQ 9B THEN BEGIN
            self->MoveTab
            RETURN, 0
         ENDIF

            <span class="comments">; Get the current contents of text widget. Validate it.</span>

         Widget_Control, self.textID, Get_Value=newText
         newText = newText[0]
         validText = self->Validate(newText)

            <span class="comments">; If it is valid, leave it alone. If not, go back to previous text.</span>

         IF validText NE newText THEN BEGIN

            Widget_Control, self.textID, Set_Value=previousText, Set_Text_Select=[textLocation[0]-1,0]

         ENDIF ELSE BEGIN

            self.theText = validText
            testValue  = self->ReturnValue(validText)
            IF String(testValue) EQ "NULLVALUE" THEN BEGIN
               Ptr_Free, self.theValue
               self.theValue = Ptr_New(/Allocate_Heap)
            ENDIF ELSE *self.theValue = testValue

         ENDELSE

            <span class="comments">; Is this a Carriage Return event?</span>

         IF event.ch EQ 10B then cr_event = 1
      ENDCASE

   'INSERT MULTIPLE CHARACTERS': BEGIN

            <span class="comments">; Same as above, but for all the characters you are inserting.</span>

         Widget_Control, self.textID, Get_Value=newText
         newText = newText[0]
         validText = self->Validate(newText)
         IF validText NE newText THEN BEGIN
            Widget_Control, self.textID, Set_Value=previousText, Set_Text_Select=[textLocation[0]-1,0]
         ENDIF ELSE BEGIN
            self.theText = validText
            testValue  = self->ReturnValue(validText)
            IF String(testValue) EQ "NULLVALUE" THEN BEGIN
               Ptr_Free, self.theValue
               self.theValue = Ptr_New(/Allocate_Heap)
            ENDIF ELSE *self.theValue = testValue
         ENDELSE
      ENDCASE

   'DELETE TEXT': BEGIN

            <span class="comments">; Get the current contents of text widget. Validate it.</span>

         Widget_Control, self.textID, Get_Value=newText
         newText = newText[0]
         validText = self->Validate(newText)

            <span class="comments">; Load the valid text.</span>

        Widget_Control, self.textID, Set_Value=validText, Set_Text_Select=[textLocation[0],0]
        self.theText = validText
        testValue  = self->ReturnValue(validText)
        IF String(testValue) EQ "NULLVALUE" THEN BEGIN
            Ptr_Free, self.theValue
            self.theValue = Ptr_New(/Allocate_Heap)
        ENDIF ELSE *self.theValue = testValue

      ENDCASE

   'SELECT TEXT': <span class="comments">; Nothing to do.</span>

ENDCASE

   <span class="comments">; Do you report all events, or only Carriage Return events?</span>

IF self.cr_only THEN BEGIN

   IF self.event_func NE "" THEN BEGIN
      thisEvent = {FSC_Field_Event, self.tlb, event.top, 0L, self.theValue, self.dataType, self}
      IF cr_event THEN RETURN, thisEvent
   ENDIF

   IF self.event_pro NE "" THEN BEGIN
      thisEvent = {FSC_Field_Event, self.tlb, event.top, 0L, self.theValue, self.dataType, self}
      IF cr_event THEN RETURN, thisEvent
   ENDIF

ENDIF ELSE BEGIN

   IF self.event_func NE "" THEN BEGIN
      thisEvent = {FSC_Field_Event, self.tlb, event.top, 0L, self.theValue, self.dataType, self}
      RETURN, thisEvent
   ENDIF

   IF self.event_pro NE "" THEN BEGIN
      thisEvent = {FSC_Field_Event, self.tlb, event.top, 0L, self.theValue, self.dataType, self}
      RETURN, thisEvent
   ENDIF

ENDELSE

RETURN, 0
END <span class="comments">;-----------------------------------------------------------------------------------------------------------------------------</span>



PRO FSC_Field::GetProperty, $
<span class="comments">;</span>
<span class="comments">; This method allows you to obtain various properties of the compound widget via output keywords.</span>
<span class="comments">;</span>
   CR_Only=cr_only, $               <span class="comments">; Set this keyword if you only want Carriage Return events.</span>
   DataType=datatype, $             <span class="comments">; The datatype of the compound widget.</span>
   Decimal=decimal, $               <span class="comments">; The number of digits to the right of the decimal point in FLOAT numbers.</span>
   Digits=digits, $                 <span class="comments">; The number of digits permitted in INTERGERVALUE and LONGVALUE numbers.</span>
   Event_Func=event_func, $         <span class="comments">; Set this keyword to the name of an Event Function.</span>
   Event_Pro=event_pro, $           <span class="comments">; Set this keyword to the name of an Event Procedure.</span>
   Focus_Events=focus_events, $     <span class="comments">; Set this keyword to inquire whether or not events are reported when the keyboard focus is lost.</span>
   Has_Focus=has_focus, $           <span class="comments">; Set this keyword to inquire whether of not the widget has the keyboard focus currently.</span>
   Highlight=highlight, $           <span class="comments">; The highlight flag.</span>
   Name=Name, $                     <span class="comments">; The name of the object.</span>
   NoEdit=noedit, $                 <span class="comments">; Setting this keywords makes the text widget non-editable.</span>
   NonSensitive=nonsensitive, $     <span class="comments">; Setting this keywords makes the text widget non-sensitive.</span>
   Undefined=undefined, $           <span class="comments">; The "value" of any undefined value.</span>
   UValue=uvalue, $                 <span class="comments">; A user value for any purpose.</span>
   Value=value                      <span class="comments">; The "value" of the compound widget.</span>

   <span class="comments">; Error Handling.</span>

Catch, theError
IF theError NE 0 THEN BEGIN
   Catch, /Cancel
   ok = FSC_Field_Error_Message(/Traceback)
   RETURN
ENDIF

   <span class="comments">; Get the properties.</span>

cr_only = self.cr_only
datatype = self.datatype
decimal = self.decimal
digits = self.digits
event_func = self.event_func
event_pro = self.event_pro
focus_events = self.focus
has_focus = self.has_focus
highlight = self.highlight
name = self.name
noedit = self.noedit
nonsensitive = self.nonsensitive
Widget_Control, self.tlb, Get_UValue=uvalue
IF N_Elements(*self.theValue) EQ 0 THEN value = *self.undefined ELSE value = *self.thevalue
undefined = *self.undefined

END <span class="comments">;--------------------------------------------------------------------------------------------------------------</span>



PRO FSC_Field::SetProperty, $
<span class="comments">;</span>
<span class="comments">; This method allows you to set various properties of the compound widget.</span>
<span class="comments">;</span>
   CR_Only=cr_only, $               <span class="comments">; Set this keyword if you only want Carriage Return events.</span>
   Decimal=decimal, $               <span class="comments">; Set this keyword to the number of digits to the right of the decimal point in FLOAT values..</span>
   Digits=digits, $                 <span class="comments">; Set this keyword to the number of digits permitted in INTEGER values.</span>
   Event_Func=event_func, $         <span class="comments">; Set this keyword to the name of an Event Function.</span>
   Event_Pro=event_pro, $           <span class="comments">; Set this keyword to the name of an Event Procedure.</span>
   Focus_Events=focus_events, $     <span class="comments">; Set this keyword to turn on or off event reporting when the keyboard focus is lost.</span>
   Highlight=highlight, $           <span class="comments">; Set this keyword to highlight the text when it gets keyboard focus.</span>
   LabelSize=labelsize, $           <span class="comments">; The X screen size of the Label Widget.</span>
   Name=name, $                     <span class="comments">; A scalar string name for the object.</span>
   NoEdit=noedit, $                 <span class="comments">; Setting this keywords makes the text widget non-editable.</span>
   NonSensitive=nonsensitive, $     <span class="comments">; Setting this keywords makes the text widget non-sensitive.</span>
   Scr_XSize=scr_xsize, $           <span class="comments">; The X screen size of the text widget.</span>
   Scr_YSize=scr_ysize, $           <span class="comments">; The Y screen size of the text widget.</span>
   Title=title, $                   <span class="comments">; The text to go on the Label Widget.</span>
   Undefined=undefinded, $          <span class="comments">; Set to "value" of undefined value.</span>
   UValue=uvalue, $                 <span class="comments">; A user value for any purpose.</span>
   Value=value, $                   <span class="comments">; The "value" of the compound widget.</span>
   XSize=xsize                      <span class="comments">; The X size of the Text Widget.</span>

   <span class="comments">; Error Handling.</span>

Catch, theError
IF theError NE 0 THEN BEGIN
   Catch, /Cancel
   ok = FSC_Field_Error_Message(/Traceback)
   RETURN
ENDIF

   <span class="comments">; Set the properties, if needed.</span>

IF N_Elements(cr_only) NE 0 THEN self.cr_only = cr_only
IF Keyword_Set(decimal)THEN self.decimal = decimal
IF Keyword_Set(digits)THEN self.digits = digits
IF N_Elements(event_func) NE 0 THEN self.event_func = event_func
IF N_Elements(event_pro) NE 0 THEN self.event_pro = event_pro
IF N_Elements(focus_events) NE 0 THEN self.focus = keyword_set(focus)
IF N_Elements(highlight) NE 0 THEN self.highlight = Keyword_Set(highlight)
IF N_Elements(labelsize) NE 0 THEN BEGIN
   Widget_Control, self.labelID, XSize=labelsize
ENDIF
IF N_Elements(scr_xsize) NE 0 THEN BEGIN
   Widget_Control, self.textID, Scr_XSize=scr_xsize
ENDIF
IF N_Elements(scr_ysize) NE 0 THEN BEGIN
   Widget_Control, self.textID, Scr_YSize=scr_ysize
ENDIF
IF N_Elements(title) NE 0 THEN Widget_Control, self.labelID, Set_Value=title
IF N_Elements(uvalue) NE 0 THEN Widget_Control, self.tlb, Set_UValue=uvalue
If N_Elements(name) NE 0 Then self.name = String(name[0])
IF N_Elements(xsize) NE 0 THEN BEGIN
   Widget_Control, self.textID, XSize=xsize
ENDIF
IF N_Elements(noedit) THEN BEGIN
   self.noedit = Keyword_Set(noedit)
   Widget_Control, self.textID, Editable=1-self.noedit
ENDIF
IF N_Elements(nonsensitive) THEN BEGIN
   self.nonsensitive = Keyword_Set(nonsensitive)
   Widget_Control, self.textID, Sensitive=1-self.nonsensitive
ENDIF

   <span class="comments">; Set up the value.</span>

IF N_Elements(value) NE 0 THEN BEGIN

   <span class="comments">; Set up data type and general type.</span>

   dataType = Size(value, /TNAME)
   CASE dataType OF
      'BYTE'   : BEGIN
         genType = 'INTEGER'
         dataType = 'INT'
         positive = 1
         *self.theValue = Fix(*self.theValue)
         Message, 'BYTE data not supported. Value will be converted to INT.', /Informational
         END
      'INT'    : genType = 'INTEGER'
      'LONG'   : genType = 'INTEGER'
      'LONG64' : genType = 'INTEGER'
      'UINT'   : genType = 'UNSIGNED'
      'ULONG'  : genType = 'UNSIGNED'
      'ULONG64': genType = 'UNSIGNED'
      'FLOAT'  : genType = 'FLOAT'
      'DOUBLE' : genType = 'DOUBLE'
      'STRING' : genType = 'STRING'
      ELSE     : BEGIN
         Ptr_Free, self.theValue
         self.theValue = Ptr_New(/Allocate_Heap)
         Message, 'Data type ' + dataType + ' is not supported. Returning.', /NoName
         END
   ENDCASE
   CASE Size(value, /TNAME) OF
       'FLOAT': self.theText = StrTrim(String(value, FORMAT='(F0)'),2)
       'DOUBLE': self.theText = StrTrim(String(value, FORMAT='(D0)'),2)
       ELSE: self.theText = StrTrim(value, 2)
   ENDCASE
   *self.theValue = self->ReturnValue(self.theText)
   Widget_Control, self.textID, Set_Value=self.theText
   self.dataType = datatype
   self.genType = genType
   IF N_Elements(undefined) EQ 0 THEN BEGIN
   IF genType EQ 'STRING' THEN undefined = "" ELSE undefined = !VALUES.F_NAN
ENDIF

ENDIF

END <span class="comments">;--------------------------------------------------------------------------------------------------------------</span>



PRO FSC_Field::SetEdit, editvalue

   <span class="comments">; Error Handling.</span>

Catch, theError
IF theError NE 0 THEN BEGIN
   Catch, /Cancel
   ok = FSC_Field_Error_Message(/Traceback)
   RETURN
ENDIF

IF N_Elements(editvalue) NE 0 THEN $
   Widget_Control, self.textID, Editable=Keyword_Set(editvalue)
END <span class="comments">;--------------------------------------------------------------------------------------------------------------</span>


PRO FSC_Field::SetSensitive, value

   <span class="comments">; Error Handling.</span>

Catch, theError
IF theError NE 0 THEN BEGIN
   Catch, /Cancel
   ok = FSC_Field_Error_Message(/Traceback)
   RETURN
ENDIF

IF N_Elements(value) EQ 0 THEN value = 1
self.nonsensitive = 1-value
Widget_Control, self.textID, Sensitive=value
END <span class="comments">;--------------------------------------------------------------------------------------------------------------</span>



FUNCTION FSC_Field::INIT, $         <span class="comments">; The compound widget FSC_Field INIT method..</span>
   parent, $                        <span class="comments">; The parent widget. Required for all compound widgets.</span>
   Column=column, $                 <span class="comments">; Set this keyword to have Label above Text Widget.</span>
   CR_Only=cr_only, $               <span class="comments">; Set this keyword if you only want Carriage Return events.</span>
   Decimal=decimal, $               <span class="comments">; Set this keyword to the number of digits to the right of the decimal point in FLOAT.</span>
   Digits=digits, $                 <span class="comments">; Set this keyword to the number of digits permitted in INTEGER values.</span>
   Event_Func=event_func, $         <span class="comments">; Set this keyword to the name of an Event Function.</span>
   Event_Pro=event_pro, $           <span class="comments">; Set this keyword to the name of an Event Procedure.</span>
   _Extra=extra, $                  <span class="comments">; Passes along extra keywords to the text widget.</span>
   FieldFont=fieldfont, $           <span class="comments">; The font name for the text in the Text Widget.</span>
   Focus_Events=focus_events, $     <span class="comments">; Set this keyword to enable event reporting when the keyboard focus is lost or gained.</span>
   Frame=frame, $                   <span class="comments">; Set this keyword to put a frame around the compound widget.</span>
   Highlight=highlight, $           <span class="comments">; If this keyword is set, the text is highlighted.</span>
   Label_Left=label_left, $         <span class="comments">; Set this keyword to align the label to the left of the label.</span>
   Label_Right=label_right, $       <span class="comments">; Set this keyword to align the labe to the right of the label.</span>
   LabelFont=labelfont, $           <span class="comments">; The font name for the text in the Label Widget.</span>
   LabelSize=labelsize, $           <span class="comments">; The X screen size of the Label Widget.</span>
   Name=name, $                     <span class="comments">; A scalar string name for the object.</span>
   NoEdit=noedit, $                 <span class="comments">; Setting this keywords makes the text widget non-editable.</span>
   NonSensitive=nonsensitive, $     <span class="comments">; Setting this keywords makes the text widget non-sensitive.</span>
   Positive=positive, $             <span class="comments">; Set this keyword to indicate only positive numbers allowed in the field.</span>
   Scr_XSize=scr_xsize, $           <span class="comments">; The X screen size of the text widget.</span>
   Scr_YSize=scr_ysize, $           <span class="comments">; The Y screen size of the text widget.</span>
   Title=title, $                   <span class="comments">; The text to go on the Label Widget.</span>
   Undefined=undefined, $           <span class="comments">; Set to the value for "undefined" field values.</span>
   UValue=uvalue, $                 <span class="comments">; A user value for any purpose.</span>
   Value=value, $                   <span class="comments">; The "value" of the compound widget.</span>
   XSize=xsize                      <span class="comments">; The X size of the Text Widget.</span>

   <span class="comments">; Error Handling.</span>

Catch, theError
IF theError NE 0 THEN BEGIN
   Catch, /Cancel
   ok = FSC_Field_Error_Message(/Traceback)
   RETURN, 0
ENDIF

   <span class="comments">; A parent is required.</span>

IF N_Elements(parent) EQ 0 THEN BEGIN
   Message, 'A PARENT argument is required. Returning...', /NoName
ENDIF

   <span class="comments">; Check keyword values.</span>

IF N_Elements(column) EQ 0 THEN column = 0
IF N_Elements(digits) EQ 0 THEN digits = 0 ELSE digits = Fix(digits)
IF N_Elements(decimal) EQ 0 THEN decimal = -1 ELSE decimal = Fix(decimal)
IF N_Elements(event_func) EQ 0 THEN event_func = ""
IF N_Elements(event_pro) EQ 0 THEN event_pro = ""

IF N_Elements(fieldfont) EQ 0 THEN fieldfont = FSC_Field_WidgetFont()
IF N_Elements(frame) EQ 0 THEN frame = 0
IF N_Elements(labelfont) EQ 0 THEN labelfont = FSC_Field_WidgetFont()
IF N_Elements(labelsize) EQ 0 THEN labelsize = 0
IF N_Elements(name) EQ 0 THEN name = ""
noedit = Keyword_Set(noedit)
nonsensitive = Keyword_Set(nonsensitive)
positive = Keyword_Set(positive)
IF N_Elements(scr_xsize) EQ 0 THEN scr_xsize = 0
IF N_Elements(scr_ysize) EQ 0 THEN scr_ysize = 0
IF N_Elements(title) EQ 0 THEN title = "Input Value: "
IF N_Elements(uvalue) EQ 0 THEN uvalue = ""
IF N_Elements(value) EQ 0 THEN value = ""
IF N_Elements(xsize) EQ 0 THEN xsize = 0

   <span class="comments">; What data type are we looking for?</span>

dataType = Size(value, /TNAME)
CASE dataType OF
   'BYTE'   : BEGIN
      genType = 'INTEGER'
      dataType = 'INT'
      positive = 1
      value = Fix(value)
      Message, 'BYTE data not supported. Value will be converted to INT.', /Informational
      END
   'INT'    : genType = 'INTEGER'
   'LONG'   : genType = 'INTEGER'
   'LONG64' : genType = 'INTEGER'
   'UINT'   : genType = 'UNSIGNED'
   'ULONG'  : genType = 'UNSIGNED'
   'ULONG64': genType = 'UNSIGNED'
   'FLOAT'  : genType = 'FLOAT'
   'DOUBLE' : genType = 'DOUBLE'
   'STRING' : genType = 'STRING'
   ELSE     : Message, 'Data type ' + dataType + ' is not supported. Returning.', /NoName
ENDCASE

IF N_Elements(undefined) EQ 0 THEN BEGIN
   IF genType EQ 'STRING' THEN undefined = "" ELSE undefined = !VALUES.F_NAN
ENDIF

   <span class="comments">; Populate the object.</span>

self.cr_only = Keyword_Set(cr_only)
self.datatype = datatype
self.decimal = decimal
self.digits = digits
self.focus = Keyword_Set(focus_events)
self.highlight = Keyword_Set(highlight)
self.gentype = genType
self.parent = parent
self.event_pro = event_pro
self.event_func = event_func
self.positive = positive
self.undefined = Ptr_New(undefined)
self.noedit = noedit
self.nonsensitive = nonsensitive
If N_Elements(name) NE 0 Then self.name = String(name[0])
IF Keyword_Set(column) THEN row = 0 ELSE row = 1

   <span class="comments">; Validate the input value.</span>

IF self.gentype EQ 'DOUBLE' THEN value = DblToStr(value) ELSE value = StrTrim(value,2)
value = self->Validate(value)
self.theText = value

   <span class="comments">; Create the widgets.</span>

self.tlb = Widget_Base( parent, $  <span class="comments">; The top-level base of the compound widget.</span>
   Frame=frame, $
   Row=row, $
   Column=Keyword_Set(column), $
   Base_Align_Center=1, $
   UName=name, $
   UValue=uvalue, $
   Event_Pro=event_pro, $
   Func_Get_Value='FSC_Field_Get_Compound_Widget_Value', $
   Pro_Set_Value='FSC_Field_Set_Compound_Widget_Value', $
   Event_Func=event_func )

<span class="comments">; Update for tabbing in IDL 6.2.</span>
IF Float(!Version.Release) GT 6.1 THEN BEGIN
   Widget_Control, self.tlb, TAB_MODE=1
ENDIF

self.labelID = Widget_Label( self.tlb, Value=title, $ <span class="comments">; The Label Widget.</span>
  Scr_XSize=labelsize, UValue=self, Align_Left=Keyword_Set(label_left), Align_Right=Keyword_Set(label_right))

self.textID = Widget_Text( self.tlb, $  <span class="comments">; The Text Widget.</span>
   Value=value, $
   XSize=xsize, $
   YSize=1, $
   Scr_XSize=scr_xsize, $
   Scr_YSize=scr_ysize, $
   sensitive=1-self.nonsensitive, $
   Font=fieldfont, $
   All_Events=1, $
   _Extra=extra, $
   kbrd_focus_events=1, $
   Event_Func='FSC_Field_Event_Handler', $
   UValue={Method:"TextEvents", Object:self}, $
   Kill_Notify='FSC_Field_Kill_Notify', $
   Editable=1-noedit )

self.theValue = Ptr_New(self->ReturnValue(value))

   <span class="comments">; If CR_ONLY or FOCUS_EVENTS are turned on and EVENT_PRO and EVENT_FUNC keywords are</span>
   <span class="comments">; unspecified, issue a warning message to the command log.</span>

IF self.cr_only AND (self.event_pro EQ "" AND self.event_func EQ "") THEN $
   Message, /Information, 'WARNING: There is no specified event handler for carriage return events for this widget. Events will be swallowed.'
IF self.focus AND (self.event_pro EQ "" AND self.event_func EQ "") THEN $
   Message, /Information, 'WARNING: There is no specified event handler for keyboard focus events for this widget. Events will be swallowed.'

RETURN, 1
END <span class="comments">;--------------------------------------------------------------------------------------------------------------</span>



PRO FSC_Field::CLEANUP

<span class="comments">; This method makes sure there are not pointers left on the heap.</span>

Ptr_Free, self.theValue
Ptr_Free, self.undefined
END <span class="comments">;--------------------------------------------------------------------------------------------------------------</span>



FUNCTION FSC_Field_Event_Handler, event

<span class="comments">; The main event handler for the compound widget. It reacts</span>
<span class="comments">; to "messages" in the UValue of the text widget.</span>
<span class="comments">; The message indicates which object method to call. A message</span>
<span class="comments">; consists of an object method and the self object reference.</span>

Widget_Control, event.ID, Get_UValue=theMessage
event = Call_Method(theMessage.method, theMessage.object, event)

RETURN, event
END <span class="comments">;-----------------------------------------------------------------------------------------------------------------------------</span>



PRO FSC_Field_Event__Define

<span class="comments">; The FSC_Field Event Structure. Sent only if EVENT_PRO or EVENT_FUNC keywords</span>
<span class="comments">; have defined an event handler for the top-level base of the compound widget.</span>

   event = { FSC_Field_Event, $   <span class="comments">; The name of the event structure.</span>
             ID: 0L, $            <span class="comments">; The ID of the compound widget's top-level base.</span>
             TOP: 0L, $           <span class="comments">; The widget ID of the top-level base of the hierarchy.</span>
             HANDLER: 0L, $       <span class="comments">; The event handler ID. Filled out by IDL.</span>
             Value: Ptr_New(), $  <span class="comments">; A pointer to the widget value.</span>
             Type: "", $          <span class="comments">; A string indicating the type of data in the VALUE field.</span>
             Object: Obj_New()}   <span class="comments">; The "self" object.</span>


END <span class="comments">;-----------------------------------------------------------------------------------------------------------------------------</span>



PRO FSC_Field_Set_Compound_Widget_Value, tlb, value

<span class="comments">; This utilty routine is invoked when the user tries to set</span>
<span class="comments">; the value of the compound widget using the base widget</span>
<span class="comments">; identifier of the top-level base of the compound widget.</span>
<span class="comments">; The self object is located, and the Set_Value method is called</span>
<span class="comments">; on the object.</span>

firstChildID = Widget_Info(tlb, /Child)
Widget_Control, firstChildID, Get_UValue=self
self->Set_Value, value
END <span class="comments">;--------------------------------------------------------------------------------------------------------------</span>



FUNCTION FSC_Field_Get_Compound_Widget_Value, tlb

<span class="comments">; This utilty routine is invoked when the user tries to get</span>
<span class="comments">; the value of the compound widget using the base widget</span>
<span class="comments">; identifier of the top-level base of the compound widget.</span>
<span class="comments">; The self object is located, and the Get_Value method is called</span>
<span class="comments">; on the object.</span>

firstChildID = Widget_Info(tlb, /Child)
Widget_Control, firstChildID, Get_UValue=self
IF N_Elements(self->Get_Value()) EQ 0 THEN RETURN, *self.undefined ELSE RETURN, self->Get_Value()
END <span class="comments">;--------------------------------------------------------------------------------------------------------------</span>



PRO FSC_Field_Kill_Notify, textID

<span class="comments">; This widget call-back procedure makes sure the self object is</span>
<span class="comments">; destroyed when the widget is destroyed.</span>

Widget_Control, textID, Get_UValue=message
Obj_Destroy, message.object
END <span class="comments">;--------------------------------------------------------------------------------------------------------------</span>



PRO FSC_Field__Define

   objectClass = { FSC_FIELD, $             <span class="comments">; The object class name.</span>
                   parent: 0L, $            <span class="comments">; The parent widget ID.</span>
                   tlb: 0L, $               <span class="comments">; The top-level base of the compound widget.</span>
                   labelID: 0L, $           <span class="comments">; The label widget ID.</span>
                   textID: 0L, $            <span class="comments">; The text widget ID.</span>
                   theText: "", $           <span class="comments">; The actual text in the text widget.</span>
                   theValue: Ptr_New(), $   <span class="comments">; The actual "value" of the text in the text widget. :-)</span>
                   event_func: "", $        <span class="comments">; The name of the specified event handler function.</span>
                   event_pro: "", $         <span class="comments">; The name of the specified event handler procedrue</span>
                   cr_only: 0L, $           <span class="comments">; A flag meaning send only carriage return events.</span>
                   focus: 0L, $             <span class="comments">; A flag to indicate that events should be reported if the keyboard focus is lost</span>
                   has_focus: 0L, $         <span class="comments">; A flag to indicate that the widget has the keyboard focus currently.</span>
                   highlight: 0L, $         <span class="comments">; A flag for highlighting.</span>
                   tabnext: 0L, $           <span class="comments">; The identifier of a widget to receive the cursor focus if a TAB character is detected.</span>
                   decimal: 0, $            <span class="comments">; The number of decimals points in FLOAT and DOUBLE numbers.</span>
                   digits: 0, $             <span class="comments">; The number of digits in INT and LONG numbers.</span>
                   positive: 0, $           <span class="comments">; A flag meaning only positive numbers allowed.</span>
                   datatype: "",$           <span class="comments">; The type of data to be returned from the text widget.</span>
                   gentype: "", $           <span class="comments">; The "general" type of data: INTEGER, UNSIGNED, FLOAT, or STRING.</span>
                   undefined: Ptr_New(), $  <span class="comments">; The "undefined" value. Used in Get_Value methods, etc.</span>
                   noedit: 0L, $            <span class="comments">; A flag indicating whether text widget is editable (0) or not (1).</span>
                   nonsensitive: 0L, $      <span class="comments">; A flag indicating whether text widget is sensitive (0) or not (1).</span>
                   name:"" $                <span class="comments">; a scalar string name for the object</span>
                  }

END <span class="comments">;--------------------------------------------------------------------------------------------------------------</span>



FUNCTION FSC_FIELD, $               <span class="comments">; The compound widget FSC_Field.</span>
   parent, $                        <span class="comments">; The parent widget. Required for all compound widgets.</span>
   Column=column, $                 <span class="comments">; Set this keyword to have Label above Text Widget.</span>
   CR_Only=cr_only, $               <span class="comments">; Set this keyword if you only want Carriage Return events.</span>
   Decimal=decimal, $               <span class="comments">; Set this keyword to the number of digits to the right of the decimal point in FLOAT numbers.</span>
   Digits=digits, $                 <span class="comments">; Set this keyword to the number of digits permitted in INTEGER numbers.</span>
   Event_Func=event_func, $         <span class="comments">; Set this keyword to the name of an Event Function.</span>
   Event_Pro=event_pro, $           <span class="comments">; Set this keyword to the name of an Event Procedure.</span>
   _Extra=extra, $                  <span class="comments">; Passes along extra keywords to the text widget.</span>
   FieldFont=fieldfont, $           <span class="comments">; The font name for the text in the Text Widget.</span>
   Focus_Events=focus_events, $     <span class="comments">; Set this keyword to enable event reporting when the keyboard focus is lost.</span>
   Frame=frame, $                   <span class="comments">; Set this keyword to put a frame around the compound widget.</span>
   Highlight=highlight, $           <span class="comments">; If this keyword is set, the text is highlighted.</span>
   Label_Left=label_left, $         <span class="comments">; Set this keyword to align the label to the left of the label.</span>
   Label_Right=label_right, $       <span class="comments">; Set this keyword to align the labe to the right of the label.</span>
   LabelFont=labelfont, $           <span class="comments">; The font name for the text in the Label Widget.</span>
   LabelSize=labelsize, $           <span class="comments">; The X screen size of the Label Widget.</span>
   Name=name, $                     <span class="comments">; The name of the object.</span>
   NoEdit=noedit, $                 <span class="comments">; Setting this keywords makes the text widget non-editable.</span>
   NonSensitive=nonsensitive, $     <span class="comments">; Setting this keywords makes the text widget non-sensitive.</span>
   Object = obj, $                  <span class="comments">; An output keyword that contains the object reference.</span>
   Positive=positive, $             <span class="comments">; Set this keyword to indicate only positive numbers allowed in the field.</span>
   Scr_XSize=scr_xsize, $           <span class="comments">; The X screen size of the text widget.</span>
   Scr_YSize=scr_ysize, $           <span class="comments">; The Y screen size of the text widget.</span>
   Title=title, $                   <span class="comments">; The text to go on the Label Widget.</span>
   Undefined=undefined, $           <span class="comments">; Set to the value for "undefined" field values.</span>
   UValue=uvalue, $                 <span class="comments">; A user value for any purpose.</span>
   Value=value, $                   <span class="comments">; The "value" of the compound widget.</span>
   XSize=xsize                      <span class="comments">; The X size of the Text Widget.</span>

obj = Obj_New("FSC_FIELD", $
   parent, $                        <span class="comments">; The parent widget. Required for all compound widgets.</span>
   Column=column, $                 <span class="comments">; Set this keyword to have Label above Text Widget.</span>
   CR_Only=cr_only, $               <span class="comments">; Set this keyword if you only want Carriage Return events.</span>
   Decimal=decimal, $               <span class="comments">; Set this keyword to the number of digits to the right of the decimal point in FLOAT numbers.</span>
   Digits=digits, $                 <span class="comments">; Set this keyword to the number of digits permitted in INTEGER numbers.</span>
   Event_Func=event_func, $         <span class="comments">; Set this keyword to the name of an Event Function.</span>
   Event_Pro=event_pro, $           <span class="comments">; Set this keyword to the name of an Event Procedure.</span>
   _Extra=extra, $                  <span class="comments">; Passes along extra keywords to the text widget.</span>
   FieldFont=fieldfont, $           <span class="comments">; The font name for the text in the Text Widget.</span>
   Focus_events=focus_events, $     <span class="comments">; Set this keyword to enable event reporting when the keyboard focus is lost.</span>
   Frame=frame, $                   <span class="comments">; Set this keyword to put a frame around the compound widget.</span>
   Highlight=highlight, $           <span class="comments">; If this keyword is set, the text is highlighted.</span>
   Label_Left=label_left, $         <span class="comments">; Set this keyword to align the label to the left of the label.</span>
   Label_Right=label_right, $       <span class="comments">; Set this keyword to align the labe to the right of the label.</span>
   LabelFont=labelfont, $           <span class="comments">; The font name for the text in the Label Widget.</span>
   LabelSize=labelsize, $           <span class="comments">; The X screen size of the Label Widget.</span>
   Name=name, $                     <span class="comments">; The name of the object.</span>
   NoEdit=noedit, $                 <span class="comments">; Setting this keywords makes the text widget non-editable.</span>
   NonSensitive=nonsensitive, $     <span class="comments">; Setting this keywords makes the text widget non-sensitive.</span>
   Positive=positive, $             <span class="comments">; Set this keyword to indicate only positive numbers allowed in the field.</span>
   Scr_XSize=scr_xsize, $           <span class="comments">; The X screen size of the text widget.</span>
   Scr_YSize=scr_ysize, $           <span class="comments">; The Y screen size of the text widget.</span>
   Title=title, $                   <span class="comments">; The text to go on the Label Widget.</span>
   Undefined=undefined, $           <span class="comments">; Set to the value for "undefined" field values.</span>
   UValue=uvalue, $                 <span class="comments">; A user value for any purpose.</span>
   Value=value, $                   <span class="comments">; The "value" of the compound widget.</span>
   XSize=xsize)                     <span class="comments">; The X size of the Text Widget.</span>

IF Obj_Valid(obj) THEN RETURN, obj->GetID() ELSE RETURN, -1L
END <span class="comments">;--------------------------------------------------------------------------------------------------------------</span>



PRO Example_Event, event


<span class="comments">; An example event handler for FSC_Field.</span>

Widget_Control, event.top, Get_UValue=info
Widget_Control, event.id, Get_UValue=thisEvent


<span class="comments">; Not interested in losing keyboard focus events.</span>
theName = Tag_Names(event, /Structure_Name)
IF theName EQ 'WIDGET_KBRD_EVENT' THEN BEGIN
   IF event.type EQ 0 THEN RETURN
ENDIF

<span class="comments">; What kind of event is this?</span>
CASE thisEvent OF
   'Field 1 Event': BEGIN

      <span class="comments">; Demonstate various ways to test if the value from this field is undefined.</span>

      Print, ''
      IF N_Elements(*event.value) EQ 0 THEN Print, 'Field 1 Value is Undefined from Event Structure' ELSE $
         Print, 'Field 1 Value from Event Structure: ', *event.value

      Widget_Control, info.field1id, Get_Value=theValue
      IF Finite(theValue) EQ 0 THEN Print, 'Field 1 Value is Undefined and Assigned Value: ', theValue ELSE $
         Print, 'Field 1 Value via Widget Get_Value Function: ', theValue


      IF Finite(info.field1->Get_Value()) EQ 0 THEN Print, 'Field 1 Value is Undefined from Object Get_Value Function' ELSE $
         Print, 'Field 1 Value via Object Get_Value Function: ', info.field1->Get_Value()

      END
   'Field 2 Event': BEGIN
      Print, ''
      IF N_Elements(*event.value) EQ 0 THEN Print, 'Field 2 Value is Undefined' ELSE $
         Print, 'Field 2 Value: ', *event.value

      theValue = info.field2->Get_Value()
      IF theValue EQ -9999.0 THEN Print, 'Field 2 Value is Undefined and Assigned Value: ', theValue ELSE $
         Print, 'Field 2 Value via Object Get_Value Function: ', theValue

      END

   'Print It': BEGIN
      theValue =info.field3->Get_Value()
      Print, ''
      Print, 'Field 3 Value: ', theValue
      END
   'Set It': BEGIN
      info.field3->Set_Value, 'Coyote Rules!'
      END
   'Quit': Widget_Control, event.top, /Destroy
   'ChangeToUInt': BEGIN
      info.field1->SetProperty, Title='Unsigned:', Value=UINT(RandomU(seed, 1)*100)
      END
   'ChangeToFloat': BEGIN
      info.field1->SetProperty, Title='Float:', Value=RandomU(seed, 1)*100
      END
   'ChangeToString': BEGIN
      info.field1->SetProperty, Title='String:', Value='Coyote Jules'
      END
   'PrintFloat': BEGIN
      IF N_Elements(info.field2->Get_Value()) EQ 0 THEN Print, 'Field 2 Value is Undefined' ELSE $
         Print, 'Field 2 Value: ', info.field2->Get_Value()
      END
   'LabelSize': BEGIN
      Widget_Control, event.top, Update=0
      currentSize = info.labelsize
      info.field1->SetProperty, LabelSize=info.labelsize
      info.field2->SetProperty, LabelSize=info.labelsize
      info.field3->SetProperty, LabelSize=info.labelsize
      IF currentsize EQ 75 THEN info.labelsize = 50 ELSE info.labelsize = 75
      Widget_Control, event.top, Update=1
      END
   'MakeEditable': BEGIN
      Widget_Control, event.id, Get_Value=buttonValue
      CASE buttonValue OF
         'Make String Field Editable': BEGIN
            info.field3->SetEdit, 1
            Widget_Control, event.id, Set_Value='Make String Field Non-Editable'
            END
         'Make String Field Non-Editable': BEGIN
            info.field3->SetEdit, 0
            Widget_Control, event.id, Set_Value='Make String Field Editable'
            END
      ENDCASE
      END
   'MakeSensitive': BEGIN
      Widget_Control, event.id, Get_Value=buttonValue
      CASE buttonValue OF
         'Make String Field Sensitive': BEGIN
            info.field3->SetSensitive, 1
            Widget_Control, event.id, Set_Value='Make String Field Non-Sensitive'
            END
         'Make String Field Non-Sensitive': BEGIN
            info.field3->SetSensitive, 0
            Widget_Control, event.id, Set_Value='Make String Field Sensitive'
            END
      ENDCASE
      END
ENDCASE

IF Widget_Info(event.top, /Valid_ID) THEN Widget_Control, event.top, Set_UValue=info
END <span class="comments">;--------------------------------------------------------------------------------------------------------------</span>



PRO Example, field1, field2, field3

<span class="comments">; An example program to exercise some of the features of FSC_FIELD.</span>

tlb = Widget_Base(Column=1)
button = Widget_Button(tlb, Value='Change First Field to Float', $
   UValue='ChangeToFloat')
button = Widget_Button(tlb, Value='Change First Field to String', $
   UValue='ChangeToString')
button = Widget_Button(tlb, Value='Change First Field to Unsigned Integer', $
   UValue='ChangeToUInt')

   <span class="comments">; Create an integer field, no more than 4 digits in the value.</span>

field1id = FSC_FIELD(tlb, Title='Integer:', LabelSize=50, Digits=4, Object=field1, $
   Value=5, UValue='Field 1 Event', Event_Pro='Example_Event', /CR_Only, /Highlight)

   <span class="comments">; Create a floating point field. Only two decimal points to the right of the decimal.</span>
   <span class="comments">; Set the Undefined value to -9999.0.</span>

field = FSC_FIELD(tlb, Title='Double:', LabelSize=50, Value=-123456789.1234567891234D, Object=field2, Undefined=-9999.0, $
   /CR_Only, UValue='Field 2 Event', Event_Pro='Example_Event', Decimal=8, /Highlight)

   <span class="comments">; Create a string field.</span>

field = FSC_FIELD(tlb, Title='String:', LabelSize=50, Value='Coyote Rules!', Object=field3, /Nonsensitive, /Highlight)

   <span class="comments">; Set up TABing between fields.</span>

<span class="comments">;field1->SetTabNext, field2->GetTextID()</span>
<span class="comments">;field2->SetTabNext, field3->GetTextID()</span>
<span class="comments">;field3->SetTabNext, field1->GetTextID()</span>

button = Widget_Button(tlb, Value='Print Value of String', UValue="Print It")
button = Widget_Button(tlb, Value='Set Value of String', UValue='Set It')
button = Widget_Button(tlb, Value='Change Size of Labels', UValue='LabelSize')
button = Widget_Button(tlb, Value='Print Floating Value', UValue='PrintFloat')
button = Widget_Button(tlb, Value='Make String Field Editable', UValue='MakeEditable')
button = Widget_Button(tlb, Value='Make String Field Sensitive', UValue='MakeSensitive')
button = Widget_Button(tlb, Value='Quit', UValue='Quit')
Widget_Control, tlb, /Realize, Set_UValue={field1:field1, field2:field2, field3:field3, field1id:field1id, labelsize:75}
XManager, 'example', tlb, /No_Block
END
</code>
    </div>
  </body>
</html>