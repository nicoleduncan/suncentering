<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN"
 "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">

<!-- Generated by IDLdoc 3.5.1 on Mon Sep 30 16:58:24 2013 -->

<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en">
  <head>
    <meta http-equiv="Content-Type" content="text/html;charset=utf-8"/>
    <title>fsc_fileselect.pro (Documentation for ./)</title>

    
    <link rel="stylesheet" type="text/css" media="all"
          href="../../idldoc-resources/main.css" />
    <link rel="stylesheet" type="text/css" media="print"
          href="../../idldoc-resources/main-print.css" />
    

    <script type="text/javascript">
      function setTitle() {
        parent.document.title="fsc_fileselect.pro (Documentation for ./)";
      }
    </script>
  </head>

  <body onload="setTitle();" id="root">
    <div class="content">
      <code class="source"><span class="comments">;+</span>
<span class="comments">; NAME:</span>
<span class="comments">;   FSC_FILESELECT</span>
<span class="comments">;</span>
<span class="comments">; PURPOSE:</span>
<span class="comments">;</span>
<span class="comments">;   The purpose of this compound widget is to provide a means</span>
<span class="comments">;   by which the user can type or select a file name. The</span>
<span class="comments">;   program is written as an "object widget", meaning that</span>
<span class="comments">;   the guts of the program is an object of class FSC_FILESELECT.</span>
<span class="comments">;   This is meant to be an example of the obvious advantages of</span>
<span class="comments">;   writing compound widget programs as objects.</span>
<span class="comments">;</span>
<span class="comments">; AUTHOR:</span>
<span class="comments">;</span>
<span class="comments">;   FANNING SOFTWARE CONSULTING</span>
<span class="comments">;   David Fanning, Ph.D.</span>
<span class="comments">;   1645 Sheely Drive</span>
<span class="comments">;   Fort Collins, CO 80526 USA</span>
<span class="comments">;   Phone: 970-221-0438</span>
<span class="comments">;   E-mail: david@idlcoyote.com</span>
<span class="comments">;   Coyote's Guide to IDL Programming: http://www.idlcoyote.com/</span>
<span class="comments">;</span>
<span class="comments">; CATEGORY:</span>
<span class="comments">;</span>
<span class="comments">;   General programming.</span>
<span class="comments">;</span>
<span class="comments">; CALLING SEQUENCE:</span>
<span class="comments">;</span>
<span class="comments">;   filenameID = FSC_FileSelect(parent)</span>
<span class="comments">;</span>
<span class="comments">; INPUT PARAMETERS:</span>
<span class="comments">;</span>
<span class="comments">;   parent -- The parent widget ID of the compound widget. Required.</span>
<span class="comments">;</span>
<span class="comments">; INPUT KEYWORDS:</span>
<span class="comments">;</span>
<span class="comments">;   Event_Pro -- The event handler procedure for this compound widget.By default: "".</span>
<span class="comments">;   Event_Func -- The event handler function for this compound widget. By default: "".</span>
<span class="comments">;</span>
<span class="comments">;      If neither EVENT_PRO or EVENT_FUNC is defined, program events are handled internally by the compound widget.</span>
<span class="comments">;</span>
<span class="comments">;   DirectoryName -- The initial name of the directory. By defaut: current directory.</span>
<span class="comments">;   Filename -- The initial file name in the filename text widget.</span>
<span class="comments">;   Filter -- The file filter. By default: "*".</span>
<span class="comments">;   Frame -- Set this keyword for a frame around the compound widget.</span>
<span class="comments">;   LabelFont -- The font for the label widget. By default: "".</span>
<span class="comments">;   LabelName -- The text on the label widgt. By default: "Filename: ".</span>
<span class="comments">;   LabelSize -- The X screen size of the label widget. By default: 0.</span>
<span class="comments">;   MustExist -- A flag that indicates selected files must exist. By default: 0.</span>
<span class="comments">;   NoMaxSize -- A flag to prohibit automatic text widget sizing. By default: 0.</span>
<span class="comments">;</span>
<span class="comments">;     If this keyword is not set, the compound widget will automatically resize itself to</span>
<span class="comments">;     the largest widget in its parent base widget. It will do this by changing the size of</span>
<span class="comments">;     the text widgets holding the file and directory names.</span>
<span class="comments">;</span>
<span class="comments">;   Read -- Set this keyword to have file selection for reading a file. By default: 1.</span>
<span class="comments">;   SelectDirectory -- The default directory for file selection. In other words, this is the</span>
<span class="comments">;     default directory for DIALOG_PICKFILE, which is accessed via the BROWSE buttons.</span>
<span class="comments">;   SelectFont -- The font for the "Browse" button. By default: "".</span>
<span class="comments">;   SelectTitle -- The title bar text on the file selection dialog. By default: "Select a File...".</span>
<span class="comments">;   TextFont -- The font for the filename text widget. By default: "".</span>
<span class="comments">;   UValue -- User value for any purpose.</span>
<span class="comments">;   Write -- Set this keyword to open a file for writing. By default: 0.</span>
<span class="comments">;   XSize -- The X size of the text widget holding the filename. By default: StrLen(filename) * 1.5 > 40.</span>
<span class="comments">;</span>
<span class="comments">; OUTPUT KEYWORDS:</span>
<span class="comments">;</span>
<span class="comments">;   ObjectRef -- Assign this keyword to an output variable that will hold the internal object reference.</span>
<span class="comments">;                With the object reference you can call object methods to easily change many properties of</span>
<span class="comments">;                the compound widget.</span>
<span class="comments">;</span>
<span class="comments">; COMMON BLOCKS:</span>
<span class="comments">;</span>
<span class="comments">;   None.</span>
<span class="comments">;</span>
<span class="comments">; RESTRICTIONS:</span>
<span class="comments">;</span>
<span class="comments">;   Requires the folling files from the Coyote Library:</span>
<span class="comments">;</span>
<span class="comments">;      http://www.idlcoyote.com/programs/error_message.pro</span>
<span class="comments">;</span>
<span class="comments">; EVENT STRUCTURE:</span>
<span class="comments">;</span>
<span class="comments">;   All events are handled internally unless either the Event_Pro or Event_Func</span>
<span class="comments">;   keywords are used to assign an event handler to the compound widget. All events</span>
<span class="comments">;   generated by the text widgets are passed to the assigned event handler.</span>
<span class="comments">;</span>
<span class="comments">;   event = { CW_FILESELECT, $     ; The name of the event structure.</span>
<span class="comments">;             ID: 0L, $            ; The ID of the compound widget's top-level base.</span>
<span class="comments">;             TOP: 0L, $           ; The widget ID of the top-level base of the hierarchy.</span>
<span class="comments">;             HANDLER: 0L, $       ; The event handler ID. Filled out by IDL.</span>
<span class="comments">;             Basename: "", $      ; The base filename without directory specifiers.</span>
<span class="comments">;             Filename: "", $      ; The fully qualified filename.</span>
<span class="comments">;             Directory: "", $     ; The name of the current file directory.</span>
<span class="comments">;           }</span>
<span class="comments">;</span>
<span class="comments">; EXAMPLE:</span>
<span class="comments">;</span>
<span class="comments">;   An example program is provided at the end of the FSC_FILESELECT code. To run it,</span>
<span class="comments">;   type these commands:</span>
<span class="comments">;</span>
<span class="comments">;      IDL> .Compile fsc_fileselect</span>
<span class="comments">;      IDL> Example</span>
<span class="comments">;</span>
<span class="comments">;   Or, if you want to obtain the object reference, type this:</span>
<span class="comments">;</span>
<span class="comments">;      IDL> Example, theObject</span>
<span class="comments">;</span>
<span class="comments">;   Now you can call the object's methods. For example:</span>
<span class="comments">;</span>
<span class="comments">;      IDL theObject->SetProperty, XSize=150</span>
<span class="comments">;</span>
<span class="comments">; GETTING and SETTING VALUES:</span>
<span class="comments">;</span>
<span class="comments">;   So as not to disrupt the accepted paradigm in using compound widgets, you</span>
<span class="comments">;   can use the return value of the FSC_FILESELECT function with WIDGET_CONTROL to</span>
<span class="comments">;   get and set the "value" of the widget.</span>
<span class="comments">;</span>
<span class="comments">;       Widget_Control, filenameID, Set_Value='C:\RSI\IDL52\DATA\cyclone.dat'</span>
<span class="comments">;</span>
<span class="comments">;   The program will automatically separate the file name portion of the value</span>
<span class="comments">;   from the directory portion and put things in the correct text widgets.</span>
<span class="comments">;</span>
<span class="comments">;   Similarly, you can get the "value" of the widget:</span>
<span class="comments">;</span>
<span class="comments">;       Widget_Control, filenameID, Get_Value=theValue</span>
<span class="comments">;       Print, theValue</span>
<span class="comments">;</span>
<span class="comments">;           C:\RSI\IDL52\DATA\cyclone.dat</span>
<span class="comments">;</span>
<span class="comments">;   The return value is the fully qualified file path to the file.</span>
<span class="comments">;</span>
<span class="comments">; USING OBJECT METHODS to CHANGE PROGRAM PROPERTIES:</span>
<span class="comments">;</span>
<span class="comments">;   If you obtain the object reference, you have a great deal more control</span>
<span class="comments">;   over the properties of the compound widget. You obtain the object reference</span>
<span class="comments">;   by calling the function like this:</span>
<span class="comments">;</span>
<span class="comments">;      filenameID = FSC_FILESELECT(parent, ObjectRef=theObject)</span>
<span class="comments">;</span>
<span class="comments">; OBJECT PROCEDURE METHODS:</span>
<span class="comments">;</span>
<span class="comments">;   GetProperty -- This method allows various properties of the widget to be</span>
<span class="comments">;       returned via output keywords. The keywords that are available are:</span>
<span class="comments">;</span>
<span class="comments">;      DirectoryName -- The current directory.</span>
<span class="comments">;      Event_Func -- The name of the event handler function for this compound widget.</span>
<span class="comments">;      Event_Pro -- The name of the event handler procedure for this compound widget.</span>
<span class="comments">;      Filename -- The current base filename.</span>
<span class="comments">;      Filter -- The current file filter.</span>
<span class="comments">;      LabelName -- The text on the label widget.</span>
<span class="comments">;      LabelSize -- The X screen size of the label widget.</span>
<span class="comments">;      MustExist -- A flag that indicates selected files must exist to be selected.</span>
<span class="comments">;      Parent -- The parent widget of the compound widget.</span>
<span class="comments">;      Read=read -- The file selection for reading flag.</span>
<span class="comments">;      SelectTitle -- The title bar text on the file selection dialog.</span>
<span class="comments">;      TLB -- The top-level base of the compound widget.</span>
<span class="comments">;      UValue -- The user value of the compound widget.</span>
<span class="comments">;      Write -- The file selection for writing flag.</span>
<span class="comments">;      XSize -- The X size of the text widget holding the filename.</span>
<span class="comments">;</span>
<span class="comments">;   LabelSize -- This method makes sure that the directory name and file name labels</span>
<span class="comments">;      are the same size. Normally, this procedure is called internally. No parameters.</span>
<span class="comments">;</span>
<span class="comments">;   MatchSize -- This method resizes the compound widget so that it is as long as the</span>
<span class="comments">;      the longest widget in the parent base widget. This is done automatically upon</span>
<span class="comments">;      realization unless the NOMAXSIZE keyword is set. The method aids in writing</span>
<span class="comments">;      resizeable widget programs.</span>
<span class="comments">;</span>
<span class="comments">;   SetProperty -- This method allows various properties of the widget to be</span>
<span class="comments">;       set via input keywords. The keywords that are available are:</span>
<span class="comments">;</span>
<span class="comments">;      DirectoryName -- The current directory.</span>
<span class="comments">;      Event_Func -- The name of the event handler function for this compound widget.</span>
<span class="comments">;      Event_Pro -- The name of the event handler procedure for this compound widget.</span>
<span class="comments">;      Filename -- The current base filename.</span>
<span class="comments">;      Filter -- The current file filter.</span>
<span class="comments">;      LabelName -- The text on the label widget.</span>
<span class="comments">;      LabelSize -- The X screen size of the label widget.</span>
<span class="comments">;      MustExist -- A flag that indicates selected files must exist to be selected.</span>
<span class="comments">;      Read -- The file selection for reading flag.</span>
<span class="comments">;      SelectTitle -- The title bar text on the file selection dialog.</span>
<span class="comments">;      UValue -- The user value of the compound widget.</span>
<span class="comments">;      Write -- The file selection for writing flag.</span>
<span class="comments">;      XSize -- The X size of the text widget holding the filename.</span>
<span class="comments">;</span>
<span class="comments">;   TextSelect - Allows you to create a selection in filename text widget. See the</span>
<span class="comments">;                documentation for the SET_TEXT_SELECT keyword to Widget_Control.</span>
<span class="comments">;</span>
<span class="comments">;      selection -- A two-element array containing the starting position and selection length.</span>
<span class="comments">;</span>
<span class="comments">; OBJECT FUNCTION METHODS:</span>
<span class="comments">;</span>
<span class="comments">;      GetFileName -- Returns the fully qualified filename. No parameters.</span>
<span class="comments">;</span>
<span class="comments">;      GetTLB -- Returns the top-level base ID of the compound widget. No Parameters.</span>
<span class="comments">;</span>
<span class="comments">;      Inspect_DirectoryName -- Inspects the directory name for correctness. Requires one positional parameter.</span>
<span class="comments">;</span>
<span class="comments">;        directoryName -- The name of the directory from the directory text widget.</span>
<span class="comments">;        textSelection -- The current text selection position.</span>
<span class="comments">;</span>
<span class="comments">;        At the moment all this does is remove any blank characters from either</span>
<span class="comments">;        end of the directory name and makes sure the last character of the directory</span>
<span class="comments">;        name does not end in a subdirectory specifier (except for VMS).</span>
<span class="comments">;</span>
<span class="comments">;     Inspect_Filename -- Inspects the file name for correctness. Requires one positional parameter.</span>
<span class="comments">;</span>
<span class="comments">;        filename -- The name of the file from the filename text widget.</span>
<span class="comments">;        textSelection -- The current text selection position.</span>
<span class="comments">;</span>
<span class="comments">;        At the moment all this does is remove any blank characters from either</span>
<span class="comments">;        end of the file name</span>
<span class="comments">;</span>
<span class="comments">; MODIFICATION HISTORY:</span>
<span class="comments">;</span>
<span class="comments">;   Written by: David W. Fanning, 21 NOV 1999.</span>
<span class="comments">;   Fixed bug in File Name selection button. 18 MAR 2000. DWF.</span>
<span class="comments">;   Fixed an error in which directory the Browse buttons should start</span>
<span class="comments">;       searching. 29 SEP 2000. DWF.</span>
<span class="comments">;   Previously returned events only for typing in text widgets. Now</span>
<span class="comments">;       Browse button events are also returned. 29 SEP 2000. DWF.</span>
<span class="comments">;   Fixed a bug in setting the file filter. 29 SEP 2000. DWF.</span>
<span class="comments">;   Removed the Directory Browse button 10 AUG 2002. DWF.</span>
<span class="comments">;   Added ERROR_MESSAGE to error handling. 10 AUG 2002. DWF.</span>
<span class="comments">;   Changed the ability to specify a file filter as a string array, instead</span>
<span class="comments">;       of just as a scalar string. This required the use of a pointer, which</span>
<span class="comments">;       meant that I had to remove the FILTER field from the CW_FILESELECT</span>
<span class="comments">;       event structure to avoid likely memory leakage. This is a dangerous</span>
<span class="comments">;       change because it means programs that relied on this (I expect there</span>
<span class="comments">;       are very, very few) will break and it goes against my philosopy of</span>
<span class="comments">;       keeping my programs backward compatible. Let me know if you have</span>
<span class="comments">;       problems. In testing, I discoved no problems in my own code. 31 OCT 2002. DWF.</span>
<span class="comments">;   Fixed a problem with DIALOG_PICKFILE that sometimes allowed users to change</span>
<span class="comments">;       directories without selecting a file. 3 Nov 2002. DWF.</span>
<span class="comments">;   Fixed a problem with widget resizing with the help of Bob Portman that had plagued</span>
<span class="comments">;       me from the beginning. Thanks, Bob! 5 August 2003. DWF</span>
<span class="comments">;   Added TEXTSELECT method. 5 Aug 2003. DWF.</span>
<span class="comments">;   Had to add FORWARD_FUNCTION statement to get error handler compiled when using</span>
<span class="comments">;       DIRECTORY keyword. 24 Nov 2003. DWF.</span>
<span class="comments">;   Fixed a problem with too many events going to an event handler specified with</span>
<span class="comments">;       the EVENT_PRO or EVENT_FUNC keyword from the text widget. Now only Carriage</span>
<span class="comments">;       Return events are passed on to the user-specified event handler. 8 July 2004. DWF.</span>
<span class="comments">;   Replace all "\" characters with "/" characters in directory names. 8 Januay 2006. DWF.</span>
<span class="comments">;   Set the default fonts to be the current widget font, rather than the default widget font. 4 Oct 2008. DWF.</span>
<span class="comments">;-</span>
<span class="comments">;</span>
<span class="comments">;******************************************************************************************;</span>
<span class="comments">;  Copyright (c) 2008, by Fanning Software Consulting, Inc.                                ;</span>
<span class="comments">;  All rights reserved.                                                                    ;</span>
<span class="comments">;                                                                                          ;</span>
<span class="comments">;  Redistribution and use in source and binary forms, with or without                      ;</span>
<span class="comments">;  modification, are permitted provided that the following conditions are met:             ;</span>
<span class="comments">;                                                                                          ;</span>
<span class="comments">;      * Redistributions of source code must retain the above copyright                    ;</span>
<span class="comments">;        notice, this list of conditions and the following disclaimer.                     ;</span>
<span class="comments">;      * Redistributions in binary form must reproduce the above copyright                 ;</span>
<span class="comments">;        notice, this list of conditions and the following disclaimer in the               ;</span>
<span class="comments">;        documentation and/or other materials provided with the distribution.              ;</span>
<span class="comments">;      * Neither the name of Fanning Software Consulting, Inc. nor the names of its        ;</span>
<span class="comments">;        contributors may be used to endorse or promote products derived from this         ;</span>
<span class="comments">;        software without specific prior written permission.                               ;</span>
<span class="comments">;                                                                                          ;</span>
<span class="comments">;  THIS SOFTWARE IS PROVIDED BY FANNING SOFTWARE CONSULTING, INC. ''AS IS'' AND ANY        ;</span>
<span class="comments">;  EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES    ;</span>
<span class="comments">;  OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT     ;</span>
<span class="comments">;  SHALL FANNING SOFTWARE CONSULTING, INC. BE LIABLE FOR ANY DIRECT, INDIRECT,             ;</span>
<span class="comments">;  INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED    ;</span>
<span class="comments">;  TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS;         ;</span>
<span class="comments">;  LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND             ;</span>
<span class="comments">;  ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT              ;</span>
<span class="comments">;  (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS           ;</span>
<span class="comments">;  SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.                            ;</span>
<span class="comments">;******************************************************************************************;</span>
FUNCTION FSC_Fileselect_WidgetFont, DEFAULT=default

   <span class="comments">; Build a small widget to determine the current </span>
   <span class="comments">; and default widget fonts.</span>
   
   base = Widget_Base(MAP=0)
   button = Widget_Button(base, Value='TEST')
   
   <span class="comments">; Checking before realization gives default font.</span>
   defaultFont = Widget_Info(button, /FONTNAME)
   
   <span class="comments">; Checking after realization gives current font.</span>
   Widget_Control, base, /REALIZE
   currentFont = Widget_Info(button, /FONTNAME)
   
   <span class="comments">; Clean up.</span>
   Widget_Control, base, /DESTROY

   IF Keyword_Set(default) THEN $
        RETURN, defaultFont ELSE $
        RETURN, currentFont
    
END <span class="comments">;-----------------------------------------------------------------------------------------------------------------------------</span>



PRO FSC_FileSelect::Directory_Events, event

<span class="comments">; This method handles any typing in the directory name text widget.</span>

Catch, theError
IF theError NE 0 THEN BEGIN
   ok = Error_Message(/Traceback)
   RETURN
ENDIF

   <span class="comments">; Don't deal with selection events.</span>

IF event.type EQ 3 THEN RETURN

   <span class="comments">; Inspect the directory name and set it.</span>

Widget_Control, self.dirtextID, Get_Value=dirname
dirname = dirname[0]
textSelection = Widget_Info(self.dirtextID, /Text_Select)
dirname = self->Inspect_DirectoryName(dirname, textSelection[0])
<span class="comments">; See notes in INIT method.</span>
<span class="comments">;;;dirname = StrJoin( StrSplit(dirname, '\\', /Regex, /Extract, /Preserve_Null), '/')</span>

self.directoryname = dirname
END <span class="comments">;-----------------------------------------------------------------------------------------------------------------------------</span>



PRO FSC_FileSelect::Filename_Events, event

<span class="comments">; This method handles any typing in the file name text widget.</span>

Catch, theError
IF theError NE 0 THEN BEGIN
   ok = Error_Message(/Traceback)
   RETURN
ENDIF

   <span class="comments">; Don't deal with selection events.</span>

IF event.type EQ 3 THEN RETURN

   <span class="comments">; Inspect the file name and set it.</span>

Widget_Control, self.filetextID, Get_Value=filename
filename = filename[0]
textSelection = Widget_Info(self.filetextID, /Text_Select)
filename = self->Inspect_Filename(filename, textSelection[0])
<span class="comments">; See notes in INIT method.</span>
<span class="comments">;;;filename = StrJoin( StrSplit(filename, '\\', /Regex, /Extract, /Preserve_Null), '/')</span>
self.filename = filename
END <span class="comments">;-----------------------------------------------------------------------------------------------------------------------------</span>



FUNCTION FSC_FileSelect::GetFilename

<span class="comments">; This method returns the fully-qualified filename. Checks to be</span>
<span class="comments">; sure the last character in the directory name is not a directory</span>
<span class="comments">; specifier.</span>

Catch, theError
IF theError NE 0 THEN BEGIN
   ok = Error_Message()
   RETURN, -1
ENDIF

   <span class="comments">; Get the correct directory separator.</span>

CASE StrUpCase(!Version.OS_Family) OF
   'MACOS'   : sep = ':'    <span class="comments">; Macintoshes</span>
   'VMS'     : sep = ']'    <span class="comments">; VMS machines</span>
   ELSE      : sep = '/'    <span class="comments">; Unix machines</span>
ENDCASE

IF StrUpCase(!Version.OS_Family) NE "VMS" THEN BEGIN
   length = StrLen(self.directoryName)
   WHILE StrMid(self.directoryName, length-1, 1) EQ sep DO BEGIN
      self.directoryName = StrMid(self.directoryName, 0, length-1)
      Widget_Control, self.dirTextID, Set_Value=self.directoryName
      length = StrLen(self.directoryName)
   ENDWHILE
ENDIF

filename = Filepath(Root_Dir=self.directoryName, self.filename)
<span class="comments">; See notes in INIT method.</span>
<span class="comments">;;filename = StrJoin( StrSplit(filename, '\\', /Regex, /Extract, /Preserve_Null), '/')</span>
RETURN, filename
END <span class="comments">;-------------------------------------------------------------------------------</span>



FUNCTION FSC_FileSelect::GetNoMaxSize
RETURN, self.nomaxsize
END <span class="comments">;-----------------------------------------------------------------------------------------------------------------------------</span>



PRO FSC_FileSelect::GetProperty, $
   DirectoryName=dirname, $        <span class="comments">; The current directory.</span>
   Event_Func=event_func, $        <span class="comments">; The name of the event handler function for this compound widget.</span>
   Event_Pro=event_pro, $          <span class="comments">; The name of the event handler procedure for this compound widget.</span>
   Filename=filename, $            <span class="comments">; The current filename.</span>
   Filter=filter, $                <span class="comments">; The current file filter.</span>
   LabelName=labelname, $          <span class="comments">; The text on the label widget.</span>
   LabelSize=labelsize, $          <span class="comments">; The X screen size of the label widget.</span>
   MustExist=mustexist, $          <span class="comments">; A flag that indicates selected files must exist to be selected.</span>
   Parent=parent, $                <span class="comments">; The parent widget of the compound widget.</span>
   Read=read, $                    <span class="comments">; The file selection for reading flag.</span>
   SelectTitle=selecttitle, $      <span class="comments">; The title bar text on the file selection dialog.</span>
   TLB=tlb, $                      <span class="comments">; The top-level base of the compound widget.</span>
   UValue=uvalue, $                <span class="comments">; The user value of the compound widget.</span>
   Write=write, $                  <span class="comments">; The file selection for writing flag.</span>
   XSize=xsize                     <span class="comments">; The X size of the text widget holding the filename.</span>

Catch, theError
IF theError NE 0 THEN BEGIN
   ok = Error_Message()
   RETURN
ENDIF

dirname = self.directoryname
event_pro = self.event_pro
event_func = self.event_func
filename = self.filename
filter = *self.filter
labelname = self.labelname
labelsize = self.labelsize
mustexist = self.mustexist
parent=self.parent
read = self.read
selecttitle = self.selecttitle
tlb = self.tlb
uvalue = *self.uvalue
wirte = self.write
xsize = self.xsize

END <span class="comments">;-----------------------------------------------------------------------------------------------------------------------------</span>



FUNCTION FSC_FileSelect::GetTLB
RETURN, self.tlb
END <span class="comments">;-----------------------------------------------------------------------------------------------------------------------------</span>



FUNCTION FSC_FileSelect::Inspect_DirectoryName, dirname, textSelection

<span class="comments">; This method removes leading and trailing blank characters</span>
<span class="comments">; in the directory name.</span>

Catch, theError
IF theError NE 0 THEN BEGIN
   ok = Error_Message()
   RETURN, -1
ENDIF

thisDir = StrTrim(dirname, 2)
IF StrLen(thisDir) NE StrLen(dirname) THEN $
   textSelection = textSelection-( StrLen(dirname) - StrLen(thisDir) )
Widget_Control, Set_Value=thisDir, self.dirtextID, Set_Text_Select=[textSelection, 0]
RETURN, thisDir
END <span class="comments">;-----------------------------------------------------------------------------------------------------------------------------</span>



FUNCTION FSC_FileSelect::Inspect_Filename, filename, textSelection

<span class="comments">; This method removes leading and trailing blank characters</span>
<span class="comments">; in the filename.</span>

Catch, theError
IF theError NE 0 THEN BEGIN
   Catch, /Cancel
   ok = Error_Message()
   RETURN, -1
ENDIF

thisFile = StrTrim(filename, 2)
IF StrLen(thisFile) NE StrLen(filename) THEN $
   textSelection = textSelection-( StrLen(filename) - StrLen(thisFile) )
Widget_Control, Set_Value=thisFile, self.filetextID, Set_Text_Select=[textSelection, 0]
RETURN, thisFile
END <span class="comments">;-----------------------------------------------------------------------------------------------------------------------------</span>



PRO FSC_FileSelect::LabelSize

<span class="comments">; This method ensures the directory and filename labels are the same size.</span>

Catch, theError
IF theError NE 0 THEN BEGIN
   ok = Error_Message()
   RETURN
ENDIF

fgeo = Widget_Info(self.filelabelID, /Geometry)
dgeo = Widget_Info(self.dirlabelID, /Geometry)
bestSize = fgeo.scr_xsize > dgeo.scr_xsize
Widget_Control, self.fileLabelID, Scr_XSize=bestSize
Widget_Control, self.dirLabelID, Scr_XSize=bestSize
END <span class="comments">;-------------------------------------------------------------------------------</span>



PRO FSC_FileSelect::MatchSize

<span class="comments">; This method makes the compound widget the same size as the largest</span>
<span class="comments">; widget in the parent base widget. It does this by adjusting</span>
<span class="comments">; the text widget sizes.</span>

Catch, theError
IF theError NE 0 THEN BEGIN
   ok = Error_Message()
   RETURN
ENDIF

   <span class="comments">; Get the geometries of the filename widgets.</span>

tgeo = Widget_Info(self.tlb, /Geometry)
fgeo = Widget_Info(self.filetextID, /Geometry)
bbgeo = Widget_Info(self.filebaseID, /Geometry)
lbgeo = Widget_Info(Widget_Info(self.dirbaseID, /Parent), /Geometry)
rbgeo = Widget_Info(Widget_Info(self.filebrowseID, /Parent), /Geometry)

   <span class="comments">; Add the bits and bobs together.</span>

fspacing = tgeo.space
fpadding = 2*tgeo.xpad + 2*lbgeo.xpad
ffixedsize = ( bbgeo.scr_xsize - 2*bbgeo.margin - (fgeo.scr_xsize) + $
               rbgeo.scr_xsize - 2*rbgeo.margin )

   <span class="comments">; Get the geometries of the directory name widgets.</span>

fgeo = Widget_Info(self.dirtextID, /Geometry)
bbgeo = Widget_Info(self.dirbaseID, /Geometry)

   <span class="comments">; Add the bits and bobs together.</span>

dspacing = tgeo.space
dpadding = 2*tgeo.xpad + 2*lbgeo.xpad
dfixedsize = ( bbgeo.scr_xsize - 2*bbgeo.margin - (fgeo.scr_xsize) + $
               rbgeo.scr_xsize - 2*rbgeo.margin )

   <span class="comments">; Go find all the children and find the biggest one of them.</span>

thisSize = 0
childID = Widget_Info(self.parent, /Child)
geom = Widget_Info(childID, /Geometry)
testSize = geom.scr_xsize - 2*geom.margin
thisSize = thisSize > testSize
WHILE childID NE 0 DO BEGIN
   childID = Widget_Info(childID, /Sibling)
   IF childID GT 0 THEN BEGIN
      geom = Widget_Info(childID, /Geometry)
      testSize = geom.scr_xsize - 2*geom.margin
      thisSize = thisSize > testSize
   ENDIF
ENDWHILE

   <span class="comments">; Resize the widgets</span>

ftextsize = thisSize - (fspacing + fpadding + ffixedsize)
Widget_Control, self.filetextID, Scr_XSize=ftextsize

dtextsize = thisSize - (dspacing + dpadding + dfixedsize)
Widget_Control, self.dirtextID, Scr_XSize=dtextsize

END <span class="comments">;-------------------------------------------------------------------------------</span>



FUNCTION FSC_FileSelect_RStrPos, Expr, SubStr, Pos

  ON_ERROR, 2
  N = N_PARAMS()
  if (n lt 2) then message, 'Incorrect number of arguments.'

  <span class="comments">; Is expr an array or a scalar? In either case, make a result</span>
  <span class="comments">; that matches.</span>
  if (size(expr, /n_dimensions) eq 0) then result = 0 $
  else result = make_array(dimension=size(expr,/dimensions), /INT)

  RSubStr = STRING(REVERSE(BYTE(SubStr))) <span class="comments">; Reverse the substring</span>

  for i = 0, n_elements(expr) - 1 do begin
    Len = STRLEN(Expr[i])
    IF (N_ELEMENTS(Pos) EQ 0) THEN Start=0 ELSE Start = Len - Pos

    RString = STRING(REVERSE(BYTE(Expr[i]))) <span class="comments">; Reverse the string</span>

    SubPos = STRPOS(RString, RSubStr, Start)
    IF SubPos NE -1 THEN SubPos = Len - SubPos - STRLEN(SubStr)
    result[i] = SubPos
  endfor

  RETURN, result
END <span class="comments">;-------------------------------------------------------------------------</span>



PRO FSC_FileSelect::Select_File, event

Catch, theError
IF theError NE 0 THEN BEGIN
   ok = Error_Message()
   RETURN
ENDIF

<span class="comments">; Use the file selection dialog to obtain a filename.</span>

currentFilename = self.filename
self.filename = ""
filename = Dialog_Pickfile( File=currentFilename, $
                            Filter=*self.filter, $
                            Path=self.directoryname, $
                            Get_Path=thePath, $
                            Must_Exist=self.mustexist, $
                            Read=self.read, $
                            Title=self.selecttitle, $
                            Write=self.write )
                            
IF filename EQ "" THEN BEGIN
   self.filename = currentFilename
   RETURN
ENDIF

   <span class="comments">; Remove the directory from the name.</span>

index = StrLen(thePath)
theFilename = StrMid(filename, index)

   <span class="comments">; Strip the last directory separator.</span>

length = StrLen(thePath)
thePath = StrMid(thePath, 0, length-1)

   <span class="comments">; Inspect the file name to make sure it is valid.</span>

textSelection = Widget_Info(self.filetextID, /Text_Select)
filename = self->Inspect_Filename(filename, textSelection[0])

   <span class="comments">; Update the display.</span>

self->SetProperty, Filename=theFilename, DirectoryName=thePath

END <span class="comments">;-----------------------------------------------------------------------------------------------------------------------------</span>



PRO FSC_FileSelect::SetFilename, theName

<span class="comments">; This method separates the filename into directory and filename</span>
<span class="comments">; and adds them to the interface.</span>

Catch, theError
IF theError NE 0 THEN BEGIN
   ok = Error_Message()
   RETURN
ENDIF

<span class="comments">; Change back slashes to forward slashes.</span>
<span class="comments">;;theName = StrJoin( StrSplit(theName, '\\', /Regex, /Extract, /Preserve_Null), '/')</span>

   <span class="comments">; Get the correct directory separator.</span>

CASE StrUpCase(!Version.OS_Family) OF
   'MACOS'   : sep = ':'    <span class="comments">; Macintoshes</span>
   'VMS'     : sep = ']'    <span class="comments">; VMS machines</span>
   ELSE      : sep = '/'    <span class="comments">; Unix machines</span>
ENDCASE

index = FSC_FileSelect_RStrPos(theName, sep)
IF index EQ -1 THEN BEGIN
   self->SetProperty, Filename=theName
   self.filename = theName
ENDIF ELSE BEGIN
   directoryName = StrMid(theName, 0, index)
   filename = StrMid(theName, index+1)
   self->SetProperty, Filename=filename, Directory=directoryName
   self.filename = filename
   self.directoryName = directoryName
ENDELSE
END <span class="comments">;-------------------------------------------------------------------------------</span>



PRO FSC_FileSelect::SetProperty, $
   DirectoryName=dirname, $        <span class="comments">; The initial name of the directory. By defaut: current directory.</span>
   Event_Func=event_func, $        <span class="comments">; The event handler function for this compound widget.</span>
   Event_Pro=event_pro, $          <span class="comments">; The event handler procedure for this compound widget.</span>
   Filename=filename, $            <span class="comments">; The initial file name in the filename text widget.</span>
   Filter=filter, $                <span class="comments">; The file filter.</span>
   LabelName=labelname, $          <span class="comments">; The text on the label widgt.</span>
   LabelSize=labelsize, $          <span class="comments">; The X screen size of the label widget.</span>
   MustExist=mustexist, $          <span class="comments">; A flag that indicates selected files must exist.</span>
   Read=read, $                    <span class="comments">; Set this keyword to have file selection for reading a file.</span>
   SelectTitle=selecttitle, $      <span class="comments">; The title bar text on the file selection dialog.</span>
   UValue=uvalue, $                <span class="comments">; User value for any purpose.</span>
   Write=write, $                  <span class="comments">; Set this keyword to open a file for writing.</span>
   XSize=xsize                     <span class="comments">; The X size of the text widget holding the filename.</span>

Catch, theError
IF theError NE 0 THEN BEGIN
   ok = Error_Message()
   RETURN
ENDIF

IF N_Elements(dirname) NE 0 THEN BEGIN
   <span class="comments">;;dirname = StrJoin( StrSplit(dirname, '\\', /Regex, /Extract, /Preserve_Null), '/')</span>
   self.directoryname = dirname
   Widget_Control, self.dirtextID, Set_Value=dirname
ENDIF

IF N_Elements(event_pro) NE 0 THEN BEGIN
   self.event_pro = event_pro
   Widget_Control, self.tlb, Event_Pro=event_pro
ENDIF

IF N_Elements(event_func) NE 0 THEN BEGIN
   self.event_func = event_func
   Widget_Control, self.tlb, Event_Func=event_func
ENDIF

IF N_Elements(filename) NE 0 THEN BEGIN
   self.filename = filename
   strlength = StrLen(filename)
   Widget_Control, self.filetextID, Set_Value=filename
ENDIF

IF N_Elements(labelname) NE 0 THEN BEGIN
   self.labelname = labelname
   Widget_Control, self.filelabelID, Set_Value=labelname
ENDIF

IF N_Elements(labelsize) NE 0 THEN BEGIN
   Widget_Control, self.filelabelID, Scr_XSize=labelsize
ENDIF

IF N_Elements(filter) NE 0 THEN *self.filter = filter
IF N_Elements(mustexist) NE 0 THEN self.mustexist = mustexist
IF N_Elements(read) NE 0 THEN self.read = read
IF N_Elements(selecttitle) NE 0 THEN self.selecttitle = selecttitle
IF N_Elements(uvalue) NE 0 THEN *self.uvalue = uvalue
IF N_Elements(write) NE 0 THEN self.write = write

IF N_Elements(xsize) NE 0 THEN BEGIN
   Widget_Control, self.filetextID, XSize=xsize
   Widget_Control, self.dirtextID, XSize=xsize
ENDIF

END <span class="comments">;-----------------------------------------------------------------------------------------------------------------------------</span>



PRO FSC_FileSelect::TextSelect, selection

Catch, theError
IF theError NE 0 THEN BEGIN
   ok = Error_Message()
   RETURN
ENDIF

IF N_Elements(selection) NE 2 THEN Message, 'Text selection array is not a two-element array: [beginSelection, selectionLength]'
Widget_Control, self.fileTextID, Set_Text_Select=selection

END <span class="comments">;-----------------------------------------------------------------------------------------------------------------------------</span>



PRO FSC_FileSelect::CLEANUP
Ptr_Free, self.uvalue
Ptr_Free, self.filter
END <span class="comments">;-----------------------------------------------------------------------------------------------------------------------------</span>



FUNCTION FSC_FileSelect::INIT, $
   parent, $                       <span class="comments">; The parent widget ID of the compound widget.</span>
   DirectoryName=dirname, $        <span class="comments">; The initial name of the directory. By defaut: current directory.</span>
   Event_Pro=event_pro, $          <span class="comments">; The event handler procedure for this compound widget.By default: "".</span>
   Event_Func=event_func, $        <span class="comments">; The event handler function for this compound widget. By default: "".</span>
   Filename=filename, $            <span class="comments">; The initial file name in the filename text widget.</span>
   Filter=filter, $                <span class="comments">; The file filter. By default: "*".</span>
   Frame=frame, $                  <span class="comments">; Set this keyword for a frame around the compound widget.</span>
   LabelFont=labelfont, $          <span class="comments">; The font for the label widget. By default: "".</span>
   LabelName=labelname, $          <span class="comments">; The text on the label widgt. By default: "Filename: ".</span>
   LabelSize=labelsize, $          <span class="comments">; The X screen size of the label widget. By default: 0.</span>
   MustExist=mustexist, $          <span class="comments">; A flag that indicates selected files must exist. By default: 0.</span>
   NoMaxSize=nomaxsize, $          <span class="comments">; A flag to prohibit automatica text widget sizing. By default: 0.</span>
   Read=read, $                    <span class="comments">; Set this keyword to have file selection for reading a file. By default: 1.</span>
   Scr_XSize=scr_xsize, $          <span class="comments">; The X screen size of the compound widget. By default: 0</span>
   SelectFont=selectfont, $        <span class="comments">; The font for the "Browse" button. By default: "".</span>
   Selectdir=selectdir, $          <span class="comments">; The inital directory for file and directory selections.</span>
   SelectTitle=selecttitle, $      <span class="comments">; The title bar text on the file selection dialog. By default: "Select a File...".</span>
   TextFont=textfont, $            <span class="comments">; The font for the filename text widget. By default: "".</span>
   UValue=uvalue, $                <span class="comments">; User value for any purpose.</span>
   Write=write, $                  <span class="comments">; Set this keyword to open a file for writing. By default: 0.</span>
   XSize=xsize                     <span class="comments">; The X size of the text widget holding the filename. By default: StrLen(filename) * 1.5 > 40.</span>

Catch, theError
<span class="comments">;theError = 0</span>
IF theError NE 0 THEN BEGIN
   Catch, /Cancel
   ok = Error_Message()
   RETURN, 0
ENDIF

   <span class="comments">; Populate self object.</span>

self.parent = parent
self.directoryname = dirname
self.event_pro = event_pro
self.event_func = event_func
self.filename = filename
self.filter = Ptr_New(filter)
self.frame = frame
self.labelfont = labelfont
self.labelname = labelname
self.labelsize = labelsize
self.mustexist = mustexist
self.nomaxsize = nomaxsize
self.read = read
self.selectdir = selectdir
self.selectfont = selectfont
self.selecttitle = selecttitle
self.textfont = textfont
self.uvalue = Ptr_New(uvalue)
self.write = write
self.xsize = xsize


   <span class="comments">; Build widgets.</span>

self.tlb = Widget_Base( self.parent, $
                        Event_Func = self.event_func, $
                        Event_Pro = self.event_pro, $
                        Frame = self.frame, $
                        Func_Get_Value='FSC_FileSelect_Get_Value', $
                        Pro_Set_Value='FSC_FileSelect_Set_Value', $
                        Row = 1, $
                        Base_Align_Center=1, $
                        UValue=*self.uvalue )

   <span class="comments">; Put the self object in the first child of the TLB.</span>

leftbase = Widget_Base(self.tlb, Row=2, UValue=self)

self.dirbaseID = Widget_Base(leftbase, Row=1)

self.dirlabelID = Widget_Label( self.dirbaseID, $
                             Font=self.labelfont, $
                             Scr_XSize=self.labelsize, $
                             Value='Directory: ' )


self.dirtextID = Widget_Text( self.dirbaseID, $
                           All_Events=1, $
                           Editable=1, $
                           Event_Func='FSC_FileSelect_Event_Handler', $
                           Font=self.textfont, $
                           UValue={method:'Directory_Events', object:self}, $
                           Value=self.directoryname, $
                           XSize=self.xsize, $
                           YSize=1 )


self.filebaseID = Widget_Base(leftbase, Row=1)

self.filelabelID = Widget_Label( self.filebaseID, $
                             Font=self.labelfont, $
                             Kill_Notify='FSC_FileSelect_Kill_Notify', $
                             Notify_Realize='FSC_FileSelect_Notify_Realize', $
                             Scr_XSize=self.labelsize, $
                             UValue=self, $
                             Value=self.labelname )


self.filetextID = Widget_Text( self.filebaseID, $
                           All_Events=1, $
                           Editable=1, $
                           Event_Func='FSC_FileSelect_Event_Handler', $
                           Font=self.textfont, $
                           UValue={method:'Filename_Events', object:self}, $
                           Value=self.filename, $
                           XSize=self.xsize, $
                           YSize=1 )

rightbase = Widget_Base(self.tlb, Row=1, Base_Align_Center=1)
self.filebrowseID = Widget_Button( rightbase, $
                               Event_Func='FSC_FileSelect_Event_Handler', $
                               UValue={method:'Select_File', object:self}, $
                               Value='Browse')


RETURN, 1
END <span class="comments">;-----------------------------------------------------------------------------------------------------------------------------</span>



PRO FSC_FileSelect__Define

   objectClass = { FSC_FileSelect, $      <span class="comments">; The object class FSC_FILESELECT.</span>
                   parent: 0L, $          <span class="comments">; The ID of the parent widget.</span>
                   tlb: 0L, $             <span class="comments">; The ID of the top-level base widget for the compound widget.</span>
                   filebaseID: 0L, $      <span class="comments">; The ID of the filename base widget.</span>
                   dirbaseID: 0L, $       <span class="comments">; The ID of the directory base widget.</span>
                   filelabelID: 0L, $     <span class="comments">; The ID of the filename label widget.</span>
                   dirlabelID: 0L, $      <span class="comments">; The ID of the directory label widget.</span>
                   filetextID: 0L, $      <span class="comments">; The ID of the filename text widget.</span>
                   dirtextID: 0L, $       <span class="comments">; The ID of the directory text widget.</span>
                   filebrowseID: 0L, $    <span class="comments">; The ID of the file browse button widget.</span>
                   dirbrowseID: 0L, $     <span class="comments">; Unused currently. Left here for compatibility reasons..</span>
                   event_pro: "", $       <span class="comments">; The user-defined name of the event procedure for the widget.</span>
                   event_func: "", $      <span class="comments">; The user-defined name of the event function for the widget.</span>
                   filename: "", $        <span class="comments">; The current contents of the filename text widget.</span>
                   directoryname: "", $   <span class="comments">; The current contents of the directory text widget.</span>
                   frame: 0L, $           <span class="comments">; A flag to indicate a frame around the compound widget.</span>
                   filter: Ptr_New(), $   <span class="comments">; The current file filter used in file selection.</span>
                   labelfont: "", $       <span class="comments">; The font for the label widgets.</span>
                   labelname: "", $       <span class="comments">; The VALUE of the file name label widget.</span>
                   labelsize: 0L, $       <span class="comments">; The X screen size fo the label widgets.</span>
                   mustexist: 0L, $       <span class="comments">; If this field is 1, the user can only select files that exist.</span>
                   nomaxsize: 0L, $       <span class="comments">; This widget sizes itself to the largest widget in the base, unless this is set.</span>
                   read: 0L, $            <span class="comments">; Set this keyword to select a file for reading.</span>
                   selectdir: "", $       <span class="comments">; The inital directory used for file selection.</span>
                   selectfont: "", $      <span class="comments">; The font used for the Browse buttons.</span>
                   selecttitle: "", $     <span class="comments">; The text on the Dialog_Pickfile widget.</span>
                   textfont: "", $        <span class="comments">; The font used in the text widgets.</span>
                   uvalue: Ptr_New(), $   <span class="comments">; The user's user value. Unused by the program.</span>
                   write: 0L, $           <span class="comments">; Set this keyword to select a file for writing.</span>
                   xsize: 0L $            <span class="comments">; The X size of the text widgets.</span>
                 }

END <span class="comments">;-----------------------------------------------------------------------------------------------------------------------------</span>



PRO FSC_FileSelect_Set_Value, id, filename

<span class="comments">; This function sets the filename of the widget, using the</span>
<span class="comments">; traditional "Widget_Control, fieldID, Set_Value=filename" syntax.</span>
<span class="comments">; The directory will be stripped off the filename, if required.</span>

firstChild = Widget_Info(id, /Child)
Widget_Control, firstChild, Get_UValue=self
self->SetFilename,filename
END <span class="comments">;-----------------------------------------------------------------------------------------------------------------------------</span>



FUNCTION FSC_FileSelect_Get_Value, id

<span class="comments">; This function returns the filename of the widget, using the</span>
<span class="comments">; traditional "Widget_Control, fieldID, Get_Value=filename" syntax.</span>

firstChild = Widget_Info(id, /Child)
Widget_Control, firstChild, Get_UValue=self
RETURN, self->GetFilename()
END <span class="comments">;-----------------------------------------------------------------------------------------------------------------------------</span>



PRO CW_FileSelect__Define

<span class="comments">; The event structure definition. This is the event structure returned by the compound widget.</span>

   eventStructure = { CW_FileSelect, $     <span class="comments">; The name of the event structure.</span>
                      ID: 0L,  $           <span class="comments">; The ID of the top-level base of the compound widget.</span>
                      TOP: 0L, $           <span class="comments">; The ID of the top-level base.</span>
                      HANDLER: 0L, $       <span class="comments">; The ID of the event handler widget.</span>
                      Filename: "", $      <span class="comments">; The fully qualified file name.</span>
                      Basename: "", $      <span class="comments">; The base file name without directory attached.</span>
                      Directory: "" }      <span class="comments">; The file directory.</span>
END <span class="comments">;-----------------------------------------------------------------------------------------------------------------------------</span>



FUNCTION FSC_FileSelect_Event_Handler, event

<span class="comments">; The main event handler for the compound widget. It reacts</span>
<span class="comments">; to "messages" in the UValues of widgets that generate events.</span>
<span class="comments">; The messages indicate which object method to call. A message</span>
<span class="comments">; consists of an object method and the self object reference.</span>

Widget_Control, event.ID, Get_UValue=theMessage

Call_Method, theMessage.method, theMessage.object, event

   <span class="comments">; Events will be sent only if there is an assigned event handler.</span>

self = theMessage.object
self->GetProperty, Filename=filename, DirectoryName=directory, $
   Event_Pro=event_pro, Event_Func=event_func
IF event_pro NE "" OR event_func NE "" THEN BEGIN

    pseudoevent = {CW_FileSelect, self->GetTLB(), event.top, 0L, $
      Filepath(Root_Dir=directory, filename),  filename, directory}

    <span class="comments">; Don't return events from text widgets, unless the user has typed</span>
    <span class="comments">; a cariage return.</span>
    eventName = Tag_Names(event, /Structure_Name)
    IF eventName NE "" THEN BEGIN

       IF StrMid(eventName, 0, 11) EQ 'WIDGET_TEXT' THEN BEGIN
          IF (eventName EQ 'WIDGET_TEXT_CH') THEN BEGIN
            IF (event.ch EQ 10) THEN RETURN, pseudoevent ELSE RETURN, 0
          ENDIF ELSE RETURN, 0
       ENDIF
    ENDIF
    RETURN, pseudoevent
ENDIF ELSE RETURN, 0
END <span class="comments">;-----------------------------------------------------------------------------------------------------------------------------</span>


PRO FSC_FileSelect_Notify_Realize, labelID

<span class="comments">; When the compound widget is realized, make sure sizes are correct.</span>

Widget_Control, labelID, Get_UValue=self
IF self->GetNoMaxSize() THEN RETURN
self->Matchsize
END <span class="comments">;-----------------------------------------------------------------------------------------------------------------------------</span>



PRO FSC_FileSelect_Kill_Notify, labelID

<span class="comments">; When the compound widget dies, destroy the self object.</span>

Widget_Control, labelID, Get_UValue=self
Obj_Destroy, self
END <span class="comments">;-----------------------------------------------------------------------------------------------------------------------------</span>



FUNCTION FSC_FileSelect, $
   parent, $                       <span class="comments">; The parent widget ID of the compound widget.</span>
   Event_Pro=event_pro, $          <span class="comments">; The event handler procedure for this compound widget.By default: "".</span>
   Event_Func=event_func, $        <span class="comments">; The event handler function for this compound widget. By default: "".</span>
   DirectoryName=dirname, $        <span class="comments">; The initial name of the directory. By defaut: current directory.</span>
   Filename=filename, $            <span class="comments">; The initial file name in the filename text widget.</span>
   Filter=filter, $                <span class="comments">; The file filter. By default: "*".</span>
   Frame=frame, $                  <span class="comments">; Set this keyword for a frame around the compound widget.</span>
   LabelFont=labelfont, $          <span class="comments">; The font for the label widget. By default: "".</span>
   LabelName=labelname, $          <span class="comments">; The text on the label widgt. By default: "Filename: ".</span>
   LabelSize=labelsize, $          <span class="comments">; The X screen size of the label widget. By default: 0.</span>
   MustExist=mustexist, $          <span class="comments">; A flag that indicates selected files must exist. By default: 0.</span>
   NoMaxSize=nomaxsize, $          <span class="comments">; A flag to prohibit automatica text widget sizing. By default: 0.</span>
   ObjectRef=objectref, $          <span class="comments">; An output keyword containing the object reference.</span>
   Read=read, $                    <span class="comments">; Set this keyword to have file selection for reading a file. By default: 1.</span>
   SelectDirectory=selectdir, $    <span class="comments">; The default directory for file selection.</span>
   SelectFont=selectfont, $        <span class="comments">; The font for the "Browse" button. By default: "".</span>
   SelectTitle=selecttitle, $      <span class="comments">; The title bar text on the file selection dialog. By default: "Select a File...".</span>
   TextFont=textfont, $            <span class="comments">; The font for the filename text widget. By default: "".</span>
   UValue=uvalue, $                <span class="comments">; User value for any purpose.</span>
   Write=write, $                  <span class="comments">; Set this keyword to open a file for writing. By default: 0.</span>
   XSize=xsize                     <span class="comments">; The X size of the text widget holding the filename. By default: StrLen(filename) * 1.5 > 40.</span>

Catch, theError
<span class="comments">;theError = 0</span>
IF theError NE 0 THEN BEGIN
   ok = Error_Message()
   RETURN, 0
ENDIF

   <span class="comments">; Need a parent parameter.</span>

IF N_Elements(parent) EQ 0 THEN BEGIN
   ok = Dialog_Message('FSC_FILESELECT: A parent parameter is required.')
   RETURN, -1
ENDIF

   <span class="comments">; Check keyword arguments.</span>

IF N_Elements(dirname) EQ 0 THEN CD, Current=dirname

<span class="comments">; 4 Oct 2008. I don't know why this line is here, but it prevents Windows</span>
<span class="comments">; computers from starting Dialog_Pickfile in the correct directory, since it</span>
<span class="comments">; thinks the directory is not "formatted correctly". This is used throughout</span>
<span class="comments">; the code, so I have just commented all the instances out, rather than remove</span>
<span class="comments">; them. Search for "StrJoin".</span>
<span class="comments">;dirname = StrJoin( StrSplit(dirname, '\\', /Regex, /Extract, /Preserve_Null), '/')</span>
IF N_Elements(event_pro) EQ 0 THEN event_pro = ""
IF N_Elements(event_func) EQ 0 THEN event_func = ""
IF N_Elements(filename) EQ 0 THEN filename = ""
IF N_Elements(filter) EQ 0 THEN filter = '*'
IF N_Elements(Frame) EQ 0 THEN Frame = 0
IF N_Elements(labelfont) EQ 0 THEN labelfont = FSC_Fileselect_WidgetFont()
IF N_Elements(labelname) EQ 0 THEN labelname = "Filename: "
IF N_Elements(labelsize) EQ 0 THEN labelsize = 0
IF N_Elements(mustexist) EQ 0 THEN mustexist = 0
IF N_Elements(nomaxsize) EQ 0 THEN nomaxsize = 0
IF N_Elements(read) EQ 0 THEN read = 0
IF N_Elements(selectdir) EQ 0 THEN selectdir = dirname
IF N_Elements(selectfont) EQ 0 THEN selectfont = FSC_Fileselect_WidgetFont()
IF Keyword_Set(read) EQ 0 AND Keyword_Set(write) EQ 0 AND $
   N_Elements(selecttitle) EQ 0 THEN selecttitle = "Select a File..." ELSE selecttitle = ""
IF N_Elements(textfont) EQ 0 THEN textfont = FSC_Fileselect_WidgetFont()
IF N_Elements(uvalue) EQ 0 THEN uvalue = ""
IF N_Elements(write) EQ 0 THEN write = 0
IF N_Elements(xsize) EQ 0 THEN xsize = Long(StrLen(dirname) * 1.20 > 40)

   <span class="comments">; Create the underlying structure.</span>

objectref = Obj_New('FSC_FileSelect', $
   parent, $
   DirectoryName=dirname, $
   Event_Pro=event_pro, $
   Event_Func=event_func, $
   Filename=filename, $
   Filter=filter, $
   Frame=frame, $
   LabelFont=labelfont, $
   LabelName=labelname, $
   LabelSize=labelsize, $
   MustExist=mustexist, $
   NoMaxSize=nomaxsize, $
   Read=read, $
   Scr_XSize=scr_xsize, $
   SelectFont=selectfont, $
   SelectTitle=selecttitle, $
   Selectdir=selectdir, $
   TextFont=textfont, $
   UValue=uvalue, $
   Write=write, $
   XSize=xsize )

   <span class="comments">; Return the ID of the top-level base of the compound widget.</span>

  RETURN, objectref->GetTLB()
END <span class="comments">;-----------------------------------------------------------------------------------------------------------------------------</span>



PRO Example_Set_Size, event
Widget_Control, event.top, Get_UValue=theObject
theObject->MatchSize
END <span class="comments">;-----------------------------------------------------------------------------------------------------------------------------</span>



PRO Example_Set_Filter, event
Widget_Control, event.top, Get_UValue=theObject
theObject->SetProperty, Filter='*.pro', Filename='cyclone.pro'
END <span class="comments">;-----------------------------------------------------------------------------------------------------------------------------</span>



PRO Example_Set_Filename, event
Widget_Control, event.top, Get_UValue=theObject
filename = Filepath(Subdir=['examples', 'data'], 'worldelv.dat')
theObject->SetFilename, filename
END <span class="comments">;-----------------------------------------------------------------------------------------------------------------------------</span>



PRO Example_Shrink, event
Widget_Control, event.top, Get_UValue=theObject
theObject->SetProperty, XSize=40
END <span class="comments">;-----------------------------------------------------------------------------------------------------------------------------</span>



PRO Example_Print_Filename, event
Widget_Control, event.top, Get_UValue=theObject
Print, theObject->GetFilename()
END <span class="comments">;-----------------------------------------------------------------------------------------------------------------------------</span>



PRO Example_Quit, event
Widget_Control, event.top, /Destroy
END <span class="comments">;-----------------------------------------------------------------------------------------------------------------------------</span>



PRO Example, theObject

tlb = Widget_Base(Title='Exercise FSC_FILESELECT...', Column=1)
button = Widget_Button(tlb, Value='Make Compound Widget As Big As Me', $
   Event_Pro='Example_Set_Size', Scr_XSize=500)
button = Widget_Button(tlb, Value='Set Filename to worldelv.data in Data Directory', $
   Event_Pro='Example_Set_Filename')
button = Widget_Button(tlb, Value='Print Filename', $
   Event_Pro='Example_Print_Filename')
button = Widget_Button(tlb, Value="Shrink the Text Fields", $
   Event_Pro='Example_Shrink')
CD, Current=thisDir
filenameID = FSC_FileSelect(tlb, Directory=thisDir, Filename='fileselect.pro', $
   /NoMaxSize, ObjectRef=theObject)
button = Widget_Button(tlb, Value='Quit', Event_Pro='Example_Quit')
Widget_Control, tlb, /Realize, Set_UValue=theObject
XManager, 'example', tlb, /No_Block
END <span class="comments">;-----------------------------------------------------------------------------------------------------------------------------</span>

</code>
    </div>
  </body>
</html>