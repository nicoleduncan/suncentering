<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN"
 "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">

<!-- Generated by IDLdoc 3.5.1 on Mon Sep 30 16:57:40 2013 -->

<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en">
  <head>
    <meta http-equiv="Content-Type" content="text/html;charset=utf-8"/>
    <title>hcongrid.pro (Documentation for ./)</title>

    
    <link rel="stylesheet" type="text/css" media="all"
          href="../idldoc-resources/main.css" />
    <link rel="stylesheet" type="text/css" media="print"
          href="../idldoc-resources/main-print.css" />
    

    <script type="text/javascript">
      function setTitle() {
        parent.document.title="hcongrid.pro (Documentation for ./)";
      }
    </script>
  </head>

  <body onload="setTitle();" id="root">
    <div class="content">
      <code class="source"><a id="hcongrid:source"></a>pro hcongrid, oldim, oldhd, newim, newhd, newx, newy, HALF_HALF = half_half, $
              INTERP=interp, OUTSIZE = outsize, CUBIC = cubic, ERRMSG = errmsg,$
	      ALT = alt
<span class="comments">;+</span>
<span class="comments">; NAME:</span>
<span class="comments">;       HCONGRID</span>
<span class="comments">; PURPOSE:</span>
<span class="comments">;       CONGRID an image and update astrometry in a FITS header</span>
<span class="comments">; EXPLANATION:</span>
<span class="comments">;       Expand or contract an image using CONGRID and update the </span>
<span class="comments">;       associated FITS header array.</span>
<span class="comments">;</span>
<span class="comments">; CALLING SEQUENCE:</span>
<span class="comments">;       HCONGRID, oldhd                       ;Update FITS header only</span>
<span class="comments">;       HCONGRID, oldim, oldhd, [ newim, newhd, newx, newy, /HALF_HALF</span>
<span class="comments">;                                 CUBIC = , INTERP=, OUTSIZE=, ERRMSG=, ALT= ]</span>
<span class="comments">;</span>
<span class="comments">; INPUTS:</span>
<span class="comments">;       OLDIM - the original image array</span>
<span class="comments">;       OLDHD - the original image FITS header, string array</span>
<span class="comments">;</span>
<span class="comments">; OPTIONAL INPUTS:</span>
<span class="comments">;       NEWX - size of the new image in the X direction</span>
<span class="comments">;       NEWY - size of the new image in the Y direction</span>
<span class="comments">;               The OUTSIZE keyword can be used instead of the </span>
<span class="comments">;               NEWX, NEWY parameters</span>
<span class="comments">;</span>
<span class="comments">; OPTIONAL OUTPUTS:</span>
<span class="comments">;       NEWIM - the image after expansion or contraction with CONGRID</span>
<span class="comments">;       NEWHD - header for newim containing updated astrometry info</span>
<span class="comments">;               If output parameters are not supplied, the program</span>
<span class="comments">;               will modify the input parameters OLDIM and OLDHD</span>
<span class="comments">;               to contain the new array and updated header.</span>
<span class="comments">;</span>
<span class="comments">; OPTIONAL KEYWORD INPUTS:</span>
<span class="comments">;      ALT - Single character 'A' through 'Z' or ' ' specifying which astrometry</span>
<span class="comments">;          system to modify in the FITS header.    The default is to use the</span>
<span class="comments">;          primary astrometry of ALT = ' '.    See Greisen and Calabretta (2002)</span>
<span class="comments">;          for information about alternate astrometry keywords.</span>

<span class="comments">;       CUBIC - If set and non-zero, then cubic interpolation is used.   Valid</span>
<span class="comments">;               ranges are  -1 &lt;= Cubic &lt; 0.   Setting /CUBIC is equivalent to</span>
<span class="comments">;               CUBIC = -1 and also equivalent to INTERP = 2.   See INTERPOLATE</span>
<span class="comments">;               for more info.    Setting CUBIC = -0.5 is recommended.</span>
<span class="comments">;       ERRMSG - If this keyword is supplied, then any error mesasges will be</span>
<span class="comments">;               returned to the user in this parameter rather than depending on</span>
<span class="comments">;               on the MESSAGE routine in IDL.   If no errors are encountered</span>
<span class="comments">;               then a null string is returned.               </span>
<span class="comments">;       /HALF_HALF - Due to edge effects, the default behaviour of CONGRID is </span>
<span class="comments">;           to introduce a slight shift in the image center.  Craig Markwardt</span>
<span class="comments">;           (http://cow.physics.wisc.edu/~craigm/idl/misc.html) has written</span>
<span class="comments">;           a modified version of CONGRID called CMCONGRID that when used with</span>
<span class="comments">;           the /HALF_HALF keyword eliminates any shift.   The use of the </span>
<span class="comments">;           /HALF keyword emulates CMCONGRID and eliminates any shift in the</span>
<span class="comments">;           image centroid. </span>
<span class="comments">;       INTERP   - 0 for nearest neighbor, 1 for bilinear interpolation</span>
<span class="comments">;               (default), 2 for cubic (=-1) interpolation.   </span>
<span class="comments">;       OUTSIZE - Two element integer vector which can be used instead of the</span>
<span class="comments">;               NEWX and NEWY parameters to specify the output image dimensions</span>
<span class="comments">; OPTIONAL KEYWORD OUTPUT:</span>
<span class="comments">;       ERRMSG - If this keyword is supplied, then any error mesasges will be</span>
<span class="comments">;               returned to the user in this parameter rather than depending on</span>
<span class="comments">;               on the MESSAGE routine in IDL.   If no errors are encountered</span>
<span class="comments">;               then a null string is returned.               </span>
<span class="comments">; PROCEDURE:</span>
<span class="comments">;       Expansion or contraction is done using the CONGRID function, unless</span>
<span class="comments">;       HALF_HALF is set. </span>
<span class="comments">;</span>
<span class="comments">;       The parameters BSCALE, NAXIS1, NAXIS2, CRPIX1, and CRPIX2 and</span>
<span class="comments">;       the CD (or CDELT) parameters are updated for the new header.</span>
<span class="comments">;</span>
<span class="comments">; NOTES:</span>
<span class="comments">;       A FITS header can be supplied as the first parameter without having</span>
<span class="comments">;       to supply an image array.   The astrometry in the FITS header will be</span>
<span class="comments">;       updated to be appropriate to the specified image size.</span>
<span class="comments">;</span>
<span class="comments">;       If the FITS header contains astrometry from a ST Guide Star image,</span>
<span class="comments">;       then the astrometry will be converted to an approximately equivalent</span>
<span class="comments">;       tangent projection before applying CONGRID.</span>
<span class="comments">; EXAMPLE:</span>
<span class="comments">;       Congrid an 512 x 512 image array IM and FITS header H to size 300 x 300</span>
<span class="comments">;       using cubic interpolation.   Use the HALF_HALF keyword to avoid </span>
<span class="comments">;       a shift of the image centroid</span>
<span class="comments">;</span>
<span class="comments">;       IDL> hcongrid, IM ,H, OUT = [300, 300], CUBIC = -0.5, /HALF</span>
<span class="comments">;</span>
<span class="comments">;       The variables IM and H will be modified to the new image size.</span>
<span class="comments">;</span>
<span class="comments">; PROCEDURES CALLED:</span>
<span class="comments">;       CHECK_FITS, CONGRID(), EXTAST, GSSS_STDAST, SXADDHIST, </span>
<span class="comments">;       SXADDPAR, SXPAR(), ZPARCHECK</span>
<span class="comments">; MODIFICATION HISTORY:</span>
<span class="comments">;       Written, Aug. 1986 W. Landsman, STI Corp.</span>
<span class="comments">;       Added interp keywords, J. Isensee, July, 1990</span>
<span class="comments">;       Add cubic interpolation W. Landsman HSTX   January 1994</span>
<span class="comments">;       Recognize a GSSS FITS header   W. Landsman   June 1994</span>
<span class="comments">;       Fix case where header but not image supplied  W. Landsman  May 1995</span>
<span class="comments">;       Remove call to SINCE_VERSION()   W. Landsman   March 1996</span>
<span class="comments">;       Assume since IDL V3.5, add CUBIC keyword      W. Landsman   March 1997</span>
<span class="comments">;       Update BSCALE even if no astrometry present   W. Landsman   May 1997</span>
<span class="comments">;       Converted to IDL V5.0   W. Landsman   September 1997</span>
<span class="comments">;       Added HALF_HALF keyword  W. Landsman  February 2000</span>
<span class="comments">;       Added ERRMSG keyword, use double precision formatting W.L.  April 2000</span>
<span class="comments">;       Recognize PC00n00m astrometry format  W. Landsman   December 2001</span>
<span class="comments">;       Now works when both /INTERP and /HALF are set W. Landsman January 2002</span>
<span class="comments">;       Fix output astrometry for non-equal plate scales for PC matrix or</span>
<span class="comments">;       CROTA2 keyword, added ALT keyword.   W. Landsman May 2005</span>
<span class="comments">;       Update distortion parameters if present  W. Landsman January 2008</span>
<span class="comments">;       Don't update BSCALE/BZERO for unsigned integer W.Landsman Mar 2008</span>
<span class="comments">;       Write CRPIX as Double precision if necessary W. Landsman Oct 2012</span>
<span class="comments">;- </span>
 On_error,2
 compile_opt idl2
 Npar = N_params()      <span class="comments">;Check # of parameters</span>

 if Npar EQ 0  then begin 
     print,'    Syntax - HCONGRID, oldim, oldhd,[ newim, newhd, newx, newy'
     print,'           ALT=, CUBIC = , INTERP =, /HALF, OUTSIZE = , ERRMSG=]'
     return
 endif

 save_err = arg_present(errmsg)
 if Npar EQ 1 then begin 

        zparcheck, 'HCONGRID', oldim, 1, 7, 1, 'Image header'
        oldhd = oldim
        xsize = sxpar( oldhd,'NAXIS1')
        ysize = sxpar( oldhd,'NAXIS2')

 endif else begin
<span class="comments">;   Check for valid 2-D image & header</span>
  check_FITS, oldim, oldhd, dimen, /NOTYPE,ERRMSG = errmsg

  if errmsg NE '' then begin
        if not save_err then message,'ERROR - ' + errmsg,/CON
        return
  endif
  if N_elements(dimen) NE 2 then begin 
      errmsg =  'Input image array must be 2-dimensional'
      if ~save_err then message,'ERROR - ' + errmsg,/CON
      return
  endif 
  xsize = dimen[0]  &  ysize = dimen[1]
 endelse
    tname = size(oldim,/tname)

 if keyword_set(CUBIC) then interp = 2
 if N_elements(interp) EQ 0 then interp = 1

 case interp of 
 0:   type = ' Nearest Neighbor Approximation'
 1:   type = ' Bilinear Interpolation'
 2:   type = ' Cubic Interpolation'
 else: begin 
       errmsg = 'Illegal value of INTERP keyword, must be 0, 1, or 2'
       if ~save_err then  message,'ERROR - ' + errmsg,/CON
       return
       end
 endcase

 if npar LT 6 then begin
    if ( N_elements(OUTSIZE) NE 2 ) then begin
      message, /INF, $
        'Original array size is '+ strn( xsize ) + ' by ' + strn(ysize)
      read,'Enter size of new image in the X direction: ',newx
      read,'Enter size of new image in the Y direction: ',newy
   endif else begin
      newx = outsize[0]
      newy = outsize[1]
   endelse 
 endif
 
 if ( xsize EQ newx ) && ( ysize EQ newy ) then begin 
       message,'Output image size equals input image size',/INF
       return
 endif

 xratio = float(newx)/xsize
 yratio = float(newy)/ysize
 lambda = yratio/xratio         <span class="comments">;Measures change in aspect ratio.</span>


 if ( npar GT 1 ) then begin

 if keyword_set(half_half) then begin
   srx = (findgen(newx) + 0.5)/xratio - 0.5
   sry = (findgen(newy) + 0.5)/yratio - 0.5
   if interp GT 0 then begin
      if ( npar GT 2 ) then $
        newim = interpolate(oldim, srx,sry,/GRID, CUBIC = cubic) else $
        oldim = interpolate(oldim, srx,sry,/GRID, CUBIC = cubic)
   endif else begin
        xr = float(xsize)/newx  & yr = float(ysize)/newy
        if (npar GT 2) then $
        newim = POLY_2D(oldim, [[xr/2.,0],[xr,0]], $
                            [ [xr/2.,yr],[0,0] ],0,newx,newy) else $
        oldim = POLY_2D(oldim, [[yr/2.,0],[yr,0] ], $
                            [[ yr/2.,yr],[0,0] ],0,newx,newy) 
   endelse
 endif else begin
 
 if ( npar GT 2 ) then $
      newim = congrid( oldim, newx, newy, INTERP = interp, CUBIC = cubic) else $
      oldim = congrid( temporary(oldim), newx, newy, $
                CUBIC = cubic, INTERP=interp )
 endelse

 endif

 newhd = oldhd
 sxaddpar, newhd, 'NAXIS1', fix(newx)
 sxaddpar, newhd, 'NAXIS2', fix(newy)
 label = 'HCONGRID:' + strmid(systime(),4,20)
 history =   ' Original Image Size Was '+ strn(xsize) + ' by ' + strn(ysize)
 sxaddhist, label + history, newhd
 if npar GT 1 then sxaddhist, label+type, newhd

<span class="comments">; Update astrometry info if it exists</span>

 extast, newhd ,astr, noparams, ALT = alt
 if noparams GE 0 then begin
 if strmid(astr.ctype[0],5,3) EQ 'GSS' then begin
        gsss_stdast, newhd
        extast, newhd, astr, noparams
 endif

 pix_ratio = xratio*yratio      <span class="comments">;Ratio of pixel areas</span>
 
  crpix = astr.crpix - 1.0
  
 if keyword_set(half_half) then begin
     sxaddpar, newhd, 'CRPIX1' + alt, $
                      (crpix[0]+0.5)*xratio + 0.5
     sxaddpar, newhd, 'CRPIX2' + alt,  $
                       (crpix[1]+0.5)*yratio + 0.5
 endif else begin 
     sxaddpar, newhd, 'CRPIX1' + alt , crpix[0]*xratio + 1.0
     sxaddpar, newhd, 'CRPIX2' + alt , crpix[1]*yratio + 1.0
 endelse 


 if tag_exist(astr,'DISTORT') then begin
         distort = astr.distort
	 message,'Updating SIP distortion parameters',/INF
         update_distort,distort, [1./xratio,0],[1./yratio,0]
	 astr.distort= distort
	 add_distort, newhd, astr
   endif	 



 if (noparams NE 2) then begin 

    cdelt = astr.cdelt
    sxaddpar, newhd, 'CDELT1' + alt , CDELT[0]/xratio
    sxaddpar, newhd, 'CDELT2' + alt , CDELT[1]/yratio
<span class="comments">; Adjust the PC matrix if non-equal plate scales.   See equation 187 in </span>
<span class="comments">; Calabretta & Greisen (2002)</span>
    if lambda NE 1.0 then begin
        cd = astr.cd
	if noparams EQ 1 then begin
<span class="comments">;Can no longer  use the simple CROTA2 convention, change to PC keywords</span>
	 sxaddpar,newhd,'PC1_1'+alt, cd[0,0] 
	 sxaddpar, newhd,'PC2_2'+alt, cd[1,1]
         sxdelpar, newhd, ['CROTA2','CROTA1']
        endif	
        sxaddpar, newhd, 'PC1_2'+alt, cd[0,1]/lambda
        sxaddpar, newhd, 'PC2_1'+alt, cd[1,0]*lambda
   endif	


 endif else begin

    cd = astr.cd
    sxaddpar, newhd, 'CD1_1' + alt, cd[0,0]/xratio
    sxaddpar, newhd, 'CD1_2' + alt, cd[0,1]/yratio
    sxaddpar, newhd, 'CD2_1' + alt, cd[1,0]/xratio
    sxaddpar, newhd, 'CD2_2' + alt , cd[1,1]/yratio

 endelse
 endif 

<span class="comments">; Adjust BZERO and BSCALE for new pixel size, unless these values are used</span>
<span class="comments">; to define unsigned integer data types.  </span>

 bscale = sxpar( oldhd, 'BSCALE')
 bzero = sxpar( oldhd, 'BZERO')
 unsgn = (tname EQ 'UINT') || (tname EQ 'ULONG') 

 if ~unsgn then begin 
 if (bscale NE 0) && (bscale NE 1) then $
    sxaddpar, newhd, 'BSCALE', bscale/pix_ratio, 'Calibration Factor'
 if (bzero NE 0) then sxaddpar, newhd, 'BZERO', bzero/pix_ratio, $
       ' Additive Constant for Calibration'
 endif 

 if npar EQ 2 then oldhd = newhd else $
       if npar EQ 1 then oldim = newhd


 return
 end
</code>
    </div>
  </body>
</html>