<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN"
 "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">

<!-- Generated by IDLdoc 3.5.1 on Mon Sep 30 16:57:54 2013 -->

<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en">
  <head>
    <meta http-equiv="Content-Type" content="text/html;charset=utf-8"/>
    <title>nutate.pro (Documentation for ./)</title>

    
    <link rel="stylesheet" type="text/css" media="all"
          href="../idldoc-resources/main.css" />
    <link rel="stylesheet" type="text/css" media="print"
          href="../idldoc-resources/main-print.css" />
    

    <script type="text/javascript">
      function setTitle() {
        parent.document.title="nutate.pro (Documentation for ./)";
      }
    </script>
  </head>

  <body onload="setTitle();" id="root">
    <div class="content">
      <code class="source"><a id="nutate:source"></a>pro nutate, jd, nut_long, nut_obliq
<span class="comments">;+</span>
<span class="comments">; NAME:</span>
<span class="comments">;       NUTATE</span>
<span class="comments">; PURPOSE:</span>
<span class="comments">;       Return the nutation in longitude and obliquity for a given Julian date</span>
<span class="comments">;</span>
<span class="comments">; CALLING SEQUENCE:</span>
<span class="comments">;       NUTATE, jd, Nut_long, Nut_obliq</span>
<span class="comments">;</span>
<span class="comments">; INPUT:</span>
<span class="comments">;       jd - Julian ephemeris date, scalar or vector, double precision  </span>
<span class="comments">; OUTPUT:</span>
<span class="comments">;       Nut_long - the nutation in longitude, same # of elements as jd</span>
<span class="comments">;       Nut_obliq - nutation in latitude, same # of elements as jd</span>
<span class="comments">;</span>
<span class="comments">; EXAMPLE:</span>
<span class="comments">;       (1) Find the nutation in longitude and obliquity 1987 on Apr 10 at Oh.</span>
<span class="comments">;              This is example 22.a from Meeus</span>
<span class="comments">;        IDL> jdcnv,1987,4,10,0,jul</span>
<span class="comments">;        IDL> nutate, jul, nut_long, nut_obliq</span>
<span class="comments">;             ==> nut_long = -3.788    nut_obliq = 9.443</span>
<span class="comments">;            </span>
<span class="comments">;       (2) Plot the large-scale variation of the nutation in longitude </span>
<span class="comments">;               during the 20th century</span>
<span class="comments">;</span>
<span class="comments">;       IDL> yr = 1900 + indgen(100)     ;Compute once a year        </span>
<span class="comments">;       IDL> jdcnv,yr,1,1,0,jul          ;Find Julian date of first day of year</span>
<span class="comments">;       IDL> nutate,jul, nut_long        ;Nutation in longitude</span>
<span class="comments">;       IDL> plot, yr, nut_long</span>
<span class="comments">;</span>
<span class="comments">;       This plot will reveal the dominant (18.6 year) period, but a finer</span>
<span class="comments">;       grid is needed to display the shorter periods in the nutation.</span>
<span class="comments">; METHOD:</span>
<span class="comments">;       Uses the formula in Chapter 22 of ``Astronomical Algorithms'' by Jean </span>
<span class="comments">;       Meeus (1998, 2nd ed.) which is based on the 1980 IAU Theory of Nutation</span>
<span class="comments">;       and includes all terms larger than 0.0003".</span>
<span class="comments">;</span>
<span class="comments">; PROCEDURES CALLED:</span>
<span class="comments">;       POLY()                       (from IDL User's Library)</span>
<span class="comments">;       CIRRANGE, ISARRAY()          (from IDL Astronomy Library)</span>
<span class="comments">;</span>
<span class="comments">; REVISION HISTORY:</span>
<span class="comments">;       Written, W.Landsman (Goddard/HSTX)      June 1996       </span>
<span class="comments">;       Converted to IDL V5.0   W. Landsman   September 1997</span>
<span class="comments">;       Corrected minor typos in values of d_lng W. Landsman  December 2000</span>
<span class="comments">;       Updated typo in cdelt term              December 2000</span>
<span class="comments">;       Avoid overflow for more than 32767 input dates W. Landsman January 2005</span>
<span class="comments">;-</span>
 compile_opt idl2
 On_error,2
 
 if N_params() LT 2 then begin
        print,'Syntax - NUTATE, jd, nut_long, nut_obliq'
        return
 endif

 dtor = !DPI/180.0d
 <span class="comments">;  form time in Julian centuries from 1900.0</span>

 t = (jd[*] - 2451545.0d)/36525.0d0


<span class="comments">; Mean elongation of the Moon</span>

   coeff1 = [297.85036d,  445267.111480d, -0.0019142, 1.d/189474d0 ]
  d = poly(T, coeff1)*dtor
  cirrange,d,/rad

<span class="comments">; Sun's mean anomaly</span>

   coeff2 = [357.52772d, 35999.050340d, -0.0001603d, -1.d/3d5 ]
   M = poly(T,coeff2)*dtor
   cirrange, M,/rad

<span class="comments">; Moon's mean anomaly</span>

   coeff3 = [134.96298d, 477198.867398d, 0.0086972d, 1.0/5.625d4 ]
   Mprime = poly(T,coeff3)*dtor
   cirrange, Mprime,/rad

<span class="comments">; Moon's argument of latitude</span>

    coeff4 = [93.27191d, 483202.017538d, -0.0036825, -1.0d/3.27270d5 ]
    F = poly(T, coeff4 )*dtor 
    cirrange, F,/RAD

<span class="comments">; Longitude of the ascending node of the Moon's mean orbit on the ecliptic,</span>
<span class="comments">;  measured from the mean equinox of the date</span>

  coeff5 = [125.04452d, -1934.136261d, 0.0020708d, 1.d/4.5d5]
  omega = poly(T, coeff5)*dtor
  cirrange,omega,/RAD

 d_lng = [0,-2,0,0,0,0,-2,0,0,-2,-2,-2,0,2,0,2,0,0,-2,0,2,0,0,-2,0,-2,0,0,2,$
   -2,0,-2,0,0,2,2,0,-2,0,2,2,-2,-2,2,2,0,-2,-2,0,-2,-2,0,-1,-2,1,0,0,-1,0,0, $
     2,0,2]

 m_lng = [0,0,0,0,1,0,1,0,0,-1,intarr(17),2,0,2,1,0,-1,0,0,0,1,1,-1,0, $
  0,0,0,0,0,-1,-1,0,0,0,1,0,0,1,0,0,0,-1,1,-1,-1,0,-1]

 mp_lng = [0,0,0,0,0,1,0,0,1,0,1,0,-1,0,1,-1,-1,1,2,-2,0,2,2,1,0,0,-1,0,-1, $
   0,0,1,0,2,-1,1,0,1,0,0,1,2,1,-2,0,1,0,0,2,2,0,1,1,0,0,1,-2,1,1,1,-1,3,0]

 f_lng = [0,2,2,0,0,0,2,2,2,2,0,2,2,0,0,2,0,2,0,2,2,2,0,2,2,2,2,0,0,2,0,0, $
   0,-2,2,2,2,0,2,2,0,2,2,0,0,0,2,0,2,0,2,-2,0,0,0,2,2,0,0,2,2,2,2]

 om_lng = [1,2,2,2,0,0,2,1,2,2,0,1,2,0,1,2,1,1,0,1,2,2,0,2,0,0,1,0,1,2,1, $
   1,1,0,1,2,2,0,2,1,0,2,1,1,1,0,1,1,1,1,1,0,0,0,0,0,2,0,0,2,2,2,2]

 sin_lng = [-171996, -13187, -2274, 2062, 1426, 712, -517, -386, -301, 217, $
    -158, 129, 123, 63, 63, -59, -58, -51, 48, 46, -38, -31, 29, 29, 26, -22, $
     21, 17, 16, -16, -15, -13, -12, 11, -10, -8, 7, -7, -7, -7, $
     6,6,6,-6,-6,5,-5,-5,-5,4,4,4,-4,-4,-4,3,-3,-3,-3,-3,-3,-3,-3 ]
 
 sdelt = [-174.2, -1.6, -0.2, 0.2, -3.4, 0.1, 1.2, -0.4, 0, -0.5, 0, 0.1, $
     0,0,0.1, 0,-0.1,dblarr(10), -0.1, 0, 0.1, dblarr(33) ] 


 cos_lng = [ 92025, 5736, 977, -895, 54, -7, 224, 200, 129, -95,0,-70,-53,0, $
    -33, 26, 32, 27, 0, -24, 16,13,0,-12,0,0,-10,0,-8,7,9,7,6,0,5,3,-3,0,3,3,$
     0,-3,-3,3,3,0,3,3,3, intarr(14) ]

 cdelt = [8.9, -3.1, -0.5, 0.5, -0.1, 0.0, -0.6, 0.0, -0.1, 0.3, dblarr(53) ]


<span class="comments">; Sum the periodic terms </span>

 n = N_elements(jd)
 nut_long = dblarr(n)
 nut_obliq = dblarr(n)
 arg = d_lng#d + m_lng#m +mp_lng#mprime + f_lng#f +om_lng#omega
 sarg = sin(arg)
 carg = cos(arg)
 for i=0L,n-1 do begin
        nut_long[i] =  0.0001d*total( (sdelt*t[i] + sin_lng)*sarg[*,i] )
        nut_obliq[i] = 0.0001d*total( (cdelt*t[i] + cos_lng)*carg[*,i] )
 end
 if ~isarray(jd) then begin
        nut_long = nut_long[0]
        nut_obliq = nut_obliq[0]
 endif

 return
 end
</code>
    </div>
  </body>
</html>