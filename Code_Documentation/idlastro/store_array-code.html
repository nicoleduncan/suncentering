<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN"
 "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">

<!-- Generated by IDLdoc 3.5.1 on Mon Sep 30 16:58:02 2013 -->

<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en">
  <head>
    <meta http-equiv="Content-Type" content="text/html;charset=utf-8"/>
    <title>store_array.pro (Documentation for ./)</title>

    
    <link rel="stylesheet" type="text/css" media="all"
          href="../idldoc-resources/main.css" />
    <link rel="stylesheet" type="text/css" media="print"
          href="../idldoc-resources/main-print.css" />
    

    <script type="text/javascript">
      function setTitle() {
        parent.document.title="store_array.pro (Documentation for ./)";
      }
    </script>
  </head>

  <body onload="setTitle();" id="root">
    <div class="content">
      <code class="source">	PRO STORE_ARRAY, DESTINATION, INSERT, INDEX
<span class="comments">;+</span>
<span class="comments">; NAME:</span>
<span class="comments">;	STORE_ARRAY</span>
<span class="comments">; PURPOSE:</span>
<span class="comments">;	Insert array INSERT into the array DESTINATION</span>
<span class="comments">; EXPLANATION:</span>
<span class="comments">;	The dimensions of the DESTINATION array are adjusted to accommodate</span>
<span class="comments">;	the inserted array.</span>
<span class="comments">; CATEGOBY:</span>
<span class="comments">;	Utility</span>
<span class="comments">; CALLING SEQUENCE:</span>
<span class="comments">;	STORE_ARRAY, DESTINATION, INSERT, INDEX</span>
<span class="comments">; INPUT:</span>
<span class="comments">;	DESTINATION	= Array to be expanded.</span>
<span class="comments">;	INSERT		= Array to insert into DESTINATION.</span>
<span class="comments">;	INDEX		= Index of the final dimension of DESTINATION to insert</span>
<span class="comments">;			  INSERT into.</span>
<span class="comments">; OUTPUTS:</span>
<span class="comments">;	DESTINATION	= Expanded output array.  If both input arrays have the</span>
<span class="comments">;			  same number of dimensions, then the DESTINATION will</span>
<span class="comments">;			  be replaced with INSERT.</span>
<span class="comments">; RESTRICTIONS:</span>
<span class="comments">;	DESTINATION and INSERT have to be either both of type string or both of</span>
<span class="comments">;	numerical types.</span>
<span class="comments">;</span>
<span class="comments">;	INSERT must not have more dimensions than DESTINATION.</span>
<span class="comments">;</span>
<span class="comments">; MODIFICATION HISTOBY:</span>
<span class="comments">;	William Thompson, Feb. 1992, from BOOST_ARRAY by D. Zarro and P. Hick.</span>
<span class="comments">;	Converted to IDL V5.0   W. Landsman   September 1997</span>
<span class="comments">;-</span>
<span class="comments">;</span>
	ON_ERROR, 2			<span class="comments">;On error, return to caller</span>
<span class="comments">;</span>
<span class="comments">;  Check the number of parameters.</span>
<span class="comments">;</span>
	IF N_PARAMS() NE 3 THEN MESSAGE,	$
		'Syntax:  STORE_ARRAY, DESTINATION, INSERT, INDEX'
<span class="comments">;</span>
<span class="comments">;  Make sure everything is defined.</span>
<span class="comments">;</span>
	IF N_ELEMENTS(INSERT) EQ 0 THEN MESSAGE,'INSERT not defined'
	IF N_ELEMENTS(INDEX) EQ 0 THEN MESSAGE,'INDEX not defined'
<span class="comments">;</span>
<span class="comments">;  If DESTINATION is not defined, then set it equal to INSERT.</span>
<span class="comments">;</span>
	IF N_ELEMENTS(DESTINATION) EQ 0 THEN BEGIN
		DESTINATION = INSERT
		RETURN
	ENDIF
<span class="comments">;</span>
<span class="comments">;  Get the array types and dimensions of DESTINATION and INSERT.</span>
<span class="comments">;</span>
	SD = SIZE(DESTINATION)
	SA = SIZE(INSERT)
	D_NDIM = SD[0]
	A_NDIM = SA[0]
	IF D_NDIM EQ 0 THEN D_DIM = 1 ELSE D_DIM = SD[1:D_NDIM]
	IF A_NDIM EQ 0 THEN A_DIM = 1 ELSE A_DIM = SA[1:A_NDIM]
	D_TYPE = SD[N_ELEMENTS(SD)-2]
	A_TYPE = SA[N_ELEMENTS(SA)-2]
<span class="comments">;</span>
<span class="comments">;  Treat scalars as one-dimensional arrays.</span>
<span class="comments">;</span>
	D_NDIM = D_NDIM > 1
	A_NDIM = A_NDIM > 1
<span class="comments">; </span>
<span class="comments">;  Check to see if both arrays are of type string or numeric.</span>
<span class="comments">;</span>
	IF D_TYPE EQ 7 THEN D_STRING = 1  ELSE D_STRING = 0
	IF A_TYPE EQ 7 THEN A_STRING = 1  ELSE A_STRING = 0
	IF D_STRING NE A_STRING THEN MESSAGE,	$
		'Data arrays should be either both string or both non-string'
<span class="comments">;</span>
<span class="comments">;  If both arrays have the same number of elements, then replace DESTINATION</span>
<span class="comments">;  with INSERT.</span>
<span class="comments">;</span>
	IF D_NDIM EQ A_NDIM THEN BEGIN
		DESTINATION = INSERT
		RETURN
<span class="comments">;</span>
<span class="comments">;  Otherwise, make sure that INSERT has fewer dimensions than DESTINATION.</span>
<span class="comments">;</span>
	END ELSE IF D_NDIM LT A_NDIM THEN MESSAGE,	$
		'INSERT has more dimensions than DESTINATION'
<span class="comments">;</span>
<span class="comments">;  Check INDEX</span>
<span class="comments">;</span>
	LAST = D_DIM[D_NDIM-1] - 1
	IF (INDEX LT 0) OR (INDEX GT LAST) THEN MESSAGE,	$
		'INDEX must be between 0 and ' + STRTRIM(LAST,2)
<span class="comments">;</span>
<span class="comments">;  Merge the dimensions of DESTINATION and INSERT.</span>
<span class="comments">;</span>
	R_DIM = D_DIM
	FOR I = 0,A_NDIM-1 DO R_DIM[I] = D_DIM[I] > A_DIM[I]
<span class="comments">;</span>
<span class="comments">;  Create the output array with the correct number of elements, and the greater</span>
<span class="comments">;  of the types of DESTINATION and INSERT.</span>
<span class="comments">;</span>
	OUTPUT = MAKE_ARRAY(DIMENSION=R_DIM, TYPE=(D_TYPE > A_TYPE))
	R_NDIM = N_ELEMENTS(R_DIM)
<span class="comments">;</span>
<span class="comments">;  If INDEX is not zero, then store the first part of DESTINATION in the output</span>
<span class="comments">;  array.</span>
<span class="comments">;</span>
	IF INDEX NE 0 THEN BEGIN
	    K = INDEX - 1
	    CASE R_NDIM OF
	    	2:  OUTPUT[0,0] = DESTINATION[*,0:K]
	    	3:  OUTPUT[0,0,0] = DESTINATION[*,*,0:K]
	    	4:  OUTPUT[0,0,0,0] = DESTINATION[*,*,*,0:K]
	    	5:  OUTPUT[0,0,0,0,0] = DESTINATION[*,*,*,*,0:K]
	    	6:  OUTPUT[0,0,0,0,0,0] = DESTINATION[*,*,*,*,*,0:K]
	    	7:  OUTPUT[0,0,0,0,0,0,0] = DESTINATION[*,*,*,*,*,*,0:K]
	    ENDCASE
	ENDIF
<span class="comments">;</span>
<span class="comments">;  Add INSERT.</span>
<span class="comments">;</span>
	CASE R_NDIM OF
		2:  OUTPUT[0,INDEX] = INSERT
		3:  OUTPUT[0,0,INDEX] = INSERT
		4:  OUTPUT[0,0,0,INDEX] = INSERT
		5:  OUTPUT[0,0,0,0,INDEX] = INSERT
		6:  OUTPUT[0,0,0,0,0,INDEX] = INSERT
		7:  OUTPUT[0,0,0,0,0,0,INDEX] = INSERT
	ENDCASE
<span class="comments">;</span>
<span class="comments">;  Store the remainder of DESTINATION, if any, in the output array.</span>
<span class="comments">;</span>
	IF INDEX NE LAST THEN BEGIN
	    K = INDEX + 1
	    CASE R_NDIM OF
	    	2:  OUTPUT[0,K] = DESTINATION[*,K:*]
	    	3:  OUTPUT[0,0,K] = DESTINATION[*,*,K:*]
	    	4:  OUTPUT[0,0,0,K] = DESTINATION[*,*,*,K:*]
	    	5:  OUTPUT[0,0,0,0,K] = DESTINATION[*,*,*,*,K:*]
	    	6:  OUTPUT[0,0,0,0,0,K] = DESTINATION[*,*,*,*,*,K:*]
	    	7:  OUTPUT[0,0,0,0,0,0,K] = DESTINATION[*,*,*,*,*,*,K:*]
	    ENDCASE
	ENDIF
<span class="comments">;</span>
<span class="comments">;  Replace DESTINATION with OUTPUT, and return.</span>
<span class="comments">;</span>
	DESTINATION = OUTPUT
	RETURN
	END
</code>
    </div>
  </body>
</html>