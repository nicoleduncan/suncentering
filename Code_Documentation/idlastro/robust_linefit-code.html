<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN"
 "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">

<!-- Generated by IDLdoc 3.5.1 on Mon Sep 30 16:58:01 2013 -->

<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en">
  <head>
    <meta http-equiv="Content-Type" content="text/html;charset=utf-8"/>
    <title>robust_linefit.pro (Documentation for ./)</title>

    
    <link rel="stylesheet" type="text/css" media="all"
          href="../idldoc-resources/main.css" />
    <link rel="stylesheet" type="text/css" media="print"
          href="../idldoc-resources/main-print.css" />
    

    <script type="text/javascript">
      function setTitle() {
        parent.document.title="robust_linefit.pro (Documentation for ./)";
      }
    </script>
  </head>

  <body onload="setTitle();" id="root">
    <div class="content">
      <code class="source">FUNCTION  ROBUST_LINEFIT,XIN,YIN,YFIT,SIG,SS, NUMIT=THIS_MANY, BISECT=TYPE, $
                         Bisquare_Limit=Bisquare_Limit, $
                         Close_Factor=Close_Factor
<span class="comments">;+</span>
<span class="comments">; NAME:</span>
<span class="comments">;       ROBUST_LINEFIT</span>
<span class="comments">;</span>
<span class="comments">; PURPOSE:</span>
<span class="comments">;       An outlier-resistant two-variable linear regression. </span>
<span class="comments">; EXPLANATION:</span>
<span class="comments">;       Either Y on X or, for the case in which there is no true independent </span>
<span class="comments">;       variable, the bisecting line of Y vs X and X vs Y is calculated. No </span>
<span class="comments">;       knowledge of the errors of the input points is assumed.</span>
<span class="comments">;</span>
<span class="comments">; CALLING SEQUENCE:</span>
<span class="comments">;       COEFF = ROBUST_LINEFIT( X, Y, YFIT, SIG, COEF_SIG, [ /BISECT,</span>
<span class="comments">;                       BiSquare_Limit = , Close_factor = , NumIT = ] )</span>
<span class="comments">;</span>
<span class="comments">; INPUTS:</span>
<span class="comments">;       X = Independent variable vector, floating-point or double-precision</span>
<span class="comments">;       Y = Dependent variable vector</span>
<span class="comments">;</span>
<span class="comments">; OUTPUTS:</span>
<span class="comments">;       Function result = coefficient vector. </span>
<span class="comments">;       If = 0.0 (scalar), no fit was possible.</span>
<span class="comments">;       If vector has more than 2 elements (the last=0) then the fit is dubious.</span>
<span class="comments">;</span>
<span class="comments">; OPTIONAL OUTPUT PARAMETERS:</span>
<span class="comments">;       YFIT = Vector of calculated y's</span>
<span class="comments">;       SIG  = The "standard deviation" of the fit's residuals. If BISECTOR </span>
<span class="comments">;               is set, this will be smaller by ~ sqrt(2).</span>
<span class="comments">;       COEF_SIG  = The estimated standard deviations of the coefficients. If </span>
<span class="comments">;               BISECTOR is set, however, this becomes the vector of fit </span>
<span class="comments">;               residuals measured orthogonal to the line.</span>
<span class="comments">;</span>
<span class="comments">; OPTIONAL INPUT KEYWORDS:</span>
<span class="comments">;       NUMIT = the number of iterations allowed. Default = 25</span>
<span class="comments">;       BISECT  if set, the bisector of the "Y vs X" and "X vs Y" fits is </span>
<span class="comments">;               determined.  The distance PERPENDICULAR to this line is used </span>
<span class="comments">;               in calculating weights. This is better when the uncertainties </span>
<span class="comments">;               in X and Y are comparable, so there is no true independent </span>
<span class="comments">;               variable.  Bisquare_Limit  Limit used for calculation of </span>
<span class="comments">;               bisquare weights. In units of outlier-resistant standard </span>
<span class="comments">;               deviations. Default: 6.</span>
<span class="comments">;               Smaller limit ==>more resistant, less efficient</span>
<span class="comments">; Close_Factor  - Factor used to determine when the calculation has converged.</span>
<span class="comments">;               Convergence if the computed standard deviation changes by less </span>
<span class="comments">;               than Close_Factor * ( uncertainty of the std dev of a normal</span>
<span class="comments">;               distribution ). Default: 0.03.</span>
<span class="comments">; SUBROUTINE CALLS:</span>
<span class="comments">;       ROB_CHECKFIT</span>
<span class="comments">;       ROBUST_SIGMA, to calculate a robust analog to the std. deviation</span>
<span class="comments">;</span>
<span class="comments">; PROCEDURE:</span>
<span class="comments">;       For the initial estimate, the data is sorted by X and broken into 2</span>
<span class="comments">;       groups. A line is fitted to the x and y medians of each group.</span>
<span class="comments">;       Bisquare ("Tukey's Biweight") weights are then calculated, using the </span>
<span class="comments">;       a limit of 6 outlier-resistant standard deviations.</span>
<span class="comments">;       This is done iteratively until the standard deviation changes by less </span>
<span class="comments">;       than CLOSE_ENOUGH = CLOSE_FACTOR * {uncertainty of the standard </span>
<span class="comments">;               deviation of a normal distribution}</span>
<span class="comments">;</span>
<span class="comments">; REVISION HISTORY:</span>
<span class="comments">;       Written, H. Freudenreich, STX, 4/91.</span>
<span class="comments">;       4/13/93 to return more realistic SS's HF</span>
<span class="comments">;       2/94 --more error-checking, changed convergence criterion HF</span>
<span class="comments">;       5/94 --added BISECT option. HF.</span>
<span class="comments">;       8/94 --added Close_Factor and Bisquare_Limit options  Jack Saba.</span>
<span class="comments">;       4/02 --V5.0 version, use MEDIAN(/EVEN)  W. Landsman</span>
<span class="comments">;-</span>

ON_ERROR,2

IF N_ELEMENTS(THIS_MANY) GT 0 THEN ITMAX=THIS_MANY ELSE ITMAX=25

IF ( NOT KEYWORD_SET ( Close_Factor   ) ) THEN Close_Factor = 0.03

DEL = 5.0E-07
EPS = 1.0E-20

N = N_ELEMENTS(XIN)

<span class="comments">; First, shift X and Y to their centers of gravity:</span>
 X0 = TOTAL(XIN)/N  &  Y0=TOTAL(YIN)/N
 X = XIN-X0       &  Y = YIN-Y0 

 CC=FLTARR(2)
 SS=FLTARR(2)
 SIG=0.
 YFIT=YIN
 BADFIT=0
 NGOOD=N

<span class="comments">; Make sure the independent variables are not all the same.</span>
 XRANGE=MAX(X)-MIN(X)
 AVEX= (TOTAL(ABS(X))/N) > EPS
 IF (XRANGE LT EPS) OR (XRANGE/AVEX LT DEL) THEN BEGIN
   message,'Independent variables the same. No fit possible.',/CON
   RETURN,0.
ENDIF 

<span class="comments">; First guess: </span>
LSQ=0
YP=Y
IF N GT 5 THEN BEGIN
<span class="comments">;  We divide the data into 2 groups and fit a line to their X and Y medians.</span>
   S=SORT(X) &  U=X[S]  &  V=Y[S]
   NHALF=N/2-1
   X1=MEDIAN(U[0:NHALF],/EVEN) & X2=MEDIAN(U[NHALF+1:N-1],/EVEN)
   Y1=MEDIAN(V[0:NHALF],/EVEN) & Y2=MEDIAN(V[NHALF+1:N-1],/EVEN)
   IF ABS(X2-X1) LT EPS THEN BEGIN
<span class="comments">;     The X medians are too close. Select the end-points instead.</span>
      X1=U[0]  &  X2=U[N-1]
      Y1=V[0]  &  Y2=V[N-1]
   ENDIF
   CC[1]=(Y2-Y1)/(X2-X1)  & CC[0]=Y1-CC[1]*X1
   YFIT = CC[0]+CC[1]*X
   ISTAT = ROB_CHECKFIT(YP,YFIT,EPS,DEL,  SIG,FRACDEV,NGOOD,W,S)
   IF NGOOD LT 2 THEN LSQ=1
ENDIF 
IF (LSQ EQ 1) OR (N LT 6) THEN BEGIN  <span class="comments">; Try a least-squares fit</span>
   SX=TOTAL(X) & SY=TOTAL(Y) & SXY=TOTAL(X*Y) & SXX=TOTAL(X*X) 
   D=SXX-SX*SX
   IF ABS(D) LT EPS THEN BEGIN
      PRINT,'ROBUST_LINEFIT: No fit possible.'
      RETURN,0.
   ENDIF 
   YSLOP=(SXY-SX*SY)/D      &   YYINT=(SXX*SY-SX*SXY)/D 

   IF KEYWORD_SET(TYPE) THEN BEGIN    
<span class="comments">;     Get the X vs Y line.</span>
      SYY=TOTAL(Y*Y)
      D=SYY-SY*SY
      IF ABS(D) LT EPS THEN BEGIN
         PRINT,'ROBUST_LINEFIT: No fit possible.'
         RETURN,0.
      ENDIF
      TSLOP=(SXY-SY*SX)/D   &   TYINT=(SYY*SX-SY*SXY)/D 
<span class="comments">;     Now invert it to get the form Y=a+bX:</span>
      IF ABS(TSLOP) LT EPS THEN BEGIN
         message,'No fit possible.',/CON
         RETURN,0.
      ENDIF
      XSLOP = 1./TSLOP       &   XYINT=-TYINT/TSLOP
<span class="comments">;     Now calculate the equation of the bisector of the 2 lines:</span>
      IF YSLOP GT XSLOP THEN BEGIN
         A1=YYINT  &  B1=YSLOP  &  R1=SQRT(1.+YSLOP^2)
         A2=XYINT  &  B2=XSLOP  &  R2=SQRT(1.+XSLOP^2)
      ENDIF ELSE BEGIN
         A2=YYINT  &  B2=YSLOP  &  R2=SQRT(1.+YSLOP^2)
         A1=XYINT  &  B1=XSLOP  &  R1=SQRT(1.+XSLOP^2)
      ENDELSE
      YINT = (R1*A2+R2*A1)/(R1+R2) 
      SLOP = (R1*B2+R2*B1)/(R1+R2)
<span class="comments">;     Now find the orthogonal distance to the line. Convert to normal</span>
<span class="comments">;     coordinates.</span>
      R = SQRT(1.+SLOP^2)  & IF YINT GT 0. THEN R=-R
      U1 = SLOP/R  & U2=-1./R  &  U3=YINT/R 
      YP = U1*X+U2*Y+U3  <span class="comments">; = orthog. distance to line</span>
      YFIT = FLTARR(N)   <span class="comments">; to fool ROB_CHECKFIT</span>
      SS=YP
   ENDIF ELSE BEGIN
      SLOP=YSLOP               &   YINT=YYINT
      YFIT = YINT+SLOP*X
   ENDELSE
   CC = [YINT,SLOP]
   ISTAT = ROB_CHECKFIT(YP,YFIT,EPS,DEL,  SIG,FRACDEV,NGOOD,W,S)
ENDIF

 IF ISTAT EQ 0 THEN GOTO,AFTERFIT

 IF NGOOD LT 2 THEN BEGIN
   message,'Data Dangerously Weird. Fit Questionable.',/CON
   BADFIT=1
   GOTO,AFTERFIT
ENDIF

<span class="comments">; Now iterate until the solution converges:</span>
 SIG_1= (100.*SIG) &lt<span class="comments">; 1.0E20</span>
 CLOSE_ENOUGH = Close_Factor * SQRT(.5/(N-1)) > DEL
 DIFF= 1.0E20
 NIT = 0
 WHILE( (DIFF GT CLOSE_ENOUGH) AND (NIT LT ITMAX) ) DO BEGIN
  NIT=NIT+1
  SIG_2=SIG_1
  SIG_1=SIG
  SX=TOTAL(W*X) & SY=TOTAL(W*Y) & SXY=TOTAL(W*X*Y) & SXX=TOTAL(W*X*X) 
  D=SXX-SX*SX
  IF ABS(D) LT EPS THEN BEGIN
     message,'No fit possible.',/CON
     RETURN,0.
  ENDIF 
  YSLOP = (SXY-SX*SY)/D      &   YYINT = (SXX*SY-SX*SXY)/D 
  SLOP = YSLOP               &   YINT = YYINT
  IF KEYWORD_SET(TYPE) THEN BEGIN    
<span class="comments">;    Get the X vs Y line.</span>
     SYY=TOTAL(W*Y*Y) 
     D=SYY-SY*SY
     IF ABS(D) LT EPS THEN BEGIN
        PRINT,'ROBUST_LINEFIT: No fit possible.'
        RETURN,0.
     ENDIF
     TSLOP=(SXY-SY*SX)/D   &   TYINT=(SYY*SX-SY*SXY)/D 
<span class="comments">;    Now invert it to get the form Y=a+bX:</span>
     IF ABS(TSLOP) LT EPS THEN BEGIN
        PRINT,'ROBUST_LINEFIT: No fit possible.'
        RETURN,0.
     ENDIF
     XSLOP=1./TSLOP       &   XYINT=-TYINT/TSLOP
<span class="comments">;    Now calculate the equation of the bisector of the 2 lines:</span>
     IF YSLOP GT XSLOP THEN BEGIN
        A1=YYINT  &  B1=YSLOP  &  R1=SQRT(1.+YSLOP^2)
        A2=XYINT  &  B2=XSLOP  &  R2=SQRT(1.+XSLOP^2)
     ENDIF ELSE BEGIN
        A2=YYINT  &  B2=YSLOP  &  R2=SQRT(1.+YSLOP^2)
        A1=XYINT  &  B1=XSLOP  &  R1=SQRT(1.+XSLOP^2)
     ENDELSE
     YINT=(R1*A2+R2*A1)/(R1+R2)
     SLOP=(R1*B2+R2*B1)/(R1+R2)
     R=SQRT(1.+SLOP^2)  & IF YINT GT 0. THEN R=-R
     U1=SLOP/R  & U2=-1./R  &  U3=YINT/R 
     YP=U1*X+U2*Y+U3  <span class="comments">; = orthog distance to line</span>
     YFIT=FLTARR(N) & YFIT[*]=0.
     SS=YP
  ENDIF ELSE BEGIN
     YFIT = YINT+SLOP*X
  ENDELSE
  CC=[YINT,SLOP] 
  ISTAT=ROB_CHECKFIT(YP,YFIT,EPS,DEL,  SIG,FRACDEV,NGOOD,W,S, $
                     Bisquare_Limit=Bisquare_Limit )

  IF ISTAT EQ 0 THEN GOTO,AFTERFIT
  IF NGOOD LT 2 THEN BEGIN
     PRINT,'ROBUST_LINEFIT: Data Dangerously Weird. Fit Questionable.'
     BADFIT=1
     GOTO,AFTERFIT
  ENDIF
  DIFF = (ABS(SIG_1-SIG)/SIG) &lt<span class="comments">; (ABS(SIG_2-SIG)/SIG)</span>
ENDWHILE

AFTERFIT:
<span class="comments">; Untranslate the coefficients</span>
 CC[0] = CC[0]+Y0-CC[1]*X0

IF N_PARAMS(0) GT 2 THEN YFIT = CC[0] + CC[1]*XIN
 IF KEYWORD_SET(BISECT) THEN RETURN,CC

 IF (N_PARAMS(0) GT 3) AND (SIG GT EPS) AND (NGOOD GT 2) THEN BEGIN
   <span class="comments">; Here we use an empirical formula to approximate the standard deviations</span>
   <span class="comments">; of the coefficients. They are usually accurate to ~ 25%.</span>
   SX2 = TOTAL(W*X*X) 
   UU = S*S
   DEV = YIN-YFIT
   Y0 = TOTAL( W*DEV )
   Q = WHERE(UU LE 1.0,COUNT)
   DEN1 = ABS(TOTAL( (1.-UU[Q])*(1.-5.*UU[Q]) ))
   SIG = ROBUST_SIGMA(DEV,/ZERO)
   <span class="comments">; Now empirically derived estimates of the uncertainties:</span>
   SS[0] = SIG/SQRT(DEN1)/1.105 
   SS[1] = SS[0]/SQRT(SX2)
   <span class="comments">; Take the X shift into account:</span>
   SS[0] = SQRT(SS[0]^2+X0*SS[1]^2)
 ENDIF

 IF BADFIT EQ 1 THEN CC=[CC,0.]

 RETURN,CC
 END
</code>
    </div>
  </body>
</html>