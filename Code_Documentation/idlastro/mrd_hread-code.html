<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN"
 "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">

<!-- Generated by IDLdoc 3.5.1 on Mon Sep 30 16:57:48 2013 -->

<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en">
  <head>
    <meta http-equiv="Content-Type" content="text/html;charset=utf-8"/>
    <title>mrd_hread.pro (Documentation for ./)</title>

    
    <link rel="stylesheet" type="text/css" media="all"
          href="../idldoc-resources/main.css" />
    <link rel="stylesheet" type="text/css" media="print"
          href="../idldoc-resources/main-print.css" />
    

    <script type="text/javascript">
      function setTitle() {
        parent.document.title="mrd_hread.pro (Documentation for ./)";
      }
    </script>
  </head>

  <body onload="setTitle();" id="root">
    <div class="content">
      <code class="source"><a id="mrd_hread:source"></a>pro mrd_hread, unit, header, status, SILENT = silent, FIRSTBLOCK = firstblock, $
    ERRMSG = errmsg,SKIPDATA=skipdata,NO_BADHEADER=no_badheader
<span class="comments">;+</span>
<span class="comments">; NAME: </span>
<span class="comments">;     MRD_HREAD</span>
<span class="comments">;</span>
<span class="comments">; PURPOSE: </span>
<span class="comments">;     Reads a FITS header from an opened disk file or Unix pipe</span>
<span class="comments">; EXPLANATION:</span>
<span class="comments">;     Like FXHREAD but also works with compressed Unix files</span>
<span class="comments">;</span>
<span class="comments">; CALLING SEQUENCE: </span>
<span class="comments">;     MRD_HREAD, UNIT, HEADER  [, STATUS, /SILENT, ERRMSG =, /FIRSTBLOCK ]</span>
<span class="comments">; INPUTS: </span>
<span class="comments">;     UNIT    = Logical unit number of an open FITS file</span>
<span class="comments">; OUTPUTS: </span>
<span class="comments">;     HEADER  = String array containing the FITS header.</span>
<span class="comments">; OPT. OUTPUTS: </span>
<span class="comments">;     STATUS  = Condition code giving the status of the read.  Normally, this</span>
<span class="comments">;                 is zero, but is set to -1 if an error occurs, or if the</span>
<span class="comments">;                 first byte of the header is zero (ASCII null).</span>
<span class="comments">; OPTIONAL KEYWORD INPUT:</span>
<span class="comments">;      /FIRSTBLOCK - If set, then only the first block (36 lines or less) of </span>
<span class="comments">;                the FITS header are read into the output variable.   If only</span>
<span class="comments">;                size information (e.g. BITPIX, NAXIS) is needed from the</span>
<span class="comments">;                header, then the use of this keyword can save time.  The </span>
<span class="comments">;                file pointer is still positioned at the end of the header,</span>
<span class="comments">;                even if the /FIRSTBLOCK keyword is supplied.</span>
<span class="comments">;      /SILENT - If set, then warning messages about any invalid characters in</span>
<span class="comments">;                the header are suppressed.</span>
<span class="comments">;      /SKIPDATA - If set, then the file point is positioned at the end of the</span>
<span class="comments">;                HDU after the header is read, i.e. the following data block</span>
<span class="comments">;                is skipped.   Useful, when one wants to the read the headers</span>
<span class="comments">;                of multiple extensions.</span>
<span class="comments">;      /NO_BADHEADER - if set, returns if FITS header has illegal characters</span>
<span class="comments">;                By default, MRD_HREAD replaces bad characters with blanks,</span>
<span class="comments">;                issues a warning, and continues.</span>
<span class="comments">; OPTIONAL OUTPUT PARAMETER:</span>
<span class="comments">;       ERRMSG  = If this keyword is present, then any error messages will be</span>
<span class="comments">;                 returned to the user in this parameter rather than</span>
<span class="comments">;                 depending on the MESSAGE routine in IDL.  If no errors are</span>
<span class="comments">;                 encountered, then a null string is returned.</span>
<span class="comments">; RESTRICTIONS: </span>
<span class="comments">;      The file must already be positioned at the start of the header.  It</span>
<span class="comments">;      must be a proper FITS file.</span>
<span class="comments">; SIDE EFFECTS: </span>
<span class="comments">;       The file ends by being positioned at the end of the FITS header, unless</span>
<span class="comments">;       an error occurs.</span>
<span class="comments">; REVISION HISTORY:</span>
<span class="comments">;      Written,  Thomas McGlynn                     August 1995</span>
<span class="comments">;      Modified, Thomas McGlynn		     January 1996</span>
<span class="comments">;         Changed MRD_HREAD to handle Headers which have null characters</span>
<span class="comments">;          A warning message is printed out but the program continues.</span>
<span class="comments">;          Previously MRD_HREAD would fail if the null characters were</span>
<span class="comments">;          not in the last 2880 byte block of the header.  Note that</span>
<span class="comments">;          such characters are illegal in the header but frequently</span>
<span class="comments">;          are produced by poor FITS writers.</span>
<span class="comments">;      Added /SILENT keyword   W. Landsman   December 2000</span>
<span class="comments">;      Added /FIRSTBLOCK keyword  W. Landsman   February 2003</span>
<span class="comments">;      Added ERRMSG, SKIPDATA keyword W. Landsman          April 2009</span>
<span class="comments">;      Close file unit even after error message   W.L.  October 2010</span>
<span class="comments">;      Added /NO_BADHEADER  Zarro (ADNET), January 2012</span>
<span class="comments">;-</span>
  On_error,2
  compile_opt idl2
  printerr = ~arg_present(errmsg)
  errmsg = ''

  block = string(replicate(32b, 80, 36))
		
  Nend = 0                  <span class="comments">;Signal if 'END     ' statement is found</span>
  nblock = 0

  while Nend EQ 0 do begin
		
<span class="comments">; Shouldn't get eof in middle of header.</span>
       if eof(unit) then begin
                errmsg = 'EOF encountered in middle of FITS header'
		if printerr then message,errmsg,/CON
		free_lun, unit
		status = -1
		return
		endif
		
	on_ioerror, error_return
	readu, unit, block
	on_ioerror, null

<span class="comments">; Check that there aren't improper null characters in strings that are causing </span>
<span class="comments">; them to be truncated.   Issue a warning but continue if problems are</span>
<span class="comments">; found (unless /NO_BADHEADER is set)</span>

	w = where(strlen(block) ne 80, Nbad)
	if (Nbad GT 0) then begin
                warning='Warning-Invalid characters in header'
		if ~keyword_set(SILENT) then message,warning,/INF
                if keyword_set(NO_BADHEADER) then begin
                  status=-1 & errmsg=warning & free_lun,unit & return
                endif
		block[w] = string(replicate(32b, 80))
	endif	       
	w = where(strmid(block, 0, 8) eq 'END     ', Nend)
        if nblock EQ 0 then begin
               header = Nend GT 0 ?  block[ 0:w[0] ] : block
	       nblock =1
        endif else $
	       if ~keyword_set(firstblock) then $
	         header = Nend GT 0 ? [header,block[0:w[0]]] : [header, block]
			
	endwhile
		
        if keyword_set(skipdata) then begin 
                bitpix = fxpar(header,'bitpix')
                naxis  = fxpar(header,'naxis')
                gcount = fxpar(header,'gcount') 
                if gcount eq 0 then gcount = 1
                pcount = fxpar(header,'pcount')
               
                if naxis gt 0 then begin 
                        dims = fxpar(header,'naxis*')           <span class="comments">;read dimensions</span>
  			ndata = product(dims,/integer)
                endif else ndata = 0
                
                nbytes = long64(abs(bitpix) / 8) * gcount * (pcount + ndata)
	        mrd_skip, unit, nbytes
	endif	
	status = 0
	return
error_return:
        status = -1
	errmsg = 'END Statement not found in FITS header'
        if printerr then message, 'ERROR ' + errmsg	
	return
end
			
</code>
    </div>
  </body>
</html>