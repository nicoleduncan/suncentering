<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN"
 "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">

<!-- Generated by IDLdoc 3.5.1 on Mon Sep 30 16:58:04 2013 -->

<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en">
  <head>
    <meta http-equiv="Content-Type" content="text/html;charset=utf-8"/>
    <title>t_aper.pro (Documentation for ./)</title>

    
    <link rel="stylesheet" type="text/css" media="all"
          href="../idldoc-resources/main.css" />
    <link rel="stylesheet" type="text/css" media="print"
          href="../idldoc-resources/main-print.css" />
    

    <script type="text/javascript">
      function setTitle() {
        parent.document.title="t_aper.pro (Documentation for ./)";
      }
    </script>
  </head>

  <body onload="setTitle();" id="root">
    <div class="content">
      <code class="source"><a id="t_aper:source"></a>pro t_aper,image,fitsfile,apr,skyrad,badpix,PRINT=print,SILENT=silent, $
        NEWTABLE = newtable, SETSKYVAL = setskyval,EXACT = Exact
<span class="comments">;+</span>
<span class="comments">; NAME:</span>
<span class="comments">;       T_APER</span>
<span class="comments">; PURPOSE:</span>
<span class="comments">;       Driver procedure (for APER) to compute concentric aperture photometry.</span>
<span class="comments">; EXPLANATION:</span>
<span class="comments">;       Data is read from and written to disk FITS ASCII tables.   </span>
<span class="comments">;       Part of the IDL-DAOPHOT photometry sequence</span>
<span class="comments">;</span>
<span class="comments">; CALLING SEQUENCE:</span>
<span class="comments">;       T_APER, image, fitsfile, [ apr, skyrad, badpix, PRINT=, NEWTABLE=, </span>
<span class="comments">;                       /EXACT, /SILENT, SETSKYVAL = ]</span>
<span class="comments">;</span>
<span class="comments">; INPUTS:</span>
<span class="comments">;       IMAGE   - input data array</span>
<span class="comments">;       FITSFILE  - disk FITS ASCII table name (from T_FIND).  Must contain</span>
<span class="comments">;               the keywords 'X' and 'Y' giving the centroid of the source</span>
<span class="comments">;               positions in FORTRAN (first pixel is 1) convention.   An</span>
<span class="comments">;               extension of .fit is assumed if not supplied.</span>
<span class="comments">;</span>
<span class="comments">; OPTIONAL INPUTS:</span>
<span class="comments">;       User will be prompted for the following parameters if not supplied.</span>
<span class="comments">;</span>
<span class="comments">;       APR    -  Vector of up to 12 REAL photometry aperture radii.</span>
<span class="comments">;       SKYRAD  - Two element vector giving the inner and outer radii</span>
<span class="comments">;               to be used for the sky annulus</span>
<span class="comments">;       BADPIX  - Two element vector giving the minimum and maximum</span>
<span class="comments">;               value of a good pixel (Default [-32765,32767])</span>
<span class="comments">;</span>
<span class="comments">; OPTIONAL KEYWORDS INPUTS:</span>
<span class="comments">;       /EXACT - If this keyword is set, then intersection of the circular</span>
<span class="comments">;               aperture is computed exactly (and slowly) rather than using</span>
<span class="comments">;               an approximation.   See APER for more info.</span>
<span class="comments">;       /PRINT - if set and non-zero then NSTAR will also write its results to</span>
<span class="comments">;               a file aper.prt.   One can specify a different output file </span>
<span class="comments">;               name by setting PRINT = 'filename'.</span>
<span class="comments">;       /SILENT - If this keyword is set and non-zero, then APER will not</span>
<span class="comments">;               display photometry results at the screen, and the results </span>
<span class="comments">;               will be automatically incorporated in the FITS table without</span>
<span class="comments">;               prompting the user</span>
<span class="comments">;       NEWTABLE  - Name of output disk FITS ASCII table, scalar string.   </span>
<span class="comments">;               If not supplied, then the input FITSFILE will be updated with </span>
<span class="comments">;               the aperture photometry results.</span>
<span class="comments">;       SETSKYVAL - Use this keyword to force the sky to a specified value </span>
<span class="comments">;               rather than have APER compute a sky value.    SETSKYVAL </span>
<span class="comments">;               can either be a scalar specifying the sky value to use for </span>
<span class="comments">;               all sources, or a 3 element vector specifying the sky value, </span>
<span class="comments">;               the sigma of the sky value, and the number of elements used </span>
<span class="comments">;               to compute a sky value.   The 3 element form of SETSKYVAL</span>
<span class="comments">;               is needed for accurate error budgeting.</span>
<span class="comments">;</span>
<span class="comments">; PROMPTS:</span>
<span class="comments">;       T_APER requires the number of photons per analog digital unit</span>
<span class="comments">;       (PHPADU), so that it can compute Poisson noise statistics to assign</span>
<span class="comments">;       photometry errors.    It first tries to find the PHPADU keyword in the</span>
<span class="comments">;       original image header, and if not found will look for the GAIN, </span>
<span class="comments">;       CCDGAIN and finally ATODGAIN keywords.   If still not found, T_APER </span>
<span class="comments">;       will prompt the user for this value.</span>
<span class="comments">;</span>
<span class="comments">; PROCEDURES:</span>
<span class="comments">;       APER, FTADDCOL, FTGET(), FTINFO, FTPUT, READFITS(), SXADDPAR, </span>
<span class="comments">;       SXPAR(), WRITEFITS </span>
<span class="comments">; REVISON HISTORY:</span>
<span class="comments">;       Written   W. Landsman   ST Systems Co.            May 1988</span>
<span class="comments">;       Store results as flux or magnitude                August 1988</span>
<span class="comments">;       Added SILENT keyword  W. Landsman                 Sep. 1991</span>
<span class="comments">;       Changed ERR SKY to ERR_SKY W. Landsman   March 1996</span>
<span class="comments">;       Replace TEXTOUT keyword with PRINT keyword  W. Landsman  May 1996</span>
<span class="comments">;       Check CCDGAIN or ATODGAIN keywords to find phpadu W. Landsman May 1997</span>
<span class="comments">;       Converted to IDL V5.0   W. Landsman   September 1997</span>
<span class="comments">;       Updated for new FTINFO calling sequence   W. Landsman  May 2000</span>
<span class="comments">;       Added /EXACT keyword                      W. Landsman  June 2000</span>
<span class="comments">;       </span>
<span class="comments">;-</span>
 On_error,2                         <span class="comments">;Return to caller</span>

 if N_params() LT 2 then begin
    print,'Syntax - T_APER, image, fitsfile, [ apr, skyrad, badpix'
    print,'              /EXACT, SETSKY = ,PRINT = , NEWTABLE = ,/SILENT ]'
  return
 endif

 newfile = keyword_set(NEWTABLE)
 if not keyword_set(NEWTABLE) then newtable = fitsfile

 dummy = readfits( fitsfile, hprimary, /SILENT )
 tab = readfits( fitsfile, h, /exten)

 ftinfo,h,ft_str
 ttype = strtrim(ft_str.ttype,2)
 xc = ftget( ft_str, tab, 'X' ) - 1.      <span class="comments">;Subtract to conv from FORTRAN to IDL</span>
 yc = ftget( ft_str, tab, 'Y' ) - 1.

 phpadu = sxpar( hprimary, 'PHPADU', Count = n )  <span class="comments">;Try to get photons per ADU</span>
 if n EQ 0 then begin
        phpadu = sxpar( hprimary, 'GAIN', Count  = n)
        if n EQ 0 then phpadu = sxpar( hprimary, 'CCDGAIN', Count = n)
        if n EQ 0 then phpadu = sxpar( hprimary, 'ATODGAIN', Count = n)
        if n EQ 0 then begin
        read,'Enter photons per ADU (CCD Gain):  ',phpadu
        message,'Storing photon/ADU value of ' + strtrim(phpadu,2) + $
               ' in header',/INF
       sxaddpar,hprimary,'PHPADU',phpadu,'Photons Per ADU',before = 'HISTORY'
       endif
 endif

 message,'Using photon/ADU value of ' + strtrim(phpadu,2),/INF

 aper, image, xc, yc, mags, errap, sky, skyerr, phpadu, apr, skyrad,$
     badpix, PRINT = print, SILENT=silent, SETSKYVAL = setskyval, EXACT = exact

 ans=''
 if NOT keyword_set(SILENT) and (NOT newfile) then read, $
    'T_APER: Update table with current results [Y]? ',ans

 if strupcase(ans) NE 'N' then begin   
    sxaddpar,h,'EXTNAME','IDL DAOPHOT: APER',' Last DAOPHOT step'
    sxaddpar,h,'SKYIN',skyrad[0],' Inner Sky Radius','TTYPE1' 
    sxaddpar,h,'SKYOUT',skyrad[1],' Outer Sky Radius','TTYPE1'
    sxaddpar,h,'BADPIX1',badpix[0],' Bad Pixel Value: LOW','TTYPE1'
    sxaddpar,h,'BADPIX2',badpix[1],' Bad Pixel Value: HIGH','TTYPE1'

    gsky = where(ttype EQ 'SKY', N_sky)
    if N_sky EQ 0 then ftaddcol,h,tab,'SKY',8,'F8.3'
    ftput,h,tab,'SKY',0,sky
    
    gskyerr = where(ttype EQ 'ERR_SKY', N_skyerr)
    if N_skyerr EQ 0 then ftaddcol,h,tab,'ERR_SKY',8,'F8.3'
    ftput,h,tab,'ERR_SKY',0,skyerr
    nstars = N_elements(xc)
    name = 'MAG'       &  e_name = 'ERR_AP'
    units = ' MAG'
    f_format = 'F7.3'  &  e_format ='F6.3'

    for i = 1,N_elements(apr) do begin    
       ii = strtrim(i,2)
      apsize = 'APR' + ii
      sxaddpar,h,apsize,apr[i-1],' Aperture ' + ii + ' Size','TTYPE1'
      field = 'AP' + ii + '_' + name                
      efield = e_name + ii 
      gap = where(ttype EQ field, Nap)
     
      if Nap EQ 0 then begin            <span class="comments">;Create new columns?</span>
           ftaddcol,h,tab,field,8,f_format,units
           ftaddcol,h,tab,efield,8,e_format,units
      endif
      ftput,h,tab,field,0,fltarr(nstars) + mags[i-1,*]
      ftput,h,tab,efield,0,fltarr(nstars) + errap[i-1,*]
    endfor     

    sxaddhist,'T_APER: '+ systime(),h
 endif

 writefits, newtable, 0, hprimary
 writefits, newtable, tab,h,/append

 return
 end
</code>
    </div>
  </body>
</html>