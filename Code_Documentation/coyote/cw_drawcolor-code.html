<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN"
 "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">

<!-- Generated by IDLdoc 3.5.1 on Mon Sep 30 16:56:41 2013 -->

<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en">
  <head>
    <meta http-equiv="Content-Type" content="text/html;charset=utf-8"/>
    <title>cw_drawcolor.pro (Documentation for ./)</title>

    
    <link rel="stylesheet" type="text/css" media="all"
          href="../idldoc-resources/main.css" />
    <link rel="stylesheet" type="text/css" media="print"
          href="../idldoc-resources/main-print.css" />
    

    <script type="text/javascript">
      function setTitle() {
        parent.document.title="cw_drawcolor.pro (Documentation for ./)";
      }
    </script>
  </head>

  <body onload="setTitle();" id="root">
    <div class="content">
      <code class="source"><span class="comments">;+</span>
<span class="comments">; NAME:</span>
<span class="comments">;       CW_DRAWCOLOR</span>
<span class="comments">;</span>
<span class="comments">; PURPOSE:</span>
<span class="comments">;</span>
<span class="comments">;       This compound widget is used to place a label or color name next</span>
<span class="comments">;       to a color patch. Clicking on the color patch allows the user</span>
<span class="comments">;       to select another color</span>
<span class="comments">;</span>
<span class="comments">; AUTHOR:</span>
<span class="comments">;</span>
<span class="comments">;       FANNING SOFTWARE CONSULTING</span>
<span class="comments">;       David Fanning, Ph.D.</span>
<span class="comments">;       1645 Sheely Drive</span>
<span class="comments">;       Fort Collins, CO 80526 USA</span>
<span class="comments">;       Phone: 970-221-0438</span>
<span class="comments">;       E-mail: david@idlcoyote.com</span>
<span class="comments">;       Coyote's Guide to IDL Programming: http://www.idlcoyote.com</span>
<span class="comments">;</span>
<span class="comments">; CATEGORY:</span>
<span class="comments">;</span>
<span class="comments">;       Graphics</span>
<span class="comments">;</span>
<span class="comments">; CALLING SEQUENCE:</span>
<span class="comments">;</span>
<span class="comments">;       colorpatchID = CW_DrawColor(parent)</span>
<span class="comments">;</span>
<span class="comments">; REQUIRED INPUTS:</span>
<span class="comments">;</span>
<span class="comments">;       parent - The identifier of a parent base widget.</span>
<span class="comments">;</span>
<span class="comments">; OUTPUTS:</span>
<span class="comments">;</span>
<span class="comments">;       colorpatchID - The widget identifier of the top-level base of this compound widget</span>
<span class="comments">;</span>
<span class="comments">; INPUT KEYWORDS:</span>
<span class="comments">;</span>
<span class="comments">;   COLOR - The name of the color to be displayed. Color names come from cgPickColorName.</span>
<span class="comments">;   COLUMN - Set this keyword to stack widgets in a column. Default is in a row.</span>
<span class="comments">;   EVENT_FUNC - The name of an event handler function for this compound widget.</span>
<span class="comments">;   EVENT_PRO -The name of an event handler procedure for this compound widget.</span>
<span class="comments">;   INDEX - An index number where the color should be loaded. !D.Table_Size-2, by default.</span>
<span class="comments">;   FILENAME - An optional input to cgPickColorName specifying different</span>
<span class="comments">;              colors.  See the cgPickColorName documenation for the file format.</span>
<span class="comments">;   LABEL_LEFT - Set this keyword to have the label text aligned on the left of the label. Default is to center.</span>
<span class="comments">;   LABEL_RIGHT - Set this keyword to have the label text aligned on the right of the label. Default is to center.</span>
<span class="comments">;   LABELSIZE - This is the X size of the label widget (containing the label) in device coordinates. Default is natural size.</span>
<span class="comments">;   LABELTEXT - This is the text on the label. Example, "Background Color", etc.</span>
<span class="comments">;   TITLE - This is the title on the cgPickColorName program that allows the user to select another color.</span>
<span class="comments">;   UVALUE - A user value for the widget.</span>
<span class="comments">;   XSIZE - The xsize (in pixel units) of the color patch. By default, 20.</span>
<span class="comments">;   YSIZE - The xsize (in pixel units) of the color patch. By default, 20.</span>
<span class="comments">;</span>
<span class="comments">; OUTPUT KEYWORDS:</span>
<span class="comments">;</span>
<span class="comments">;   OBJECT - The object reference. Use this to call methods, etc.</span>
<span class="comments">;</span>
<span class="comments">; OBJECT PROCEDURE METHODS:</span>
<span class="comments">;</span>
<span class="comments">;   Set_Value -- this method takes one argument, the new color name.</span>
<span class="comments">;               It will change the color of the widget if it has</span>
<span class="comments">;               already been realized.</span>
<span class="comments">;</span>
<span class="comments">;   Get_Value -- this method returns the color name the widget is displaying</span>
<span class="comments">;</span>
<span class="comments">; DEPENDENCIES:</span>
<span class="comments">;</span>
<span class="comments">;       Reqires cgColor and cgPickColorName from the Coyote Library:</span>
<span class="comments">;</span>
<span class="comments">;                     http://www.idlcoyote.com/programs/cgColor.pro</span>
<span class="comments">;                     http://www.idlcoyote.com/programs/cgPickColorName.pro</span>
<span class="comments">;</span>
<span class="comments">; MODIFICATION HISTORY:</span>
<span class="comments">;</span>
<span class="comments">;       Written by David W. Fanning, March 2001.</span>
<span class="comments">;       Fixed a problem with self object cleanup. 7 March 2006. DWF.</span>
<span class="comments">;       Allow addition to already realized widget hierarchies, October 2007. L. Anderson.</span>
<span class="comments">;       Added set_value and get_value methods to the widget can be</span>
<span class="comments">;            updated after being realized. October 2007. L. Anderson.</span>
<span class="comments">;       Added option to pass filename on to cgPickColorName. October</span>
<span class="comments">;            2007. L. Anderson</span>
<span class="comments">;-</span>
<span class="comments">;</span>
<span class="comments">;******************************************************************************************;</span>
<span class="comments">;  Copyright (c) 2008, by Fanning Software Consulting, Inc.                                ;</span>
<span class="comments">;  All rights reserved.                                                                    ;</span>
<span class="comments">;                                                                                          ;</span>
<span class="comments">;  Redistribution and use in source and binary forms, with or without                      ;</span>
<span class="comments">;  modification, are permitted provided that the following conditions are met:             ;</span>
<span class="comments">;                                                                                          ;</span>
<span class="comments">;      * Redistributions of source code must retain the above copyright                    ;</span>
<span class="comments">;        notice, this list of conditions and the following disclaimer.                     ;</span>
<span class="comments">;      * Redistributions in binary form must reproduce the above copyright                 ;</span>
<span class="comments">;        notice, this list of conditions and the following disclaimer in the               ;</span>
<span class="comments">;        documentation and/or other materials provided with the distribution.              ;</span>
<span class="comments">;      * Neither the name of Fanning Software Consulting, Inc. nor the names of its        ;</span>
<span class="comments">;        contributors may be used to endorse or promote products derived from this         ;</span>
<span class="comments">;        software without specific prior written permission.                               ;</span>
<span class="comments">;                                                                                          ;</span>
<span class="comments">;  THIS SOFTWARE IS PROVIDED BY FANNING SOFTWARE CONSULTING, INC. ''AS IS'' AND ANY        ;</span>
<span class="comments">;  EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES    ;</span>
<span class="comments">;  OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT     ;</span>
<span class="comments">;  SHALL FANNING SOFTWARE CONSULTING, INC. BE LIABLE FOR ANY DIRECT, INDIRECT,             ;</span>
<span class="comments">;  INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED    ;</span>
<span class="comments">;  TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS;         ;</span>
<span class="comments">;  LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND             ;</span>
<span class="comments">;  ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT              ;</span>
<span class="comments">;  (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS           ;</span>
<span class="comments">;  SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.                            ;</span>
<span class="comments">;******************************************************************************************;</span>
FUNCTION CW_Drawcolor_FindTLB, startID

<span class="comments">; This function traces up the widget hierarcy to find the top-level base.</span>


Catch, theError
IF theError NE 0 THEN BEGIN
   Catch, /Cancel
   RETURN, 0L
ENDIF

FORWARD_FUNCTION CW_Drawcolor_FindTLB

parent = Widget_Info(startID, /Parent)
IF parent EQ 0 THEN RETURN, startID ELSE parent = CW_Drawcolor_FindTLB(parent)
RETURN, parent
END <span class="comments">;----------------------------------------------------------------------------</span>



FUNCTION CW_Drawcolor_Error_Message, theMessage, Traceback=traceback, NoName=noName, _Extra=extra

<span class="comments">;  The standard event handler function.</span>

On_Error, 2

   <span class="comments">; Check for presence and type of message.</span>

IF N_Elements(theMessage) EQ 0 THEN theMessage = !Error_State.Msg
s = Size(theMessage)
messageType = s[s[0]+1]
IF messageType NE 7 THEN BEGIN
   Message, "The message parameter must be a string.", _Extra=extra
ENDIF

   <span class="comments">; Get the call stack and the calling routine's name.</span>

Help, Calls=callStack
callingRoutine = (Str_Sep(StrCompress(callStack[1])," "))[0]

   <span class="comments">; Are widgets supported? Doesn't matter in IDL 5.3 and higher.</span>

widgetsSupported = ((!D.Flags AND 65536L) NE 0) OR Float(!Version.Release) GE 5.3
IF widgetsSupported THEN BEGIN
   IF Keyword_Set(noName) THEN answer = Dialog_Message(theMessage, _Extra=extra) ELSE BEGIN
      IF StrUpCase(callingRoutine) EQ "$MAIN$" THEN answer = Dialog_Message(theMessage, _Extra=extra) ELSE $
         answer = Dialog_Message(StrUpCase(callingRoutine) + ": " + theMessage, _Extra=extra)
   ENDELSE
ENDIF ELSE BEGIN
      Message, theMessage, /Continue, /NoPrint, /NoName, /NoPrefix, _Extra=extra
      Print, '%' + callingRoutine + ': ' + theMessage
      answer = 'OK'
ENDELSE

   <span class="comments">; Provide traceback information if requested.</span>

IF Keyword_Set(traceback) THEN BEGIN
   Help, /Last_Message, Output=traceback
   Print,''
   Print, 'Traceback Report from ' + StrUpCase(callingRoutine) + ':'
   Print, ''
   FOR j=0,N_Elements(traceback)-1 DO Print, "     " + traceback[j]
ENDIF

RETURN, answer
END <span class="comments">;-----------------------------------------------------------------------------------------------------------------------------</span>



FUNCTION CW_DrawColor::GetTLB

<span class="comments">; This method function returns the identifier of the top-level base widget.</span>

RETURN, self.tlb
END <span class="comments">;-----------------------------------------------------------------------------------------------------------------------------</span>



PRO CW_DrawColor::Realize

<span class="comments">; This method initializes the draw widget and fills it with a color.</span>

Widget_Control, self.drawID, Get_Value=wid
self.wid = wid
currentwindow = !D.Window
WSet, wid
PolyFill, [0, 0, 1, 1, 0], [0, 1, 1, 0, 0], /Normal, Color=cgColor(self.color, self.index)
IF currentwindow NE -1 THEN WSet, currentwindow
END <span class="comments">;-----------------------------------------------------------------------------------------------------------------------------</span>



PRO CW_DrawColor_Realize, drawID
<span class="comments">; This procedure initializes the compound widget when it is realized.</span>

Widget_Control, drawID, Get_UValue=selfObj
selfObj->Realize
END <span class="comments">;-----------------------------------------------------------------------------------------------------------------------------</span>



FUNCTION CW_DrawColor_Pickcolor, event

<span class="comments">; This event handler responds to events in the color patch.</span>

   <span class="comments">; Only interested in button down events.</span>

IF event.type NE 0 THEN RETURN, 0

   <span class="comments">; Get a new color. The event structure will be modified by the</span>
   <span class="comments">; PickColor method.</span>

Widget_Control, event.id, Get_UValue=selfObj
event = selfObj->PickColor()

   <span class="comments">; Return the modified event structure.</span>

RETURN, event
END <span class="comments">;-----------------------------------------------------------------------------------------------------------------------------</span>



FUNCTION CW_DrawColor::Pickcolor

<span class="comments">; This object method allows the user to pick another color.</span>
<span class="comments">; Upon successfully picking the color, an event is sent to</span>
<span class="comments">; a specified event handler, or it is simply passed up the</span>
<span class="comments">; widget hierarchy in the normal fashion.</span>

   <span class="comments">; Get the current color.</span>

currentColor = self.color

   <span class="comments">; Allow the user to pick another color name.</span>
IF self.filename NE "" THEN BEGIN
    self.color = cgPickColorName(self.color, Group_Leader=self.tlb, Title=self.title, Filename = self.filename)
ENDIF ELSE BEGIN
    self.color = cgPickColorName(self.color, Group_Leader=self.tlb, Title=self.title)
ENDELSE
IF currentColor EQ self.color THEN RETURN, 0

   <span class="comments">; Create an event structure for this compound widget.</span>

event = { CW_DRAWCOLOR_EVENT, $
          ID: self.tlb, $
          TOP: CW_Drawcolor_FindTLB(self.parent), $
          HANDLER: 0L, $
          COLOR: self.color, $
          INDEX: self.index }

   <span class="comments">; Load the new color in the color patch.</span>

WSet, self.wid
PolyFill, [0,0,1,1,0], [0,1,1,0,0], /Normal, Color=cgColor(self.color, self.index)

   <span class="comments">; If an event handler is specified, call it.</span>

IF self.event_pro NE "" THEN BEGIN
   Call_Procedure, self.event_pro, event
   event = 0
ENDIF

IF self.event_func NE "" THEN BEGIN
   ok = Call_Function(self.event_func, event)
   event = 0
ENDIF

RETURN, event
END <span class="comments">;-----------------------------------------------------------------------------------------------------------------------------</span>



PRO CW_DrawColor_Set_Value, id, newcolor

   <span class="comments">; Get self reference</span>

drawID = Widget_Info(id, /child)
Widget_Control, drawID, Get_UValue = self

   <span class="comments">; Call method</span>

Call_Method, 'Set_Value', self, newcolor

END <span class="comments">;-----------------------------------------------------------------------------------------------------------------------------</span>



PRO CW_DrawColor::Set_Value, newcolor

   <span class="comments">; Check if the color is defined in the cgColor program</span>

IF N_Elements(newcolor) EQ 0 THEN Message, /NoName, 'Please pass a color name.'
newcolor = StrCompress(StrTrim(newcolor, 2))
IF where(StrUpCase(newcolor) EQ StrUpCase(cgColor(/Names))) EQ -1 THEN newColor = 'White'

   <span class="comments">; Store the value</span>

self.color = newColor

   <span class="comments">; Load the new color in the color patch.</span>

realized = Widget_Info(self.tlb, /Realize) <span class="comments">; Check if realized already</span>
IF realized THEN BEGIN
    WSet, self.wid
    PolyFill, [0,0,1,1,0], [0,1,1,0,0], /Normal, Color=cgColor(self.color, self.index)
ENDIF

END
<span class="comments">;-----------------------------------------------------------------------------------------------------------------------------</span>



FUNCTION CW_DrawColor_Get_Value, id

   <span class="comments">; Get self reference</span>

drawID = Widget_Info(id, /child)
Widget_Control, drawID, Get_UValue = self

   <span class="comments">; Call method</span>

RETURN, Call_Method('Get_Value', self)

END <span class="comments">;-----------------------------------------------------------------------------------------------------------------------------</span>



FUNCTION CW_DrawColor::Get_Value

RETURN, self.color
END <span class="comments">;-----------------------------------------------------------------------------------------------------------------------------</span>



FUNCTION CW_DRAWCOLOR::Init, $

<span class="comments">; The INIT method for the object. Builds the widget hierarchy.</span>

   parent, $                   <span class="comments">; The parent widget identifier for this compound widget.</span>
   Color=color, $              <span class="comments">; The name of the color to be displayed. Color names come from cgPickColorName.</span>
   Column=column, $            <span class="comments">; Set this keyword to stack widgets in a column. Default is in a row.</span>
   Event_Func=event_func, $    <span class="comments">; The name of an event handler function for this compound widget.</span>
   Event_Pro=event_pro, $      <span class="comments">; The name of an event handler procedure for this compound widget.</span>
   Filename=filename, $        <span class="comments">; An optional input to cgPickColorName specifying different colors.</span>
   Index=index, $              <span class="comments">; An index number where the color should be loaded. !D.Table_Size-2, by default.</span>
   Label_Left=label_left, $    <span class="comments">; Set this keyword to have the label text aligned on the left of the label. Default is to center.</span>
   Label_Right=label_right, $  <span class="comments">; Set this keyword to have the label text aligned on the right of the label. Default is to center.</span>
   LabelSize=labelsize, $      <span class="comments">; This is the X size of the label widget (containing the label) in device coordinates. Default is natural size.</span>
   LabelText=label, $          <span class="comments">; This is the text on the label. Example, "Background Color", etc.</span>
   Title=title, $              <span class="comments">; This is the title on the cgPickColorName program that allows the user to select another color.</span>
   UValue=uvalue, $            <span class="comments">; A user value for the widget.</span>
   XSize=xsize, $              <span class="comments">; The xsize (in pixel units) of the color patch. By default, 20.</span>
   YSize=ysize                 <span class="comments">; The xsize (in pixel units) of the color patch. By default, 20.</span>

   <span class="comments">; Error handling.</span>
Catch, theError
IF theError NE 0 THEN BEGIN
   ok = CW_Drawcolor_Error_Message(/Traceback)
   RETURN, 0
ENDIF

   <span class="comments">; Check keywords and parameters.</span>

IF N_Elements(parent) EQ 0 THEN Message, /NoName, 'Parent widget ID required.'
IF N_Elements(color) EQ 0 THEN color = 'WHITE'
IF N_Elements(event_func) EQ 0 THEN event_func = ""
IF N_Elements(event_pro) EQ 0 THEN event_pro = ""
IF N_Elements(index) EQ 0 THEN index = !D.Table_Size-2
IF N_Elements(filename) EQ 0 THEN filename = ""
IF N_Elements(label) EQ 0 THEN label = "Color"
IF N_Elements(labelsize) EQ 0 THEN labelsize = 0
IF N_Elements(title) EQ 0 THEN title=""
IF N_Elements(uvalue) EQ 0 THEN uvalue = ""
IF N_Elements(xsize) EQ 0 THEN xsize = 20
IF N_Elements(ysize) EQ 0 THEN ysize = 20
label_left = Keyword_Set(label_left)
label_right = Keyword_Set(label_right)
column = Keyword_Set(column)
IF column THEN row = 0 ELSE row = 1

   <span class="comments">; Create the widgets.</span>

tlb = Widget_Base(parent, $
   Pro_Set_Value='CW_Drawcolor_Set_Value', $
   Func_Get_Value='CW_Drawcolor_Get_Value', $
   UValue=uvalue, $
   Base_Align_Center=1 )

base = Widget_Base(tlb, Row=row, Column=column, UValue=self, $
   Base_Align_Center=1)

labelID = Widget_Label(base, Value=label, Scr_XSize=labelsize, $
   Align_Left=label_left, Align_Right=label_right)

drawID = Widget_Draw(base, XSize=xsize, YSize=xsize, Kill_Notify='CW_Drawcolor_Kill_Notify', $
                     UValue=self, Event_Func='CW_Drawcolor_PickColor', Button_Events=1, /Align_Center)

   <span class="comments">; Populate the object.</span>

self.parent = parent
self.tlb = tlb
self.labelID = labelID
self.drawID = drawid
self.color = color
self.index = index
self.filename = filename
self.title = title

<span class="comments">; Adding a notify_realize link here allows the block of code above to</span>
<span class="comments">; run, regardless of whether the parent is already realized</span>
Widget_Control, drawID, Notify_Realize='CW_Drawcolor_Realize'

<span class="comments">; Have to realize widget forcibly if parent is already realized</span>
IF Widget_Info(parent, /Realized) THEN CW_Drawcolor_Realize, drawID

RETURN, 1
END <span class="comments">;-------------------------------------------------------------------------------------</span>



PRO CW_DRAWCOLOR_Kill_Notify, drawID

<span class="comments">; Come here when draw widget dies. Get UValue (self) and destroy object.</span>
Widget_Control, drawID, Get_UValue=self
Obj_Destroy, self

END <span class="comments">;-------------------------------------------------------------------------------------</span>


PRO CW_DRAWCOLOR::Cleanup

<span class="comments">; Nothing to clean up in this Cleanup method.</span>

END <span class="comments">;-------------------------------------------------------------------------------------</span>


PRO CW_DRAWCOLOR__DEFINE

<span class="comments">; The class definition of the CW_DRAWCOLOR object.</span>

   struct = { CW_DRAWCOLOR, $    <span class="comments">; The class name.</span>
              parent: 0L, $      <span class="comments">; The identifier of the parent widget.</span>
              event_pro:"", $    <span class="comments">; The name of an event handler procedure.</span>
              event_func: "", $  <span class="comments">; The name of an event handler function.</span>
              filename: "", $    <span class="comments">; The filename for colors to use in cgPickColorName</span>
              tlb: 0L, $         <span class="comments">; The top-level base widget of this compound widget.</span>
              labelID: 0L, $     <span class="comments">; The identifier of the label widget.</span>
              drawID: 0L, $      <span class="comments">; The identifier of the draw widget.</span>
              wid: 0L, $         <span class="comments">; The window index number of the draw widget.</span>
              index: 0L, $       <span class="comments">; The color table index number where color is loaded.</span>
              title: "", $       <span class="comments">; The title of the cgPickColorName program.</span>
              color: "" }        <span class="comments">; The name of the color displayed in the color patch.</span>

END <span class="comments">;-------------------------------------------------------------------------------------</span>



FUNCTION CW_DRAWCOLOR, $

<span class="comments">; A wrapper function for the CW_DRAWCOLOR object.</span>

   parent, $                   <span class="comments">; The parent widget identifier for this compound widget.</span>
   Color=color, $              <span class="comments">; The name of the color to be displayed. Color names come from cgPickColorName.</span>
   Column=column, $            <span class="comments">; Set this keyword to stack widgets in a column. Default is in a row.</span>
   Event_Func=event_func, $    <span class="comments">; The name of an event handler function for this compound widget.</span>
   Event_Pro=event_pro, $      <span class="comments">; The name of an event handler procedure for this compound widget.</span>
   Index=index, $              <span class="comments">; An index number where the color should be loaded. !D.Table_Size-2, by default.</span>
   Filename=filename, $        <span class="comments">; An optional input to cgPickColorName specifying different colors.</span>
   Label_Left=label_left, $    <span class="comments">; Set this keyword to have the label text aligned on the left of the label. Default is to center.</span>
   Label_Right=label_right, $  <span class="comments">; Set this keyword to have the label text aligned on the right of the label. Default is to center.</span>
   LabelSize=labelsize, $      <span class="comments">; This is the X size of the label widget (containing the label) in device coordinates. Default is natural size.</span>
   LabelText=label, $          <span class="comments">; This is the text on the label. Example, "Background Color", etc.</span>
   Object=object, $            <span class="comments">; The object reference for the CW_DRAWCOLOR object. (Output keyword.)</span>
   Title=title, $              <span class="comments">; This is the title on the cgPickColorName program that allows the user to select another color.</span>
   UValue=uvalue, $            <span class="comments">; A user value for the widget.</span>
   XSize=xsize, $              <span class="comments">; The xsize (in pixel units) of the color patch. By default, 20.</span>
   YSize=ysize                 <span class="comments">; The xsize (in pixel units) of the color patch. By default, 20.</span>

object = Obj_New("CW_DRAWCOLOR", $
   parent, $                   <span class="comments">; The parent widget identifier for this compound widget.</span>
   Color=color, $              <span class="comments">; The name of the color to be displayed. Color names come from cgPickColorName.</span>
   Column=column, $            <span class="comments">; Set this keyword to stack widgets in a column. Default is in a row.</span>
   Event_Func=event_func, $    <span class="comments">; The name of an event handler function for this compound widget.</span>
   Event_Pro=event_pro, $      <span class="comments">; The name of an event handler procedure for this compound widget.</span>
   Index=index, $              <span class="comments">; An index number where the color should be loaded. !D.Table_Size-2, by default.</span>
   Filename=filename, $        <span class="comments">; An optional input to cgPickColorName specifying different colors.</span>
   Label_Left=label_left, $    <span class="comments">; Set this keyword to have the label text aligned on the left of the label. Default is to center.</span>
   Label_Right=label_right, $  <span class="comments">; Set this keyword to have the label text aligned on the right of the label. Default is to center.</span>
   LabelSize=labelsize, $      <span class="comments">; This is the X size of the label widget (containing the label) in device coordinates. Default is natural size.</span>
   LabelText=label, $          <span class="comments">; This is the text on the label. Example, "Background Color", etc.</span>
   Title=title, $              <span class="comments">; This is the title on the cgPickColorName program that allows the user to select another color.</span>
   UValue=uvalue, $            <span class="comments">; A user value for the widget.</span>
   XSize=xsize, $              <span class="comments">; The xsize (in pixel units) of the color patch. By default, 20.</span>
   YSize=ysize)                <span class="comments">; The xsize (in pixel units) of the color patch. By default, 20.</span>


   <span class="comments">; Return the TLB of the compound widget.</span>

IF Obj_Valid(object) THEN RETURN, object->GetTLB() ELSE RETURN, -1L
END <span class="comments">;-------------------------------------------------------------------------------------</span>
</code>
    </div>
  </body>
</html>