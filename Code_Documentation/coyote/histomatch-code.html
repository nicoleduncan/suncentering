<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN"
 "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">

<!-- Generated by IDLdoc 3.5.1 on Mon Sep 30 16:56:57 2013 -->

<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en">
  <head>
    <meta http-equiv="Content-Type" content="text/html;charset=utf-8"/>
    <title>histomatch.pro (Documentation for ./)</title>

    
    <link rel="stylesheet" type="text/css" media="all"
          href="../idldoc-resources/main.css" />
    <link rel="stylesheet" type="text/css" media="print"
          href="../idldoc-resources/main-print.css" />
    

    <script type="text/javascript">
      function setTitle() {
        parent.document.title="histomatch.pro (Documentation for ./)";
      }
    </script>
  </head>

  <body onload="setTitle();" id="root">
    <div class="content">
      <code class="source"><span class="comments">;+</span>
<span class="comments">; NAME:</span>
<span class="comments">;       HistoMatch</span>
<span class="comments">;</span>
<span class="comments">; PURPOSE:</span>
<span class="comments">;</span>
<span class="comments">;       This is a function for Histogram Matching, in which an image</span>
<span class="comments">;       is manipulated in such a way that it's final histogram approximates</span>
<span class="comments">;       the histogram of an input image or histogram. Histogram matching</span>
<span class="comments">;       allows the user to specify the shape of the histogram of the final</span>
<span class="comments">;       product.</span>
<span class="comments">;</span>
<span class="comments">; AUTHOR:</span>
<span class="comments">;</span>
<span class="comments">;       FANNING SOFTWARE CONSULTING</span>
<span class="comments">;       David Fanning, Ph.D.</span>
<span class="comments">;       1645 Sheely Drive</span>
<span class="comments">;       Fort Collins, CO 80526 USA</span>
<span class="comments">;       Phone: 970-221-0438</span>
<span class="comments">;       E-mail: david@idlcoyote.com</span>
<span class="comments">;       Coyote's Guide to IDL Programming: http://www.idlcoyote.com</span>
<span class="comments">;</span>
<span class="comments">; CATEGORY:</span>
<span class="comments">;</span>
<span class="comments">;       Image Processing</span>
<span class="comments">;</span>
<span class="comments">; CALLING SEQUENCE:</span>
<span class="comments">;</span>
<span class="comments">;       output_image = HistoMatch(image, histogram_to_match)</span>
<span class="comments">;</span>
<span class="comments">; INPUTS:</span>
<span class="comments">;</span>
<span class="comments">;       image - The input image to be manipulated. Assumed to be a 2D byte array.</span>
<span class="comments">;</span>
<span class="comments">;       histogram_to_match - Can be either a 1D long vector of 256 elements specifying</span>
<span class="comments">;           the histogram to match, or a 2D byte array from which the histogram to</span>
<span class="comments">;           match is calculated.</span>
<span class="comments">;</span>
<span class="comments">; OUTPUTS:</span>
<span class="comments">;</span>
<span class="comments">;       output_image - The manipulated image adjusted to the histogram specifications.</span>
<span class="comments">;</span>
<span class="comments">; INPUT KEYWORDS:</span>
<span class="comments">;</span>
<span class="comments">;       None.</span>
<span class="comments">;</span>
<span class="comments">; OUTPUT KEYWORDS:</span>
<span class="comments">;</span>
<span class="comments">;       None.</span>
<span class="comments">;</span>
<span class="comments">; DEPENDENCIES:</span>
<span class="comments">;</span>
<span class="comments">;       None.</span>
<span class="comments">;</span>
<span class="comments">; METHOD:</span>
<span class="comments">;</span>
<span class="comments">;       Based on the Histogram Matching method on pages 94-102 of Digital</span>
<span class="comments">;       Image Processing, 2nd Edition, Rafael C. Gonzalez and Richard E. Woods,</span>
<span class="comments">;       ISBN 0-20-118075-8.</span>
<span class="comments">;</span>
<span class="comments">; EXAMPLE:</span>
<span class="comments">;</span>
<span class="comments">;       There is an example program at the end of this file. It will require cgImage</span>
<span class="comments">;       from the Coyote Library to run. You can also find an explanation of this program</span>
<span class="comments">;       at http://www.idlcoyote.com/ip_tips/histomatch.html.</span>
<span class="comments">;</span>
<span class="comments">; MODIFICATION HISTORY:</span>
<span class="comments">;</span>
<span class="comments">;       Written by David W. Fanning, January 2003.</span>
<span class="comments">;-</span>
<span class="comments">;</span>
<span class="comments">;******************************************************************************************;</span>
<span class="comments">;  Copyright (c) 2008, by Fanning Software Consulting, Inc.                                ;</span>
<span class="comments">;  All rights reserved.                                                                    ;</span>
<span class="comments">;                                                                                          ;</span>
<span class="comments">;  Redistribution and use in source and binary forms, with or without                      ;</span>
<span class="comments">;  modification, are permitted provided that the following conditions are met:             ;</span>
<span class="comments">;                                                                                          ;</span>
<span class="comments">;      * Redistributions of source code must retain the above copyright                    ;</span>
<span class="comments">;        notice, this list of conditions and the following disclaimer.                     ;</span>
<span class="comments">;      * Redistributions in binary form must reproduce the above copyright                 ;</span>
<span class="comments">;        notice, this list of conditions and the following disclaimer in the               ;</span>
<span class="comments">;        documentation and/or other materials provided with the distribution.              ;</span>
<span class="comments">;      * Neither the name of Fanning Software Consulting, Inc. nor the names of its        ;</span>
<span class="comments">;        contributors may be used to endorse or promote products derived from this         ;</span>
<span class="comments">;        software without specific prior written permission.                               ;</span>
<span class="comments">;                                                                                          ;</span>
<span class="comments">;  THIS SOFTWARE IS PROVIDED BY FANNING SOFTWARE CONSULTING, INC. ''AS IS'' AND ANY        ;</span>
<span class="comments">;  EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES    ;</span>
<span class="comments">;  OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT     ;</span>
<span class="comments">;  SHALL FANNING SOFTWARE CONSULTING, INC. BE LIABLE FOR ANY DIRECT, INDIRECT,             ;</span>
<span class="comments">;  INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED    ;</span>
<span class="comments">;  TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS;         ;</span>
<span class="comments">;  LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND             ;</span>
<span class="comments">;  ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT              ;</span>
<span class="comments">;  (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS           ;</span>
<span class="comments">;  SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.                            ;</span>
<span class="comments">;******************************************************************************************;</span>

FUNCTION HISTOMATCH_ERROR_MESSAGE, theMessage, Error=error, Informational=information, $
   Traceback=traceback, NoName=noname, Title=title, _Extra=extra

<span class="comments">; An error handler for the program.</span>

On_Error, 2

   <span class="comments">; Check for presence and type of message.</span>

IF N_Elements(theMessage) EQ 0 THEN theMessage = !Error_State.Msg
s = Size(theMessage)
messageType = s[s[0]+1]
IF messageType NE 7 THEN BEGIN
   Message, "The message parameter must be a string.", _Extra=extra
ENDIF

   <span class="comments">; Get the call stack and the calling routine's name.</span>

Help, Calls=callStack
IF Float(!Version.Release) GE 5.2 THEN $
   callingRoutine = (StrSplit(StrCompress(callStack[1])," ", /Extract))[0] ELSE $
   callingRoutine = (Str_Sep(StrCompress(callStack[1])," "))[0]

   <span class="comments">; Are widgets supported?</span>

widgetsSupported = ((!D.Flags AND 65536L) NE 0)
IF widgetsSupported THEN BEGIN

      <span class="comments">; If this is an error produced with the MESSAGE command, it is a trapped</span>
      <span class="comments">; error and will have the name "IDL_M_USER_ERR".</span>

   IF !ERROR_STATE.NAME EQ "IDL_M_USER_ERR" THEN BEGIN

      IF N_Elements(title) EQ 0 THEN title = 'Trapped Error'

         <span class="comments">; If the message has the name of the calling routine in it,</span>
         <span class="comments">; it should be stripped out. Can you find a colon in the string?</span>

      colon = StrPos(theMessage, ":")
      IF colon NE -1 THEN BEGIN

            <span class="comments">; Extract the text up to the colon. Is this the same as</span>
            <span class="comments">; the callingRoutine? If so, strip it.</span>

         IF StrMid(theMessage, 0, colon) EQ callingRoutine THEN $
            theMessage = StrMid(theMessage, colon+1)

      ENDIF

         <span class="comments">; Add the calling routine's name, unless NONAME is set.</span>

      IF Keyword_Set(noname) THEN BEGIN
         answer = Dialog_Message(theMessage, Title=title, _Extra=extra, $
            Error=error, Information=information)
      ENDIF ELSE BEGIN
         answer = Dialog_Message(StrUpCase(callingRoutine) + ": " + $
            theMessage, Title=title, _Extra=extra, $
            Error=error, Information=information)
      ENDELSE

   ENDIF ELSE BEGIN

         <span class="comments">; Otherwise, this is an IDL system error.</span>

      IF N_Elements(title) EQ 0 THEN title = 'System Error'

      IF StrUpCase(callingRoutine) EQ "$MAIN$" THEN $
         answer = Dialog_Message(theMessage, _Extra=extra, Title=title, $
            Error=error, Information=information) ELSE $
      IF Keyword_Set(noname) THEN BEGIN
         answer = Dialog_Message(theMessage, _Extra=extra, Title=title, $
            Error=error, Information=information)
      ENDIF ELSE BEGIN
         answer = Dialog_Message(StrUpCase(callingRoutine) + "--> " + $
            theMessage, _Extra=extra, Title=title, $
            Error=error, Information=information)
      ENDELSE
   ENDELSE
ENDIF ELSE BEGIN
      Message, theMessage, /Continue, /NoPrint, /NoName, /NoPrefix, _Extra=extra
      Print, '%' + callingRoutine + ': ' + theMessage
      answer = 'OK'
ENDELSE

   <span class="comments">; Provide traceback information if requested.</span>

IF Keyword_Set(traceback) THEN BEGIN
   Help, /Last_Message, Output=traceback
   Print,''
   Print, 'Traceback Report from ' + StrUpCase(callingRoutine) + ':'
   Print, ''
   FOR j=0,N_Elements(traceback)-1 DO Print, "     " + traceback[j]
ENDIF

RETURN, answer
END
<span class="comments">; ----------------------------------------------------------------------------</span>



FUNCTION HISTOMATCH, image, histogram_to_match

   <span class="comments">; Error handling.</span>

Catch, theError
IF theError NE 0 THEN BEGIN
   Catch, /Cancel
   ok = Histomatch_Error_Message(/Traceback)
   IF N_Elements(image) NE 0 THEN RETURN, image ELSE RETURN, -1L
ENDIF

   <span class="comments">; We require two input parameters.</span>

IF N_Params() NE 2 THEN Message, 'Two arguments required. Please read the program documentation.'

   <span class="comments">; Must have 2D image array.</span>

IF Size(image, /N_Dimensions) NE 2 THEN Message, 'Image argument must be 2D. Returning.'

   <span class="comments">; Is the histogram_to_match variable a 1D or 2D array? Branch accordingly.</span>

CASE Size(histogram_to_match, /N_Dimensions) OF
   1: BEGIN
      IF N_Elements(histogram_to_match) NE 256 THEN $
         Message, 'Histogram to match has incorrect size. Returning.'
      match_histogram =    histogram_to_match
      END
   2: match_histogram = Histogram(Byte(histogram_to_match), Min=0, Max=255, Binsize=1)
   ELSE: Message, 'Histogram to match has incorrect number of dimensions. Returning.'
ENDCASE

   <span class="comments">; Calculate the histogram of the input image.</span>

h = Histogram(Byte(image), Binsize=1, Min=0, Max=255)

   <span class="comments">; Make sure the two histograms have the same number of pixels. This will</span>
   <span class="comments">; be a problem if the two images are different sizes, you are matching a</span>
   <span class="comments">; histogram from an image subset, etc.</span>

totalPixels = Float(N_Elements(image))
totalHistogramPixels = Float(Total(match_histogram))

IF totalPixels NE totalHistogramPixels THEN $
   factor = totalPixels / totalHistogramPixels ELSE $
   factor = 1.0

match_histogram = match_histogram * factor

   <span class="comments">; Find a mapping from the input pixels to the transformation function s.</span>

s = FltArr(256)
FOR k=0,255 DO BEGIN
  s[k] = Total(h(0:k) / totalPixels)
ENDFOR

   <span class="comments">; Find a mapping from input histogram to the transformation function v.</span>

v = FltArr(256)
FOR q=0,255 DO BEGIN
  v[q] = Total(match_histogram(0:q) / Total(match_histogram))
ENDFOR

   <span class="comments">; Find probablitly density function z from v and s.</span>

z = BytArr(256)
FOR j=0,255 DO BEGIN
   i = Where(v LT s[j], count)
   IF count GT 0 THEN z[j] = (Reverse(i))[0] ELSE z[j]=0
ENDFOR

   <span class="comments">; Create the matched image.</span>

matchedImage = z[Byte(image)]
RETURN, matchedImage
END
<span class="comments">; ----------------------------------------------------------------------------</span>



PRO Example

   <span class="comments">; Get an image whose histogram you want to match.</span>

filename = Filepath('ctscan.dat', Subdir=['examples', 'data'])
OpenR, lun, filename, /Get_Lun
image_to_match = BytArr(256, 256)
ReadU, lun, image_to_match
Free_Lun, lun

   <span class="comments">; Get an image to apply the histogram to.</span>

filename = Filepath('worldelv.dat', Subdir=['examples', 'data'])
OpenR, lun, filename, /Get_Lun
image = BytArr(360, 360)
ReadU, lun, image
Free_Lun, lun


Window, 0, XSize=500, YSize=250, Title='Match this Image Histogram', XPos=100, YPos=100
!P.Multi=[0,2,1]
cgImage, image_to_match
Plot, Histogram(image_to_match), Max_Value=5000
!P.Multi=0

Window, 1, XSize=500, YSize=250, Title='Manipulate this Image', XPos=100, YPos=360
!P.Multi=[0,2,1]
cgImage, image
Plot, Histogram(image), Max_Value=5000
!P.Multi=0

Window, 2, XSize=500, YSize=250, Title='Final Result', XPos=100, YPos=630
!P.Multi=[0,2,1]
match = HistoMatch(image, image_to_match)
cgImage, match
Plot, Histogram(match), Max_Value=5000
!P.Multi=0
END
</code>
    </div>
  </body>
</html>