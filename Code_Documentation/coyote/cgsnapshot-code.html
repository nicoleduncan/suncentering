<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN"
 "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">

<!-- Generated by IDLdoc 3.5.1 on Mon Sep 30 16:56:29 2013 -->

<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en">
  <head>
    <meta http-equiv="Content-Type" content="text/html;charset=utf-8"/>
    <title>cgsnapshot.pro (Documentation for ./)</title>

    
    <link rel="stylesheet" type="text/css" media="all"
          href="../idldoc-resources/main.css" />
    <link rel="stylesheet" type="text/css" media="print"
          href="../idldoc-resources/main-print.css" />
    

    <script type="text/javascript">
      function setTitle() {
        parent.document.title="cgsnapshot.pro (Documentation for ./)";
      }
    </script>
  </head>

  <body onload="setTitle();" id="root">
    <div class="content">
      <code class="source"><span class="comments">; docformat = 'rst'</span>
<span class="comments">;</span>
<span class="comments">; NAME:</span>
<span class="comments">;   cgSnapshot</span>
<span class="comments">;</span>
<span class="comments">; PURPOSE:</span>
<span class="comments">;   To get accurate screen dumps with the IDL command TVRD on 24-bit</span>
<span class="comments">;   PC and Macintosh computers, you have to be sure to set color</span>
<span class="comments">;   decomposition on. This program adds that capability automatically.</span>
<span class="comments">;   In addition, the program will optionally write BMP, GIF, JPEG,</span>
<span class="comments">;   PICT, PNG, and TIFF color image files of the screen dump.</span>
<span class="comments">;</span>
<span class="comments">;******************************************************************************************;</span>
<span class="comments">;                                                                                          ;</span>
<span class="comments">;  Copyright (c) 2011, by Fanning Software Consulting, Inc. All rights reserved.           ;</span>
<span class="comments">;                                                                                          ;</span>
<span class="comments">;  Redistribution and use in source and binary forms, with or without                      ;</span>
<span class="comments">;  modification, are permitted provided that the following conditions are met:             ;</span>
<span class="comments">;                                                                                          ;</span>
<span class="comments">;      * Redistributions of source code must retain the above copyright                    ;</span>
<span class="comments">;        notice, this list of conditions and the following disclaimer.                     ;</span>
<span class="comments">;      * Redistributions in binary form must reproduce the above copyright                 ;</span>
<span class="comments">;        notice, this list of conditions and the following disclaimer in the               ;</span>
<span class="comments">;        documentation and/or other materials provided with the distribution.              ;</span>
<span class="comments">;      * Neither the name of Fanning Software Consulting, Inc. nor the names of its        ;</span>
<span class="comments">;        contributors may be used to endorse or promote products derived from this         ;</span>
<span class="comments">;        software without specific prior written permission.                               ;</span>
<span class="comments">;                                                                                          ;</span>
<span class="comments">;  THIS SOFTWARE IS PROVIDED BY FANNING SOFTWARE CONSULTING, INC. ''AS IS'' AND ANY        ;</span>
<span class="comments">;  EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES    ;</span>
<span class="comments">;  OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT     ;</span>
<span class="comments">;  SHALL FANNING SOFTWARE CONSULTING, INC. BE LIABLE FOR ANY DIRECT, INDIRECT,             ;</span>
<span class="comments">;  INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED    ;</span>
<span class="comments">;  TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS;         ;</span>
<span class="comments">;  LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND             ;</span>
<span class="comments">;  ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT              ;</span>
<span class="comments">;  (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS           ;</span>
<span class="comments">;  SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.                            ;</span>
<span class="comments">;******************************************************************************************;</span>
<span class="comments">;</span>
<span class="comments">;+</span>
<span class="comments">; To get accurate screen dumps with the IDL command TVRD on 24-bit</span>
<span class="comments">; PC and Macintosh computers, you have to be sure to set color</span>
<span class="comments">; decomposition on. This program adds that capability automatically.</span>
<span class="comments">; In addition, the program will optionally write BMP, GIF, JPEG,</span>
<span class="comments">; PICT, PNG, and TIFF color image files of the screen dump.</span>
<span class="comments">;</span>
<span class="comments">; :Categories:</span>
<span class="comments">;    Graphics</span>
<span class="comments">;    </span>
<span class="comments">; :Returns:</span>
<span class="comments">;    The returned image will be a 2D image on 8-bit systems and a 24-bit pixel </span>
<span class="comments">;    interleaved true-color image on 24-bit systems. A -1 will be returned if a </span>
<span class="comments">;    file output keyword is used (e.g., JPEG, TIFF, etc.).</span>
<span class="comments">;    </span>
<span class="comments">; :Params:</span>
<span class="comments">;    xstart: in, optional, type=integer, default=0</span>
<span class="comments">;       The starting column index of the rectantular area that is to be copied.</span>
<span class="comments">;    ystart: in, optional, type=integer, default=0</span>
<span class="comments">;       The starting row index of the rectantular area that is to be copied.</span>
<span class="comments">;    ncols: in, optional, type=integer</span>
<span class="comments">;       The number of columns to read in the rectantular area that is to be </span>
<span class="comments">;       copied. By default, !D.X_Size - xstart.</span>
<span class="comments">;    nrows: in, optional, type=integer</span>
<span class="comments">;       The number of rows to read in the rectantular area that is to be </span>
<span class="comments">;       copied. By default, !D.Y_Size - ystart.</span>
<span class="comments">;</span>
<span class="comments">; :Keywords:</span>
<span class="comments">;    bmp: in, optional, type=boolean, default=0</span>
<span class="comments">;        Set this keyword to write the screen dump as a color BMP file.</span>
<span class="comments">;    cancel: out, optional, type=boolean, default=0</span>
<span class="comments">;        An output keyword set to 1 if the user cancels out of a filename dialog. </span>
<span class="comments">;        Set to 0 otherwise.</span>
<span class="comments">;    colors: in, optional, type=integer, default=256</span>
<span class="comments">;        If a 24-bit image has to be quantized, this will set the number of colors in </span>
<span class="comments">;        the output image. Applies to BMP, GIF, PICT, and PNG formats written from </span>
<span class="comments">;        24-bit displays.(See the COLOR_QUAN documentation for details.)</span>
<span class="comments">;    cube: in, optional, type=integer</span>
<span class="comments">;        If this keyword is set to a value between 2 and 6 the color quantization will </span>
<span class="comments">;        use a cubic method of quantization. Applies to BMP, GIF, PICT, and PNG formats </span>
<span class="comments">;        written from 24-bit displays.(See the COLOR_QUAN documentation for details.)</span>
<span class="comments">;    dither: in, optional, type=boolean, default=0 </span>
<span class="comments">;        If this keyword is set the quantized image will be dithered. Applies to BMP, </span>
<span class="comments">;        GIF, PICT, and PNG formats written from 24-bit displays.(See the COLOR_QUAN </span>
<span class="comments">;        documentation for details.)</span>
<span class="comments">;    filename: in, optional, type=string</span>
<span class="comments">;        The name of the output file. If you specify a name with a file extension of the</span>
<span class="comments">;        type of file you want to create (e.g., *.jpg, *.png, etc), then you do not have</span>
<span class="comments">;        to use the file type keywords (e.g., JPEG, PNG, etc.). Otherwise, you can specify</span>
<span class="comments">;        the name of the the file without an extension, use the file keywords, and a file</span>
<span class="comments">;        extension will be added to the filename automatically, depending upon the type of</span>
<span class="comments">;        output file selected.</span>
<span class="comments">;    gif: in, optional, type=boolean, default=0</span>
<span class="comments">;        Set this keyword to write the screen dump as a color GIF file.</span>
<span class="comments">;    jpeg: in, optional, type=boolean, default=0</span>
<span class="comments">;        Set this keyword to write the screen dump as a color JPEG file.</span>
<span class="comments">;    nodialog: in, optional, type=boolean, default=0        </span>
<span class="comments">;        Set this keyword if you wish to avoid the DIALOG_PICKFILE dialog that asks you </span>
<span class="comments">;        to name the output file. This keyword should be set, for example, if you are </span>
<span class="comments">;        processing screens in batch mode.</span>
<span class="comments">;    order: in, optional, type=boolean, default=0</span>
<span class="comments">;        Set this keyword to determine the image order for reading the display. Corresponds to </span>
<span class="comments">;        !Order and set to such as the default.</span>
<span class="comments">;    overwrite_prompt: in, optional, type=boolean, default=0       </span>
<span class="comments">;        Set this keyword if you would like to get a prompt if you are overwriting a file. </span>
<span class="comments">;        This applies only to operations involving DIALOG_PICKFILE.</span>
<span class="comments">;    pict: in, optional, type=boolean, default=0</span>
<span class="comments">;        Set this keyword to write the screen dump as a color PICT file.</span>
<span class="comments">;    png: in, optional, type=boolean, default=0</span>
<span class="comments">;        Set this keyword to write the screen dump as a color PNG file.</span>
<span class="comments">;    position: in, optional, type=float</span>
<span class="comments">;        An alternative way of setting the `xstart`, `ystart`, `ncols` and `nrows` parameters</span>
<span class="comments">;        by specifying a four-element normalized array, [x0,y0,x1,y1].</span>
<span class="comments">;    tiff: in, optional, type=boolean, default=0</span>
<span class="comments">;        Set this keyword to write the screen dump as a color TIFF file.</span>
<span class="comments">;    true: in, optional, type=integer, default=1</span>
<span class="comments">;        Set this keyword to the type of interleaving you want. 1 = Pixel interleaved, </span>
<span class="comments">;        2 = row interleaved, 3 = band interleaved.</span>
<span class="comments">;    type: in, optional, type=string</span>
<span class="comments">;        Set this keyword to the type of file to write. Use this instead of</span>
<span class="comments">;        setting BMP, GIF, JPEG, PICT, PNG, or TIFF keywords: TYPE='JPEG'. The</span>
<span class="comments">;        primary purpose of this is to make widget event handlers easier to write.</span>
<span class="comments">;    quality: in, optional, type=integer, default=75</span>
<span class="comments">;        This keyword sets the amount of compression for JPEG images. It should be set to a </span>
<span class="comments">;        value between 0 and 100. (See the WRITE_JPEG documentation for details.)</span>
<span class="comments">;    wid: in, optional, type=integer</span>
<span class="comments">;        The index number of the window to read from. The current graphics window</span>
<span class="comments">;        (!D.Window) is selected by default. An error is issued if no windows are</span>
<span class="comments">;         currently open on a device that supports windows.</span>
<span class="comments">;    _ref_extra: in, optional</span>
<span class="comments">;        Any keywords that are appropriate for the WRITE_*** routines are also accepted via </span>
<span class="comments">;        keyword inheritance.</span>
<span class="comments">;          </span>
<span class="comments">; :Examples:</span>
<span class="comments">;    To obtain an image of the current graphics window::</span>
<span class="comments">;    </span>
<span class="comments">;       IDL> image = cgSnapshot()</span>
<span class="comments">;       </span>
<span class="comments">;    To create a PNG file, named "test.png", of the current graphics window::</span>
<span class="comments">;    </span>
<span class="comments">;       IDL> void = cgSnapshot(FILENAME='test.png')</span>
<span class="comments">;       </span>
<span class="comments">;    To obtain the lower quadrant of a 512-by-512 graphics window as a</span>
<span class="comments">;    band interleaved image::</span>
<span class="comments">;    </span>
<span class="comments">;       IDL> image = cgSnapshot(0, 0, 256, 256, TRUE=3)</span>
<span class="comments">;       </span>
<span class="comments">; :Author:</span>
<span class="comments">;       FANNING SOFTWARE CONSULTING::</span>
<span class="comments">;           David W. Fanning </span>
<span class="comments">;           1645 Sheely Drive</span>
<span class="comments">;           Fort Collins, CO 80526 USA</span>
<span class="comments">;           Phone: 970-221-0438</span>
<span class="comments">;           E-mail: david@idlcoyote.com</span>
<span class="comments">;           Coyote's Guide to IDL Programming: http://www.idlcoyote.com</span>
<span class="comments">;</span>
<span class="comments">; :History:</span>
<span class="comments">;     Change History::</span>
<span class="comments">;        Renamed TVRead to cgSnapshot and retired TVRead. 20 February 2011. DWF.</span>
<span class="comments">;        Added the ability to get the file type from the file name extension. 26 Dec 2011. DWF.</span>
<span class="comments">;        Added a POSITION keyword to select a position inside the window for capture. 20 October 2012. DWF.</span>
<span class="comments">;        Fixed a problem with not setting back to incoming decomposed state on an error. 20 Nov 2012. DWF.</span>
<span class="comments">; :Copyright:</span>
<span class="comments">;     Copyright (c) 2011, Fanning Software Consulting, Inc.</span>
<span class="comments">;-</span>
FUNCTION cgSnapshot, xstart, ystart, ncols, nrows, $
   BMP=bmp, $
   Cancel=cancel, $
   Colors=colors, $
   Cube=cube, $
   Dither=dither, $
   Filename=filename, $
   GIF=gif, $
   JPEG=jpeg, $
   NoDialog=nodialog, $
   Order=order, $
   Overwrite_Prompt=overwrite_prompt, $
   PICT=pict, $
   PNG=png, $
   POSITION=position, $
   TIFF=tiff, $
   True=true, $
   Type=type, $
   Quality=quality, $
   WID=wid, $
   _Ref_Extra=extra
   

   <span class="comments">; Error handling.</span>
   Catch, theError
   IF theError NE 0 THEN BEGIN
       Catch, /Cancel
       ok = Error_Message()
       IF N_Elements(thisWindow) EQ 0 THEN RETURN, -1
       IF thisWindow GE 0 THEN WSet, thisWindow
       
       <span class="comments">; Need to set color decomposition back?</span>
       IF (N_Elements(theDecomposedState) NE 0) && (theDepth GT 0) THEN BEGIN
           Device, Decomposed=theDecomposedState
       ENDIF
       RETURN, -1
    ENDIF
    
    cancel = 0
    
    <span class="comments">; Check for availability of GIF files.</span>
    thisVersion = Float(!Version.Release)
    IF (thisVersion LT 5.3) OR (thisVersion GE 6.1) THEN haveGif = 1 ELSE haveGIF = 0
    
    <span class="comments">; Go to correct window.</span>
    IF N_Elements(wid) EQ 0 THEN wid =!D.Window
    thisWindow = !D.Window
    IF (!D.Flags AND 256) NE 0 THEN WSet, wid
    
    <span class="comments">; Did the user specify a normalized position in the window?</span>
    IF N_Elements(position) NE 0 THEN BEGIN
       xstart = position[0] * !D.X_VSize
       ystart = position[1] * !D.Y_VSize
       ncols = (position[2]*!D.X_VSize) - xstart
       nrows = (position[3]*!D.Y_VSize) - ystart
    ENDIF
    
    <span class="comments">; Check keywords and parameters. Define values if necessary.</span>
    IF N_Elements(xstart) EQ 0 THEN xstart = 0
    IF N_Elements(ystart) EQ 0 THEN ystart = 0
    IF N_Elements(ncols) EQ 0 THEN ncols = !D.X_VSize - xstart
    IF N_Elements(nrows) EQ 0 THEN nrows = !D.Y_VSize - ystart
    IF N_Elements(order) EQ 0 THEN order = !Order
    IF N_Elements(true) EQ 0 THEN true = 1
    dialog = 1 - Keyword_Set(nodialog)
    
    <span class="comments">; Is the FILENAME keyword being used? If so, get the type of the</span>
    <span class="comments">; file from the filename extension.</span>
    IF N_Elements(filename) NE 0 THEN BEGIN
       root_name = cgRootName(filename, DIRECTORY=theDir, EXTENSION=ext)
       IF ext NE "" THEN BEGIN
           type = StrUpCase(ext)
           typeFromExtension = 1
       ENDIF ELSE typeFromExtension = 0
    ENDIF ELSE typeFromExtension = 0
    
    <span class="comments">; Do you want to write an image file instead of capturing an image?</span>
    IF N_Elements(type) NE 0 THEN BEGIN
       CASE StrUpCase(type) OF
          'BMP': bmp = 1
          'GIF': gif = 1
          'JPEG': jpeg = 1
          'JPG': jpeg = 1
          'PICT': pict = 1
          'PNG': png = 1
          'TIFF': tiff = 1
          'TIF': tif = 1
          ELSE: Message, 'Cannot write a file of type: ' + StrUpCase(type) + '.'
       ENDCASE
    ENDIF
    writeImage = 0
    fileType = ""
    extention = ""
    IF Keyword_Set(bmp)THEN BEGIN
       writeImage = 1
       fileType = 'BMP'
       extension = 'bmp'
    ENDIF
    IF Keyword_Set(gif) THEN BEGIN
       IF havegif THEN BEGIN
          writeImage = 1
          fileType = 'GIF'
          extension = 'gif'
        ENDIF ELSE BEGIN
           ok = Dialog_Message('GIF files not supported in this IDL version. Replacing with JPEG.')
           writeImage = 1
          fileType = 'JPEG'
          extension = 'jpg'
       ENDELSE
    ENDIF
    IF Keyword_Set(jpeg) THEN BEGIN
       writeImage = 1
       fileType = 'JPEG'
       extension = 'jpg'
    ENDIF
    IF Keyword_Set(PICT) THEN BEGIN
       writeImage = 1
       fileType = 'PICT'
       extension = 'pict'
    ENDIF
    IF Keyword_Set(png) THEN BEGIN
       writeImage = 1
       fileType = 'PNG'
       extension = 'png'
    ENDIF
    IF Keyword_Set(tiff) THEN BEGIN
       writeImage = 1
       fileType = 'TIFF'
       extension = 'tif'
    ENDIF
    
    IF N_Elements(colors) EQ 0 THEN colors = 256
    IF N_Elements(quality) EQ 0 THEN quality = 75
    dither = Keyword_Set(dither)
    
    <span class="comments">; On 24-bit displays, make sure color decomposition is ON.</span>
    IF (!D.Flags AND 256) NE 0 THEN BEGIN
       Device, Get_Decomposed=theDecomposedState, Get_Visual_Depth=theDepth
       IF theDepth GT 8 THEN BEGIN
          Device, Decomposed=1
          IF theDepth EQ 24 THEN truecolor = true ELSE truecolor = 0
       ENDIF ELSE truecolor = 0
       IF wid LT 0 THEN $
          Message, 'No currently open windows. Returning.', /NoName
    ENDIF ELSE BEGIN
       truecolor = 0
       theDepth = 8
    ENDELSE
    
    <span class="comments">; Fix for 24-bit Z-buffer.</span>
    IF (Float(!Version.Release) GE 6.4) AND (!D.NAME EQ 'Z') THEN BEGIN
       Device, Get_Decomposed=theDecomposedState, Get_Pixel_Depth=theDepth
       IF theDepth EQ 24 THEN truecolor = true ELSE truecolor = 0
    ENDIF
    
   <span class="comments">; Get the screen dump. 2D image on 8-bit displays. 3D image on 24-bit displays.</span>
    image = TVRD(xstart, ystart, ncols, nrows, True=truecolor, Order=order)
    
    <span class="comments">; Need to set color decomposition back?</span>
    IF theDepth GT 8 THEN Device, Decomposed=theDecomposedState
    
    <span class="comments">; If we need to write an image, do it here.</span>
    IF writeImage THEN BEGIN
    
       <span class="comments">; Get the name of the output file.</span>
       IF N_Elements(filename) EQ 0 THEN BEGIN
          filename = 'idl.' + StrLowCase(extension)
       ENDIF ELSE BEGIN
          IF typeFromExtension EQ 0 THEN filename = filename + "." + StrLowCase(extension)
       ENDELSE
       IF dialog THEN filename = Dialog_Pickfile(/Write, File=filename, OVERWRITE_PROMPT=Keyword_Set(overwrite_prompt))
    
       IF filename EQ "" THEN BEGIN
          cancel = 1
          RETURN, image
       ENDIF
    
       <span class="comments">; Write the file.</span>
       CASE fileType OF
    
          'BMP': BEGIN
             IF truecolor THEN BEGIN
                <span class="comments">; BMP files assume blue, green, red planes.</span>
                temp = image[0,*,*]
                image[0,*,*] = image[2,*,*]
                image[2,*,*] = temp
                Write_BMP, filename, image, _Extra=extra
             ENDIF ELSE BEGIN
                TVLCT, r, g, b, /Get
                Write_BMP, filename, image, r, g, b, _Extra=extra
             ENDELSE
             END
    
          'GIF': BEGIN
             IF truecolor THEN BEGIN
                CASE Keyword_Set(cube) OF
                   0: image2D = Color_Quan(image, 1, r, g, b, Colors=colors, Dither=dither)
                   1: image2D = Color_Quan(image, 1, r, g, b, Cube=2 > cube &lt<span class="comments">; 6)</span>
                ENDCASE
             ENDIF ELSE BEGIN
                TVLCT, r, g, b, /Get
                image2D = image
             ENDELSE
             Write_GIF, filename, image2D, r, g, b, _Extra=extra
             END
    
          'JPEG': BEGIN
             IF truecolor THEN BEGIN
                image3D = image
             ENDIF ELSE BEGIN
                s = Size(image, /Dimensions)
                image3D = BytArr(3, s[0], s[1])
                TVLCT, r, g, b, /Get
                image3D[0,*,*] = r[image]
                image3D[1,*,*] = g[image]
                image3D[2,*,*] = b[image]
             ENDELSE
             Write_JPEG, filename, image3D, True=1, Quality=quality, _Extra=extra
             END
    
          'PICT': BEGIN
             IF truecolor THEN BEGIN
                CASE Keyword_Set(cube) OF
                   0: image2D = Color_Quan(image, 1, r, g, b, Colors=colors, Dither=dither)
                   1: image2D = Color_Quan(image, 1, r, g, b, Cube=2 > cube &lt<span class="comments">; 6)</span>
                ENDCASE
             ENDIF ELSE BEGIN
                TVLCT, r, g, b, /Get
                image2D = image
             ENDELSE
             Write_PICT, filename, image2D, r, g, b
             END
    
          'PNG': BEGIN
             IF truecolor THEN BEGIN
                Write_PNG, filename, image, _Extra=extra
             ENDIF ELSE BEGIN
                TVLCT, r, g, b, /Get
                image2D = image
                Write_PNG, filename, image2D, r, g, b, _Extra=extra
             ENDELSE
             END
    
          'TIFF': BEGIN
             IF truecolor THEN BEGIN
                image3D = Reverse(image,3)
             ENDIF ELSE BEGIN
                s = Size(image, /Dimensions)
                image3D = BytArr(3, s[0], s[1])
                TVLCT, r, g, b, /Get
                image3D[0,*,*] = r[image]
                image3D[1,*,*] = g[image]
                image3D[2,*,*] = b[image]
                image3D = Reverse(Temporary(image3D), 3)
             ENDELSE
             Write_TIFF, filename, image3D, 1, _Extra=extra
             END
       ENDCASE
       RETURN, -1
    ENDIF
    
    <span class="comments">; Return the screen dump image.</span>
    RETURN, image
    
END <span class="comments">;-------------------------------------------------------------------------------</span>
</code>
    </div>
  </body>
</html>