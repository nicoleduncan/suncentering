<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN"
 "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">

<!-- Generated by IDLdoc 3.5.1 on Mon Sep 30 16:56:05 2013 -->

<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en">
  <head>
    <meta http-equiv="Content-Type" content="text/html;charset=utf-8"/>
    <title>cgdrawshapes.pro (Documentation for ./)</title>

    
    <link rel="stylesheet" type="text/css" media="all"
          href="../idldoc-resources/main.css" />
    <link rel="stylesheet" type="text/css" media="print"
          href="../idldoc-resources/main-print.css" />
    

    <script type="text/javascript">
      function setTitle() {
        parent.document.title="cgdrawshapes.pro (Documentation for ./)";
      }
    </script>
  </head>

  <body onload="setTitle();" id="root">
    <div class="content">
      <code class="source"><span class="comments">; docformat = 'rst'</span>
<span class="comments">;</span>
<span class="comments">; NAME:</span>
<span class="comments">;   cgDrawShapes</span>
<span class="comments">;</span>
<span class="comments">; PURPOSE:</span>
<span class="comments">;   Draws entities in a shapefile, containing latitude and longitude polygons  </span>
<span class="comments">;   or polylines, on a map projection. To draw the shapefile entities</span>
<span class="comments">;   automatically, no matter what kind of values the polygon vertices</span>
<span class="comments">;   have, set the AUTODRAW keyword.</span>
<span class="comments">;</span>
<span class="comments">;******************************************************************************************;</span>
<span class="comments">;                                                                                          ;</span>
<span class="comments">;  Copyright (c) 2011, by Fanning Software Consulting, Inc. All rights reserved.           ;</span>
<span class="comments">;                                                                                          ;</span>
<span class="comments">;  Redistribution and use in source and binary forms, with or without                      ;</span>
<span class="comments">;  modification, are permitted provided that the following conditions are met:             ;</span>
<span class="comments">;                                                                                          ;</span>
<span class="comments">;      * Redistributions of source code must retain the above copyright                    ;</span>
<span class="comments">;        notice, this list of conditions and the following disclaimer.                     ;</span>
<span class="comments">;      * Redistributions in binary form must reproduce the above copyright                 ;</span>
<span class="comments">;        notice, this list of conditions and the following disclaimer in the               ;</span>
<span class="comments">;        documentation and/or other materials provided with the distribution.              ;</span>
<span class="comments">;      * Neither the name of Fanning Software Consulting, Inc. nor the names of its        ;</span>
<span class="comments">;        contributors may be used to endorse or promote products derived from this         ;</span>
<span class="comments">;        software without specific prior written permission.                               ;</span>
<span class="comments">;                                                                                          ;</span>
<span class="comments">;  THIS SOFTWARE IS PROVIDED BY FANNING SOFTWARE CONSULTING, INC. ''AS IS'' AND ANY        ;</span>
<span class="comments">;  EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES    ;</span>
<span class="comments">;  OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT     ;</span>
<span class="comments">;  SHALL FANNING SOFTWARE CONSULTING, INC. BE LIABLE FOR ANY DIRECT, INDIRECT,             ;</span>
<span class="comments">;  INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED    ;</span>
<span class="comments">;  TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS;         ;</span>
<span class="comments">;  LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND             ;</span>
<span class="comments">;  ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT              ;</span>
<span class="comments">;  (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS           ;</span>
<span class="comments">;  SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.                            ;</span>
<span class="comments">;******************************************************************************************;</span>
<span class="comments">;</span>
<span class="comments">;+--------------------------------------------------------------------------</span>
<span class="comments">;   This program draws the entities in a shapefile, containing latitude and </span>
<span class="comments">;   longitude polygons or polylines, on a map projection. To draw the shapefile </span>
<span class="comments">;   entities automatically, no matter what kind of values the polygon vertices</span>
<span class="comments">;   have, and without a map projection, set the AUTODRAW keyword.</span>
<span class="comments">;</span>
<span class="comments">;   It is assumed a map projection command has been issued and is in effect at</span>
<span class="comments">;   the time this program is called. Alternatively, you can use a cgMap object,</span>
<span class="comments">;   which will set up the map coordinate space and provide the map structure required</span>
<span class="comments">;   for plotting in that XY map coordinate space.</span>
<span class="comments">;</span>
<span class="comments">;   If the ATTRVALUES keyword is undefined, all entities are drawn, but only a single value</span>
<span class="comments">;   for COLORS, FCOLORS, FILL, LINESTYLE, and THICK is allowed.</span>
<span class="comments">;</span>
<span class="comments">; .. image:: cgdrawshapes.png</span>
<span class="comments">; </span>
<span class="comments">; :Categories:</span>
<span class="comments">;    Graphics, Map Projections</span>
<span class="comments">;       </span>
<span class="comments">; :Author:</span>
<span class="comments">;    FANNING SOFTWARE CONSULTING::</span>
<span class="comments">;       David W. Fanning </span>
<span class="comments">;       1645 Sheely Drive </span>
<span class="comments">;       Fort Collins, CO 80526 USA </span>
<span class="comments">;       Phone: 970-221-0438 </span>
<span class="comments">;       E-mail: david@idlcoyote.com </span>
<span class="comments">;       Coyote's Guide to IDL Programming: http://www.idlcoyote.com</span>
<span class="comments">;       </span>
<span class="comments">; :Examples:</span>
<span class="comments">;    Example using cgMap_Set to set up the map projection space::</span>
<span class="comments">;       shapefile = Filepath(subdir=['examples','data'], 'states.shp')</span>
<span class="comments">;       cgDisplay, 800, 700</span>
<span class="comments">;       cgMap_Set, 40.0, -117.5, /Albers, /IsoTropic, Limit=[30, -125, 50, -108], $</span>
<span class="comments">;           Position=[0.05, 0.05, 0.95, 0.95]</span>
<span class="comments">;       cgDrawShapes, shapefile, AttrName='STATE_ABBR', $</span>
<span class="comments">;           AttrValues=['CA', 'OR', 'WA', 'AZ', 'UT', 'ID', 'NV'], Thick=1, $</span>
<span class="comments">;           FColors=['firebrick', 'grn4', 'pur4', 'tan4', 'steel blue', 'org4', 'yellow'], $</span>
<span class="comments">;           Fill = Replicate(1, 7), Colors='charcoal'</span>
<span class="comments">;       cgMap_Grid, LatDel = 2.0, LonDel = 2.0, /Box_Axes, Color='charcoal'</span>
<span class="comments">;</span>
<span class="comments">;    Example using cgMap to set up the map projection space::</span>
<span class="comments">;</span>
<span class="comments">;       shapefile = Filepath(subdir=['examples','data'], 'states.shp')</span>
<span class="comments">;       cgDisplay, 800, 700, WID=1</span>
<span class="comments">;       mapCoord = Obj_New('cgMap', 'Albers Equal Area', Limit=[30, -125, 50, -108], $</span>
<span class="comments">;                  Position=[0.2, 0.05, 0.8, 0.95], CENTER_LATITUDE=40.0, $</span>
<span class="comments">;                  CENTER_LONGITUDE=-117, STANDARD_PAR1=40, STANDARD_PAR2=-39)</span>
<span class="comments">;       cgDrawShapes, shapefile, AttrName='STATE_ABBR', $</span>
<span class="comments">;           AttrValues=['CA', 'OR', 'WA', 'AZ', 'UT', 'ID', 'NV'], Thick=1, $</span>
<span class="comments">;           FColors=['firebrick', 'grn4', 'pur4', 'tan4', 'steel blue', 'org4', 'yellow'], $</span>
<span class="comments">;           Fill = Replicate(1, 7), Colors='charcoal', MapCoord=mapCoord, /USELIMIT</span>
<span class="comments">;       cgMap_Grid,  /Box_Axes, Color='charcoal', Map_Structure=mapCoord</span>
<span class="comments">;</span>
<span class="comments">;    Example drawing the states.shp file automatically, without a map projection::</span>
<span class="comments">;  </span>
<span class="comments">;       shapefile = Filepath(subdir=['examples','data'], 'states.shp')</span>
<span class="comments">;       cgDrawShapes, shapefile, /Autodraw</span>
<span class="comments">;</span>
<span class="comments">;    Example using cgMap to set up the map projection space in a resizeable graphics window::</span>
<span class="comments">;</span>
<span class="comments">;       shapefile = Filepath(subdir=['examples','data'], 'states.shp')</span>
<span class="comments">;       cgWindow, WAspect=800.0/700</span>
<span class="comments">;       mapCoord = Obj_New('cgMap', 'Albers Equal Area', LIMIT=[30, -125, 45, -108], $</span>
<span class="comments">;                  Position=[0.05, 0.05, 0.95, 0.95], CENTER_LATITUDE=37.5, $</span>
<span class="comments">;                  CENTER_LONGITUDE=-117, STANDARD_PAR1=40, STANDARD_PAR2=-39)</span>
<span class="comments">;       mapCoord -> AddCmd</span>
<span class="comments">;       cgDrawShapes, shapefile, AttrName='STATE_ABBR', $</span>
<span class="comments">;           AttrValues=['CA', 'OR', 'WA', 'AZ', 'UT', 'ID', 'NV'], Thick=1, $</span>
<span class="comments">;           FColors=['firebrick', 'grn4', 'pur4', 'tan4', 'steel blue', 'org4', 'yellow'], $</span>
<span class="comments">;           Fill = Replicate(1, 7), Colors='charcoal', MapCoord=mapCoord, /AddCmd</span>
<span class="comments">;       cgMap_Grid, LatDel = 2.0, LonDel = 2.0, /Box_Axes, Color=cgColor('charcoal'), $</span>
<span class="comments">;            Map_Structure=mapCoord, /AddCmd</span>
<span class="comments">;</span>
<span class="comments">; :History:</span>
<span class="comments">;    Modification History::</span>
<span class="comments">;       Written by David W. Fanning by modifiying DrawStates to be more</span>
<span class="comments">;          general, 13 May 2010. DWF.</span>
<span class="comments">;       Added the AUTODRAW keyword for automatic drawing. 15 May 2010. DWF.</span>
<span class="comments">;       Added COMPILE_OPT idl2 to make sure all loop variables are longs. 5 July 2010. DWF.</span>
<span class="comments">;       Corrected an aspect ratio problem with AUTODRAW and upgraded to Coyote Graphics. </span>
<span class="comments">;          3 January 2011. DWF.</span>
<span class="comments">;       Previous method of freeing entity pointers took 10 times times longer than freeing</span>
<span class="comments">;          pointers as I go. Also added MinNumVerts keyword to screen out the drawing of</span>
<span class="comments">;          small polygons. 6 October 2011. DWF.</span>
<span class="comments">;       Wrong string case for discovering particular attributes caused them not to be drawn. 27 Oct 2011. DWF.</span>
<span class="comments">;       Added the ability to draw point shapefiles. Changed default color to "opposite". 20 Aug 2012. DWF.</span>
<span class="comments">;       Made sure a window is open when the default color is chosen. 29 Aug 2012. DWF.</span>
<span class="comments">;       </span>
<span class="comments">; :Copyright:</span>
<span class="comments">;    Copyright (c) 2011, Fanning Software Consulting, Inc.</span>
<span class="comments">;---------------------------------------------------------------------------</span>
<span class="comments">;</span>
<span class="comments">;+</span>
<span class="comments">; Draw the shape file entity on the map projection.</span>
<span class="comments">;</span>
<span class="comments">; :Params:</span>
<span class="comments">;     entity: in, required, type=structure</span>
<span class="comments">;        The shapefile entity that is read out of the shape file. In should be</span>
<span class="comments">;        a polygon or a polyline.</span>
<span class="comments">;</span>
<span class="comments">; :Keywords:</span>
<span class="comments">;     color: in, required, type=string</span>
<span class="comments">;         The name of the color used to draw the entity. Used as fill color</span>
<span class="comments">;         for filled symbols when drawing points.</span>
<span class="comments">;     fcolor: in, required, type=string</span>
<span class="comments">;         The name of the color used to fill the entity. Used if the FILL</span>
<span class="comments">;         keyword is set.</span>
<span class="comments">;     fill: in, required, type=boolean</span>
<span class="comments">;         Set this keyword to fill the entity with a color.</span>
<span class="comments">;     linestyle: in, required, type=integer</span>
<span class="comments">;         Set this keyword to the line style type to draw the entity.</span>
<span class="comments">;     mapCoord: in, required, type=object</span>
<span class="comments">;         A map coordinate object (e.g., cgMap). Required to draw the</span>
<span class="comments">;         entity on a GCTP map projection set up with MAP_PROJ_INIT.</span>
<span class="comments">;     projected_xy: in, required, type=boolean, default=0</span>
<span class="comments">;         The program assumes the shapefile entities are expressed in latitude and</span>
<span class="comments">;         longitude values. If the entiites are expressed in projected XY Cartesian</span>
<span class="comments">;         coordinates, then set this value.</span>
<span class="comments">;     psym: in, optional, type=integer, default=16</span>
<span class="comments">;         When drawing points, the symbol to use. May be any value supported by</span>
<span class="comments">;         cgSymbol. The default is a filled circle, filled with `Colors`.</span>
<span class="comments">;     symsize: in, optional, type=float, default=1.0</span>
<span class="comments">;         The default symbol size. Used only when displaying points.</span>
<span class="comments">;     thick: in, required, type=integer</span>
<span class="comments">;         The thickness of the line used to draw the entity.</span>
<span class="comments">;-</span>
PRO cgDrawShapes_DrawEntity, entity, $
    COLOR=color, $
    FCOLOR=fcolor, $
    FILL=fill, $
    LINESTYLE=linestyle, $
    MAPCOORD=mapCoord, $
    PROJECTED_XY=projected_xy, $
    PSYM=psym, $
    SYMSIZE=symsize, $
    THICK=thick

   Compile_Opt idl2

   <span class="comments">; Error handling.</span>
   Catch, theError
   IF theError NE 0 THEN BEGIN
      ok = Error_Message(/Traceback)
      IF Obj_Valid(shapefile) THEN Obj_Destroy, shapefile
      IF Ptr_Valid(entities) THEN Heap_Free, entities
      RETURN
   ENDIF

   <span class="comments">; Drawing is going to be done based on the shape type.</span>
   CASE 1 OF

      <span class="comments">; Polygon shapes.</span>
      entity.shape_type EQ 5 OR $    <span class="comments">; Polygon.</span>
      entity.shape_type EQ 15 OR $   <span class="comments">; PolygonZ (ignoring Z)</span>
      entity.shape_type EQ 25: BEGIN <span class="comments">; PolygonM (ignoring M)</span>

         IF Ptr_Valid(entity.parts) THEN BEGIN
            cuts = [*entity.parts, entity.n_vertices]
            FOR j=0, entity.n_parts-1 DO BEGIN
               IF N_Elements(mapCoord) NE 0 THEN BEGIN
                   IF Obj_Valid(mapCoord) THEN BEGIN
                        mapCoord -> Draw, /NoGraphics
                        mapStruct = mapCoord->GetMapStruct()
                   ENDIF ELSE mapStruct = mapCoord
                   IF projected_xy THEN BEGIN
                       x = Reform((*entity.vertices)[0, cuts[j]:cuts[j+1]-1])
                       y = Reform((*entity.vertices)[1, cuts[j]:cuts[j+1]-1])
                   ENDIF ELSE BEGIN
                       xy = Map_Proj_Forward((*entity.vertices)[0, cuts[j]:cuts[j+1]-1], $
                                             (*entity.vertices)[1, cuts[j]:cuts[j+1]-1], $
                                              MAP_STRUCTURE=mapStruct)
                       x = Reform(xy[0,*])
                       y = Reform(xy[1,*])
                   ENDELSE
               ENDIF ELSE BEGIN
                   x = (*entity.vertices)[0, cuts[j]:cuts[j+1]-1]
                   y = (*entity.vertices)[1, cuts[j]:cuts[j+1]-1]
               ENDELSE
               CASE fill OF
                  0: cgPlotS, x, y, COLOR=color, LINESTYLE=linestyle, THICK=thick, NOCLIP=0
                  1: BEGIN
                     cgColorFill, x, y, COLOR=fcolor, NOCLIP=0
                     cgPlotS, x, y, COLOR=color, LINESTYLE=linestyle, THICK=thick, NOCLIP=0
                     END
               ENDCASE
            ENDFOR
         ENDIF
      ENDCASE <span class="comments">; Polygon shapes.</span>

      <span class="comments">; Polyline shapes.</span>
      entity.shape_type EQ  3 OR $   <span class="comments">; PolyLine</span>
      entity.shape_type EQ 13 OR $   <span class="comments">; PolyLineZ (ignoring Z)</span>
      entity.shape_type EQ 23: BEGIN <span class="comments">; PolyLineM (ignoring M)</span>

         IF Ptr_Valid(entity.parts) THEN BEGIN
            cuts = [*entity.parts, entity.n_vertices]
            FOR j=0, entity.n_parts-1 DO BEGIN
               IF N_Elements(mapCoord) NE 0 THEN BEGIN
                   IF Obj_Valid(mapCoord) THEN BEGIN
                        mapCoord -> Draw
                        mapStruct = mapCoord->GetMapStruct()
                   ENDIF ELSE mapStruct = mapCoord
                   xy = Map_Proj_Forward((*entity.vertices)[0, cuts[j]:cuts[j+1]-1], $
                                         (*entity.vertices)[1, cuts[j]:cuts[j+1]-1], $
                                          MAP_STRUCTURE=mapStruct)
                   x = Reform(xy[0,*])
                   y = Reform(xy[1,*])
               ENDIF ELSE BEGIN
                   x = (*entity.vertices)[0, cuts[j]:cuts[j+1]-1]
                   y = (*entity.vertices)[1, cuts[j]:cuts[j+1]-1]
               ENDELSE
               CASE fill OF
                  0: cgPlotS, x, y, COLOR=color, LINESTYLE=linestyle, THICK=thick, NOCLIP=0
                  1: BEGIN
                     cgColorFill, x, y, COLOR=fcolor, NOCLIP=0
                     cgPlotS, x, y, COLOR=color, LINESTYLE=linestyle, THICK=thick, NOCLIP=0
                     END
               ENDCASE
            ENDFOR
         ENDIF
     ENDCASE <span class="comments">; Polyline shapes.</span>
         
     <span class="comments">; Various kinds of points.</span>
     entity.shape_type eq  1 or $   <span class="comments">; Point</span>
     entity.shape_type eq 11 or $   <span class="comments">; PointZ (ignoring Z)</span>
     entity.shape_type eq 21 or $   <span class="comments">; PointM (ignoring M)</span>
     entity.shape_type eq  8 or $   <span class="comments">; MultiPoint</span>
     entity.shape_type eq 18 or $   <span class="comments">; MultiPointZ (ignoring Z)</span>
     entity.shape_type eq 28: BEGIN <span class="comments">; MultiPointM (ignoring M)</span>
         IF N_Elements(mapCoord) NE 0 THEN BEGIN
                   IF Obj_Valid(mapCoord) THEN BEGIN
                        mapCoord -> Draw
                        mapStruct = mapCoord->GetMapStruct()
                   ENDIF ELSE mapStruct = mapCoord
                   IF Keyword_Set(projected_xy) THEN BEGIN
                        xy = Map_Proj_Forward(entity.bounds[0], entity.bounds[1], $
                                          MAP_STRUCTURE=mapStruct)
                        x = Reform(xy[0,*])
                        y = Reform(xy[1,*])
                        cgPlotS, x, y, PSYM=psym, COLOR=color, SYMSIZE=symsize, THICK=thick
                   ENDIF ELSE BEGIN
                      cgPlotS, entity.bounds[0], entity.bounds[1], $
                          PSYM=psym, COLOR=color, SYMSIZE=symsize, THICK=thick
                   ENDELSE
         ENDIF ELSE BEGIN 
            cgPlotS, entity.bounds[0], entity.bounds[1], $
                PSYM=psym, COLOR=color, SYMSIZE=symsize, THICK=thick
         ENDELSE
         ENDCASE
         
 
      ELSE: Message, 'Not currently handling entity type: ' + StrTrim(entity.shape_type,2)

   ENDCASE

END <span class="comments">;---------------------------------------------------------------------------------</span>

<span class="comments">;+</span>
<span class="comments">;   This program draws the entities in a shapefile, containing latitude and </span>
<span class="comments">;   longitude points, polygons, polylines, or polypoings on a map projection. </span>
<span class="comments">;   Values may also be in projected meter space, if the proper map coordinate</span>
<span class="comments">;   object is passed to the program (e.g, via the MapCoord keyword). To draw the shapefile </span>
<span class="comments">;   entities automatically, no matter what kind of values the polygon vertices</span>
<span class="comments">;   or points have, and without a map projection, set the AUTODRAW keyword.</span>
<span class="comments">;</span>
<span class="comments">; :Params:</span>
<span class="comments">;    shapefile: in, optional, type=string</span>
<span class="comments">;        The name of a shapefile. If this variable is not provided, the user will</span>
<span class="comments">;        be asked to select a shapefile with the normal file dialogs.</span>
<span class="comments">;        </span>
<span class="comments">; :Keywords:</span>
<span class="comments">;     addcmd: in, optional, type=boolean</span>
<span class="comments">;         Set this keyword to add the command to a cgWindow.</span>
<span class="comments">;     autodraw: in, optional, type=boolean</span>
<span class="comments">;         If set, the shapefile entities are examined to find the X and Y</span>
<span class="comments">;         boundaries and a data coordinate space is set up to display the </span>
<span class="comments">;         entities automatically. If the shapefile is drawn automatically,</span>
<span class="comments">;         the restriction that the shapefile vertices be in latitude and</span>
<span class="comments">;         longitude values is removed. Do NOT set this keyword, if you</span>
<span class="comments">;         are using the MAPCOORD keyword, too. If no map projection is</span>
<span class="comments">;         currently in effect (!X.TYPE NE 3) and no map coordinate object</span>
<span class="comments">;         has been provided, this keyword is automatically set.</span>
<span class="comments">;     attrname: in, optional, type=string</span>
<span class="comments">;         The name of the attribute in the file that you wish to draw.</span>
<span class="comments">;         By default, this is set to the first attribute name in the file.</span>
<span class="comments">;         If you are unsure of the attribute names in your shapefile,</span>
<span class="comments">;         use the Coyote Library program `cgShapeInfo` to browse the file</span>
<span class="comments">;         ahead of time.</span>
<span class="comments">;     attrvalues: in, optional, type=varies</span>
<span class="comments">;         The value of the attribute identified by ATTRNAME. This variable can</span>
<span class="comments">;         be a scalar or vector string array identifying a subset of attribute</span>
<span class="comments">;         values to be drawn. For example, if ATTRNAME='STATE_NAME', then</span>
<span class="comments">;         ATTRVALUE can identify the particular states to draw. For example,</span>
<span class="comments">;         ATTRVALUE=['ARIZONA', 'WYOMING', 'OREGON']. By default, the value</span>
<span class="comments">;         is "ALL", which means that all of the shape entities identified by</span>
<span class="comments">;         ATTRNAME will be drawn.</span>
<span class="comments">;     colors: in, optional, type=string, default="opposite"</span>
<span class="comments">;         The name of the color or colors used to draw the entity. This</span>
<span class="comments">;         may be a string array of the same size as ATTRVALUES.</span>
<span class="comments">;     fcolors: in, optional, type=string</span>
<span class="comments">;         The name of the color used to fill the entity. Used if the FILL</span>
<span class="comments">;         keyword is set. By default, the same as the COLORS keyword.</span>
<span class="comments">;     fill: in, optional, type=boolean</span>
<span class="comments">;         Set this keyword to fill the entity with a color.</span>
<span class="comments">;     linestyle: in, optional, type=integer, default=0</span>
<span class="comments">;         The normal linestyle keyword index to choose plotting line styles.</span>
<span class="comments">;         By default, set to 0 and solid lines. May be a vector of the same</span>
<span class="comments">;         size as ATTRVALUES.</span>
<span class="comments">;     mapCoord: in, optional, type=object</span>
<span class="comments">;         A map coordinate object (e.g., cgMap). Required to draw the</span>
<span class="comments">;         entity on a GCTP map projection set up with MAP_PROJ_INIT.</span>
<span class="comments">;         Note that this could also be a map structure as returned from MAP_PROJ_INIT,</span>
<span class="comments">;         but in that case the user is resposible for setting up the XY map</span>
<span class="comments">;         coordinate space independently and outside of this program.</span>
<span class="comments">;     minnumverts: in, optional, type=long, default=3</span>
<span class="comments">;         Set this keyword to the minimum number of vertices required to actually</span>
<span class="comments">;         draw a polygon. In other words, to to drawn, a polygon must have at least</span>
<span class="comments">;         this number of vertices. </span>
<span class="comments">;     projected_xy: in, optional, type=boolean, default=0</span>
<span class="comments">;         The program assumes the shapefile entities are expressed in latitude and</span>
<span class="comments">;         longitude values. If the entiites are expressed in projected XY Cartesian</span>
<span class="comments">;         coordinates, then set this value.</span>
<span class="comments">;     psym: in, optional, type=integer, default=16</span>
<span class="comments">;         When drawing points, the symbol to use. May be any value supported by</span>
<span class="comments">;         cgSymbol. The default is a filled circle, filled with `Colors`.</span>
<span class="comments">;     symsize: in, optional, type=float, default=1.0</span>
<span class="comments">;         The default symbol size. Used only when displaying points.</span>
<span class="comments">;     thick: in, optional, type=integer, default=1</span>
<span class="comments">;         The thickness of the line used to draw the entity.</span>
<span class="comments">;     uselimit: in, optional, type=boolean, default=0</span>
<span class="comments">;         Set this keyword to use the LIMIT as determined from the map coordinate object.</span>
<span class="comments">;         This keyword applies only if a valid `MapCoord` object is passed to the program.</span>
<span class="comments">;     window: in, optional, type=boolean, default=0</span>
<span class="comments">;         Set this keyword to add this command to a current cgWindow program. If a</span>
<span class="comments">;         cgWindow doesn't exist, one is created.</span>
<span class="comments">;-</span>
PRO cgDrawShapes, shapeFile, $
   ADDCMD=addcmd, $
   AUTODRAW=autodraw, $
   ATTRNAME=attrname, $
   ATTRVALUES=attrvalues, $
   COLORS=colors, $
   FCOLORS=fcolors, $
   FILL=fill, $
   LINESTYLE=linestyle, $
   MAPCOORD=mapCoord, $
   MINNUMVERTS=minNumVerts, $
   PROJECTED_XY=projected_xy, $
   PSYM=psym, $
   SYMSIZE=symsize, $
   THICK=thick, $
   USELIMIT=uselimit, $
   WINDOW=window
   
   Compile_Opt idl2

   <span class="comments">; Error handling.</span>
   Catch, theError
   IF theError NE 0 THEN BEGIN
      ok = Error_Message()
      IF Obj_Valid(shapefile) THEN Obj_Destroy, shapefile
      IF Ptr_Valid(entities) THEN Heap_Free, entities
      RETURN
   ENDIF
   
   <span class="comments">; A flag to allow graphics windows to be open.</span>
   nowindows = ((!D.FLAGS AND 256) EQ 0)
   
    <span class="comments">; Replace the commands in or create a cgWindow.</span>
    IF Keyword_Set(window) THEN BEGIN
    
       currentWindow = cgQuery(/CURRENT, COUNT=wincnt)
       IF wincnt EQ 0 THEN cgWindow
       cgWindow, 'cgDrawShapes', shapeFile, $
           AUTODRAW=autodraw, $
           ATTRNAME=attrname, $
           ATTRVALUES=attrvalues, $
           COLORS=colors, $
           FCOLORS=fcolors, $
           FILL=fill, $
           LINESTYLE=linestyle, $
           MAPCOORD=mapCoord, $
           MINNUMVERTS=minNumVerts, $
           PROJECTED_XY=projected_xy, $
           PSYM=psym, $
           SYMSIZE=symsize, $
           THICK=thick, $
           USELIMIT=uselimit, $
           REPLACECMD=1
          
       RETURN
       
    ENDIF

   <span class="comments">; Add this command to the current graphics window?</span>
   IF Keyword_Set(addcmd) THEN BEGIN
   
       cgWindow, 'cgDrawShapes', shapeFile, $
           AUTODRAW=autodraw, $
           ATTRNAME=attrname, $
           ATTRVALUES=attrvalues, $
           COLORS=colors, $
           FCOLORS=fcolors, $
           FILL=fill, $
           LINESTYLE=linestyle, $
           MAPCOORD=mapCoord, $
           MINNUMVERTS=minNumVerts, $
           PROJECTED_XY=projected_xy, $
           PSYM=psym, $
           SYMSIZE=symsize, $
           THICK=thick, $
           USELIMIT=uselimit, $
           ADDCMD=1
           
       RETURN
    
   ENDIF
   
   <span class="comments">; If a graphics window isn't open, open one now. Necessary to make</span>
   <span class="comments">; sure the drawing is not done white on white.</span>
   IF ~noWindows AND (!D.Window EQ -1) THEN cgDisplay
   
   <span class="comments">; Check parameters.</span>
   IF N_Elements(shapeFile) EQ 0 THEN BEGIN
      shapeFile = Filepath(Subdirectory=['examples', 'data'], 'states.shp')
      IF File_Test(shapeFile) THEN BEGIN
         shapeFile = Dialog_Pickfile(FILE=shapefile, FILTER='*.shp')
         IF shapeFile EQ "" THEN RETURN
      ENDIF ELSE BEGIN
         shapeFile = Dialog_Pickfile(FILTER='*.shp')
         IF shapeFile EQ "" THEN RETURN
      ENDELSE
   ENDIF
   IF N_Elements(colors) EQ 0 THEN colors = 'opposite'
   IF N_Elements(fcolors) EQ 0 THEN fcolors = colors
   fastColors = cgColor(colors)
   fastFillColors = cgColor(fcolors)
   IF N_Elements(fill) EQ 0 THEN fill = Keyword_Set(fill)
   IF N_Elements(linestyle) EQ 0 THEN linestyle = 0
   IF N_Elements(psym) EQ 0 THEN psym = 16
   IF N_Elements(symsize) EQ 0 THEN symsize = 1.0
   IF N_Elements(thick) EQ 0 THEN thick = 1.0
   IF N_Elements(minNumVerts) EQ 0 THEN minNumVerts = 3
   projected_xy = Keyword_Set(projected_xy)
   IF N_Elements(attrvalues) EQ 0 THEN attrvalues = 'ALL'
   
   <span class="comments">; Is there a map projection in effect, or do I have a map coordinate object</span>
   <span class="comments">; to create one? If not, then turn autodraw mode on.</span>
   IF (!X.Type NE 3) && (N_Elements(mapCoord) EQ 0) THEN autodraw = 1

   <span class="comments">; Make sure arrays have the same number of elements.</span>
   IF N_Elements(attrvalues) NE 1 THEN BEGIN
      numEntities = N_Elements(attrvalues)
      IF N_Elements(fastColors) EQ 1 THEN fastColors = Replicate(fastColors, numEntities)
      IF N_Elements(fastColors) NE numEntities THEN $
         Message, 'Number of COLORS does not match number of entity names.'
      IF N_Elements(fastFillColors) EQ 1 THEN fastFillColors = Replicate(fastFillColors, numEntities)
      IF N_Elements(fastFillColors) NE numEntities THEN $
         Message, 'Number of FCOLORS does not match number of entity names.'
      IF N_Elements(fill) EQ 1 THEN fill = Replicate(fill, numEntities)
      IF N_Elements(fill) NE numEntities THEN $
         Message, 'Number of FILL values does not match number of entity names.'
      IF N_Elements(linestyle) EQ 1 THEN linestyle = Replicate(linestyle, numEntities)
      IF N_Elements(linestyle) NE numEntities THEN $
         Message, 'Number of LINESTYLE values does not match number of entity names.'
      IF N_Elements(psym) EQ 1 THEN psym = Replicate(psym, numEntities)
      IF N_Elements(psym) NE numEntities THEN $
         Message, 'Number of PSYM values does not match number of entity names.'
      IF N_Elements(symsize) EQ 1 THEN symsize = Replicate(symsize, numEntities)
      IF N_Elements(symsize) NE numEntities THEN $
         Message, 'Number of SYMSIZE values does not match number of entity names.'
      IF N_Elements(thick) EQ 1 THEN thick = Replicate(thick, numEntities)
      IF N_Elements(thick) NE numEntities THEN $
         Message, 'Number of THICK values does not match number of entity names.'
   ENDIF

   <span class="comments">; Open the shape file and create the shape object.</span>
   shapefileObj = Obj_New('IDLffShape', shapeFile)
   IF Obj_Valid(shapefileObj) EQ 0 THEN $
      Message, 'Unable to create shape file object. Returning...'

   <span class="comments">; Make sure you have a default attribute name. If you don't have</span>
   <span class="comments">; one, get the first attribute name from the file.</span>
   IF N_Elements(attrname) EQ 0 THEN BEGIN
       shapefileObj -> GetProperty, ATTRIBUTE_NAMES=theNames
       theNames = StrUpCase(StrTrim(theNames, 2))
       attrname = theNames[0]
   ENDIF ELSE attrname = StrUpCase(attrname)
   
   <span class="comments">; Get the attribute names from the shape file.</span>
   IF N_Elements(theNames) EQ 0 THEN BEGIN
       shapefileObj -> GetProperty, ATTRIBUTE_NAMES=theNames
       theNames = StrUpCase(StrTrim(theNames, 2))
   ENDIF
   
   <span class="comments">; Find the attribute index.</span>
   attIndex = Where(theNames EQ attrname, count)
   IF count EQ 0 THEN Message, 'Unable to find attribute ' + attrname + ' in file. Returning...'

   <span class="comments">; Get all the attribute pointers from the file. These are the entities.</span>
   entities = Ptr_New(/Allocate_Heap)
   *entities = shapefileObj -> GetEntity(/All, /Attributes)
   
   <span class="comments">; Automatic drawing turned on and no map coordinate? </span>
   <span class="comments">; Then set up a drawing window with a data</span>
   <span class="comments">; coordinate system established.</span>
   IF Keyword_Set(autodraw) AND N_Elements(mapCoord) EQ 0 THEN BEGIN
       entityMinX = FltArr(N_Elements(*entities))
       entityMaxX = FltArr(N_Elements(*entities))
       entityMiny = FltArr(N_Elements(*entities))
       entityMaxY = FltArr(N_Elements(*entities))
       FOR j=0,N_Elements(*entities)-1 DO BEGIN
          thisEntity = (*entities)[j]
          entityMinX[j] =  thisEntity.bounds[0]
          entityMaxX[j] =  thisEntity.bounds[4]
          entityMiny[j] =  thisEntity.bounds[1]
          entityMaxY[j] =  thisEntity.bounds[5]
       ENDFOR
       xrange = [Min(entityMinX), Max(entityMaxX)]
       yrange = [Min(entityMinY), Max(entityMaxY)]
      
       cgPlot, xrange, yrange, XSTYLE=5, YSTYLE=5, Position=[0.05, 0.05, 0.95, 0.95], /NoData
       cgPlotS, [!X.Window[0], !X.Window[0], !X.Window[1], !X.Window[1],  !X.Window[0]], $
                [!Y.Window[0], !Y.Window[1], !Y.Window[1], !Y.Window[0],  !Y.Window[0]], $
                /NORMAL, Color='opposite', THICK=2
<span class="comments">;      Print, 'X Range: ', xrange, Format='(a10, f12.2, 2x, f12.2)'</span>
<span class="comments">;      Print, 'Y Range: ', yrange, Format='(a10, f12.2, 2x, f12.2)'</span>
   ENDIF
   
   <span class="comments">; If you have a map coordinate, then set the data range appropriately.</span>
   IF Obj_Valid(mapCoord) THEN BEGIN
      
       <span class="comments">; This is only necessary, if a LIMIT wasn't specified.</span>
       IF ~Keyword_Set(uselimit) THEN BEGIN
           entityMinX = FltArr(N_Elements(*entities))
           entityMaxX = FltArr(N_Elements(*entities))
           entityMiny = FltArr(N_Elements(*entities))
           entityMaxY = FltArr(N_Elements(*entities))
           FOR j=0,N_Elements(*entities)-1 DO BEGIN
              thisEntity = (*entities)[j]
              entityMinX[j] =  thisEntity.bounds[0]
              entityMaxX[j] =  thisEntity.bounds[4]
              entityMiny[j] =  thisEntity.bounds[1]
              entityMaxY[j] =  thisEntity.bounds[5]
           ENDFOR
           xrange = [Min(entityMinX), Max(entityMaxX)]
           yrange = [Min(entityMinY), Max(entityMaxY)]
           mapCoord -> SetProperty, XRANGE=xrange, YRANGE=yrange
       ENDIF
       
   ENDIF
   
   <span class="comments">; Cycle through each entity and draw it, if required. Free each pointer</span>
   <span class="comments">; as you go, because it take at least 10 times as long to free them with</span>
   <span class="comments">; HEAP_FREE at the end if you don't do this!</span>
   FOR j=0,N_Elements(*entities)-1 DO BEGIN
      thisEntity = (*entities)[j]
      theEntityName = StrUpCase(StrTrim((*thisEntity.attributes).(attIndex), 2))
      index = Where(StrUpCase(attrvalues) EQ theEntityName, test)
      IF attrvalues[0] EQ 'ALL' THEN BEGIN
         index = 0
         test = 1
      ENDIF
      IF (test EQ 1) THEN BEGIN
          IF Ptr_Valid(thisEntity.vertices) THEN BEGIN
              IF (N_Elements(*thisEntity.vertices) GE minNumVerts) THEN BEGIN
                  cgDrawShapes_DrawEntity, (*entities)[j], COLOR=(fastColors[index])[0], $
                     Fill=(fill[index])[0], LineStyle=(linestyle[index])[0], $
                     THICK=(thick[index])[0], PROJECTED_XY=projected_xy, $
                     MapCoord=mapCoord, FCOLOR=(fastFillColors[index])[0]
              ENDIF 
          ENDIF ELSE BEGIN
              cgDrawShapes_DrawEntity, (*entities)[j], COLOR=(fastColors[index])[0], $
                 PSYM=(psym[index])[0], THICK=(thick[index])[0], $
                 PROJECTED_XY=projected_xy, MapCoord=mapCoord, SYMSIZE=(symsize[index])[0]
          ENDELSE
      ENDIF
      Ptr_Free, thisEntity.vertices
      Ptr_Free, thisEntity.measure
      Ptr_Free, thisEntity.parts
      Ptr_Free, thisEntity.part_types
      Ptr_Free, thisEntity.attributes
      
   ENDFOR

   <span class="comments">; Clean up.</span>
   Obj_Destroy, shapefileObj
   Ptr_Free, entities
   
END <span class="comments">;---------------------------------------------------------------------------------</span>
</code>
    </div>
  </body>
</html>