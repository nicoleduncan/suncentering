<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN"
 "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">

<!-- Generated by IDLdoc 3.5.1 on Mon Sep 30 16:56:24 2013 -->

<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en">
  <head>
    <meta http-equiv="Content-Type" content="text/html;charset=utf-8"/>
    <title>cgoverplot__define.pro (Documentation for ./)</title>

    
    <link rel="stylesheet" type="text/css" media="all"
          href="../idldoc-resources/main.css" />
    <link rel="stylesheet" type="text/css" media="print"
          href="../idldoc-resources/main-print.css" />
    

    <script type="text/javascript">
      function setTitle() {
        parent.document.title="cgoverplot__define.pro (Documentation for ./)";
      }
    </script>
  </head>

  <body onload="setTitle();" id="root">
    <div class="content">
      <code class="source"><span class="comments">; docformat = 'rst'</span>
<span class="comments">;</span>
<span class="comments">; NAME:</span>
<span class="comments">;   cgOverPlot__Define</span>
<span class="comments">;</span>
<span class="comments">; PURPOSE:</span>
<span class="comments">;   The purpose of this program is to create a data object that can be plotted or drawn on</span>
<span class="comments">;   a set of axes set up by another plotting command.</span>
<span class="comments">;</span>
<span class="comments">;******************************************************************************************;</span>
<span class="comments">;                                                                                          ;</span>
<span class="comments">;  Copyright (c) 2012, by Fanning Software Consulting, Inc. All rights reserved.           ;</span>
<span class="comments">;                                                                                          ;</span>
<span class="comments">;  Redistribution and use in source and binary forms, with or without                      ;</span>
<span class="comments">;  modification, are permitted provided that the following conditions are met:             ;</span>
<span class="comments">;                                                                                          ;</span>
<span class="comments">;      * Redistributions of source code must retain the above copyright                    ;</span>
<span class="comments">;        notice, this list of conditions and the following disclaimer.                     ;</span>
<span class="comments">;      * Redistributions in binary form must reproduce the above copyright                 ;</span>
<span class="comments">;        notice, this list of conditions and the following disclaimer in the               ;</span>
<span class="comments">;        documentation and/or other materials provided with the distribution.              ;</span>
<span class="comments">;      * Neither the name of Fanning Software Consulting, Inc. nor the names of its        ;</span>
<span class="comments">;        contributors may be used to endorse or promote products derived from this         ;</span>
<span class="comments">;        software without specific prior written permission.                               ;</span>
<span class="comments">;                                                                                          ;</span>
<span class="comments">;  THIS SOFTWARE IS PROVIDED BY FANNING SOFTWARE CONSULTING, INC. ''AS IS'' AND ANY        ;</span>
<span class="comments">;  EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES    ;</span>
<span class="comments">;  OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT     ;</span>
<span class="comments">;  SHALL FANNING SOFTWARE CONSULTING, INC. BE LIABLE FOR ANY DIRECT, INDIRECT,             ;</span>
<span class="comments">;  INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED    ;</span>
<span class="comments">;  TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS;         ;</span>
<span class="comments">;  LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND             ;</span>
<span class="comments">;  ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT              ;</span>
<span class="comments">;  (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS           ;</span>
<span class="comments">;  SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.                            ;</span>
<span class="comments">;******************************************************************************************;</span>
<span class="comments">;</span>
<span class="comments">;+</span>
<span class="comments">; The purpose of this program is to create a data object that can be plotted or drawn on</span>
<span class="comments">; a set of axes set up by another plotting command.</span>
<span class="comments">;</span>
<span class="comments">; :Categories:</span>
<span class="comments">;    Graphics</span>
<span class="comments">;    </span>
<span class="comments">; :Examples:</span>
<span class="comments">;    Use, for example, with the cgPlot command::</span>
<span class="comments">;       oplotObj1 = Obj_New('cgOverPlot', cgDemoData(17), Color='red', PSYM=-1, LINESTYLE=2)</span>
<span class="comments">;       oplotObj2 = Obj_New('cgOverPlot', cgDemoData(17), Color='blue', PSYM=-2, LINESTYLE=4)</span>
<span class="comments">;       cgPlot, cgDemoData(17), Color='purple', PSYM=-4, OPLOTS=[oplotObj1, oplotObj2]</span>
<span class="comments">;       Obj_Destroy, [oplotObj1, oplotObj2]</span>
<span class="comments">;       </span>
<span class="comments">; :Author:</span>
<span class="comments">;    FANNING SOFTWARE CONSULTING::</span>
<span class="comments">;        David W. Fanning </span>
<span class="comments">;        1645 Sheely Drive</span>
<span class="comments">;        Fort Collins, CO 80526 USA</span>
<span class="comments">;        Phone: 970-221-0438</span>
<span class="comments">;        E-mail: david@idlcoyote.com</span>
<span class="comments">;        Coyote's Guide to IDL Programming: http://www.idlcoyote.com</span>
<span class="comments">;</span>
<span class="comments">; :History:</span>
<span class="comments">;     Change History::</span>
<span class="comments">;        Written, 18 July  2012. DWF.</span>
<span class="comments">;        The THICK keyword was not being set in the INIT method. Fixed. 2 Nov 2012. DWF.</span>
<span class="comments">;         </span>
<span class="comments">; :Copyright:</span>
<span class="comments">;     Copyright (c) 2012, Fanning Software Consulting, Inc.</span>
<span class="comments">;-</span>

<span class="comments">;+</span>
<span class="comments">; The initialization method of the object. Called automatically when the object</span>
<span class="comments">; is created.</span>
<span class="comments">; </span>
<span class="comments">; :Params:</span>
<span class="comments">;    x: in, required, type=any</span>
<span class="comments">;         If X is provided without Y, a vector representing the dependent values to be </span>
<span class="comments">;         plotted If both X and Y are provided, X is the independent parameter and </span>
<span class="comments">;         Y is the dependent parameter to be plotted.</span>
<span class="comments">;    y: in, optional, type=any</span>
<span class="comments">;         A vector representing the dependent values to be plotted.</span>
<span class="comments">;       </span>
<span class="comments">; :Keywords:</span>
<span class="comments">;     addcmd: in, optional, type=boolean, default=0</span>
<span class="comments">;        Set this keyword to add the command to the resizeable graphics window cgWindow.</span>
<span class="comments">;     color: in, optional, type=string, default='opposite'</span>
<span class="comments">;        The name of the data color. This is the color of the data line.</span>
<span class="comments">;     draw: in, optional, type=boolean, default=0</span>
<span class="comments">;        If this keyword is set, the data is drawn as soon as the object is created.</span>
<span class="comments">;     linestyle: in, optional, type=integer, default=0</span>
<span class="comments">;        The line style for drawing the line.</span>
<span class="comments">;     psym: in, optional, type=integer</span>
<span class="comments">;        Any normal IDL PSYM values, plus any value supported by the Coyote Library</span>
<span class="comments">;        routine cgSYMCAT. An integer between 0 and 46.</span>
<span class="comments">;     skip: in, optional, type=integer, default=1</span>
<span class="comments">;        The number of data points to skip when the line is drawn. The default is to</span>
<span class="comments">;        not skip any data points, but to plot them all.</span>
<span class="comments">;     symcolor: in, optional, type=string</span>
<span class="comments">;        The name of the symbol color. By default, the same as the `COLOR` keyword.</span>
<span class="comments">;     symsize: in, optional, type=float, default=1.0</span>
<span class="comments">;        The symbol size.</span>
<span class="comments">;     symthick: in, optional, type=float, default=1.0</span>
<span class="comments">;        The thickness of the symbol.</span>
<span class="comments">;     thick: in, optional, type=float, default = 1.0</span>
<span class="comments">;        The thickness of the line.</span>
<span class="comments">;     visible: in, optional, type=boolean, default=1</span>
<span class="comments">;        Set this keyword to determine in the line should be drawn (visible=1), or</span>
<span class="comments">;        if the line should not be drawn (visible=0).</span>
<span class="comments">;-</span>
FUNCTION cgOverPlot::INIT, x, y, $
    ADDCMD=addcmd, $
    COLOR=color, $
    DRAW=draw, $
    LINESTYLE=linestyle, $
    PSYM=psym, $
    SKIP=skip, $
    SYMCOLOR=symcolor, $
    SYMSIZE=symsize, $
    SYMTHICK=symthick, $
    THICK=thick, $
    VISIBLE=visible
    
    Compile_Opt idl2

    Catch, theError
    IF theError NE 0 THEN BEGIN
        Catch, /CANCEL
        void = Error_Message()
        IF N_Elements(currentState) NE 0 THEN SetDecomposedState, currentState
        RETURN, 0
    ENDIF
    
    <span class="comments">; Set up PostScript device for working with colors.</span>
    IF !D.Name EQ 'PS' THEN Device, COLOR=1, BITS_PER_PIXEL=8
    
    <span class="comments">; Check parameters.</span>
    IF N_Params() EQ 0 THEN BEGIN
        Print, 'USE SYNTAX: cgOverPlot, x, y'
        RETURN, 0
    ENDIF
    
    <span class="comments">; Adding this object to a window?</span>
    IF Keyword_Set(addcmd) THEN BEGIN
        cgControl, Execute=0
        cgWindow, 'Draw', self, ADDCMD=1
       ENDIF
     
    <span class="comments">; Sort out which is the dependent and which is independent data.</span>
    CASE N_Params() OF
      
       1: BEGIN
       dep = x
       indep = Findgen(N_Elements(dep))
       ENDCASE
    
       2: BEGIN
       dep = y
       indep = x
       ENDCASE
    
    ENDCASE
    
    <span class="comments">; If either of these input vectors are scalars, make them vectors.</span>
    IF N_Elements(dep) EQ 1 THEN dep = [dep]
    IF N_Elements(indep) EQ 1 THEN indep = [indep]
    
    <span class="comments">; Default colors.</span>
    SetDecomposedState, 1, CURRENTSTATE=currentState
    color = cgDefaultColor(color, DEFAULT="opposite")
    symcolor = cgDefaultColor(symcolor, DEFAULT=color)

    <span class="comments">; Other parameter defaults.</span>
    IF N_Elements(linestyle) EQ 0 THEN linestyle = 0
    IF N_Elements(psym) EQ 0 THEN psym = 0
    IF N_Elements(thick) EQ 0 THEN thick = 1.0
    IF N_Elements(skip) EQ 0 THEN skip = 1
    IF N_Elements(symsize) EQ 0 THEN symsize = 1.0
    IF N_Elements(symthick) EQ 0 THEN symthick = 1.0
    IF N_Elements(visible) EQ 0 THEN visible = 1
    
    <span class="comments">; Load the object.</span>
    self.dep = Ptr_New(dep)
    self.indep = Ptr_New(indep)
    self.color = color
    self.linestyle = linestyle    
    self.psym = psym
    self.thick = thick
    self.skip = 1
    self.symcolor = symcolor
    self.symsize = symsize
    self.symthick = symthick
    self.xrange = [Min(indep), Max(indep)]
    self.yrange = [Min(dep), Max(dep)]
    self.visible = visible
    
    IF Keyword_Set(draw) THEN self -> Draw
    IF Keyword_Set(addcmd) THEN cgControl, Execute=1
    
    RETURN, 1
 
END

<span class="comments">;+</span>
<span class="comments">; The clean-up routine for the object. Destroy pointers, etc.</span>
<span class="comments">;-</span>
PRO cgOverPlot::CLEANUP

    Ptr_Free, self.dep
    Ptr_Free, self.indep
    
END



<span class="comments">;+</span>
<span class="comments">; This method draws the overplot object. It assumes a set of axes has been</span>
<span class="comments">; established by some other graphics command.</span>
<span class="comments">;</span>
<span class="comments">; :Keywords:</span>
<span class="comments">;     skip: in, optional, type=integer</span>
<span class="comments">;         Set this keyword to the number of data points to skip when drawing the</span>
<span class="comments">;         data vectors. Similar to the SKIP parameter, but this keyword gives you</span>
<span class="comments">;         the opportunity to temporarily over-ride the value of the SKIP parameter.</span>
<span class="comments">;-</span>
PRO cgOverPlot::Draw, SKIP=skip

    Compile_Opt idl2

    Catch, theError
    IF theError NE 0 THEN BEGIN
        Catch, /CANCEL
        void = Error_Message()
        RETURN
    ENDIF
    
    <span class="comments">; If the plot is not visible, return now.</span>
    IF ~self.visible THEN RETURN
    
    <span class="comments">; Are we skipping data points?</span>
    IF N_Elements(skip) NE 0 THEN skipCount = skip
    IF N_Elements(skipCount) EQ 0 THEN skipCount = self.skip

    <span class="comments">; If you aren't skipping, draw all the data.</span>
    IF skipCount EQ 1 THEN BEGIN
        asymbol = cgSymCat(self.psym, THICK=self.symThick, COLOR=self.symcolor)
        IF self.psym LT 0 THEN asymbol = -asymbol
        OPlot, *self.indep, *self.dep, COLOR=cgColor(self.color), LINESTYLE=self.linestyle, $
            PSYM=asymbol, SYMSIZE=self.symsize, THICK=self.thick
    ENDIF ELSE BEGIN
        count = N_Elements(*self.dep)-1
        asymbol = cgSymCat(self.psym, THICK=self.symThick, COLOR=self.symcolor)
        IF self.psym LT 0 THEN asymbol = -asymbol 
        OPlot, (*self.indep)[0:count:skipCount], (*self.dep)[0:count:skipCount], $
            COLOR=cgColor(self.color), LINESTYLE=self.linestyle, $
            PSYM=asymbol, SYMSIZE=self.symsize, THICK=self.thick
    ENDELSE
    
END


<span class="comments">;+</span>
<span class="comments">; This method obtains the current properties of the object. </span>
<span class="comments">; </span>
<span class="comments">; :Keywords:</span>
<span class="comments">;     color: out, optional, type=string</span>
<span class="comments">;        The name of the data color. This is the color of the data line.</span>
<span class="comments">;     dep: out, optional, type=varies</span>
<span class="comments">;        The current dependent data of the object.</span>
<span class="comments">;     draw: out, optional, type=boolean</span>
<span class="comments">;        If this keyword is set, the data is drawn as soon as the object is created.</span>
<span class="comments">;     indep: out, optional, type=varies</span>
<span class="comments">;        The current independent data of the object.</span>
<span class="comments">;     linestyle: out, optional, type=integer</span>
<span class="comments">;        The line style for drawing the line.</span>
<span class="comments">;     psym: out, optional, type=integer</span>
<span class="comments">;        Any normal IDL PSYM values, plus any value supported by the Coyote Library</span>
<span class="comments">;        routine cgSYMCAT. An integer between 0 and 46.</span>
<span class="comments">;     skip: out, optional, type=integer, default=1</span>
<span class="comments">;        The number of data points to skip when the line is drawn. </span>
<span class="comments">;     symcolor: out, optional, type=string</span>
<span class="comments">;        The name of the symbol color. </span>
<span class="comments">;     symsize: out, optional, type=float</span>
<span class="comments">;        The symbol size.</span>
<span class="comments">;     symthick: out, optional, type=float</span>
<span class="comments">;        The thickness of the symbol.</span>
<span class="comments">;     thick: out, optional, type=float</span>
<span class="comments">;        The thickness of the line.</span>
<span class="comments">;     xrange: out, optional, type=fltarr</span>
<span class="comments">;        The range of the dependent data.</span>
<span class="comments">;     yrange: out, optional, type=fltarr</span>
<span class="comments">;        The range of the independent data.</span>
<span class="comments">;     visible: out, optional, type=boolean</span>
<span class="comments">;        The visibility of the objects plot line.</span>
<span class="comments">;-</span>
PRO cgOverPlot::GetProperty, $
    COLOR=color, $
    DEP=dep, $
    DRAW=draw, $
    INDEP=indep, $
    LINESTYLE=linestyle, $
    PSYM=psym, $
    SKIP=skip, $
    SYMCOLOR=symcolor, $
    SYMSIZE=symsize, $
    SYMTHICK=symthick, $
    THICK=thick, $
    XRANGE=xrange, $
    YRANGE=yrange, $
    VISIBLE=visible

    Compile_Opt idl2

    Catch, theError
    IF theError NE 0 THEN BEGIN
        Catch, /CANCEL
        void = Error_Message()
        RETURN
    ENDIF

    IF Arg_Present(color) THEN color = self.color
    IF Arg_Present(dep) THEN dep = *self.dep
    IF Arg_Present(indep) THEN indep = *self.indep
    IF Arg_Present(linestyle) THEN linestyle = self.linestyle
    IF Arg_Present(psym) THEN psym = self.psym
    IF Arg_Present(skip) THEN skip = self.skip
    IF Arg_Present(symcolor) THEN symcolor = self.symcolor
    IF Arg_Present(symsize) THEN symsize = self.symsize
    IF Arg_Present(symthick) THEN symthick = self.symthick
    IF Arg_Present(thick) THEN thick = self.thick
    IF Arg_Present(xrange) THEN xrange = self.xrange
    IF Arg_Present(yrange) THEN yrange = self.yrange
    IF Arg_Present(visible) THEN visible = self.visible
    
END
    

<span class="comments">;+</span>
<span class="comments">; This method sets the properties of the object.</span>
<span class="comments">;</span>
<span class="comments">; :Keywords:</span>
<span class="comments">;     color: in, optional, type=string</span>
<span class="comments">;        The name of the data color. This is the color of the data line.</span>
<span class="comments">;     dep: in, optional, type=varies</span>
<span class="comments">;        A vector of dependent data.</span>
<span class="comments">;     draw: in, optional, type=boolean, default=0</span>
<span class="comments">;        If this keyword is set, the data is drawn as soon as the properties are set.</span>
<span class="comments">;     indep: in, optional, type=varies</span>
<span class="comments">;        A vector of independent data.</span>
<span class="comments">;     linestyle: in, optional, type=integer</span>
<span class="comments">;        The line style for drawing the line.</span>
<span class="comments">;     psym: in, optional, type=integer</span>
<span class="comments">;        Any normal IDL PSYM values, plus any value supported by the Coyote Library</span>
<span class="comments">;        routine cgSYMCAT. An integer between 0 and 46.</span>
<span class="comments">;     skip: in, optional, type=integer, default=1</span>
<span class="comments">;        The number of data points to skip when the line is drawn. The default is to</span>
<span class="comments">;        not skip any data points, but to plot them all.</span>
<span class="comments">;     symcolor: in, optional, type=string</span>
<span class="comments">;        The name of the symbol color. By default, the same as the `COLOR` keyword.</span>
<span class="comments">;     symsize: in, optional, type=float, default=1.0</span>
<span class="comments">;        The symbol size.</span>
<span class="comments">;     symthick: in, optional, type=float, default=1.0</span>
<span class="comments">;        The thickness of the symbol.</span>
<span class="comments">;     thick: in, optional, type=float, default = 1.0</span>
<span class="comments">;        The thickness of the line.</span>
<span class="comments">;     visible: in, optional, type=boolean, default=1</span>
<span class="comments">;        Set this keyword to determine in the line should be drawn (visible=1), or</span>
<span class="comments">;        if the line should not be drawn (visible=0).</span>
<span class="comments">;-</span>
PRO cgOverPlot::SetProperty, $
    COLOR=color, $
    DEP=dep, $
    DRAW=draw, $
    INDEP=indep, $
    LINESTYLE=linestyle, $
    PSYM=psym, $
    SKIP=skip, $
    SYMCOLOR=symcolor, $
    SYMSIZE=symsize, $
    SYMTHICK=symthick, $
    THICK=thick, $
    VISIBLE=visible

    Compile_Opt idl2

    Catch, theError
    IF theError NE 0 THEN BEGIN
        Catch, /CANCEL
        void = Error_Message()
        RETURN
    ENDIF

    IF N_Elements(color) NE 0 THEN self.color = color
    IF N_Elements(dep) NE 0 THEN *self.dep = dep
    IF N_Elements(indep) NE 0 THEN *self.indep = indep
    self.yrange = [Min(*self.dep), Max(*self.dep)]
    self.xrange = [Min(*self.indep), Max(*self.indep)]
    IF N_Elements(linestyle) NE 0 THEN self.linestyle = linestyle
    IF N_Elements(psym) NE 0 THEN self.psym = psym
    IF N_Elements(skip) NE 0 THEN self.skip = skip
    IF N_Elements(symcolor) NE 0 THEN self.symcolor = symcolor
    IF N_Elements(symsize) NE 0 THEN self.symsize = symsize
    IF N_Elements(symthick) NE 0 THEN self.symthick = symthick
    IF N_Elements(thick) NE 0 THEN self.thick = thick
    IF N_Elements(visible) NE 0 THEN self.visible = visible
    
    <span class="comments">; Need to draw?</span>
    IF Keyword_Set(draw) THEN self -> Draw
     
END

<span class="comments">;+</span>
<span class="comments">; The class definition module for the object.</span>
<span class="comments">; </span>
<span class="comments">; :Params:</span>
<span class="comments">;     class: out, optional, type=struct</span>
<span class="comments">;        The class definition as a structure variable. Occasionally useful.</span>
<span class="comments">;-</span>
PRO cgOverPlot__Define, class

    class = { CGOVERPLOT, $
              INHERITS IDL_OBJECT, $ <span class="comments">; To allow IDL 8 dot referencing of properties.</span>
              dep: Ptr_New(), $      <span class="comments">; The dependent data.</span>
              indep: Ptr_New(), $    <span class="comments">; The independent data.</span>
              color: "", $           <span class="comments">; The color of the overplotted line.</span>
              linestyle: 0L, $       <span class="comments">; The linestyle of the overplotted line.</span>
              psym: 0L, $            <span class="comments">; The symbol of the overplotted line.</span>
              symcolor: "", $        <span class="comments">; The symbol color.</span>
              symthick: 0L, $        <span class="comments">; The thickness of the line symbol.</span>
              symsize: 0L, $         <span class="comments">; The size of the line symbol.</span>
              thick: 0L, $, $        <span class="comments">; The thickness of the overplotted line.</span>
              skip: 0L, $            <span class="comments">; The number of data points to skip in the drawing.</span>
              visible: 0L, $         <span class="comments">; A flag that indicates if the plot should be drawn.</span>
              xrange: FltArr(2), $   <span class="comments">; The X range of the data.</span>
              yrange: FltArr(2) $    <span class="comments">; The Y range of the data.</span>
            }
            
END
</code>
    </div>
  </body>
</html>