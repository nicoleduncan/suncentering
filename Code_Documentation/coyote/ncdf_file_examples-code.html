<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN"
 "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">

<!-- Generated by IDLdoc 3.5.1 on Mon Sep 30 16:57:08 2013 -->

<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en">
  <head>
    <meta http-equiv="Content-Type" content="text/html;charset=utf-8"/>
    <title>ncdf_file_examples.pro (Documentation for ./)</title>

    
    <link rel="stylesheet" type="text/css" media="all"
          href="../idldoc-resources/main.css" />
    <link rel="stylesheet" type="text/css" media="print"
          href="../idldoc-resources/main-print.css" />
    

    <script type="text/javascript">
      function setTitle() {
        parent.document.title="ncdf_file_examples.pro (Documentation for ./)";
      }
    </script>
  </head>

  <body onload="setTitle();" id="root">
    <div class="content">
      <code class="source"><span class="comments">;+</span>
<span class="comments">; NAME:</span>
<span class="comments">;       NCDF_FILE_EXAMPLES</span>
<span class="comments">;</span>
<span class="comments">; PURPOSE:</span>
<span class="comments">;</span>
<span class="comments">;       This is a utility routine demonstrates the several ways it is possible</span>
<span class="comments">;       to use the NCDF_FILE object to create netCDF files, copy information</span>
<span class="comments">;       from one netCDF file to another, and to read information from a netCDF</span>
<span class="comments">;       file.</span>
<span class="comments">;</span>
<span class="comments">; AUTHOR:</span>
<span class="comments">;</span>
<span class="comments">;       FANNING SOFTWARE CONSULTING</span>
<span class="comments">;       David Fanning, Ph.D.</span>
<span class="comments">;       1645 Sheely Drive</span>
<span class="comments">;       Fort Collins, CO 80526 USA</span>
<span class="comments">;       Phone: 970-221-0438</span>
<span class="comments">;       E-mail: david@idlcoyote.com</span>
<span class="comments">;       Coyote's Guide to IDL Programming: http://www.idlcoyote.com</span>
<span class="comments">;</span>
<span class="comments">; CATEGORY:</span>
<span class="comments">;</span>
<span class="comments">;       Utilities</span>
<span class="comments">;</span>
<span class="comments">; CALLING SEQUENCE:</span>
<span class="comments">;</span>
<span class="comments">;       NCDF_File_Examples</span>
<span class="comments">;</span>
<span class="comments">; ARGUMENTS:</span>
<span class="comments">;</span>
<span class="comments">;       None.</span>
<span class="comments">;</span>
<span class="comments">; KEYWORDS:</span>
<span class="comments">;</span>
<span class="comments">;       None.</span>
<span class="comments">;</span>
<span class="comments">; MODIFICATION HISTORY:</span>
<span class="comments">;</span>
<span class="comments">;       Written by:  David W. Fanning, 3 February 2010.</span>
<span class="comments">;       Updated to use a time variable for the frame number. 29 Oct 2011.</span>
<span class="comments">;-</span>
<span class="comments">;******************************************************************************************;</span>
<span class="comments">;  Copyright (c) 2010, by Fanning Software Consulting, Inc.                                ;</span>
<span class="comments">;  All rights reserved.                                                                    ;</span>
<span class="comments">;                                                                                          ;</span>
<span class="comments">;  Redistribution and use in source and binary forms, with or without                      ;</span>
<span class="comments">;  modification, are permitted provided that the following conditions are met:             ;</span>
<span class="comments">;                                                                                          ;</span>
<span class="comments">;      * Redistributions of source code must retain the above copyright                    ;</span>
<span class="comments">;        notice, this list of conditions and the following disclaimer.                     ;</span>
<span class="comments">;      * Redistributions in binary form must reproduce the above copyright                 ;</span>
<span class="comments">;        notice, this list of conditions and the following disclaimer in the               ;</span>
<span class="comments">;        documentation and/or other materials provided with the distribution.              ;</span>
<span class="comments">;      * Neither the name of Fanning Software Consulting, Inc. nor the names of its        ;</span>
<span class="comments">;        contributors may be used to endorse or promote products derived from this         ;</span>
<span class="comments">;        software without specific prior written permission.                               ;</span>
<span class="comments">;                                                                                          ;</span>
<span class="comments">;  THIS SOFTWARE IS PROVIDED BY FANNING SOFTWARE CONSULTING, INC. ''AS IS'' AND ANY        ;</span>
<span class="comments">;  EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES    ;</span>
<span class="comments">;  OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT     ;</span>
<span class="comments">;  SHALL FANNING SOFTWARE CONSULTING, INC. BE LIABLE FOR ANY DIRECT, INDIRECT,             ;</span>
<span class="comments">;  INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED    ;</span>
<span class="comments">;  TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS;         ;</span>
<span class="comments">;  LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND             ;</span>
<span class="comments">;  ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT              ;</span>
<span class="comments">;  (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS           ;</span>
<span class="comments">;  SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.                            ;</span>
<span class="comments">;******************************************************************************************;</span>
PRO NCDF_File_Examples

<span class="comments">; This is an example program to demonstrate some of the capabilities</span>
<span class="comments">; of the NCDF_FILE objects and its related objects.</span>

    <span class="comments">; Error handling.</span>
    Catch, theError
    IF theError NE 0 THEN BEGIN
        Catch, /CANCEL
        void = Error_Message()
        Obj_Destroy, fileObj
        Obj_Destroy, sObj
        Obj_Destroy, dObj
        RETURN
    ENDIF
    
    <span class="comments">; Initial definition of objects, in case there is a problem.</span>
    <span class="comments">; This will allow proper cleanup of open netCDF files.</span>
    fileObj = Obj_New()
    sObj = Obj_New()
    dObj = Obj_New()

    <span class="comments">; Open a new file for writing.</span>
    fileObj = Obj_New("NCDF_File", /CREATE, /CLOBBER, 'ncdf_example.nc')
    IF Obj_Valid(fileObj) EQ 0 THEN $
        Message, 'Invalid file object returned from NCDF_FILE INIT.'
    
    <span class="comments">; Create some data to put into the file.</span>
    data = cgDemoData(18)
    dims = Size(data, /DIMENSIONS)
    colortable = 34
    
    <span class="comments">; Add global attributes to the file.</span>
    fileObj -> WriteGlobalAttr, DATATYPE='CHAR', 'title', $
        'NCDF_FILE Example File.'
    fileObj -> WriteGlobalAttr, DATATYPE='CHAR', 'description', $
        'An example of how to create a netCDF file with the NCDF_FILE object.'
    fileObj -> WriteGlobalAttr, 'created', Systime(), DATATYPE='CHAR'
    
    <span class="comments">; Add dimensions to the file.</span>
    fileObj -> WriteDim, 'xsize', dims[0], OBJECT=xdimObj
    fileObj -> WriteDim, 'ysize', dims[1], OBJECT=ydimObj
    fileObj -> WriteDim, 'time', 24, OBJECT=timeDimObj
    
    <span class="comments">; Get the dimension names.</span>
    dimNames = [xdimObj->GetName(), ydimObj->GetName(), timeDimObj->GetName()]

    <span class="comments">; Define a variable for the file.</span>
    fileObj -> WriteVarDef, 'data', dimNames, DATATYPE='BYTE', OBJECT=dataObj
    IF Obj_Valid(dataObj) EQ 0 THEN Message, 'Invalid data object returned.'
    
    <span class="comments">; Define variable attributes.</span>
    fileObj -> WriteVarAttr, dataObj, 'comment', 'Frames of a data animation.'
    fileObj -> WriteVarAttr, 'data', 'colortable', colortable, DATATYPE='SHORT'
    
    <span class="comments">; Define a time variable for the file. Months, starting 15 Jan 2008 and going</span>
    <span class="comments">; for 24 months. Offset to 1 Jan 1600 00:00:00.</span>
    offset = Julday(1,1,1600,0,0,0)
    time = TimeGen(24, START=Julday(01,15,2008, 0, 0, 0), UNITS='Months', STEP_SIZE=1) - offset
    fileObj -> WriteVarDef, 'time', [timeDimObj->GetName()], DATATYPE='DOUBLE', OBJECT=timeObj
    IF Obj_Valid(timeObj) EQ 0 THEN Message, 'Invalid time object returned.'
    
    <span class="comments">; Define variable attributes.</span>
    fileObj -> WriteVarAttr, timeObj, 'Units', 'Julian days since 1 January 1600 00:00:00.'

    <span class="comments">; Write the data to the file. Here we are going to write 24 frames of data.</span>
    FOR j=0,23 DO BEGIN
        fileObj -> WriteVarData, 'data', cgDemoData(18), OFFSET=[0,0,j]
    ENDFOR
    
    <span class="comments">; Write the time into the file.</span>
    fileObj -> WriteVarData, 'time', time
    
    <span class="comments">; Sync the file by writing memory to disk.</span>
    fileObj -> Sync
    
    <span class="comments">; Browse the file.</span>
    fileObj -> Browse, XOFFSET=250, YOFFSET=250, TITLE='Example File'
    
    <span class="comments">; Destroy this file object.</span>
    Obj_Destroy, fileObj

<span class="comments">;**************************************************************************************</span>
<span class="comments">;**************************************************************************************</span>

    <span class="comments">; Open the file you just created and copy the information in it to another file.</span>
    sourceFile = 'ncdf_example.nc'
    destFile = 'ncdf_example_copy.nc'
    
    <span class="comments">; Open the source file in read-only mode.</span>
    sObj = Obj_New('NCDF_FILE', sourceFile, $
        ErrorLoggerName='sourcefilelogger', /TIMESTAMP)
    IF Obj_Valid(sObj) EQ 0 THEN Message, 'Source object cannot be created.'
    
    <span class="comments">; Open the destination file for writing.</span>
    dObj = Obj_New('NCDF_FILE', destFile, /CREATE, /CLOBBER, $
        ErrorLoggerName='destinationfilelogger', /TIMESTAMP)
    IF Obj_Valid(dObj) EQ 0 THEN Message, 'Destination object cannot be created.'
    
    <span class="comments">; Find all the global attributes in the source file and copy them.</span>
    attrNames = sObj -> GetGlobalAttrNames(COUNT=attrCount)
    FOR j=0,attrCount-1 DO BEGIN
        sObj -> CopyGlobalAttrTo, attrNames[j], dObj
    ENDFOR
    
    <span class="comments">; Find all the dimensions in the source file and copy them.</span>
    dimNames = sObj -> GetDimNames(COUNT=dimCount)
    FOR j=0,dimCount-1 DO BEGIN
        sObj -> CopyDimTo, dimNames[j], dObj
    ENDFOR
  
    <span class="comments">; Find all the variable definitions, attributes and data in the </span>
    <span class="comments">; source file and copy them.</span>
    varNames = sObj -> GetVarNames(COUNT=varCount)
    FOR j=0,varCount-1 DO BEGIN
        sObj -> CopyVarDefTo, varNames[j], dObj
        varAttrNames = sObj -> GetVarAttrNames(varNames[j], COUNT=varAttrCount)
        FOR k=0,varAttrCount-1 DO BEGIN
            sObj -> CopyVarAttrTo, varNames[j], varAttrNames[k], dObj
        ENDFOR
        sObj -> CopyVarDataTo, varNames[j], dObj
    ENDFOR

    <span class="comments">; Sync the destination file.</span>
    dObj -> Sync
    
    <span class="comments">; Browse the destination file.</span>
    dObj -> Browse, XOFFSET=350, YOFFSET=375, TITLE='Copy of Example File'
    
    <span class="comments">; Destroy both the source and destination objects.</span>
    Obj_Destroy, dObj
    Obj_Destroy, sObj
    
<span class="comments">;**************************************************************************************</span>
<span class="comments">;**************************************************************************************</span>

    <span class="comments">; Open the original file and make an annimation out of the data in the file.</span>
    <span class="comments">; Open the source file in read-only mode.</span>
    sObj = Obj_New('NCDF_FILE', sourceFile)
    IF Obj_Valid(sObj) EQ 0 THEN Message, 'Source object cannot be created.'
    
    ct = sObj -> GetVarAttrValue('data', 'colortable')
    LoadCT, ct, /SILENT
    
    check = sObj -> HasDim('xsize', OBJECT=xsizeObj)
    IF check THEN xsize = xsizeObj -> GetSize() ELSE Message, 'Cannot find dimension XSIZE.'

    check = sObj -> HasDim('ysize', OBJECT=ysizeObj)
    IF check THEN ysize = ysizeObj -> GetSize() ELSE Message, 'Cannot find dimension YSIZE.'
    
    check = sObj -> HasDim('time', OBJECT=timeDimObj)
    IF check THEN numMonths = timeDimObj -> GetSize() ELSE Message, 'Cannot find dimension TIME.'
    
    <span class="comments">; Set up the animation.</span>
    XInterAnimate, SET=[xsize, ysize, numMonths], /SHOWLOAD
    time = sObj -> GetVarData('time')
    time = time + Julday(1,1,1600,0,0,0)
    FOR j=0,numMonths-1 DO BEGIN
        data = sObj -> GetVarData('data', COUNT=[xsize, ysize, 1], OFFSET=[0,0,j])
        cgImage, data, /KEEP, /NOINTERP
        CalDat, time[j], month, day, year
        theDate = StrTrim(day,2) + ' ' + cgMonths(month) + ' ' + StrTrim(year,2)
        cgText, 0.1, 0.05, /NORMAL, 'Date: ' + theDate, COLOR='black', FONT=0
        XInterAnimate, FRAME=j, WINDOW=!D.Window
    ENDFOR
    XInterAnimate, 25
    
    <span class="comments">; Destroy the file object.</span>
    Obj_Destroy, sObj
    END
</code>
    </div>
  </body>
</html>